#include <cuda_runtime.h>
#include <cuda_profiler_api.h>
#include <cuda_fp16.h>
#include <mma.h>
#include <cublas_v2.h>
#include <cub/block/block_radix_sort.cuh>

#include <stdint.h>
#include <errno.h>
#include <limits.h>
#include <math.h>
#include <dlfcn.h>
#include <fcntl.h>
#include <stdio.h>
#include <stdlib.h>
#include <string.h>
#include <sys/stat.h>
#include <time.h>
#include <unistd.h>
#include <mutex>
#include <unordered_map>
#include <vector>

#include "ds4_source_formats.h"
#include "kernels/turbomind/ggml-turbomind/include/ggml-turbomind-api.h"

#ifndef M_PI
#define M_PI 3.14159265358979323846
#endif

#define CUDA_QK_K 256
#define DS4_CUDA_UNUSED __attribute__((unused))

enum {
    /* attention_decode_mixed_kernel stores raw-window scores plus visible
     * compressed scores in shared memory.  The host routes larger unmasked
     * decode calls to the online attention kernel so this fixed buffer never
     * becomes an out-of-bounds write at long context. */
    DS4_CUDA_ATTENTION_SCORE_CAP = 8192u,
    DS4_CUDA_ATTENTION_RAW_SCORE_CAP = 256u,
    DS4_CUDA_TOPK_MERGE_GROUP = 8u
};

struct ds4_gpu_tensor {
    void *ptr;
    uint64_t bytes;
    uint64_t alloc_bytes;
    int owner;
    int device;
};

struct ds4_gpu_event {
    cudaEvent_t event;
    int gpu;
};

struct ds4_gpu_arena {
    void *ptr;
    uint64_t bytes;
    uint64_t used;
    uint64_t peak_used;
    size_t free_before;
    size_t total_before;
    size_t free_after_alloc;
    size_t total_after_alloc;
    size_t free_after_upload;
    size_t total_after_upload;
    int gpu;
    int valid;
};

typedef struct {
    uint64_t weight_offset;
    uint64_t scale_offset;
    uint64_t weight_bytes_per_expert;
    uint64_t scale_bytes_per_expert;
    uint32_t n;
    uint32_t k;
    uint32_t experts_packed;
    uint32_t experts_total;
    int k_pack;
    int weight_stride;
    int scale_stride;
} ds4_gpu_turbomind_mxfp4_matrix_view;

typedef struct {
    uint64_t arena_offset;
    uint64_t byte_length;
    uint32_t rows;
    uint32_t cols;
    uint32_t row_stride_elements;
} ds4_gpu_bf16_matrix_view;

typedef struct {
    uint64_t arena_offset;
    uint64_t byte_length;
    uint32_t rows;
    uint32_t cols;
    uint32_t row_stride_bytes;
} ds4_gpu_source_row_view;

typedef struct {
    uint64_t arena_offset;
    uint64_t byte_length;
    uint32_t experts;
    uint32_t rows;
    uint32_t cols;
    uint32_t row_stride_bytes;
    uint64_t expert_stride_bytes;
} ds4_gpu_q4_k_expert_view;

typedef struct {
    uint32_t ratio;
    uint32_t slot;
    uint32_t slots;
    uint32_t raw_rows;
    uint32_t raw_row;
    uint32_t comp_rows;
    uint32_t comp_row;
    uint32_t head_dim;
    uint32_t indexer_head_dim;
    uint32_t attn_state_values;
    uint32_t indexer_state_values;
} ds4_gpu_v100_prefill_kv_update;

typedef struct {
    uint8_t scales[CUDA_QK_K / 16];
    uint8_t qs[CUDA_QK_K / 4];
    uint16_t d;
    uint16_t dmin;
} cuda_block_q2_K;

typedef struct {
    uint16_t d;
    uint16_t dmin;
    uint8_t scales[12];
    uint8_t qs[CUDA_QK_K / 2];
} cuda_block_q4_K;

typedef struct {
    float d;
    int8_t qs[CUDA_QK_K];
    int16_t bsums[CUDA_QK_K / 16];
} cuda_block_q8_K;

typedef struct {
    uint16_t d;
    uint16_t qs[CUDA_QK_K / 8];
} cuda_block_iq2_xxs;

#include "ds4_iq2_tables_cuda.inc"

static const void *g_model_host_base;
static const char *g_model_device_base;
static uint64_t g_model_registered_size;
static int g_model_registered;
static int g_model_device_owned;
static int g_model_range_mapping_supported = 1;
static int g_model_hmm_direct;
static int g_model_fd = -1;
static const void *g_model_fd_host_base;
static int g_model_direct_fd = -1;
static uint64_t g_model_direct_align = 1;
static uint64_t g_model_file_size;
static int g_model_cache_full;
static cudaStream_t g_model_prefetch_stream;
static cudaStream_t g_model_upload_stream;
static cublasHandle_t g_cublas;
static int g_cublas_ready;
static int g_quality_mode;

typedef int  (*tm_pfn_api_version)(void);
typedef int  (*tm_pfn_init)(int);
typedef void (*tm_pfn_shutdown)(void);
typedef int  (*tm_pfn_packed_bytes)(int, int, int, int, size_t *, size_t *);
typedef int  (*tm_pfn_pack_weight)(const void *, int, int, int, int, void *, void *, int *, void *);
typedef int  (*tm_pfn_mul_mat_grouped)(const void *, const int *, const int *, int,
                                       const void * const *, const void * const *,
                                       int, int, int, int, int, void *, void *);
typedef int  (*tm_pfn_mul_mat_grouped_total_tokens)(const void *, const int *, const int *, int, int,
                                                    const void * const *, const void * const *,
                                                    int, int, int, int, int, void *, void *);

typedef struct {
    void *handle;
    tm_pfn_api_version api_version;
    tm_pfn_init init;
    tm_pfn_shutdown shutdown;
    tm_pfn_packed_bytes packed_bytes;
    tm_pfn_pack_weight pack_weight;
    tm_pfn_mul_mat_grouped mul_mat_grouped;
    tm_pfn_mul_mat_grouped_total_tokens mul_mat_grouped_total_tokens;
    int attempted;
    int available;
    int warned;
} cuda_turbomind_api;

static cuda_turbomind_api g_tm_api;
static std::mutex g_tm_api_mutex;
static void cuda_tm_matrix_table_cache_release_all(void);
static void cuda_f8_f16_arena_cache_release_all(void);
static void cuda_f8_f16_arena_cache_release_arena(const ds4_gpu_arena *arena);
static void cuda_tensor_pool_release_all(void);

struct cuda_model_range {
    const void *host_base;
    uint64_t offset;
    uint64_t bytes;
    char *device_ptr;
    void *registered_base;
    char *registered_device_base;
    uint64_t registered_bytes;
    int host_registered;
    int arena_allocated;
    int device;
};

struct cuda_model_arena {
    char *device_ptr;
    uint64_t bytes;
    uint64_t used;
    int device;
};

struct cuda_q8_f16_range {
    const void *host_base;
    uint64_t offset;
    uint64_t weight_bytes;
    uint64_t in_dim;
    uint64_t out_dim;
    __half *device_ptr;
};

struct cuda_q8_f32_range {
    const void *host_base;
    uint64_t offset;
    uint64_t weight_bytes;
    uint64_t in_dim;
    uint64_t out_dim;
    float *device_ptr;
};

struct cuda_f8_f16_arena_range {
    void *arena_ptr;
    int gpu;
    uint64_t arena_offset;
    uint64_t byte_length;
    uint32_t rows;
    uint32_t cols;
    uint32_t row_stride_bytes;
    __half *device_ptr;
    uint64_t bytes;
};

struct cuda_tensor_pool_entry {
    void *ptr;
    uint64_t bytes;
    int device;
};

static std::vector<cuda_model_range> g_model_ranges;
static std::vector<cuda_model_arena> g_model_arenas;
static std::unordered_map<uint64_t, size_t> g_model_range_by_offset;
static std::vector<cuda_q8_f16_range> g_q8_f16_ranges;
static std::unordered_map<uint64_t, size_t> g_q8_f16_by_offset;
static std::vector<cuda_q8_f32_range> g_q8_f32_ranges;
static std::unordered_map<uint64_t, size_t> g_q8_f32_by_offset;
static std::vector<cuda_f8_f16_arena_range> g_f8_f16_arena_ranges;
static std::vector<cuda_tensor_pool_entry> g_tensor_pool;
static std::mutex g_f8_f16_arena_mutex;
static std::mutex g_tensor_pool_mutex;
static std::mutex g_f8_shape_trace_mutex;
static std::mutex g_tm_profile_mutex;
static uint64_t g_model_range_bytes;
static uint64_t g_q8_f16_bytes;
static uint64_t g_q8_f32_bytes;
static uint64_t g_f8_f16_arena_bytes;
static uint64_t g_tensor_pool_bytes;
static int g_q8_f16_disabled_after_oom;
static int g_q8_f16_budget_notice_printed;
static uint64_t g_model_load_progress_next;
static double g_model_load_progress_last;
static int g_model_load_progress_started;
static int g_model_load_progress_tty;
enum {
    DS4_CUDA_MAX_TMP_DEVICES = 16,
};

static void *g_cuda_tmp[DS4_CUDA_MAX_TMP_DEVICES];
static uint64_t g_cuda_tmp_bytes[DS4_CUDA_MAX_TMP_DEVICES];
static void *g_model_stage_raw[4];
static void *g_model_stage[4];
static cudaEvent_t g_model_stage_event[4];
static uint64_t g_model_stage_bytes;

static int cuda_ok(cudaError_t err, const char *what);
static const char *cuda_model_range_ptr_from_fd(
        const void *model_map,
        uint64_t offset,
        uint64_t bytes,
        const char *what);
__global__ static void dequant_q8_0_to_f16_kernel(
        __half *out,
        const unsigned char *w,
        uint64_t in_dim,
        uint64_t out_dim,
        uint64_t blocks);
__global__ static void dequant_q8_0_to_f32_kernel(
        float *out,
        const unsigned char *w,
        uint64_t in_dim,
        uint64_t out_dim,
        uint64_t blocks);

static void *cuda_tmp_alloc(uint64_t bytes, const char *what) {
    if (bytes == 0) return NULL;
    int dev = 0;
    cudaError_t dev_err = cudaGetDevice(&dev);
    if (dev_err != cudaSuccess || dev < 0 || dev >= DS4_CUDA_MAX_TMP_DEVICES) {
        fprintf(stderr,
                "ds4: CUDA temp alloc failed for %s: unsupported device %d\n",
                what ? what : "scratch",
                dev);
        (void)cudaGetLastError();
        return NULL;
    }
    if (g_cuda_tmp_bytes[dev] >= bytes) return g_cuda_tmp[dev];
    if (g_cuda_tmp[dev]) {
        (void)cudaFree(g_cuda_tmp[dev]);
        g_cuda_tmp[dev] = NULL;
        g_cuda_tmp_bytes[dev] = 0;
    }
    void *ptr = NULL;
    cudaError_t err = cudaMalloc(&ptr, (size_t)bytes);
    if (err != cudaSuccess) {
        fprintf(stderr, "ds4: CUDA temp alloc failed for %s (%.2f MiB): %s\n",
                what ? what : "scratch", (double)bytes / 1048576.0, cudaGetErrorString(err));
        (void)cudaGetLastError();
        return NULL;
    }
    g_cuda_tmp[dev] = ptr;
    g_cuda_tmp_bytes[dev] = bytes;
    return g_cuda_tmp[dev];
}

static int cuda_attention_score_buffer_fits(uint32_t n_comp) {
    return n_comp <= DS4_CUDA_ATTENTION_SCORE_CAP - DS4_CUDA_ATTENTION_RAW_SCORE_CAP;
}

static const char *cuda_model_ptr(const void *model_map, uint64_t offset) {
    if (model_map == g_model_host_base && g_model_device_base) return g_model_device_base + offset;
    return (const char *)model_map + offset;
}

static const char *cuda_model_range_ptr(const void *model_map, uint64_t offset, uint64_t bytes, const char *what) {
    if (bytes == 0) return cuda_model_ptr(model_map, offset);
    if (g_model_device_owned || g_model_registered) return cuda_model_ptr(model_map, offset);
    if (g_model_hmm_direct &&
        getenv("DS4_CUDA_WEIGHT_CACHE") == NULL &&
        getenv("DS4_CUDA_WEIGHT_PRELOAD") == NULL) {
        return cuda_model_ptr(model_map, offset);
    }
    const char *direct_env = getenv("DS4_CUDA_DIRECT_MODEL");
    if (direct_env && direct_env[0]) return cuda_model_ptr(model_map, offset);

    const uint64_t end = offset + bytes;
    int cur_dev = 0;
    if (cudaGetDevice(&cur_dev) != cudaSuccess) {
        cur_dev = 0;
        (void)cudaGetLastError();
    }
    auto exact = g_model_range_by_offset.find(offset);
    if (exact != g_model_range_by_offset.end()) {
        const cuda_model_range &r = g_model_ranges[exact->second];
        if (r.host_base == model_map && r.device == cur_dev &&
            end >= offset && bytes <= r.bytes) return r.device_ptr;
    }
    for (const cuda_model_range &r : g_model_ranges) {
        if (r.host_base == model_map && r.device == cur_dev &&
            offset >= r.offset && end >= offset && end <= r.offset + r.bytes) {
            return r.device_ptr + (offset - r.offset);
        }
        if (r.host_base == model_map && r.device == cur_dev &&
            r.host_registered && r.registered_base && r.registered_device_base) {
            const uintptr_t h0 = (uintptr_t)((const char *)model_map + offset);
            const uintptr_t h1 = h0 + bytes;
            const uintptr_t r0 = (uintptr_t)r.registered_base;
            const uintptr_t r1 = r0 + r.registered_bytes;
            if (h1 >= h0 && h0 >= r0 && h1 <= r1) return r.registered_device_base + (h0 - r0);
        }
    }

    if (getenv("DS4_CUDA_NO_FD_CACHE") == NULL) {
        const char *fd_ptr = cuda_model_range_ptr_from_fd(model_map, offset, bytes, what);
        if (fd_ptr) return fd_ptr;
    }

    cudaError_t err = cudaSuccess;
    if (g_model_range_mapping_supported) {
        const long page_sz_l = sysconf(_SC_PAGESIZE);
        const uint64_t page_sz = page_sz_l > 0 ? (uint64_t)page_sz_l : 4096u;
        const uintptr_t host_addr = (uintptr_t)((const char *)model_map + offset);
        const uintptr_t reg_addr = host_addr & ~(uintptr_t)(page_sz - 1u);
        const uint64_t reg_delta = (uint64_t)(host_addr - reg_addr);
        const uint64_t reg_bytes = (reg_delta + bytes + page_sz - 1u) & ~(page_sz - 1u);
        void *reg_dev = NULL;
        err = cudaHostRegister((void *)reg_addr,
                               (size_t)reg_bytes,
                               cudaHostRegisterMapped | cudaHostRegisterReadOnly);
        if (err == cudaSuccess) {
            err = cudaHostGetDevicePointer(&reg_dev, (void *)reg_addr, 0);
            if (err == cudaSuccess && reg_dev) {
                char *dev_ptr = (char *)reg_dev + reg_delta;
                g_model_ranges.push_back({model_map, offset, bytes, dev_ptr, (void *)reg_addr, (char *)reg_dev, reg_bytes, 1, 0, cur_dev});
                g_model_range_by_offset[offset] = g_model_ranges.size() - 1u;
                if (getenv("DS4_CUDA_WEIGHT_CACHE_VERBOSE")) {
                    fprintf(stderr, "ds4: CUDA mapped %s %.2f MiB\n",
                            what ? what : "weights",
                            (double)bytes / 1048576.0);
                }
                return dev_ptr;
            }
            fprintf(stderr, "ds4: CUDA model range map pointer failed for %s: %s\n",
                    what ? what : "weights", cudaGetErrorString(err));
            (void)cudaHostUnregister((void *)reg_addr);
            (void)cudaGetLastError();
        } else {
            if (err == cudaErrorNotSupported || err == cudaErrorInvalidValue) g_model_range_mapping_supported = 0;
            (void)cudaGetLastError();
        }
    }

    void *dev = NULL;
    err = cudaMalloc(&dev, (size_t)bytes);
    if (err != cudaSuccess) {
        (void)cudaGetLastError();
        fprintf(stderr, "ds4: CUDA model range alloc failed for %s (%.2f MiB): %s\n",
                what ? what : "weights", (double)bytes / 1048576.0, cudaGetErrorString(err));
        return NULL;
    }

    const char *src = (const char *)model_map + offset;
    const uint64_t chunk = 64ull * 1024ull * 1024ull;
    for (uint64_t done = 0; done < bytes; done += chunk) {
        uint64_t n = bytes - done < chunk ? bytes - done : chunk;
        err = cudaMemcpy((char *)dev + done, src + done, (size_t)n, cudaMemcpyHostToDevice);
        if (err != cudaSuccess) {
            fprintf(stderr, "ds4: CUDA model range copy failed for %s at %.2f/%.2f MiB: %s\n",
                    what ? what : "weights",
                    (double)done / 1048576.0,
                    (double)bytes / 1048576.0,
                    cudaGetErrorString(err));
            (void)cudaFree(dev);
            (void)cudaGetLastError();
            return NULL;
        }
    }
    g_model_ranges.push_back({model_map, offset, bytes, (char *)dev, NULL, NULL, 0, 0, 0, cur_dev});
    g_model_range_by_offset[offset] = g_model_ranges.size() - 1u;
    g_model_range_bytes += bytes;
    if (getenv("DS4_CUDA_WEIGHT_CACHE_VERBOSE")) {
        fprintf(stderr, "ds4: CUDA cached %s %.2f MiB (total %.2f GiB)\n",
                what ? what : "weights",
                (double)bytes / 1048576.0,
                (double)g_model_range_bytes / 1073741824.0);
    }
    return (const char *)dev;
}

static int cuda_model_range_is_cached(const void *model_map, uint64_t offset, uint64_t bytes) {
    if (bytes == 0) return 1;
    if (g_model_device_owned || g_model_registered) return 1;

    const uint64_t end = offset + bytes;
    if (end < offset) return 0;
    int cur_dev = 0;
    if (cudaGetDevice(&cur_dev) != cudaSuccess) {
        cur_dev = 0;
        (void)cudaGetLastError();
    }
    for (const cuda_model_range &r : g_model_ranges) {
        if (r.host_base == model_map &&
            r.device == cur_dev &&
            offset >= r.offset &&
            end <= r.offset + r.bytes) {
            return 1;
        }
        if (r.host_base == model_map &&
            r.device == cur_dev &&
            r.host_registered &&
            r.registered_base &&
            r.registered_device_base) {
            const uintptr_t h0 = (uintptr_t)((const char *)model_map + offset);
            const uintptr_t h1 = h0 + bytes;
            const uintptr_t r0 = (uintptr_t)r.registered_base;
            const uintptr_t r1 = r0 + r.registered_bytes;
            if (h1 >= h0 && h0 >= r0 && h1 <= r1) return 1;
        }
    }
    return 0;
}

static void cuda_q8_f16_cache_release_all(void) {
    for (const cuda_q8_f16_range &r : g_q8_f16_ranges) {
        (void)cudaFree(r.device_ptr);
    }
    g_q8_f16_ranges.clear();
    g_q8_f16_by_offset.clear();
    g_q8_f16_bytes = 0;
}

static void cuda_f8_f16_arena_cache_release_entry(const cuda_f8_f16_arena_range &r) {
    if (!r.device_ptr) return;
    (void)cudaSetDevice(r.gpu);
    (void)cudaFree(r.device_ptr);
}

static void cuda_f8_f16_arena_cache_release_arena(const ds4_gpu_arena *arena) {
    if (!arena) return;
    std::lock_guard<std::mutex> lk(g_f8_f16_arena_mutex);
    for (size_t i = 0; i < g_f8_f16_arena_ranges.size();) {
        const cuda_f8_f16_arena_range &r = g_f8_f16_arena_ranges[i];
        if (r.arena_ptr == arena->ptr && r.gpu == arena->gpu) {
            if (g_f8_f16_arena_bytes >= r.bytes) {
                g_f8_f16_arena_bytes -= r.bytes;
            } else {
                g_f8_f16_arena_bytes = 0;
            }
            cuda_f8_f16_arena_cache_release_entry(r);
            g_f8_f16_arena_ranges.erase(g_f8_f16_arena_ranges.begin() + i);
        } else {
            i++;
        }
    }
}

static void cuda_f8_f16_arena_cache_release_all(void) {
    std::lock_guard<std::mutex> lk(g_f8_f16_arena_mutex);
    for (const cuda_f8_f16_arena_range &r : g_f8_f16_arena_ranges) {
        cuda_f8_f16_arena_cache_release_entry(r);
    }
    g_f8_f16_arena_ranges.clear();
    g_f8_f16_arena_bytes = 0;
}

static uint64_t cuda_parse_mib_env(const char *name, int *present) {
    const char *env = getenv(name);
    if (present) *present = 0;
    if (!env || !env[0]) return 0;
    char *end = NULL;
    unsigned long long v = strtoull(env, &end, 10);
    if (end == env || *end != '\0') return 0;
    if (present) *present = 1;
    if (v > UINT64_MAX / 1048576ull) return UINT64_MAX;
    return (uint64_t)v * 1048576ull;
}

static int cuda_env_flag_enabled(const char *name) {
    const char *env = getenv(name);
    if (!env || !env[0]) return 0;
    return strcmp(env, "0") != 0 &&
           strcmp(env, "false") != 0 &&
           strcmp(env, "False") != 0 &&
           strcmp(env, "FALSE") != 0 &&
           strcmp(env, "off") != 0 &&
           strcmp(env, "Off") != 0 &&
           strcmp(env, "OFF") != 0;
}

struct cuda_f8_shape_trace_entry {
    const char *kind;
    const char *path;
    int gpu;
    uint32_t rows;
    uint32_t cols;
    uint32_t n_tokens;
    uint32_t groups;
    uint32_t rows_per_group;
    uint32_t cols_per_group;
    uint64_t calls;
};

static std::vector<cuda_f8_shape_trace_entry> g_f8_shape_trace_entries;
static int g_f8_shape_trace_registered;
static int g_f8_shape_trace_enabled_cached = -1;

static int cuda_f8_shape_trace_enabled(void) {
    if (g_f8_shape_trace_enabled_cached < 0) {
        g_f8_shape_trace_enabled_cached =
            cuda_env_flag_enabled("DS4_CUDA_F8_TRACE_SHAPES") ? 1 : 0;
    }
    return g_f8_shape_trace_enabled_cached;
}

static void cuda_f8_shape_trace_dump(void) {
    std::lock_guard<std::mutex> lk(g_f8_shape_trace_mutex);
    if (g_f8_shape_trace_entries.empty()) return;
    fprintf(stderr,
            "ds4: f8_shape_trace_summary begin entries=%zu\n",
            g_f8_shape_trace_entries.size());
    for (const cuda_f8_shape_trace_entry &e : g_f8_shape_trace_entries) {
        fprintf(stderr,
                "ds4: f8_shape_trace kind=%s path=%s gpu=%d rows=%u cols=%u n_tokens=%u groups=%u rows_per_group=%u cols_per_group=%u calls=%llu\n",
                e.kind ? e.kind : "?",
                e.path ? e.path : "?",
                e.gpu,
                e.rows,
                e.cols,
                e.n_tokens,
                e.groups,
                e.rows_per_group,
                e.cols_per_group,
                (unsigned long long)e.calls);
    }
    fprintf(stderr, "ds4: f8_shape_trace_summary end\n");
}

static int cuda_f8_shape_trace_log_now(uint64_t calls) {
    return calls != 0 && (calls == 1 || ((calls & (calls - 1)) == 0));
}

static void cuda_f8_shape_trace(const char *kind,
                                const char *path,
                                int gpu,
                                uint32_t rows,
                                uint32_t cols,
                                uint32_t n_tokens,
                                uint32_t groups,
                                uint32_t rows_per_group,
                                uint32_t cols_per_group) {
    if (!cuda_f8_shape_trace_enabled()) return;
    std::lock_guard<std::mutex> lk(g_f8_shape_trace_mutex);
    if (!g_f8_shape_trace_registered) {
        atexit(cuda_f8_shape_trace_dump);
        g_f8_shape_trace_registered = 1;
    }
    cuda_f8_shape_trace_entry *entry = NULL;
    for (cuda_f8_shape_trace_entry &e : g_f8_shape_trace_entries) {
        if (e.gpu == gpu &&
            e.rows == rows &&
            e.cols == cols &&
            e.n_tokens == n_tokens &&
            e.groups == groups &&
            e.rows_per_group == rows_per_group &&
            e.cols_per_group == cols_per_group &&
            strcmp(e.kind ? e.kind : "", kind ? kind : "") == 0 &&
            strcmp(e.path ? e.path : "", path ? path : "") == 0) {
            entry = &e;
            break;
        }
    }
    if (!entry) {
        cuda_f8_shape_trace_entry e = {};
        e.kind = kind;
        e.path = path;
        e.gpu = gpu;
        e.rows = rows;
        e.cols = cols;
        e.n_tokens = n_tokens;
        e.groups = groups;
        e.rows_per_group = rows_per_group;
        e.cols_per_group = cols_per_group;
        g_f8_shape_trace_entries.push_back(e);
        entry = &g_f8_shape_trace_entries.back();
    }
    entry->calls++;
    if (cuda_f8_shape_trace_log_now(entry->calls)) {
        fprintf(stderr,
                "ds4: f8_shape_trace kind=%s path=%s gpu=%d rows=%u cols=%u n_tokens=%u groups=%u rows_per_group=%u cols_per_group=%u calls=%llu\n",
                entry->kind ? entry->kind : "?",
                entry->path ? entry->path : "?",
                entry->gpu,
                entry->rows,
                entry->cols,
                entry->n_tokens,
                entry->groups,
                entry->rows_per_group,
                entry->cols_per_group,
                (unsigned long long)entry->calls);
    }
}

struct cuda_tm_profile_stats {
    uint64_t calls;
    uint64_t fused_calls;
    uint64_t tokens;
    uint64_t routes;
    uint64_t active_expert_sum;
    uint32_t max_routes_per_call;
    uint32_t max_routes_per_expert;
    double route_ms;
    double gather_ms;
    double gate_up_ms;
    double swiglu_ms;
    double down_ms;
    double scatter_ms;
    double total_ms;
};

static cuda_tm_profile_stats g_tm_profile_stats[DS4_CUDA_MAX_TMP_DEVICES];
static int g_tm_profile_enabled_cached = -1;
static int g_tm_profile_registered;
static int g_tm_profile_dumped;

static void cuda_tm_profile_dump(void) {
    std::lock_guard<std::mutex> lk(g_tm_profile_mutex);
    if (g_tm_profile_dumped) return;
    int any = 0;
    for (int gpu = 0; gpu < DS4_CUDA_MAX_TMP_DEVICES; gpu++) {
        if (g_tm_profile_stats[gpu].calls) {
            any = 1;
            break;
        }
    }
    if (!any) return;
    g_tm_profile_dumped = 1;
    fprintf(stderr, "ds4: turbomind_profile_summary begin\n");
    for (int gpu = 0; gpu < DS4_CUDA_MAX_TMP_DEVICES; gpu++) {
        const cuda_tm_profile_stats &s = g_tm_profile_stats[gpu];
        if (!s.calls) continue;
        const double calls = (double)s.calls;
        const double total = s.total_ms > 0.0 ? s.total_ms : 1.0;
        fprintf(stderr,
                "ds4: turbomind_profile gpu=%d calls=%llu fused_calls=%llu tokens=%llu routes=%llu avg_tokens=%.3f avg_routes=%.3f avg_active_experts=%.3f max_routes_call=%u max_routes_expert=%u total_ms=%.3f route_ms=%.3f gather_ms=%.3f gate_up_ms=%.3f swiglu_ms=%.3f down_ms=%.3f scatter_ms=%.3f gate_up_pct=%.2f down_pct=%.2f\n",
                gpu,
                (unsigned long long)s.calls,
                (unsigned long long)s.fused_calls,
                (unsigned long long)s.tokens,
                (unsigned long long)s.routes,
                (double)s.tokens / calls,
                (double)s.routes / calls,
                (double)s.active_expert_sum / calls,
                s.max_routes_per_call,
                s.max_routes_per_expert,
                s.total_ms,
                s.route_ms,
                s.gather_ms,
                s.gate_up_ms,
                s.swiglu_ms,
                s.down_ms,
                s.scatter_ms,
                100.0 * s.gate_up_ms / total,
                100.0 * s.down_ms / total);
    }
    fprintf(stderr, "ds4: turbomind_profile_summary end\n");
}

static int cuda_tm_profile_enabled(void) {
    if (g_tm_profile_enabled_cached < 0) {
        g_tm_profile_enabled_cached =
            cuda_env_flag_enabled("DS4_V100_TURBOMIND_PROFILE") ? 1 : 0;
        if (g_tm_profile_enabled_cached && !g_tm_profile_registered) {
            atexit(cuda_tm_profile_dump);
            g_tm_profile_registered = 1;
        }
    }
    return g_tm_profile_enabled_cached;
}

static void cuda_tm_profile_record(int gpu,
                                   uint32_t n_tokens,
                                   uint32_t total_routes,
                                   uint32_t active_experts,
                                   uint32_t max_routes_per_expert,
                                   int fused_gate_up,
                                   float route_ms,
                                   float gather_ms,
                                   float gate_up_ms,
                                   float swiglu_ms,
                                   float down_ms,
                                   float scatter_ms,
                                   float total_ms) {
    if (gpu < 0 || gpu >= DS4_CUDA_MAX_TMP_DEVICES) return;
    std::lock_guard<std::mutex> lk(g_tm_profile_mutex);
    cuda_tm_profile_stats &s = g_tm_profile_stats[gpu];
    s.calls++;
    if (fused_gate_up) s.fused_calls++;
    s.tokens += n_tokens;
    s.routes += total_routes;
    s.active_expert_sum += active_experts;
    if (total_routes > s.max_routes_per_call) s.max_routes_per_call = total_routes;
    if (max_routes_per_expert > s.max_routes_per_expert) {
        s.max_routes_per_expert = max_routes_per_expert;
    }
    s.route_ms += route_ms;
    s.gather_ms += gather_ms;
    s.gate_up_ms += gate_up_ms;
    s.swiglu_ms += swiglu_ms;
    s.down_ms += down_ms;
    s.scatter_ms += scatter_ms;
    s.total_ms += total_ms;
}

struct cuda_tm_profile_call {
    int enabled = 0;
    int gpu = -1;
    cudaEvent_t start = NULL;
    cudaEvent_t last = NULL;
    cudaEvent_t now = NULL;
    float route_ms = 0.0f;
    float gather_ms = 0.0f;
    float gate_up_ms = 0.0f;
    float swiglu_ms = 0.0f;
    float down_ms = 0.0f;
    float scatter_ms = 0.0f;

    void begin(int gpu_in) {
        if (!cuda_tm_profile_enabled()) return;
        gpu = gpu_in;
        if (cudaEventCreate(&start) != cudaSuccess ||
            cudaEventCreate(&last) != cudaSuccess ||
            cudaEventCreate(&now) != cudaSuccess) {
            cleanup();
            return;
        }
        if (cudaEventRecord(start, 0) != cudaSuccess ||
            cudaEventSynchronize(start) != cudaSuccess ||
            cudaEventRecord(last, 0) != cudaSuccess ||
            cudaEventSynchronize(last) != cudaSuccess) {
            cleanup();
            return;
        }
        enabled = 1;
    }

    void mark(float *dst) {
        if (!enabled || !dst) return;
        if (cudaEventRecord(now, 0) != cudaSuccess ||
            cudaEventSynchronize(now) != cudaSuccess ||
            cudaEventElapsedTime(dst, last, now) != cudaSuccess) {
            enabled = 0;
            return;
        }
        cudaEvent_t tmp = last;
        last = now;
        now = tmp;
    }

    void finish(uint32_t n_tokens,
                uint32_t total_routes,
                uint32_t active_experts,
                uint32_t max_routes_per_expert,
                int fused_gate_up) {
        if (!enabled) return;
        float total_ms = 0.0f;
        if (cudaEventElapsedTime(&total_ms, start, last) != cudaSuccess) return;
        cuda_tm_profile_record(gpu,
                               n_tokens,
                               total_routes,
                               active_experts,
                               max_routes_per_expert,
                               fused_gate_up,
                               route_ms,
                               gather_ms,
                               gate_up_ms,
                               swiglu_ms,
                               down_ms,
                               scatter_ms,
                               total_ms);
    }

    void cleanup() {
        if (start) (void)cudaEventDestroy(start);
        if (last) (void)cudaEventDestroy(last);
        if (now) (void)cudaEventDestroy(now);
        start = NULL;
        last = NULL;
        now = NULL;
        enabled = 0;
    }

    ~cuda_tm_profile_call() {
        cleanup();
    }
};

static int cuda_tensor_pool_enabled(void) {
    return cuda_env_flag_enabled("DS4_CUDA_TENSOR_POOL");
}

static uint64_t cuda_tensor_pool_max_bytes(void) {
    int present = 0;
    const uint64_t bytes = cuda_parse_mib_env("DS4_CUDA_TENSOR_POOL_MAX_MIB", &present);
    return present ? bytes : 2048ull * 1048576ull;
}

static uint64_t cuda_tensor_pool_align_bytes(uint64_t bytes) {
    const uint64_t align = 256ull;
    if (bytes == 0) bytes = 1;
    if (bytes > UINT64_MAX - (align - 1ull)) return bytes;
    return (bytes + align - 1ull) & ~(align - 1ull);
}

static void *cuda_tensor_pool_take(uint64_t bytes, uint64_t *alloc_bytes, int *device_out) {
    if (!cuda_tensor_pool_enabled()) return NULL;
    int dev = 0;
    if (cudaGetDevice(&dev) != cudaSuccess) {
        dev = 0;
        (void)cudaGetLastError();
    }
    const uint64_t need = cuda_tensor_pool_align_bytes(bytes);
    std::lock_guard<std::mutex> lk(g_tensor_pool_mutex);
    size_t best = SIZE_MAX;
    uint64_t best_bytes = UINT64_MAX;
    for (size_t i = 0; i < g_tensor_pool.size(); i++) {
        const cuda_tensor_pool_entry &e = g_tensor_pool[i];
        if (e.device == dev && e.bytes >= need && e.bytes < best_bytes) {
            best = i;
            best_bytes = e.bytes;
        }
    }
    if (best == SIZE_MAX) return NULL;
    cuda_tensor_pool_entry e = g_tensor_pool[best];
    g_tensor_pool.erase(g_tensor_pool.begin() + best);
    if (g_tensor_pool_bytes >= e.bytes) {
        g_tensor_pool_bytes -= e.bytes;
    } else {
        g_tensor_pool_bytes = 0;
    }
    if (alloc_bytes) *alloc_bytes = e.bytes;
    if (device_out) *device_out = e.device;
    return e.ptr;
}

static int cuda_tensor_pool_put(void *ptr, uint64_t bytes, int device) {
    if (!ptr || bytes == 0 || !cuda_tensor_pool_enabled()) return 0;
    const uint64_t max_bytes = cuda_tensor_pool_max_bytes();
    if (bytes > max_bytes) return 0;
    std::lock_guard<std::mutex> lk(g_tensor_pool_mutex);
    if (g_tensor_pool_bytes > max_bytes || bytes > max_bytes - g_tensor_pool_bytes) {
        return 0;
    }
    cuda_tensor_pool_entry e = {};
    e.ptr = ptr;
    e.bytes = bytes;
    e.device = device;
    g_tensor_pool.push_back(e);
    g_tensor_pool_bytes += bytes;
    return 1;
}

static void cuda_tensor_pool_release_all(void) {
    std::lock_guard<std::mutex> lk(g_tensor_pool_mutex);
    for (const cuda_tensor_pool_entry &e : g_tensor_pool) {
        if (!e.ptr) continue;
        (void)cudaSetDevice(e.device);
        (void)cudaFree(e.ptr);
    }
    g_tensor_pool.clear();
    g_tensor_pool_bytes = 0;
}

static void cuda_tm_warn_once(const char *msg) {
    std::lock_guard<std::mutex> lk(g_tm_api_mutex);
    if (g_tm_api.warned) return;
    g_tm_api.warned = 1;
    fprintf(stderr, "ds4: TurboMind routed FFN disabled for this call: %s\n", msg);
}

static int cuda_tm_load_api(void) {
    std::lock_guard<std::mutex> lk(g_tm_api_mutex);
    if (g_tm_api.attempted) return g_tm_api.available;
    g_tm_api.attempted = 1;

    const char *path = getenv("DS4_V100_TURBOMIND_LIB");
    if (!path || !path[0]) path = getenv("DS4_TURBOMIND_LIB");
    if (!path || !path[0]) path = "./build/turbomind-v100/libggml-turbomind.so";

    g_tm_api.handle = dlopen(path, RTLD_NOW | RTLD_LOCAL);
    if (!g_tm_api.handle) {
        fprintf(stderr,
                "ds4: failed to open TurboMind library %s: %s\n",
                path,
                dlerror());
        return 0;
    }
    g_tm_api.api_version =
        (tm_pfn_api_version)dlsym(g_tm_api.handle, "ggml_turbomind_api_version");
    g_tm_api.init =
        (tm_pfn_init)dlsym(g_tm_api.handle, "ggml_turbomind_init");
    g_tm_api.shutdown =
        (tm_pfn_shutdown)dlsym(g_tm_api.handle, "ggml_turbomind_shutdown");
    g_tm_api.packed_bytes =
        (tm_pfn_packed_bytes)dlsym(g_tm_api.handle, "ggml_turbomind_packed_bytes");
    g_tm_api.pack_weight =
        (tm_pfn_pack_weight)dlsym(g_tm_api.handle, "ggml_turbomind_pack_weight_expert");
    g_tm_api.mul_mat_grouped =
        (tm_pfn_mul_mat_grouped)dlsym(g_tm_api.handle, "ggml_turbomind_mul_mat_grouped");
    g_tm_api.mul_mat_grouped_total_tokens =
        (tm_pfn_mul_mat_grouped_total_tokens)dlsym(
            g_tm_api.handle, "ggml_turbomind_mul_mat_grouped_total_tokens");
    if (!g_tm_api.api_version || !g_tm_api.init || !g_tm_api.shutdown ||
        !g_tm_api.packed_bytes || !g_tm_api.pack_weight || !g_tm_api.mul_mat_grouped) {
        fprintf(stderr, "ds4: TurboMind library is missing required C ABI symbols\n");
        (void)dlclose(g_tm_api.handle);
        memset(&g_tm_api, 0, sizeof(g_tm_api));
        g_tm_api.attempted = 1;
        return 0;
    }
    if (g_tm_api.api_version() != GGML_TURBOMIND_API_VERSION) {
        fprintf(stderr,
                "ds4: TurboMind ABI mismatch: got %d expected %d\n",
                g_tm_api.api_version(),
                GGML_TURBOMIND_API_VERSION);
        (void)dlclose(g_tm_api.handle);
        memset(&g_tm_api, 0, sizeof(g_tm_api));
        g_tm_api.attempted = 1;
        return 0;
    }
    g_tm_api.available = 1;
    return 1;
}

static uint64_t cuda_q8_f16_cache_limit_bytes(void) {
    int present = 0;
    const uint64_t limit = cuda_parse_mib_env("DS4_CUDA_Q8_F16_CACHE_MB", &present);
    return present ? limit : UINT64_MAX;
}

static uint64_t cuda_q8_f16_cache_reserve_bytes(uint64_t total_bytes) {
    int present = 0;
    const uint64_t reserve = cuda_parse_mib_env("DS4_CUDA_Q8_F16_CACHE_RESERVE_MB", &present);
    if (present) return reserve;

    if (total_bytes >= 112ull * 1024ull * 1024ull * 1024ull) {
        return 512ull * 1048576ull;
    }

    /* The expanded Q8->F16 cache is only an acceleration path.  Keep enough
     * device memory free for cuBLAS workspaces, transient graph buffers, and
     * driver bookkeeping instead of letting optional cached weights consume the
     * last few GiB on 96 GiB cards. */
    const uint64_t min_reserve = 4096ull * 1048576ull;
    const uint64_t pct_reserve = total_bytes / 20u; /* 5% */
    return pct_reserve > min_reserve ? pct_reserve : min_reserve;
}

static void cuda_q8_f16_cache_budget_notice(
        const char *reason,
        uint64_t request_bytes,
        uint64_t free_bytes,
        uint64_t total_bytes,
        uint64_t reserve_bytes,
        uint64_t limit_bytes) {
    if (g_q8_f16_budget_notice_printed && getenv("DS4_CUDA_WEIGHT_CACHE_VERBOSE") == NULL) return;
    g_q8_f16_budget_notice_printed = 1;
    if (limit_bytes != UINT64_MAX && free_bytes == 0 && total_bytes == 0 && reserve_bytes == 0) {
        fprintf(stderr,
                "ds4: CUDA q8 fp16 cache %s; using q8 kernels "
                "(request=%.2f MiB cached=%.2f GiB limit=%.2f GiB)\n",
                reason,
                (double)request_bytes / 1048576.0,
                (double)g_q8_f16_bytes / 1073741824.0,
                (double)limit_bytes / 1073741824.0);
    } else if (limit_bytes == UINT64_MAX) {
        fprintf(stderr,
                "ds4: CUDA q8 fp16 cache %s; using q8 kernels "
                "(request=%.2f MiB cached=%.2f GiB free=%.2f GiB reserve=%.2f GiB total=%.2f GiB)\n",
                reason,
                (double)request_bytes / 1048576.0,
                (double)g_q8_f16_bytes / 1073741824.0,
                (double)free_bytes / 1073741824.0,
                (double)reserve_bytes / 1073741824.0,
                (double)total_bytes / 1073741824.0);
    } else {
        fprintf(stderr,
                "ds4: CUDA q8 fp16 cache %s; using q8 kernels "
                "(request=%.2f MiB cached=%.2f GiB limit=%.2f GiB free=%.2f GiB reserve=%.2f GiB total=%.2f GiB)\n",
                reason,
                (double)request_bytes / 1048576.0,
                (double)g_q8_f16_bytes / 1073741824.0,
                (double)limit_bytes / 1073741824.0,
                (double)free_bytes / 1073741824.0,
                (double)reserve_bytes / 1073741824.0,
                (double)total_bytes / 1073741824.0);
    }
}

static int cuda_q8_f16_cache_has_budget(uint64_t request_bytes, const char *label) {
    (void)label;
    const uint64_t limit = cuda_q8_f16_cache_limit_bytes();
    if (limit == 0) return 0;
    if (g_q8_f16_bytes > limit || request_bytes > limit - g_q8_f16_bytes) {
        cuda_q8_f16_cache_budget_notice("limit reached", request_bytes, 0, 0, 0, limit);
        return 0;
    }

    size_t free_b = 0;
    size_t total_b = 0;
    cudaError_t err = cudaMemGetInfo(&free_b, &total_b);
    if (err != cudaSuccess) {
        fprintf(stderr, "ds4: CUDA q8 fp16 cache memory query failed: %s; using q8 kernels\n",
                cudaGetErrorString(err));
        (void)cudaGetLastError();
        return 0;
    }

    const uint64_t free_bytes = (uint64_t)free_b;
    const uint64_t total_bytes = (uint64_t)total_b;
    const uint64_t reserve_bytes = cuda_q8_f16_cache_reserve_bytes(total_bytes);
    if (request_bytes > free_bytes ||
        free_bytes - request_bytes < reserve_bytes) {
        cuda_q8_f16_cache_budget_notice("budget exhausted", request_bytes,
                                        free_bytes, total_bytes,
                                        reserve_bytes, limit);
        return 0;
    }
    return 1;
}

static void cuda_q8_f16_cache_disable_after_failure(const char *what, uint64_t request_bytes) {
    if (!g_q8_f16_disabled_after_oom) {
        fprintf(stderr,
                "ds4: CUDA q8 fp16 cache disabled after %s "
                "(request=%.2f MiB cached=%.2f GiB); using q8 kernels\n",
                what ? what : "allocation failure",
                (double)request_bytes / 1048576.0,
                (double)g_q8_f16_bytes / 1073741824.0);
    }
    g_q8_f16_disabled_after_oom = 1;
    if (!g_q8_f16_ranges.empty()) {
        (void)cudaDeviceSynchronize();
        cuda_q8_f16_cache_release_all();
    }
    (void)cudaGetLastError();
}

static int cuda_q8_f16_cache_allowed(const char *label, uint64_t in_dim, uint64_t out_dim) {
    if (g_quality_mode) return 0;
    if (g_q8_f16_disabled_after_oom) return 0;
    if (getenv("DS4_CUDA_NO_Q8_F16_CACHE") != NULL) return 0;
    if (cuda_q8_f16_cache_limit_bytes() == 0) return 0;
    if (getenv("DS4_CUDA_Q8_F16_ALL") != NULL) return 1;
    if (!label) return 0;
    if (strstr(label, "attn_output_a") != NULL ||
        strstr(label, "attn_output_b") != NULL ||
        strstr(label, "attention_output_a") != NULL ||
        strstr(label, "attention_output_b") != NULL) {
        return getenv("DS4_CUDA_NO_ATTENTION_OUTPUT_F16_CACHE") == NULL;
    }
    if (strstr(label, "attn_q_b") != NULL) {
        return getenv("DS4_CUDA_NO_ATTN_Q_B_F16_CACHE") == NULL;
    }
    if (strstr(label, "ffn_gate_shexp") != NULL ||
        strstr(label, "ffn_up_shexp") != NULL ||
        strstr(label, "ffn_down_shexp") != NULL) {
        return 1;
    }
    return (in_dim == 4096u && out_dim == 2048u) ||
           (in_dim == 2048u && out_dim == 4096u) ||
           (in_dim == 4096u && out_dim == 1024u) ||
           (in_dim == 4096u && out_dim == 512u) ||
           (getenv("DS4_CUDA_NO_ATTN_Q_B_F16_CACHE") == NULL &&
            in_dim == 1024u && out_dim == 32768u);
}

static int cuda_q8_label_is_attention_output(const char *label) {
    return label &&
           (strstr(label, "attn_output_a") != NULL ||
            strstr(label, "attn_output_b") != NULL ||
            strstr(label, "attention_output_a") != NULL ||
            strstr(label, "attention_output_b") != NULL);
}

static int cuda_q8_use_dp4a(void) {
    return getenv("DS4_CUDA_NO_Q8_DP4A") == NULL;
}

static int cuda_q8_f16_preload_allowed(const char *label, uint64_t in_dim, uint64_t out_dim) {
    if (cuda_q8_label_is_attention_output(label) &&
        getenv("DS4_CUDA_ATTENTION_OUTPUT_PRELOAD") == NULL &&
        getenv("DS4_CUDA_Q8_F16_ALL") == NULL) {
        return 0;
    }
    return cuda_q8_f16_cache_allowed(label, in_dim, out_dim);
}

static int cuda_q8_f32_cache_allowed(const char *label, uint64_t in_dim, uint64_t out_dim) {
    if (getenv("DS4_CUDA_NO_Q8_F32_CACHE") != NULL) return 0;
    if (getenv("DS4_CUDA_Q8_F32_ALL") != NULL) return 1;
    if (label && strstr(label, "attn_q_b") != NULL) {
        return getenv("DS4_CUDA_ATTN_Q_B_F32_CACHE") != NULL;
    }
    return getenv("DS4_CUDA_Q8_F32_LARGE") != NULL &&
           in_dim == 1024u && out_dim == 32768u;
}

static const __half *cuda_q8_f16_ptr(
        const void *model_map,
        uint64_t offset,
        uint64_t weight_bytes,
        uint64_t in_dim,
        uint64_t out_dim,
        const char *label) {
    auto exact = g_q8_f16_by_offset.find(offset);
    if (exact != g_q8_f16_by_offset.end()) {
        const cuda_q8_f16_range &r = g_q8_f16_ranges[exact->second];
        if (r.host_base == model_map && r.weight_bytes == weight_bytes &&
            r.in_dim == in_dim && r.out_dim == out_dim) {
            return r.device_ptr;
        }
    }
    if (!cuda_q8_f16_cache_allowed(label, in_dim, out_dim)) return NULL;

    const char *q8 = cuda_model_range_ptr(model_map, offset, weight_bytes, "q8_0");
    if (!q8) return NULL;

    if (in_dim != 0 && out_dim > UINT64_MAX / in_dim / sizeof(__half)) return NULL;
    const uint64_t out_bytes = in_dim * out_dim * sizeof(__half);
    if (!cuda_q8_f16_cache_has_budget(out_bytes, label)) return NULL;

    __half *dev = NULL;
    cudaError_t err = cudaMalloc(&dev, (size_t)out_bytes);
    if (err != cudaSuccess) {
        fprintf(stderr, "ds4: CUDA q8 fp16 cache alloc failed (%.2f MiB): %s\n",
                (double)out_bytes / 1048576.0, cudaGetErrorString(err));
        cuda_q8_f16_cache_disable_after_failure("allocation failure", out_bytes);
        return NULL;
    }
    const uint64_t blocks = (in_dim + 31) / 32;
    const uint64_t n = in_dim * out_dim;
    dequant_q8_0_to_f16_kernel<<<(n + 255) / 256, 256>>>(dev,
                                                          (const unsigned char *)q8,
                                                          in_dim,
                                                          out_dim,
                                                          blocks);
    if (!cuda_ok(cudaGetLastError(), "q8 fp16 dequant launch")) {
        (void)cudaFree(dev);
        cuda_q8_f16_cache_disable_after_failure("dequant launch failure", out_bytes);
        return NULL;
    }
    g_q8_f16_ranges.push_back({model_map, offset, weight_bytes, in_dim, out_dim, dev});
    g_q8_f16_by_offset[offset] = g_q8_f16_ranges.size() - 1u;
    g_q8_f16_bytes += out_bytes;
    if (getenv("DS4_CUDA_WEIGHT_CACHE_VERBOSE")) {
        fprintf(stderr, "ds4: CUDA cached q8 fp16 %.2f MiB (total %.2f GiB)\n",
                (double)out_bytes / 1048576.0,
                (double)g_q8_f16_bytes / 1073741824.0);
    }
    return dev;
}

static float *cuda_q8_f32_ptr(
        const void *model_map,
        uint64_t offset,
        uint64_t weight_bytes,
        uint64_t in_dim,
        uint64_t out_dim,
        const char *label) {
    auto exact = g_q8_f32_by_offset.find(offset);
    if (exact != g_q8_f32_by_offset.end()) {
        const cuda_q8_f32_range &r = g_q8_f32_ranges[exact->second];
        if (r.host_base == model_map && r.weight_bytes == weight_bytes &&
            r.in_dim == in_dim && r.out_dim == out_dim) {
            return r.device_ptr;
        }
    }
    if (!cuda_q8_f32_cache_allowed(label, in_dim, out_dim)) return NULL;

    const char *q8 = cuda_model_range_ptr(model_map, offset, weight_bytes, label ? label : "q8_0");
    if (!q8) return NULL;

    const uint64_t out_bytes = in_dim * out_dim * sizeof(float);
    float *dev = NULL;
    cudaError_t err = cudaMalloc(&dev, (size_t)out_bytes);
    if (err != cudaSuccess) {
        fprintf(stderr, "ds4: CUDA q8 fp32 cache alloc failed (%.2f MiB): %s\n",
                (double)out_bytes / 1048576.0, cudaGetErrorString(err));
        (void)cudaGetLastError();
        return NULL;
    }
    const uint64_t blocks = (in_dim + 31) / 32;
    const uint64_t n = in_dim * out_dim;
    dequant_q8_0_to_f32_kernel<<<(n + 255) / 256, 256>>>(dev,
                                                          (const unsigned char *)q8,
                                                          in_dim,
                                                          out_dim,
                                                          blocks);
    if (!cuda_ok(cudaGetLastError(), "q8 fp32 dequant launch")) {
        (void)cudaFree(dev);
        return NULL;
    }
    g_q8_f32_ranges.push_back({model_map, offset, weight_bytes, in_dim, out_dim, dev});
    g_q8_f32_by_offset[offset] = g_q8_f32_ranges.size() - 1u;
    g_q8_f32_bytes += out_bytes;
    if (getenv("DS4_CUDA_WEIGHT_CACHE_VERBOSE")) {
        fprintf(stderr, "ds4: CUDA cached q8 fp32 %.2f MiB (total %.2f GiB)\n",
                (double)out_bytes / 1048576.0,
                (double)g_q8_f32_bytes / 1073741824.0);
    }
    return dev;
}

static int cuda_ok(cudaError_t err, const char *what) {
    if (err == cudaSuccess) return 1;
    fprintf(stderr, "ds4: CUDA %s failed: %s\n", what, cudaGetErrorString(err));
    return 0;
}

static double cuda_wall_sec(void) {
    struct timespec ts;
    clock_gettime(CLOCK_MONOTONIC, &ts);
    return (double)ts.tv_sec + (double)ts.tv_nsec * 1.0e-9;
}

static int cuda_model_load_progress_enabled(void) {
    if (getenv("DS4_CUDA_WEIGHT_CACHE_VERBOSE") != NULL) return 0;
    return 1;
}

static void cuda_model_load_progress_reset(void) {
    g_model_load_progress_next = 0;
    g_model_load_progress_last = 0.0;
    g_model_load_progress_started = 0;
    g_model_load_progress_tty = 0;
}

static void cuda_model_load_progress_note(uint64_t cached_bytes) {
    if (!cuda_model_load_progress_enabled()) return;

    const double now = cuda_wall_sec();
    if (!g_model_load_progress_started) {
        g_model_load_progress_started = 1;
        g_model_load_progress_tty = isatty(STDERR_FILENO) != 0;
        g_model_load_progress_next = (g_model_load_progress_tty ? 2ull : 16ull) *
                                     1024ull * 1024ull * 1024ull;
        g_model_load_progress_last = now;
        if (g_model_load_progress_tty) {
            fprintf(stderr, "ds4: CUDA loading model tensors into device cache: 0.00 GiB");
        } else {
            fprintf(stderr, "ds4: CUDA loading model tensors into device cache\n");
        }
    }

    if (cached_bytes < g_model_load_progress_next &&
        now - g_model_load_progress_last < (g_model_load_progress_tty ? 2.0 : 10.0)) {
        return;
    }

    if (g_model_load_progress_tty) {
        fprintf(stderr, "\rds4: CUDA loading model tensors into device cache: %.2f GiB",
                (double)cached_bytes / 1073741824.0);
    } else {
        fprintf(stderr, "ds4: CUDA loading model tensors %.2f GiB cached\n",
                (double)cached_bytes / 1073741824.0);
    }
    fflush(stderr);
    g_model_load_progress_last = now;
    const uint64_t step = (g_model_load_progress_tty ? 2ull : 16ull) *
                          1024ull * 1024ull * 1024ull;
    while (g_model_load_progress_next <= cached_bytes) {
        g_model_load_progress_next += step;
    }
}

static int cuda_model_prefetch_range(const void *model_map, uint64_t model_size, uint64_t map_offset, uint64_t map_size) {
    if (!model_map || map_size == 0 || map_offset > model_size || map_size > model_size - map_offset) return 0;
    if (getenv("DS4_CUDA_NO_MODEL_PREFETCH") != NULL ||
        getenv("DS4_CUDA_COPY_MODEL") != NULL ||
        getenv("DS4_CUDA_WEIGHT_CACHE") != NULL ||
        getenv("DS4_CUDA_WEIGHT_PRELOAD") != NULL) {
        return 0;
    }

    int device = 0;
    if (cudaGetDevice(&device) != cudaSuccess) {
        (void)cudaGetLastError();
        return 0;
    }

    int pageable = 0;
    cudaError_t err = cudaDeviceGetAttribute(&pageable, cudaDevAttrPageableMemoryAccess, device);
    if (err != cudaSuccess || !pageable) {
        (void)cudaGetLastError();
        return 0;
    }
    cudaMemLocation loc;
    memset(&loc, 0, sizeof(loc));
    loc.type = cudaMemLocationTypeDevice;
    loc.id = device;

    const long page_sz_l = sysconf(_SC_PAGESIZE);
    const uint64_t page_sz = page_sz_l > 0 ? (uint64_t)page_sz_l : 4096u;
    const uintptr_t host_addr = (uintptr_t)((const char *)model_map + map_offset);
    const uintptr_t pre_addr = host_addr & ~(uintptr_t)(page_sz - 1u);
    const uint64_t pre_delta = (uint64_t)(host_addr - pre_addr);
    const uint64_t pre_bytes = (pre_delta + map_size + page_sz - 1u) & ~(page_sz - 1u);
    void *pre_ptr = (void *)pre_addr;

    const double t0 = cuda_wall_sec();
    err = cudaMemAdvise(pre_ptr, (size_t)pre_bytes, cudaMemAdviseSetReadMostly, loc);
    if (err != cudaSuccess) {
        fprintf(stderr, "ds4: CUDA model read-mostly advise skipped: %s\n", cudaGetErrorString(err));
        (void)cudaGetLastError();
        return 0;
    }
    err = cudaMemAdvise(pre_ptr, (size_t)pre_bytes, cudaMemAdviseSetPreferredLocation, loc);
    if (err != cudaSuccess) {
        fprintf(stderr, "ds4: CUDA model preferred-location advise skipped: %s\n", cudaGetErrorString(err));
        (void)cudaGetLastError();
        return 0;
    }

    if (!g_model_prefetch_stream) {
        err = cudaStreamCreateWithFlags(&g_model_prefetch_stream, cudaStreamNonBlocking);
        if (err != cudaSuccess) {
            fprintf(stderr, "ds4: CUDA model prefetch stream creation skipped: %s\n", cudaGetErrorString(err));
            (void)cudaGetLastError();
            return 0;
        }
    }

    err = cudaMemPrefetchAsync(pre_ptr, (size_t)pre_bytes, loc, 0, g_model_prefetch_stream);
    if (err != cudaSuccess) {
        fprintf(stderr, "ds4: CUDA model prefetch skipped: %s\n", cudaGetErrorString(err));
        (void)cudaGetLastError();
        return 0;
    }
    if (getenv("DS4_CUDA_MODEL_PREFETCH_SYNC") != NULL) {
        err = cudaStreamSynchronize(g_model_prefetch_stream);
        if (err != cudaSuccess) {
            fprintf(stderr, "ds4: CUDA model prefetch sync failed: %s\n", cudaGetErrorString(err));
            (void)cudaGetLastError();
            return 0;
        }
    }
    const double t1 = cuda_wall_sec();
    fprintf(stderr,
            "ds4: CUDA ATS/HMM prefetch queued %.2f GiB of model tensors in %.3fs\n",
            (double)map_size / 1073741824.0,
            t1 - t0);
    g_model_hmm_direct = 1;
    return 1;
}

static uint64_t cuda_model_copy_chunk_bytes(void) {
    uint64_t mb = 64;
    const char *env = getenv("DS4_CUDA_MODEL_COPY_CHUNK_MB");
    if (env && env[0]) {
        char *end = NULL;
        unsigned long long v = strtoull(env, &end, 10);
        if (end != env && v > 0) mb = (uint64_t)v;
    }
    if (mb < 16) mb = 16;
    if (mb > 4096) mb = 4096;
    return mb * 1048576ull;
}

static void cuda_model_discard_source_pages(const void *model_map, uint64_t model_size, uint64_t offset, uint64_t bytes) {
#if defined(POSIX_MADV_DONTNEED)
    if (getenv("DS4_CUDA_KEEP_MODEL_PAGES") != NULL || !model_map || bytes == 0 || offset > model_size) return;
    if (bytes > model_size - offset) bytes = model_size - offset;
    const long page_sz_l = sysconf(_SC_PAGESIZE);
    const uint64_t page_sz = page_sz_l > 0 ? (uint64_t)page_sz_l : 4096u;
    const uintptr_t h0 = (uintptr_t)((const char *)model_map + offset);
    const uintptr_t h1 = h0 + bytes;
    const uintptr_t p0 = h0 & ~(uintptr_t)(page_sz - 1u);
    const uintptr_t p1 = (h1 + page_sz - 1u) & ~(uintptr_t)(page_sz - 1u);
    if (p1 > p0) (void)posix_madvise((void *)p0, (size_t)(p1 - p0), POSIX_MADV_DONTNEED);
#else
    (void)model_map;
    (void)model_size;
    (void)offset;
    (void)bytes;
#endif
}

static void cuda_model_drop_file_pages(uint64_t offset, uint64_t bytes) {
#if defined(POSIX_FADV_DONTNEED)
    if (g_model_fd < 0 || getenv("DS4_CUDA_KEEP_MODEL_PAGES") != NULL || bytes == 0) return;
    (void)posix_fadvise(g_model_fd, (off_t)offset, (off_t)bytes, POSIX_FADV_DONTNEED);
#else
    (void)offset;
    (void)bytes;
#endif
}

static uint64_t cuda_round_down(uint64_t v, uint64_t align) {
    if (align <= 1) return v;
    return (v / align) * align;
}

static uint64_t cuda_round_up(uint64_t v, uint64_t align) {
    if (align <= 1) return v;
    const uint64_t rem = v % align;
    return rem == 0 ? v : v + (align - rem);
}

static void *cuda_align_ptr(void *ptr, uint64_t align) {
    if (align <= 1) return ptr;
    uintptr_t p = (uintptr_t)ptr;
    uintptr_t a = (uintptr_t)align;
    return (void *)(((p + a - 1u) / a) * a);
}

static int cuda_model_stage_pool_alloc(uint64_t bytes) {
    if (g_model_stage_bytes >= bytes) return 1;
    for (size_t i = 0; i < 4; i++) {
        if (g_model_stage_event[i]) {
            (void)cudaEventDestroy(g_model_stage_event[i]);
            g_model_stage_event[i] = NULL;
        }
        if (g_model_stage_raw[i]) {
            (void)cudaFreeHost(g_model_stage_raw[i]);
            g_model_stage_raw[i] = NULL;
            g_model_stage[i] = NULL;
        }
    }
    g_model_stage_bytes = 0;
    if (!g_model_upload_stream) {
        cudaError_t err = cudaStreamCreateWithFlags(&g_model_upload_stream, cudaStreamNonBlocking);
        if (err != cudaSuccess) {
            fprintf(stderr, "ds4: CUDA model upload stream creation failed: %s\n", cudaGetErrorString(err));
            (void)cudaGetLastError();
            return 0;
        }
    }
    for (size_t i = 0; i < 4; i++) {
        cudaError_t err = cudaMallocHost(&g_model_stage_raw[i], (size_t)bytes);
        if (err != cudaSuccess) {
            fprintf(stderr, "ds4: CUDA pinned model staging allocation failed: %s\n", cudaGetErrorString(err));
            (void)cudaGetLastError();
            return 0;
        }
        g_model_stage[i] = cuda_align_ptr(g_model_stage_raw[i], g_model_direct_align);
        err = cudaEventCreateWithFlags(&g_model_stage_event[i], cudaEventDisableTiming);
        if (err != cudaSuccess) {
            fprintf(stderr, "ds4: CUDA model staging event creation failed: %s\n", cudaGetErrorString(err));
            (void)cudaGetLastError();
            return 0;
        }
    }
    g_model_stage_bytes = bytes;
    return 1;
}

static int cuda_pread_full(int fd, void *buf, uint64_t bytes, uint64_t offset) {
    uint64_t done = 0;
    while (done < bytes) {
        const size_t n_req = (bytes - done > (uint64_t)SSIZE_MAX) ? (size_t)SSIZE_MAX : (size_t)(bytes - done);
        ssize_t n = pread(fd, (char *)buf + done, n_req, (off_t)(offset + done));
        if (n < 0) {
            if (errno == EINTR) continue;
            return 0;
        }
        if (n == 0) return 0;
        done += (uint64_t)n;
    }
    return 1;
}

static int cuda_model_stage_read(void *stage, uint64_t stage_bytes,
                                 uint64_t offset, uint64_t bytes,
                                 const char **payload) {
    *payload = (const char *)stage;
#if defined(__linux__) && defined(O_DIRECT)
    if (g_model_direct_fd >= 0 && g_model_direct_align > 1 && g_model_file_size != 0) {
        const uint64_t aligned_off = cuda_round_down(offset, g_model_direct_align);
        const uint64_t delta = offset - aligned_off;
        uint64_t read_size = cuda_round_up(delta + bytes, g_model_direct_align);
        if (aligned_off <= g_model_file_size &&
            read_size <= stage_bytes &&
            read_size <= g_model_file_size - aligned_off) {
            const int saved_errno = errno;
            errno = 0;
            if (cuda_pread_full(g_model_direct_fd, stage, read_size, aligned_off)) {
                *payload = (const char *)stage + delta;
                errno = saved_errno;
                return 1;
            }
            const int direct_errno = errno;
            if (direct_errno == EINVAL || direct_errno == EFAULT || direct_errno == ENOTSUP || direct_errno == EOPNOTSUPP) {
                if (getenv("DS4_CUDA_WEIGHT_CACHE_VERBOSE")) {
                    fprintf(stderr, "ds4: CUDA direct model read disabled: %s\n", strerror(direct_errno));
                }
                (void)close(g_model_direct_fd);
                g_model_direct_fd = -1;
                g_model_direct_align = 1;
            }
            errno = direct_errno;
        }
    }
#else
    (void)stage_bytes;
#endif
    return cuda_pread_full(g_model_fd, stage, bytes, offset);
}

static uint64_t cuda_model_cache_limit_bytes(void) {
    uint64_t gb = 0;
    const char *env = getenv("DS4_CUDA_WEIGHT_CACHE_LIMIT_GB");
    if (env && env[0]) {
        char *end = NULL;
        unsigned long long v = strtoull(env, &end, 10);
        if (end != env) gb = (uint64_t)v;
    }
    if (gb == 0) return UINT64_MAX;
    return gb * 1073741824ull;
}

static uint64_t cuda_model_arena_chunk_bytes(uint64_t need) {
    uint64_t mb = 1792;
    const char *env = getenv("DS4_CUDA_WEIGHT_ARENA_CHUNK_MB");
    if (env && env[0]) {
        char *end = NULL;
        unsigned long long v = strtoull(env, &end, 10);
        if (end != env && v > 0) mb = (uint64_t)v;
    }
    if (mb < 256) mb = 256;
    if (mb > 8192) mb = 8192;
    uint64_t bytes = mb * 1048576ull;
    if (bytes < need) {
        const uint64_t align = 256ull * 1048576ull;
        bytes = (need + align - 1u) & ~(align - 1u);
    }
    return bytes;
}

static char *cuda_model_arena_alloc(uint64_t bytes, const char *what) {
    if (bytes == 0) return NULL;
    if (g_model_cache_full) return NULL;
    const uint64_t align = 256u;
    const uint64_t aligned = (bytes + align - 1u) & ~(align - 1u);
    int cur_dev = 0;
    if (cudaGetDevice(&cur_dev) != cudaSuccess) {
        cur_dev = 0;
        (void)cudaGetLastError();
    }

    for (cuda_model_arena &a : g_model_arenas) {
        if (a.device != cur_dev) continue;
        const uint64_t used = (a.used + align - 1u) & ~(align - 1u);
        if (used <= a.bytes && aligned <= a.bytes - used) {
            char *ptr = a.device_ptr + used;
            a.used = used + aligned;
            return ptr;
        }
    }

    const uint64_t limit = cuda_model_cache_limit_bytes();
    if (g_model_range_bytes > limit || aligned > limit - g_model_range_bytes) return NULL;

    const uint64_t chunk = cuda_model_arena_chunk_bytes(aligned);
    void *dev = NULL;
    cudaError_t err = cudaMalloc(&dev, (size_t)chunk);
    if (err != cudaSuccess) {
        fprintf(stderr, "ds4: CUDA model arena alloc failed for %s (%.2f MiB chunk): %s\n",
                what ? what : "weights",
                (double)chunk / 1048576.0,
                cudaGetErrorString(err));
        (void)cudaGetLastError();
        g_model_cache_full = 1;
        return NULL;
    }
    g_model_arenas.push_back({(char *)dev, chunk, aligned, cur_dev});
    if (getenv("DS4_CUDA_WEIGHT_CACHE_VERBOSE")) {
        uint64_t arena_bytes = 0;
        for (const cuda_model_arena &a : g_model_arenas) arena_bytes += a.bytes;
        fprintf(stderr, "ds4: CUDA model arena allocated %.2f MiB (arenas %.2f GiB)\n",
                (double)chunk / 1048576.0,
                (double)arena_bytes / 1073741824.0);
    }
    return (char *)dev;
}

static const char *cuda_model_range_ptr_from_fd(
        const void *model_map,
        uint64_t offset,
        uint64_t bytes,
        const char *what) {
    if (g_model_fd < 0 || bytes == 0) return NULL;
    if (g_model_fd_host_base != NULL && model_map != g_model_fd_host_base) return NULL;
    const uint64_t limit = cuda_model_cache_limit_bytes();
    if (g_model_range_bytes > limit || bytes > limit - g_model_range_bytes) {
        if (getenv("DS4_CUDA_WEIGHT_CACHE_VERBOSE")) {
            fprintf(stderr, "ds4: CUDA direct %s %.2f MiB (cache budget %.2f GiB exhausted)\n",
                    what ? what : "weights",
                    (double)bytes / 1048576.0,
                    (double)limit / 1073741824.0);
        }
        return cuda_model_ptr(model_map, offset);
    }

    char *dev = cuda_model_arena_alloc(bytes, what);
    if (!dev) {
        if (getenv("DS4_CUDA_STRICT_WEIGHT_CACHE") != NULL) return NULL;
        return cuda_model_ptr(model_map, offset);
    }
    cudaError_t err = cudaSuccess;

    const uint64_t chunk = cuda_model_copy_chunk_bytes();
    const uint64_t stage_bytes = chunk + (g_model_direct_align > 1 ? g_model_direct_align : 1);
    if (!cuda_model_stage_pool_alloc(stage_bytes)) return NULL;

    uint64_t copied = 0;
    uint64_t chunk_idx = 0;
    while (copied < bytes) {
        const uint64_t n = (bytes - copied < chunk) ? (bytes - copied) : chunk;
        const uint64_t bi = chunk_idx % 4u;
        if (chunk_idx >= 4u) {
            err = cudaEventSynchronize(g_model_stage_event[bi]);
            if (err != cudaSuccess) {
                fprintf(stderr, "ds4: CUDA model staging wait failed for %s: %s\n",
                        what ? what : "weights", cudaGetErrorString(err));
                (void)cudaGetLastError();
                return NULL;
            }
        }
        const char *payload = NULL;
        if (!cuda_model_stage_read(g_model_stage[bi], g_model_stage_bytes,
                                   offset + copied, n, &payload)) {
            fprintf(stderr, "ds4: CUDA model range read failed for %s at %.2f MiB: %s\n",
                    what ? what : "weights",
                    (double)copied / 1048576.0,
                    strerror(errno));
            return NULL;
        }
        err = cudaMemcpyAsync(dev + copied, payload, (size_t)n,
                              cudaMemcpyHostToDevice, g_model_upload_stream);
        if (err != cudaSuccess) {
            fprintf(stderr, "ds4: CUDA model range copy failed for %s at %.2f MiB: %s\n",
                    what ? what : "weights",
                    (double)copied / 1048576.0,
                    cudaGetErrorString(err));
            (void)cudaGetLastError();
            return NULL;
        }
        err = cudaEventRecord(g_model_stage_event[bi], g_model_upload_stream);
        if (err != cudaSuccess) {
            fprintf(stderr, "ds4: CUDA model staging record failed for %s: %s\n",
                    what ? what : "weights", cudaGetErrorString(err));
            (void)cudaGetLastError();
            return NULL;
        }
        cuda_model_drop_file_pages(offset + copied, n);
        cuda_model_discard_source_pages(model_map, g_model_registered_size, offset + copied, n);
        copied += n;
        cuda_model_load_progress_note(g_model_range_bytes + copied);
        chunk_idx++;
    }
    err = cudaStreamSynchronize(g_model_upload_stream);
    if (err != cudaSuccess) {
        fprintf(stderr, "ds4: CUDA model range upload sync failed for %s: %s\n",
                what ? what : "weights", cudaGetErrorString(err));
        (void)cudaGetLastError();
        return NULL;
    }

    int cur_dev = 0;
    if (cudaGetDevice(&cur_dev) != cudaSuccess) {
        cur_dev = 0;
        (void)cudaGetLastError();
    }
    g_model_ranges.push_back({model_map, offset, bytes, dev, NULL, NULL, 0, 0, 1, cur_dev});
    g_model_range_by_offset[offset] = g_model_ranges.size() - 1u;
    g_model_range_bytes += bytes;
    cuda_model_load_progress_note(g_model_range_bytes);
    if (getenv("DS4_CUDA_WEIGHT_CACHE_VERBOSE")) {
        fprintf(stderr, "ds4: CUDA fd-cached %s %.2f MiB (total %.2f GiB)\n",
                what ? what : "weights",
                (double)bytes / 1048576.0,
                (double)g_model_range_bytes / 1073741824.0);
    }
    return (const char *)dev;
}

static int cuda_model_copy_chunked(const void *model_map, uint64_t model_size, uint64_t map_offset, uint64_t map_size) {
    if (!model_map || model_size == 0 || map_offset > model_size || map_size > model_size - map_offset) return 0;
    if (getenv("DS4_CUDA_NO_MODEL_COPY") != NULL ||
        getenv("DS4_CUDA_DIRECT_MODEL") != NULL ||
        getenv("DS4_CUDA_WEIGHT_CACHE") != NULL ||
        getenv("DS4_CUDA_WEIGHT_PRELOAD") != NULL) {
        return 0;
    }
    if (g_model_device_owned || g_model_registered) return 1;

    void *dev = NULL;
    const double t0 = cuda_wall_sec();
    cudaError_t err = cudaMalloc(&dev, (size_t)model_size);
    if (err != cudaSuccess) {
        fprintf(stderr, "ds4: CUDA model allocation skipped: %s\n", cudaGetErrorString(err));
        (void)cudaGetLastError();
        return 0;
    }

    fprintf(stderr, "ds4: CUDA chunk-copying %.2f GiB model image\n",
            (double)model_size / 1073741824.0);

    const uint64_t chunk = cuda_model_copy_chunk_bytes();
    void *stage = NULL;
    err = cudaMallocHost(&stage, (size_t)chunk);
    if (err != cudaSuccess) {
        fprintf(stderr, "ds4: CUDA pinned model staging allocation failed: %s\n", cudaGetErrorString(err));
        (void)cudaFree(dev);
        (void)cudaGetLastError();
        return 0;
    }

    if (map_offset > 0) {
        uint64_t copied_header = 0;
        while (copied_header < map_offset) {
            const uint64_t n = (map_offset - copied_header < chunk) ? (map_offset - copied_header) : chunk;
            memcpy(stage, (const char *)model_map + copied_header, (size_t)n);
            err = cudaMemcpy((char *)dev + copied_header, stage, (size_t)n, cudaMemcpyHostToDevice);
            if (err != cudaSuccess) {
                fprintf(stderr, "ds4: CUDA model header copy failed: %s\n", cudaGetErrorString(err));
                (void)cudaFreeHost(stage);
                (void)cudaFree(dev);
                (void)cudaGetLastError();
                return 0;
            }
            copied_header += n;
        }
    }

    uint64_t copied = 0;
    double last_report = t0;
    while (copied < map_size) {
        const uint64_t n = (map_size - copied < chunk) ? (map_size - copied) : chunk;
        const uint64_t off = map_offset + copied;
        memcpy(stage, (const char *)model_map + off, (size_t)n);
        err = cudaMemcpy((char *)dev + off, stage, (size_t)n, cudaMemcpyHostToDevice);
        if (err != cudaSuccess) {
            fprintf(stderr, "ds4: CUDA model chunk copy failed at %.2f GiB: %s\n",
                    (double)copied / 1073741824.0, cudaGetErrorString(err));
            (void)cudaFreeHost(stage);
            (void)cudaFree(dev);
            (void)cudaGetLastError();
            return 0;
        }
        cuda_model_discard_source_pages(model_map, model_size, off, n);
        copied += n;
        const double now = cuda_wall_sec();
        if (getenv("DS4_CUDA_MODEL_COPY_VERBOSE") != NULL && now - last_report >= 2.0) {
            fprintf(stderr, "ds4: CUDA model chunk copy %.2f/%.2f GiB\n",
                    (double)copied / 1073741824.0,
                    (double)map_size / 1073741824.0);
            last_report = now;
        }
    }

    (void)cudaFreeHost(stage);
    g_model_device_base = (const char *)dev;
    g_model_device_owned = 1;
    g_model_hmm_direct = 0;
    const double t1 = cuda_wall_sec();
    fprintf(stderr,
            "ds4: CUDA model chunk copy complete in %.3fs (%.2f GiB tensors)\n",
            t1 - t0,
            (double)map_size / 1073741824.0);
    return 1;
}

static void cuda_model_range_release_all(void) {
    for (const cuda_model_range &r : g_model_ranges) {
        if (r.host_registered && r.registered_base) {
            (void)cudaHostUnregister(r.registered_base);
        } else if (r.device_ptr && !r.arena_allocated) {
            (void)cudaSetDevice(r.device);
            (void)cudaFree(r.device_ptr);
        }
    }
    for (const cuda_model_arena &a : g_model_arenas) {
        if (a.device_ptr) {
            (void)cudaSetDevice(a.device);
            (void)cudaFree(a.device_ptr);
        }
    }
    g_model_arenas.clear();
    g_model_ranges.clear();
    g_model_range_by_offset.clear();
    g_model_range_bytes = 0;
    cuda_model_load_progress_reset();
}

static int cublas_ok(cublasStatus_t st, const char *what) {
    if (st == CUBLAS_STATUS_SUCCESS) return 1;
    fprintf(stderr, "ds4: cuBLAS %s failed: status %d\n", what, (int)st);
    return 0;
}

extern "C" int ds4_gpu_init(void) {
    int dev = 0;
    if (!cuda_ok(cudaSetDevice(dev), "set device")) return 0;
    cudaDeviceProp prop;
    if (cudaGetDeviceProperties(&prop, dev) == cudaSuccess) {
        fprintf(stderr, "ds4: CUDA backend initialized on %s (sm_%d%d)\n",
                prop.name, prop.major, prop.minor);
    }
    if (!g_cublas_ready) {
        if (!cublas_ok(cublasCreate(&g_cublas), "create handle")) return 0;
        const cublasMath_t math_mode =
            (g_quality_mode || getenv("DS4_CUDA_NO_TF32") != NULL)
                ? CUBLAS_DEFAULT_MATH
                : CUBLAS_TF32_TENSOR_OP_MATH;
        (void)cublasSetMathMode(g_cublas, math_mode);
        g_cublas_ready = 1;
    }
    return 1;
}

extern "C" void ds4_gpu_cleanup(void) {
    (void)cudaDeviceSynchronize();
    cuda_tm_profile_dump();
    cuda_tensor_pool_release_all();
    cuda_tm_matrix_table_cache_release_all();
    cuda_f8_f16_arena_cache_release_all();
    if (g_cublas_ready) {
        (void)cublasDestroy(g_cublas);
        g_cublas_ready = 0;
        g_cublas = NULL;
    }
    cuda_model_range_release_all();
    cuda_q8_f16_cache_release_all();
    g_q8_f16_disabled_after_oom = 0;
    g_q8_f16_budget_notice_printed = 0;
    for (const cuda_q8_f32_range &r : g_q8_f32_ranges) {
        (void)cudaFree(r.device_ptr);
    }
    g_q8_f32_ranges.clear();
    g_q8_f32_by_offset.clear();
    g_q8_f32_bytes = 0;
    for (int dev = 0; dev < DS4_CUDA_MAX_TMP_DEVICES; dev++) {
        if (g_cuda_tmp[dev]) {
            (void)cudaSetDevice(dev);
            (void)cudaFree(g_cuda_tmp[dev]);
            g_cuda_tmp[dev] = NULL;
            g_cuda_tmp_bytes[dev] = 0;
        }
    }
    for (size_t i = 0; i < 4; i++) {
        if (g_model_stage_event[i]) {
            (void)cudaEventDestroy(g_model_stage_event[i]);
            g_model_stage_event[i] = NULL;
        }
        if (g_model_stage_raw[i]) {
            (void)cudaFreeHost(g_model_stage_raw[i]);
            g_model_stage_raw[i] = NULL;
            g_model_stage[i] = NULL;
        }
    }
    g_model_stage_bytes = 0;
    if (g_model_upload_stream) {
        (void)cudaStreamDestroy(g_model_upload_stream);
        g_model_upload_stream = NULL;
    }
    if (g_model_device_owned && g_model_device_base) {
        (void)cudaFree((void *)g_model_device_base);
    }
    if (g_model_registered && g_model_host_base) {
        (void)cudaHostUnregister((void *)g_model_host_base);
    }
    g_model_host_base = NULL;
    g_model_device_base = NULL;
    g_model_registered_size = 0;
    g_model_registered = 0;
    g_model_device_owned = 0;
    g_model_range_mapping_supported = 1;
    g_model_hmm_direct = 0;
    g_model_fd = -1;
    if (g_model_direct_fd >= 0) {
        (void)close(g_model_direct_fd);
        g_model_direct_fd = -1;
    }
    g_model_direct_align = 1;
    g_model_file_size = 0;
    g_model_cache_full = 0;
    if (g_model_prefetch_stream) {
        (void)cudaStreamDestroy(g_model_prefetch_stream);
        g_model_prefetch_stream = NULL;
    }
    {
        std::lock_guard<std::mutex> lk(g_tm_api_mutex);
        if (g_tm_api.available && g_tm_api.shutdown) {
            g_tm_api.shutdown();
        }
        if (g_tm_api.handle) {
            (void)dlclose(g_tm_api.handle);
        }
        memset(&g_tm_api, 0, sizeof(g_tm_api));
    }
}

extern "C" int ds4_gpu_set_device(int gpu) {
    if (gpu < 0) return 0;
    return cuda_ok(cudaSetDevice(gpu), "set device");
}

__global__ static void fill_f32_kernel(float *x, uint64_t n, float v);
__global__ static void f32_to_f16_kernel(__half *out, const float *x, uint64_t n);
__global__ static void f32_f16_round_kernel(float *x, uint64_t n);

extern "C" ds4_gpu_tensor *ds4_gpu_tensor_alloc(uint64_t bytes) {
    if (bytes == 0) bytes = 1;
    ds4_gpu_tensor *t = (ds4_gpu_tensor *)calloc(1, sizeof(*t));
    if (!t) return NULL;
    uint64_t alloc_bytes = 0;
    int device = 0;
    void *pooled = cuda_tensor_pool_take(bytes, &alloc_bytes, &device);
    if (pooled) {
        t->ptr = pooled;
        t->alloc_bytes = alloc_bytes;
        t->device = device;
    } else {
        alloc_bytes = cuda_tensor_pool_align_bytes(bytes);
        if (!cuda_ok(cudaMalloc(&t->ptr, (size_t)alloc_bytes), "tensor alloc")) {
            free(t);
            return NULL;
        }
        t->alloc_bytes = alloc_bytes;
        if (cudaGetDevice(&t->device) != cudaSuccess) {
            t->device = 0;
            (void)cudaGetLastError();
        }
    }
    t->bytes = bytes;
    t->owner = 1;
    return t;
}

extern "C" ds4_gpu_tensor *ds4_gpu_tensor_alloc_managed(uint64_t bytes) {
    if (bytes == 0) bytes = 1;
    ds4_gpu_tensor *t = (ds4_gpu_tensor *)calloc(1, sizeof(*t));
    if (!t) return NULL;
    if (!cuda_ok(cudaMallocManaged(&t->ptr, (size_t)bytes), "managed tensor alloc")) {
        free(t);
        return NULL;
    }
    t->bytes = bytes;
    t->alloc_bytes = bytes;
    t->owner = 1;
    if (cudaGetDevice(&t->device) != cudaSuccess) {
        t->device = 0;
        (void)cudaGetLastError();
    }
    return t;
}

static uint64_t cuda_managed_kv_reserve_bytes(uint64_t total_bytes) {
    const uint64_t min_reserve = 8ull * 1073741824ull;
    const uint64_t max_reserve = 40ull * 1073741824ull;
    uint64_t reserve = total_bytes / 4u;
    if (reserve < min_reserve) reserve = min_reserve;
    if (reserve > max_reserve) reserve = max_reserve;
    return reserve;
}

extern "C" int ds4_gpu_should_use_managed_kv_cache(uint64_t kv_cache_bytes, uint64_t context_bytes) {
    if (kv_cache_bytes == 0) return 0;

    /* Very large KV caches are where device-only cudaMalloc() can make a
     * unified-memory machine unresponsive.  Managed memory restores the old
     * demand-paged behavior for this one long-lived allocation class only. */
    const uint64_t huge_kv = 8ull * 1073741824ull;
    if (kv_cache_bytes >= huge_kv) return 1;

    const uint64_t large_context = 8ull * 1073741824ull;
    if (context_bytes < large_context) return 0;

    size_t free_b = 0;
    size_t total_b = 0;
    cudaError_t err = cudaMemGetInfo(&free_b, &total_b);
    if (err != cudaSuccess) {
        (void)cudaGetLastError();
        return 0;
    }

    const uint64_t free_bytes = (uint64_t)free_b;
    const uint64_t total_bytes = (uint64_t)total_b;
    const uint64_t reserve_bytes = cuda_managed_kv_reserve_bytes(total_bytes);
    if (context_bytes > free_bytes) return 1;
    return free_bytes - context_bytes < reserve_bytes;
}

extern "C" ds4_gpu_tensor *ds4_gpu_tensor_view(const ds4_gpu_tensor *base, uint64_t offset, uint64_t bytes) {
    if (!base || offset > base->bytes || bytes > base->bytes - offset) return NULL;
    ds4_gpu_tensor *t = (ds4_gpu_tensor *)calloc(1, sizeof(*t));
    if (!t) return NULL;
    t->ptr = (char *)base->ptr + offset;
    t->bytes = bytes;
    t->alloc_bytes = bytes;
    t->owner = 0;
    t->device = base->device;
    return t;
}

extern "C" void ds4_gpu_tensor_free(ds4_gpu_tensor *tensor) {
    if (!tensor) return;
    if (tensor->owner && tensor->ptr) {
        (void)cudaSetDevice(tensor->device);
        const uint64_t alloc_bytes = tensor->alloc_bytes ? tensor->alloc_bytes : tensor->bytes;
        if (!cuda_tensor_pool_put(tensor->ptr, alloc_bytes, tensor->device)) {
            (void)cudaFree(tensor->ptr);
        }
    }
    free(tensor);
}

extern "C" uint64_t ds4_gpu_tensor_bytes(const ds4_gpu_tensor *tensor) {
    return tensor ? tensor->bytes : 0;
}

extern "C" void *ds4_gpu_tensor_contents(ds4_gpu_tensor *tensor) {
    if (!tensor) return NULL;
    (void)cudaSetDevice(tensor->device);
    (void)cudaDeviceSynchronize();
    return tensor->ptr;
}

extern "C" int ds4_gpu_tensor_fill_f32(ds4_gpu_tensor *tensor, float value, uint64_t count) {
    if (!tensor || count > tensor->bytes / sizeof(float)) return 0;
    if (count == 0) return 1;
    if (!cuda_ok(cudaSetDevice(tensor->device), "tensor fill set device")) return 0;
    fill_f32_kernel<<<(count + 255u) / 256u, 256>>>((float *)tensor->ptr, count, value);
    return cuda_ok(cudaGetLastError(), "tensor fill f32 launch");
}

extern "C" int ds4_gpu_f16_round_tensor(ds4_gpu_tensor *tensor, uint64_t count) {
    if (!tensor || count > tensor->bytes / sizeof(float)) return 0;
    if (count == 0) return 1;
    if (!cuda_ok(cudaSetDevice(tensor->device), "tensor f16 round set device")) return 0;
    f32_f16_round_kernel<<<(count + 255u) / 256u, 256>>>((float *)tensor->ptr, count);
    return cuda_ok(cudaGetLastError(), "tensor f16 round launch");
}

extern "C" int ds4_gpu_tensor_write(ds4_gpu_tensor *tensor, uint64_t offset, const void *data, uint64_t bytes) {
    if (!tensor || !data || offset > tensor->bytes || bytes > tensor->bytes - offset) return 0;
    if (!cuda_ok(cudaSetDevice(tensor->device), "tensor write set device")) return 0;
    return cuda_ok(cudaMemcpy((char *)tensor->ptr + offset, data, (size_t)bytes, cudaMemcpyHostToDevice), "tensor write");
}

extern "C" int ds4_gpu_tensor_read(const ds4_gpu_tensor *tensor, uint64_t offset, void *data, uint64_t bytes) {
    if (!tensor || !data || offset > tensor->bytes || bytes > tensor->bytes - offset) return 0;
    if (!cuda_ok(cudaSetDevice(tensor->device), "tensor read set device")) return 0;
    return cuda_ok(cudaMemcpy(data, (const char *)tensor->ptr + offset, (size_t)bytes, cudaMemcpyDeviceToHost), "tensor read");
}

extern "C" int ds4_gpu_tensor_write_f32_row_ptrs(ds4_gpu_tensor *ptrs,
                                                 const ds4_gpu_tensor *const *rows,
                                                 uint32_t n_rows,
                                                 uint64_t min_row_bytes) {
    if (!ptrs || !rows || !ptrs->ptr ||
        ptrs->bytes < (uint64_t)n_rows * sizeof(float *)) {
        return 0;
    }
    std::vector<const float *> row_ptrs(n_rows);
    for (uint32_t i = 0; i < n_rows; i++) {
        const ds4_gpu_tensor *row = rows[i];
        if (!row || !row->ptr || row->device != ptrs->device ||
            row->bytes < min_row_bytes) {
            return 0;
        }
        row_ptrs[i] = (const float *)row->ptr;
    }
    if (!cuda_ok(cudaSetDevice(ptrs->device), "tensor row ptr write set device")) return 0;
    return cuda_ok(cudaMemcpy(ptrs->ptr,
                              row_ptrs.data(),
                              (size_t)n_rows * sizeof(float *),
                              cudaMemcpyHostToDevice),
                   "tensor row ptr write");
}

extern "C" int ds4_gpu_tensor_copy(ds4_gpu_tensor *dst, uint64_t dst_offset,
                                     const ds4_gpu_tensor *src, uint64_t src_offset,
                                     uint64_t bytes) {
    if (!dst || !src || dst_offset > dst->bytes || src_offset > src->bytes ||
        bytes > dst->bytes - dst_offset || bytes > src->bytes - src_offset) {
        return 0;
    }
    if (bytes == 0) return 1;
    if (dst->device == src->device) {
        if (!cuda_ok(cudaSetDevice(dst->device), "tensor copy set device")) return 0;
        return cuda_ok(cudaMemcpy((char *)dst->ptr + dst_offset,
                                  (const char *)src->ptr + src_offset,
                                  (size_t)bytes,
                                  cudaMemcpyDeviceToDevice),
                       "tensor copy");
    }
    return cuda_ok(cudaMemcpyPeer((char *)dst->ptr + dst_offset,
                                  dst->device,
                                  (const char *)src->ptr + src_offset,
                                  src->device,
                                  (size_t)bytes),
                   "tensor peer copy");
}

extern "C" int ds4_gpu_tensor_copy_async(ds4_gpu_tensor *dst, uint64_t dst_offset,
                                         const ds4_gpu_tensor *src, uint64_t src_offset,
                                         uint64_t bytes) {
    if (!dst || !src || dst_offset > dst->bytes || src_offset > src->bytes ||
        bytes > dst->bytes - dst_offset || bytes > src->bytes - src_offset) {
        return 0;
    }
    if (bytes == 0) return 1;
    if (!cuda_ok(cudaSetDevice(dst->device), "async tensor copy set device")) return 0;
    if (dst->device == src->device) {
        return cuda_ok(cudaMemcpyAsync((char *)dst->ptr + dst_offset,
                                       (const char *)src->ptr + src_offset,
                                       (size_t)bytes,
                                       cudaMemcpyDeviceToDevice,
                                       0),
                       "async tensor copy");
    }
    return cuda_ok(cudaMemcpyPeerAsync((char *)dst->ptr + dst_offset,
                                       dst->device,
                                       (const char *)src->ptr + src_offset,
                                       src->device,
                                       (size_t)bytes,
                                       0),
                   "async tensor peer copy");
}

extern "C" ds4_gpu_event *ds4_gpu_event_create(int gpu) {
    if (gpu < 0) return NULL;
    if (!cuda_ok(cudaSetDevice(gpu), "event create set device")) return NULL;
    ds4_gpu_event *event = (ds4_gpu_event *)calloc(1, sizeof(*event));
    if (!event) return NULL;
    event->gpu = gpu;
    cudaError_t err = cudaEventCreateWithFlags(&event->event, cudaEventDisableTiming);
    if (err != cudaSuccess) {
        fprintf(stderr, "ds4: CUDA event create failed: %s\n", cudaGetErrorString(err));
        free(event);
        return NULL;
    }
    return event;
}

extern "C" void ds4_gpu_event_free(ds4_gpu_event *event) {
    if (!event) return;
    if (event->event) {
        (void)cudaSetDevice(event->gpu);
        (void)cudaEventDestroy(event->event);
    }
    free(event);
}

extern "C" int ds4_gpu_event_record(ds4_gpu_event *event) {
    if (!event || !event->event) return 0;
    if (!cuda_ok(cudaSetDevice(event->gpu), "event record set device")) return 0;
    return cuda_ok(cudaEventRecord(event->event, 0), "event record");
}

extern "C" int ds4_gpu_stream_wait_event(int gpu, const ds4_gpu_event *event) {
    if (gpu < 0 || !event || !event->event) return 0;
    if (!cuda_ok(cudaSetDevice(gpu), "event wait set device")) return 0;
    return cuda_ok(cudaStreamWaitEvent(0, event->event, 0), "event wait");
}

extern "C" int ds4_gpu_tensor_copy_async_after_event(ds4_gpu_tensor *dst,
                                                     uint64_t dst_offset,
                                                     const ds4_gpu_tensor *src,
                                                     uint64_t src_offset,
                                                     uint64_t bytes,
                                                     const ds4_gpu_event *event) {
    if (!dst || !src) return 0;
    if (event && !ds4_gpu_stream_wait_event(dst->device, event)) return 0;
    return ds4_gpu_tensor_copy_async(dst, dst_offset, src, src_offset, bytes);
}

__device__ __forceinline__ static bool top1_f32_better(float av,
                                                       uint32_t ai,
                                                       float bv,
                                                       uint32_t bi) {
    return av > bv || (av == bv && ai < bi);
}

__device__ __forceinline__ static bool top1_f32_isfinite_raw(float v) {
    return (__float_as_uint(v) & 0x7f800000u) != 0x7f800000u;
}

template <uint32_t BLOCK_THREADS, uint32_t ITEMS_PER_BLOCK>
__global__ static void top1_f32_blocks_kernel(const float *logits,
                                              uint32_t n_logits,
                                              float *block_logits,
                                              uint32_t *block_tokens,
                                              uint32_t *block_bad) {
    const uint32_t tid = threadIdx.x;
    const uint32_t start = blockIdx.x * ITEMS_PER_BLOCK;
    const uint32_t end = start + ITEMS_PER_BLOCK < n_logits
        ? start + ITEMS_PER_BLOCK
        : n_logits;
    __shared__ float vals[BLOCK_THREADS];
    __shared__ uint32_t idxs[BLOCK_THREADS];
    __shared__ uint32_t bads[BLOCK_THREADS];

    float best_logit = -INFINITY;
    uint32_t best_token = UINT32_MAX;
    uint32_t bad = 0;
    for (uint32_t i = start + tid; i < end; i += BLOCK_THREADS) {
        const float v = logits[i];
        if (!top1_f32_isfinite_raw(v)) {
            bad = 1u;
        } else if (top1_f32_better(v, i, best_logit, best_token)) {
            best_logit = v;
            best_token = i;
        }
    }
    vals[tid] = best_logit;
    idxs[tid] = best_token;
    bads[tid] = bad;
    __syncthreads();

    for (uint32_t stride = BLOCK_THREADS >> 1u; stride > 0u; stride >>= 1u) {
        if (tid < stride) {
            const float ov = vals[tid + stride];
            const uint32_t oi = idxs[tid + stride];
            bads[tid] |= bads[tid + stride];
            if (top1_f32_better(ov, oi, vals[tid], idxs[tid])) {
                vals[tid] = ov;
                idxs[tid] = oi;
            }
        }
        __syncthreads();
    }

    if (tid == 0) {
        block_logits[blockIdx.x] = vals[0];
        block_tokens[blockIdx.x] = idxs[0];
        block_bad[blockIdx.x] = bads[0];
    }
}

template <uint32_t BLOCK_THREADS>
__global__ static void top1_f32_final_kernel(const float *block_logits,
                                             const uint32_t *block_tokens,
                                             const uint32_t *block_bad,
                                             uint32_t n_blocks,
                                             float *out_logit,
                                             uint32_t *out_token,
                                             uint32_t *out_bad) {
    const uint32_t tid = threadIdx.x;
    __shared__ float vals[BLOCK_THREADS];
    __shared__ uint32_t idxs[BLOCK_THREADS];
    __shared__ uint32_t bads[BLOCK_THREADS];

    float best_logit = -INFINITY;
    uint32_t best_token = UINT32_MAX;
    uint32_t bad = 0;
    for (uint32_t i = tid; i < n_blocks; i += BLOCK_THREADS) {
        const float v = block_logits[i];
        const uint32_t token = block_tokens[i];
        bad |= block_bad[i];
        if (top1_f32_better(v, token, best_logit, best_token)) {
            best_logit = v;
            best_token = token;
        }
    }
    vals[tid] = best_logit;
    idxs[tid] = best_token;
    bads[tid] = bad;
    __syncthreads();

    for (uint32_t stride = BLOCK_THREADS >> 1u; stride > 0u; stride >>= 1u) {
        if (tid < stride) {
            const float ov = vals[tid + stride];
            const uint32_t oi = idxs[tid + stride];
            bads[tid] |= bads[tid + stride];
            if (top1_f32_better(ov, oi, vals[tid], idxs[tid])) {
                vals[tid] = ov;
                idxs[tid] = oi;
            }
        }
        __syncthreads();
    }

    if (tid == 0) {
        *out_logit = vals[0];
        *out_token = idxs[0];
        *out_bad = bads[0];
    }
}

template <uint32_t BLOCK_THREADS, uint32_t ITEMS_PER_BLOCK>
__global__ static void top1_f32_rows_blocks_kernel(const float *logits,
                                                   uint32_t n_rows,
                                                   uint32_t n_logits,
                                                   float *block_logits,
                                                   uint32_t *block_tokens,
                                                   uint32_t *block_bad,
                                                   uint32_t n_blocks) {
    const uint32_t tid = threadIdx.x;
    const uint32_t block = blockIdx.x;
    const uint32_t row = blockIdx.y;
    if (row >= n_rows || block >= n_blocks) return;
    const uint32_t start = block * ITEMS_PER_BLOCK;
    const uint32_t end = start + ITEMS_PER_BLOCK < n_logits
        ? start + ITEMS_PER_BLOCK
        : n_logits;
    const float *row_logits = logits + (uint64_t)row * n_logits;
    __shared__ float vals[BLOCK_THREADS];
    __shared__ uint32_t idxs[BLOCK_THREADS];
    __shared__ uint32_t bads[BLOCK_THREADS];

    float best_logit = -INFINITY;
    uint32_t best_token = UINT32_MAX;
    uint32_t bad = 0;
    for (uint32_t i = start + tid; i < end; i += BLOCK_THREADS) {
        const float v = row_logits[i];
        if (!top1_f32_isfinite_raw(v)) {
            bad = 1u;
        } else if (top1_f32_better(v, i, best_logit, best_token)) {
            best_logit = v;
            best_token = i;
        }
    }
    vals[tid] = best_logit;
    idxs[tid] = best_token;
    bads[tid] = bad;
    __syncthreads();

    for (uint32_t stride = BLOCK_THREADS >> 1u; stride > 0u; stride >>= 1u) {
        if (tid < stride) {
            const float ov = vals[tid + stride];
            const uint32_t oi = idxs[tid + stride];
            bads[tid] |= bads[tid + stride];
            if (top1_f32_better(ov, oi, vals[tid], idxs[tid])) {
                vals[tid] = ov;
                idxs[tid] = oi;
            }
        }
        __syncthreads();
    }

    if (tid == 0) {
        const uint64_t out = (uint64_t)row * n_blocks + block;
        block_logits[out] = vals[0];
        block_tokens[out] = idxs[0];
        block_bad[out] = bads[0];
    }
}

template <uint32_t BLOCK_THREADS>
__global__ static void top1_f32_rows_final_kernel(const float *block_logits,
                                                  const uint32_t *block_tokens,
                                                  const uint32_t *block_bad,
                                                  uint32_t n_rows,
                                                  uint32_t n_blocks,
                                                  float *out_logits,
                                                  uint32_t *out_tokens,
                                                  uint32_t *out_bad) {
    const uint32_t tid = threadIdx.x;
    const uint32_t row = blockIdx.x;
    if (row >= n_rows) return;
    __shared__ float vals[BLOCK_THREADS];
    __shared__ uint32_t idxs[BLOCK_THREADS];
    __shared__ uint32_t bads[BLOCK_THREADS];

    const uint64_t row_off = (uint64_t)row * n_blocks;
    float best_logit = -INFINITY;
    uint32_t best_token = UINT32_MAX;
    uint32_t bad = 0;
    for (uint32_t i = tid; i < n_blocks; i += BLOCK_THREADS) {
        const float v = block_logits[row_off + i];
        const uint32_t token = block_tokens[row_off + i];
        bad |= block_bad[row_off + i];
        if (top1_f32_better(v, token, best_logit, best_token)) {
            best_logit = v;
            best_token = token;
        }
    }
    vals[tid] = best_logit;
    idxs[tid] = best_token;
    bads[tid] = bad;
    __syncthreads();

    for (uint32_t stride = BLOCK_THREADS >> 1u; stride > 0u; stride >>= 1u) {
        if (tid < stride) {
            const float ov = vals[tid + stride];
            const uint32_t oi = idxs[tid + stride];
            bads[tid] |= bads[tid + stride];
            if (top1_f32_better(ov, oi, vals[tid], idxs[tid])) {
                vals[tid] = ov;
                idxs[tid] = oi;
            }
        }
        __syncthreads();
    }

    if (tid == 0) {
        out_logits[row] = vals[0];
        out_tokens[row] = idxs[0];
        out_bad[row] = bads[0];
    }
}

extern "C" int ds4_gpu_top1_f32_tensor(const ds4_gpu_tensor *logits,
                                        uint32_t n_logits,
                                        uint32_t *token,
                                        float *logit) {
    if (!logits || !logits->ptr || !token || !logit || n_logits == 0 ||
        logits->bytes < (uint64_t)n_logits * sizeof(float)) {
        return 0;
    }
    if (!cuda_ok(cudaSetDevice(logits->device), "top1 set device")) return 0;

    constexpr uint32_t TOP1_THREADS = 256u;
    constexpr uint32_t TOP1_ITEMS_PER_BLOCK = 1024u;
    const uint32_t n_blocks = (n_logits + TOP1_ITEMS_PER_BLOCK - 1u) / TOP1_ITEMS_PER_BLOCK;
    const uint64_t block_logits_bytes = (uint64_t)n_blocks * sizeof(float);
    const uint64_t block_tokens_offset = (block_logits_bytes + 15u) & ~15ull;
    const uint64_t block_tokens_bytes = (uint64_t)n_blocks * sizeof(uint32_t);
    const uint64_t block_bad_offset = (block_tokens_offset + block_tokens_bytes + 15u) & ~15ull;
    const uint64_t block_bad_bytes = (uint64_t)n_blocks * sizeof(uint32_t);
    const uint64_t out_logit_offset = (block_bad_offset + block_bad_bytes + 15u) & ~15ull;
    const uint64_t out_token_offset = (out_logit_offset + sizeof(float) + 15u) & ~15ull;
    const uint64_t out_bad_offset = (out_token_offset + sizeof(uint32_t) + 15u) & ~15ull;
    const uint64_t tmp_bytes = out_bad_offset + sizeof(uint32_t);
    void *tmp = cuda_tmp_alloc(tmp_bytes, "top1 f32 reduce");
    if (!tmp) return 0;

    float *block_logits = (float *)tmp;
    uint32_t *block_tokens = (uint32_t *)((char *)tmp + block_tokens_offset);
    uint32_t *block_bad = (uint32_t *)((char *)tmp + block_bad_offset);
    float *device_logit = (float *)((char *)tmp + out_logit_offset);
    uint32_t *device_token = (uint32_t *)((char *)tmp + out_token_offset);
    uint32_t *device_bad = (uint32_t *)((char *)tmp + out_bad_offset);
    top1_f32_blocks_kernel<TOP1_THREADS, TOP1_ITEMS_PER_BLOCK><<<n_blocks, TOP1_THREADS>>>(
            (const float *)logits->ptr,
            n_logits,
            block_logits,
            block_tokens,
            block_bad);
    if (!cuda_ok(cudaGetLastError(), "top1 f32 block launch")) return 0;
    top1_f32_final_kernel<TOP1_THREADS><<<1, TOP1_THREADS>>>(
            block_logits,
            block_tokens,
            block_bad,
            n_blocks,
            device_logit,
            device_token,
            device_bad);
    if (!cuda_ok(cudaGetLastError(), "top1 f32 final launch")) return 0;
    uint32_t bad = 0;
    if (!cuda_ok(cudaMemcpy(logit,
                            device_logit,
                            sizeof(*logit),
                            cudaMemcpyDeviceToHost),
                 "top1 f32 logit read") ||
        !cuda_ok(cudaMemcpy(token,
                            device_token,
                            sizeof(*token),
                            cudaMemcpyDeviceToHost),
                 "top1 f32 token read") ||
        !cuda_ok(cudaMemcpy(&bad,
                            device_bad,
                            sizeof(bad),
                            cudaMemcpyDeviceToHost),
                 "top1 f32 status read")) {
        return 0;
    }
    return bad == 0 && *token < n_logits;
}

extern "C" int ds4_gpu_top1_f32_rows_tensor(const ds4_gpu_tensor *logits,
                                             uint32_t n_rows,
                                             uint32_t n_logits,
                                             uint32_t *tokens,
                                             float *logits_out) {
    if (!logits || !logits->ptr || !tokens || !logits_out ||
        n_rows == 0 || n_logits == 0 ||
        logits->bytes < (uint64_t)n_rows * n_logits * sizeof(float)) {
        return 0;
    }
    if (!cuda_ok(cudaSetDevice(logits->device), "top1 rows set device")) return 0;

    constexpr uint32_t TOP1_THREADS = 256u;
    constexpr uint32_t TOP1_ITEMS_PER_BLOCK = 1024u;
    const uint32_t n_blocks = (n_logits + TOP1_ITEMS_PER_BLOCK - 1u) / TOP1_ITEMS_PER_BLOCK;
    const uint64_t candidates = (uint64_t)n_rows * n_blocks;
    const uint64_t block_logits_bytes = candidates * sizeof(float);
    const uint64_t block_tokens_offset = (block_logits_bytes + 15u) & ~15ull;
    const uint64_t block_tokens_bytes = candidates * sizeof(uint32_t);
    const uint64_t block_bad_offset = (block_tokens_offset + block_tokens_bytes + 15u) & ~15ull;
    const uint64_t block_bad_bytes = candidates * sizeof(uint32_t);
    const uint64_t out_logits_offset = (block_bad_offset + block_bad_bytes + 15u) & ~15ull;
    const uint64_t out_logits_bytes = (uint64_t)n_rows * sizeof(float);
    const uint64_t out_tokens_offset = (out_logits_offset + out_logits_bytes + 15u) & ~15ull;
    const uint64_t out_tokens_bytes = (uint64_t)n_rows * sizeof(uint32_t);
    const uint64_t out_bad_offset = (out_tokens_offset + out_tokens_bytes + 15u) & ~15ull;
    const uint64_t out_bad_bytes = (uint64_t)n_rows * sizeof(uint32_t);
    const uint64_t tmp_bytes = out_bad_offset + out_bad_bytes;
    void *tmp = cuda_tmp_alloc(tmp_bytes, "top1 f32 rows reduce");
    if (!tmp) return 0;

    float *block_logits = (float *)tmp;
    uint32_t *block_tokens = (uint32_t *)((char *)tmp + block_tokens_offset);
    uint32_t *block_bad = (uint32_t *)((char *)tmp + block_bad_offset);
    float *device_logits = (float *)((char *)tmp + out_logits_offset);
    uint32_t *device_tokens = (uint32_t *)((char *)tmp + out_tokens_offset);
    uint32_t *device_bad = (uint32_t *)((char *)tmp + out_bad_offset);

    dim3 grid_blocks(n_blocks, n_rows, 1);
    top1_f32_rows_blocks_kernel<TOP1_THREADS, TOP1_ITEMS_PER_BLOCK><<<grid_blocks, TOP1_THREADS>>>(
            (const float *)logits->ptr,
            n_rows,
            n_logits,
            block_logits,
            block_tokens,
            block_bad,
            n_blocks);
    if (!cuda_ok(cudaGetLastError(), "top1 f32 rows block launch")) return 0;
    top1_f32_rows_final_kernel<TOP1_THREADS><<<n_rows, TOP1_THREADS>>>(
            block_logits,
            block_tokens,
            block_bad,
            n_rows,
            n_blocks,
            device_logits,
            device_tokens,
            device_bad);
    if (!cuda_ok(cudaGetLastError(), "top1 f32 rows final launch")) return 0;

    std::vector<uint32_t> bad(n_rows);
    if (!cuda_ok(cudaMemcpy(logits_out,
                            device_logits,
                            (size_t)n_rows * sizeof(float),
                            cudaMemcpyDeviceToHost),
                 "top1 f32 rows logits read") ||
        !cuda_ok(cudaMemcpy(tokens,
                            device_tokens,
                            (size_t)n_rows * sizeof(uint32_t),
                            cudaMemcpyDeviceToHost),
                 "top1 f32 rows tokens read") ||
        !cuda_ok(cudaMemcpy(bad.data(),
                            device_bad,
                            (size_t)n_rows * sizeof(uint32_t),
                            cudaMemcpyDeviceToHost),
                 "top1 f32 rows status read")) {
        return 0;
    }
    for (uint32_t row = 0; row < n_rows; row++) {
        if (bad[row] != 0 || tokens[row] >= n_logits) return 0;
    }
    return 1;
}

extern "C" int ds4_gpu_begin_commands(void) { return 1; }
extern "C" int ds4_gpu_flush_commands(void) { return cuda_ok(cudaDeviceSynchronize(), "flush"); }
extern "C" int ds4_gpu_end_commands(void) { return cuda_ok(cudaDeviceSynchronize(), "end commands"); }
extern "C" int ds4_gpu_synchronize(void) { return cuda_ok(cudaDeviceSynchronize(), "synchronize"); }
extern "C" int ds4_gpu_profiler_start(void) { return cuda_ok(cudaProfilerStart(), "profiler start"); }
extern "C" int ds4_gpu_profiler_stop(void) { return cuda_ok(cudaProfilerStop(), "profiler stop"); }

extern "C" int ds4_gpu_set_model_map(const void *model_map, uint64_t model_size) {
    if (!model_map || model_size == 0) return 0;
    if (g_model_host_base == model_map && g_model_registered_size == model_size) return 1;
    cuda_model_range_release_all();
    cuda_q8_f16_cache_release_all();
    g_q8_f16_disabled_after_oom = 0;
    g_q8_f16_budget_notice_printed = 0;
    for (const cuda_q8_f32_range &r : g_q8_f32_ranges) {
        (void)cudaFree(r.device_ptr);
    }
    g_q8_f32_ranges.clear();
    g_q8_f32_by_offset.clear();
    g_q8_f32_bytes = 0;
    if (g_model_device_owned && g_model_device_base) {
        (void)cudaFree((void *)g_model_device_base);
        g_model_device_owned = 0;
    }
    if (g_model_registered && g_model_host_base) {
        (void)cudaHostUnregister((void *)g_model_host_base);
        g_model_registered = 0;
    }
    g_model_host_base = model_map;
    g_model_device_base = (const char *)model_map;
    g_model_registered_size = model_size;
    g_model_range_mapping_supported = 1;
    g_model_hmm_direct = 0;
    g_model_cache_full = 0;
    if (g_model_fd >= 0 && g_model_fd_host_base == NULL) {
        g_model_fd_host_base = model_map;
    }

    const char *copy_env = getenv("DS4_CUDA_COPY_MODEL");
    if (copy_env && copy_env[0]) {
        void *dev = NULL;
        const double t0 = clock() / (double)CLOCKS_PER_SEC;
        cudaError_t err = cudaMalloc(&dev, (size_t)model_size);
        if (err == cudaSuccess) {
            fprintf(stderr, "ds4: CUDA copying %.2f GiB model to device memory\n",
                    (double)model_size / 1073741824.0);
            err = cudaMemcpy(dev, model_map, (size_t)model_size, cudaMemcpyHostToDevice);
            if (err == cudaSuccess) {
                g_model_device_base = (const char *)dev;
                g_model_device_owned = 1;
                const double t1 = clock() / (double)CLOCKS_PER_SEC;
                fprintf(stderr, "ds4: CUDA model copy complete in %.3fs\n", t1 - t0);
                return 1;
            }
            fprintf(stderr, "ds4: CUDA model copy failed: %s\n", cudaGetErrorString(err));
            (void)cudaFree(dev);
            (void)cudaGetLastError();
        } else {
            fprintf(stderr, "ds4: CUDA model allocation skipped: %s\n", cudaGetErrorString(err));
            (void)cudaGetLastError();
        }
    }

    cudaError_t err = cudaHostRegister((void *)model_map, (size_t)model_size,
                                       cudaHostRegisterMapped | cudaHostRegisterReadOnly);
    if (err == cudaSuccess) {
        void *dev = NULL;
        err = cudaHostGetDevicePointer(&dev, (void *)model_map, 0);
        if (err == cudaSuccess && dev) {
            g_model_device_base = (const char *)dev;
            g_model_registered = 1;
            fprintf(stderr, "ds4: CUDA registered %.2f GiB model mapping for device access\n",
                    (double)model_size / 1073741824.0);
        } else {
            fprintf(stderr, "ds4: CUDA host registration pointer lookup failed: %s\n", cudaGetErrorString(err));
            (void)cudaGetLastError();
        }
    } else {
        fprintf(stderr, "ds4: CUDA host registration skipped: %s\n", cudaGetErrorString(err));
        (void)cudaGetLastError();
    }
    return 1;
}

extern "C" int ds4_gpu_set_model_map_range(const void *model_map, uint64_t model_size, uint64_t map_offset, uint64_t map_size) {
    if (!ds4_gpu_set_model_map(model_map, model_size)) return 0;
    if (getenv("DS4_CUDA_COPY_MODEL_CHUNKED") != NULL &&
        !cuda_model_copy_chunked(model_map, model_size, map_offset, map_size)) {
        (void)cuda_model_prefetch_range(model_map, model_size, map_offset, map_size);
    }
    return 1;
}

extern "C" int ds4_gpu_set_model_fd(int fd) {
    g_model_fd = fd;
    g_model_fd_host_base = g_model_host_base;
    g_model_file_size = 0;
    if (g_model_direct_fd >= 0) {
        (void)close(g_model_direct_fd);
        g_model_direct_fd = -1;
    }
    g_model_direct_align = 1;
    if (fd >= 0) {
        struct stat st;
        if (fstat(fd, &st) == 0 && st.st_size > 0) {
            g_model_file_size = (uint64_t)st.st_size;
            if (st.st_blksize > 1) g_model_direct_align = (uint64_t)st.st_blksize;
        }
#if defined(__linux__) && defined(O_DIRECT)
        if (getenv("DS4_CUDA_NO_DIRECT_IO") == NULL) {
            char proc_path[64];
            snprintf(proc_path, sizeof(proc_path), "/proc/self/fd/%d", fd);
            int direct_fd = open(proc_path, O_RDONLY | O_DIRECT);
            if (direct_fd >= 0) {
                g_model_direct_fd = direct_fd;
                if (g_model_direct_align < 512) g_model_direct_align = 512;
                if (getenv("DS4_CUDA_WEIGHT_CACHE_VERBOSE")) {
                    fprintf(stderr, "ds4: CUDA model direct I/O enabled (align=%llu)\n",
                            (unsigned long long)g_model_direct_align);
                }
            } else if (getenv("DS4_CUDA_WEIGHT_CACHE_VERBOSE")) {
                fprintf(stderr, "ds4: CUDA model direct I/O unavailable: %s\n", strerror(errno));
            }
        }
#endif
    }
    return 1;
}

extern "C" int ds4_gpu_cache_model_range(const void *model_map, uint64_t model_size, uint64_t offset, uint64_t bytes, const char *label) {
    if (!model_map || bytes == 0) return 1;
    if (offset > model_size || bytes > model_size - offset) return 0;
    if (!cuda_model_range_ptr(model_map, offset, bytes, label ? label : "model_tensor")) return 0;
    return cuda_model_range_is_cached(model_map, offset, bytes);
}

extern "C" int ds4_gpu_cache_q8_f16_range(const void *model_map, uint64_t model_size, uint64_t offset, uint64_t bytes, uint64_t in_dim, uint64_t out_dim, const char *label) {
    if (!model_map || bytes == 0) return 1;
    if (offset > model_size || bytes > model_size - offset) return 0;
    static int optional_q8_preload_disabled = 0;
    if (optional_q8_preload_disabled) return 1;
    const char *cache_label = label ? label : "q8_0";
    if (getenv("DS4_CUDA_Q8_F32_PRELOAD") != NULL &&
        cuda_q8_f32_cache_allowed(cache_label, in_dim, out_dim)) {
        if (cuda_q8_f32_ptr(model_map, offset, bytes, in_dim, out_dim, cache_label)) return 1;
        optional_q8_preload_disabled = 1;
        return 1;
    }
    if (!cuda_q8_f16_preload_allowed(cache_label, in_dim, out_dim)) return 1;
    if (cuda_q8_f16_ptr(model_map, offset, bytes, in_dim, out_dim, cache_label)) return 1;
    optional_q8_preload_disabled = 1;
    return 1;
}

extern "C" void ds4_gpu_print_memory_report(const char *label) {
    size_t free_b = 0, total_b = 0;
    (void)cudaMemGetInfo(&free_b, &total_b);
    fprintf(stderr, "ds4: CUDA memory report %s: free %.2f MiB total %.2f MiB\n",
            label ? label : "", (double)free_b / 1048576.0, (double)total_b / 1048576.0);
}

extern "C" int ds4_gpu_device_count(void) {
    int n = 0;
    cudaError_t err = cudaGetDeviceCount(&n);
    if (err != cudaSuccess) {
        (void)cudaGetLastError();
        return 0;
    }
    return n;
}

static int cuda_arena_range_ok(const ds4_gpu_arena *a, uint64_t offset, uint64_t bytes) {
    return a && a->valid && offset <= a->bytes && bytes <= a->bytes - offset;
}

static int checked_mul_u64(uint64_t a, uint64_t b, uint64_t *out) {
    if (a != 0 && b > UINT64_MAX / a) return 1;
    *out = a * b;
    return 0;
}

static int f8_e4m3_b128_row_bytes(uint32_t cols, uint64_t *out) {
    if (cols == 0 || cols % 128u) return 1;
    uint64_t blocks = (uint64_t)cols / 128ull;
    if (blocks > UINT64_MAX / 129ull) return 1;
    *out = blocks * 129ull;
    return 0;
}

static int mxfp4_row_bytes(uint32_t cols, uint64_t *out) {
    if (cols == 0 || cols % 32u) return 1;
    uint64_t blocks = (uint64_t)cols / 32ull;
    if (blocks > UINT64_MAX / 17ull) return 1;
    *out = blocks * 17ull;
    return 0;
}

static int cuda_bf16_view_range_ok(const ds4_gpu_arena *arena,
                                   const ds4_gpu_bf16_matrix_view *view,
                                   const uint32_t *row_ids,
                                   uint32_t n_rows,
                                   const float *out_f32,
                                   uint64_t out_bytes,
                                   uint64_t *out_values,
                                   uint64_t *out_row_id_bytes) {
    if (!arena || !view || !row_ids || !out_f32 || !arena->valid || !arena->ptr) return 0;
    if (n_rows == 0 || view->rows == 0 || view->cols == 0) return 0;
    if (view->row_stride_elements < view->cols) return 0;
    if ((view->arena_offset & 1ull) != 0 || (view->byte_length & 1ull) != 0) return 0;
    if (!cuda_arena_range_ok(arena, view->arena_offset, view->byte_length)) return 0;

    uint64_t values = 0;
    uint64_t output_bytes = 0;
    if (checked_mul_u64((uint64_t)n_rows, (uint64_t)view->cols, &values)) return 0;
    if (checked_mul_u64(values, sizeof(float), &output_bytes)) return 0;
    if (out_bytes < output_bytes) return 0;

    uint64_t row_id_bytes = 0;
    if (checked_mul_u64((uint64_t)n_rows, sizeof(uint32_t), &row_id_bytes)) return 0;

    uint64_t total_elements = view->byte_length / sizeof(uint16_t);
    uint64_t last_row = (uint64_t)view->rows - 1u;
    uint64_t last_start = 0;
    if (checked_mul_u64(last_row, (uint64_t)view->row_stride_elements, &last_start)) return 0;
    if ((uint64_t)view->cols > total_elements ||
        last_start > total_elements - (uint64_t)view->cols) {
        return 0;
    }

    for (uint32_t i = 0; i < n_rows; i++) {
        if (row_ids[i] >= view->rows) return 0;
    }

    if (out_values) *out_values = values;
    if (out_row_id_bytes) *out_row_id_bytes = row_id_bytes;
    return 1;
}

static int cuda_f8_e4m3_b128_view_range_ok(const ds4_gpu_arena *arena,
                                           const ds4_gpu_source_row_view *view,
                                           const uint32_t *row_ids,
                                           uint32_t n_rows,
                                           const float *out_f32,
                                           uint64_t out_bytes,
                                           uint64_t *out_values,
                                           uint64_t *out_row_id_bytes) {
    if (!arena || !view || !row_ids || !out_f32 || !arena->valid || !arena->ptr) return 0;
    if (n_rows == 0 || view->rows == 0 || view->cols == 0 || view->row_stride_bytes == 0) return 0;
    if (!cuda_arena_range_ok(arena, view->arena_offset, view->byte_length)) return 0;

    uint64_t row_bytes = 0;
    if (f8_e4m3_b128_row_bytes(view->cols, &row_bytes)) return 0;
    if ((uint64_t)view->row_stride_bytes < row_bytes) return 0;

    uint64_t values = 0;
    uint64_t output_bytes = 0;
    if (checked_mul_u64((uint64_t)n_rows, (uint64_t)view->cols, &values)) return 0;
    if (checked_mul_u64(values, sizeof(float), &output_bytes)) return 0;
    if (out_bytes < output_bytes) return 0;

    uint64_t row_id_bytes = 0;
    if (checked_mul_u64((uint64_t)n_rows, sizeof(uint32_t), &row_id_bytes)) return 0;

    uint64_t last_row = (uint64_t)view->rows - 1u;
    uint64_t last_start = 0;
    if (checked_mul_u64(last_row, (uint64_t)view->row_stride_bytes, &last_start)) return 0;
    if (last_start > view->byte_length || row_bytes > view->byte_length - last_start) return 0;

    for (uint32_t i = 0; i < n_rows; i++) {
        if (row_ids[i] >= view->rows) return 0;
    }

    if (out_values) *out_values = values;
    if (out_row_id_bytes) *out_row_id_bytes = row_id_bytes;
    return 1;
}

__device__ static float arena_bf16_to_f32(uint16_t v) {
    return __uint_as_float((uint32_t)v << 16);
}

__global__ static void arena_bf16_row_gather_kernel(
        float *out,
        const uint16_t *base,
        const uint32_t *row_ids,
        uint32_t n_rows,
        uint32_t cols,
        uint32_t row_stride_elements) {
    uint64_t gid = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    uint64_t n = (uint64_t)n_rows * cols;
    if (gid >= n) return;
    uint32_t c = (uint32_t)(gid % cols);
    uint32_t r = (uint32_t)(gid / cols);
    uint64_t src = (uint64_t)row_ids[r] * row_stride_elements + c;
    out[gid] = arena_bf16_to_f32(base[src]);
}

__global__ static void arena_bf16_matmul_kernel(
        float *out,
        const uint16_t *base,
        const float *x,
        uint32_t rows,
        uint32_t cols,
        uint32_t row_stride_elements) {
    const uint32_t r = blockIdx.x;
    if (r >= rows) return;
    const uint16_t *row = base + (uint64_t)r * row_stride_elements;
    float acc = 0.0f;
    for (uint32_t c = threadIdx.x; c < cols; c += blockDim.x) {
        acc += arena_bf16_to_f32(row[c]) * x[c];
    }

    __shared__ float partial[256];
    partial[threadIdx.x] = acc;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] += partial[threadIdx.x + stride];
        __syncthreads();
    }
    if (threadIdx.x == 0) out[r] = partial[0];
}

__global__ static void arena_bf16_matmul_rows_kernel(
        float *out,
        const uint16_t *base,
        const float *x,
        uint32_t rows,
        uint32_t cols,
        uint32_t row_stride_elements,
        uint32_t n_tokens) {
    const uint32_t r = blockIdx.x;
    const uint32_t t = blockIdx.y;
    if (r >= rows || t >= n_tokens) return;
    const uint16_t *row = base + (uint64_t)r * row_stride_elements;
    const float *xt = x + (uint64_t)t * cols;
    float acc = 0.0f;
    for (uint32_t c = threadIdx.x; c < cols; c += blockDim.x) {
        acc += arena_bf16_to_f32(row[c]) * xt[c];
    }

    __shared__ float partial[256];
    partial[threadIdx.x] = acc;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] += partial[threadIdx.x + stride];
        __syncthreads();
    }
    if (threadIdx.x == 0) out[(uint64_t)t * rows + r] = partial[0];
}

__global__ static void arena_f32_matmul_kernel(
        float *out,
        const float *base,
        const float *x,
        uint32_t rows,
        uint32_t cols,
        uint32_t row_stride_bytes) {
    const uint32_t r = blockIdx.x;
    if (r >= rows) return;
    const float *row = (const float *)((const char *)base + (uint64_t)r * row_stride_bytes);
    float acc = 0.0f;
    for (uint32_t c = threadIdx.x; c < cols; c += blockDim.x) {
        acc += row[c] * x[c];
    }

    __shared__ float partial[256];
    partial[threadIdx.x] = acc;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] += partial[threadIdx.x + stride];
        __syncthreads();
    }
    if (threadIdx.x == 0) out[r] = partial[0];
}

__device__ static float arena_e8m0_to_f32(uint8_t e) {
    return __uint_as_float(e == 0 ? 0x00400000u : ((uint32_t)e << 23));
}

__device__ static float arena_e4m3fn_to_f32(uint8_t x) {
    const uint32_t sign = ((uint32_t)x & 0x80u) << 24;
    const uint32_t ax = (uint32_t)x & 0x7fu;
    if (ax == 0) return __uint_as_float(sign ? 0x80000000u : 0u);
    if (ax == 0x7f) return __uint_as_float(0x7fc00000u);
    const uint32_t exp = ax >> 3;
    const uint32_t man = ax & 0x07u;
    if (exp != 0) {
        return __uint_as_float(sign | ((exp + 120u) << 23) | (man << 20));
    }
    const uint32_t hi = man >= 4u ? 2u : (man >= 2u ? 1u : 0u);
    const uint32_t mant = (man << (23u - hi)) & 0x007fffffu;
    return __uint_as_float(sign | ((118u + hi) << 23) | mant);
}

__device__ static float arena_f8_block_scale_warp(const uint8_t *row, uint32_t c) {
    const uint32_t lane = threadIdx.x & 31u;
    const uint32_t warp_c = c - lane;
    float scale = 0.0f;
    if (lane == 0u) {
        const uint8_t *block = row + (uint64_t)(warp_c >> 7) * 129ull;
        scale = arena_e8m0_to_f32(block[0]);
    }
    return __shfl_sync(0xffffffffu, scale, 0);
}

__device__ static float arena_mxfp4_nibble_to_f32(uint8_t q) {
    switch (q & 0x0fu) {
        case 0x0u: return 0.0f;
        case 0x1u: return 0.5f;
        case 0x2u: return 1.0f;
        case 0x3u: return 1.5f;
        case 0x4u: return 2.0f;
        case 0x5u: return 3.0f;
        case 0x6u: return 4.0f;
        case 0x7u: return 6.0f;
        case 0x8u: return 0.0f;
        case 0x9u: return -0.5f;
        case 0xau: return -1.0f;
        case 0xbu: return -1.5f;
        case 0xcu: return -2.0f;
        case 0xdu: return -3.0f;
        case 0xeu: return -4.0f;
        default: return -6.0f;
    }
}

__device__ static float arena_warp_sum_f32(float v) {
    for (int offset = 16; offset > 0; offset >>= 1) {
        v += __shfl_down_sync(0xffffffffu, v, offset);
    }
    return v;
}

__device__ static float arena_block_sum_256_f32(float v) {
    __shared__ float warp_sums[8];
    v = arena_warp_sum_f32(v);
    if ((threadIdx.x & 31u) == 0u) {
        warp_sums[threadIdx.x >> 5] = v;
    }
    __syncthreads();
    v = threadIdx.x < 8u ? warp_sums[threadIdx.x] : 0.0f;
    if (threadIdx.x < 32u) {
        v = arena_warp_sum_f32(v);
    }
    return v;
}

__device__ static void arena_block_sum2_256_f32(float *a, float *b) {
    __shared__ float warp_sums_a[8];
    __shared__ float warp_sums_b[8];
    float va = arena_warp_sum_f32(*a);
    float vb = arena_warp_sum_f32(*b);
    if ((threadIdx.x & 31u) == 0u) {
        const uint32_t warp = threadIdx.x >> 5;
        warp_sums_a[warp] = va;
        warp_sums_b[warp] = vb;
    }
    __syncthreads();
    va = threadIdx.x < 8u ? warp_sums_a[threadIdx.x] : 0.0f;
    vb = threadIdx.x < 8u ? warp_sums_b[threadIdx.x] : 0.0f;
    if (threadIdx.x < 32u) {
        va = arena_warp_sum_f32(va);
        vb = arena_warp_sum_f32(vb);
    }
    *a = va;
    *b = vb;
}

__device__ static void arena_block_sum4_256_f32(float *a, float *b, float *c, float *d) {
    __shared__ float warp_sums_a[8];
    __shared__ float warp_sums_b[8];
    __shared__ float warp_sums_c[8];
    __shared__ float warp_sums_d[8];
    float va = arena_warp_sum_f32(*a);
    float vb = arena_warp_sum_f32(*b);
    float vc = arena_warp_sum_f32(*c);
    float vd = arena_warp_sum_f32(*d);
    if ((threadIdx.x & 31u) == 0u) {
        const uint32_t warp = threadIdx.x >> 5;
        warp_sums_a[warp] = va;
        warp_sums_b[warp] = vb;
        warp_sums_c[warp] = vc;
        warp_sums_d[warp] = vd;
    }
    __syncthreads();
    va = threadIdx.x < 8u ? warp_sums_a[threadIdx.x] : 0.0f;
    vb = threadIdx.x < 8u ? warp_sums_b[threadIdx.x] : 0.0f;
    vc = threadIdx.x < 8u ? warp_sums_c[threadIdx.x] : 0.0f;
    vd = threadIdx.x < 8u ? warp_sums_d[threadIdx.x] : 0.0f;
    if (threadIdx.x < 32u) {
        va = arena_warp_sum_f32(va);
        vb = arena_warp_sum_f32(vb);
        vc = arena_warp_sum_f32(vc);
        vd = arena_warp_sum_f32(vd);
    }
    *a = va;
    *b = vb;
    *c = vc;
    *d = vd;
}

__global__ static void arena_f8_e4m3_b128_row_decode_kernel(
        float *out,
        const uint8_t *base,
        const uint32_t *row_ids,
        uint32_t n_rows,
        uint32_t cols,
        uint32_t row_stride_bytes) {
    uint64_t gid = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    uint64_t n = (uint64_t)n_rows * cols;
    if (gid >= n) return;
    uint32_t c = (uint32_t)(gid % cols);
    uint32_t r = (uint32_t)(gid / cols);
    const uint8_t *row = base + (uint64_t)row_ids[r] * row_stride_bytes;
    const uint8_t *block = row + (uint64_t)(c / 128u) * 129ull;
    float scale = arena_e8m0_to_f32(block[0]);
    out[gid] = arena_e4m3fn_to_f32(block[1u + (c % 128u)]) * scale;
}

__global__ static void arena_f8_e4m3_b128_matmul_kernel(
        float *out,
        const uint8_t *base,
        const float *x,
        uint32_t rows,
        uint32_t cols,
        uint32_t row_stride_bytes) {
    const uint32_t r = blockIdx.x;
    if (r >= rows) return;
    const uint8_t *row = base + (uint64_t)r * row_stride_bytes;
    float acc = 0.0f;
    for (uint32_t c = threadIdx.x; c < cols; c += blockDim.x) {
        const uint8_t *block = row + (uint64_t)(c / 128u) * 129ull;
        const float scale = arena_e8m0_to_f32(block[0]);
        const float w = arena_e4m3fn_to_f32(block[1u + (c % 128u)]) * scale;
        acc += w * x[c];
    }

    acc = arena_block_sum_256_f32(acc);
    if (threadIdx.x == 0) out[r] = acc;
}

__global__ static void arena_f8_e4m3_b128_matmul_rows2_kernel(
        float *out,
        const uint8_t *base,
        const float *x,
        uint32_t rows,
        uint32_t cols,
        uint32_t row_stride_bytes) {
    const uint32_t r0 = blockIdx.x * 2u;
    const uint32_t r1 = r0 + 1u;
    if (r0 >= rows) return;
    const int have_r1 = r1 < rows;
    const uint8_t *row0 = base + (uint64_t)r0 * row_stride_bytes;
    const uint8_t *row1 = have_r1 ? base + (uint64_t)r1 * row_stride_bytes : row0;
    float acc0 = 0.0f;
    float acc1 = 0.0f;
    for (uint32_t c = threadIdx.x; c < cols; c += blockDim.x) {
        const float xv = x[c];
        const uint64_t block_offset = (uint64_t)(c / 128u) * 129ull;
        const uint32_t block_lane = c % 128u;
        const uint8_t *block0 = row0 + block_offset;
        const float scale0 = arena_e8m0_to_f32(block0[0]);
        acc0 += arena_e4m3fn_to_f32(block0[1u + block_lane]) * scale0 * xv;
        if (have_r1) {
            const uint8_t *block1 = row1 + block_offset;
            const float scale1 = arena_e8m0_to_f32(block1[0]);
            acc1 += arena_e4m3fn_to_f32(block1[1u + block_lane]) * scale1 * xv;
        }
    }

    arena_block_sum2_256_f32(&acc0, &acc1);
    if (threadIdx.x == 0) {
        out[r0] = acc0;
        if (have_r1) out[r1] = acc1;
    }
}

__global__ static void arena_f8_e4m3_b128_matmul_rows2_warp_scale_kernel(
        float *out,
        const uint8_t *base,
        const float *x,
        uint32_t rows,
        uint32_t cols,
        uint32_t row_stride_bytes) {
    const uint32_t r0 = blockIdx.x * 2u;
    const uint32_t r1 = r0 + 1u;
    if (r0 >= rows) return;
    const int have_r1 = r1 < rows;
    const uint8_t *row0 = base + (uint64_t)r0 * row_stride_bytes;
    const uint8_t *row1 = have_r1 ? base + (uint64_t)r1 * row_stride_bytes : row0;
    float acc0 = 0.0f;
    float acc1 = 0.0f;
    for (uint32_t c = threadIdx.x; c < cols; c += blockDim.x) {
        const float xv = x[c];
        const uint64_t block_offset = (uint64_t)(c >> 7) * 129ull;
        const uint32_t block_lane = c & 127u;
        const uint8_t *block0 = row0 + block_offset;
        const float scale0 = arena_f8_block_scale_warp(row0, c);
        acc0 += arena_e4m3fn_to_f32(block0[1u + block_lane]) * scale0 * xv;
        if (have_r1) {
            const uint8_t *block1 = row1 + block_offset;
            const float scale1 = arena_f8_block_scale_warp(row1, c);
            acc1 += arena_e4m3fn_to_f32(block1[1u + block_lane]) * scale1 * xv;
        }
    }

    arena_block_sum2_256_f32(&acc0, &acc1);
    if (threadIdx.x == 0) {
        out[r0] = acc0;
        if (have_r1) out[r1] = acc1;
    }
}

__global__ static void arena_f8_e4m3_b128_matmul_rows4_kernel(
        float *out,
        const uint8_t *base,
        const float *x,
        uint32_t rows,
        uint32_t cols,
        uint32_t row_stride_bytes) {
    const uint32_t r0 = blockIdx.x * 4u;
    const uint32_t r1 = r0 + 1u;
    const uint32_t r2 = r0 + 2u;
    const uint32_t r3 = r0 + 3u;
    if (r0 >= rows) return;
    const int have_r1 = r1 < rows;
    const int have_r2 = r2 < rows;
    const int have_r3 = r3 < rows;
    const uint8_t *row0 = base + (uint64_t)r0 * row_stride_bytes;
    const uint8_t *row1 = have_r1 ? base + (uint64_t)r1 * row_stride_bytes : row0;
    const uint8_t *row2 = have_r2 ? base + (uint64_t)r2 * row_stride_bytes : row0;
    const uint8_t *row3 = have_r3 ? base + (uint64_t)r3 * row_stride_bytes : row0;
    float acc0 = 0.0f;
    float acc1 = 0.0f;
    float acc2 = 0.0f;
    float acc3 = 0.0f;
    for (uint32_t c = threadIdx.x; c < cols; c += blockDim.x) {
        const float xv = x[c];
        const uint64_t block_offset = (uint64_t)(c / 128u) * 129ull;
        const uint32_t block_lane = c % 128u;
        const uint8_t *block0 = row0 + block_offset;
        const float scale0 = arena_e8m0_to_f32(block0[0]);
        acc0 += arena_e4m3fn_to_f32(block0[1u + block_lane]) * scale0 * xv;
        if (have_r1) {
            const uint8_t *block1 = row1 + block_offset;
            const float scale1 = arena_e8m0_to_f32(block1[0]);
            acc1 += arena_e4m3fn_to_f32(block1[1u + block_lane]) * scale1 * xv;
        }
        if (have_r2) {
            const uint8_t *block2 = row2 + block_offset;
            const float scale2 = arena_e8m0_to_f32(block2[0]);
            acc2 += arena_e4m3fn_to_f32(block2[1u + block_lane]) * scale2 * xv;
        }
        if (have_r3) {
            const uint8_t *block3 = row3 + block_offset;
            const float scale3 = arena_e8m0_to_f32(block3[0]);
            acc3 += arena_e4m3fn_to_f32(block3[1u + block_lane]) * scale3 * xv;
        }
    }

    arena_block_sum4_256_f32(&acc0, &acc1, &acc2, &acc3);
    if (threadIdx.x == 0) {
        out[r0] = acc0;
        if (have_r1) out[r1] = acc1;
        if (have_r2) out[r2] = acc2;
        if (have_r3) out[r3] = acc3;
    }
}

__global__ static void arena_f8_e4m3_b128_matmul_batch_kernel(
        float *out,
        const uint8_t *base,
        const float *x,
        uint32_t rows,
        uint32_t cols,
        uint32_t row_stride_bytes) {
    const uint32_t r = blockIdx.x;
    const uint32_t tok = blockIdx.y;
    if (r >= rows) return;
    const uint8_t *row = base + (uint64_t)r * row_stride_bytes;
    const float *x_row = x + (uint64_t)tok * cols;
    float acc = 0.0f;
    for (uint32_t c = threadIdx.x; c < cols; c += blockDim.x) {
        const uint8_t *block = row + (uint64_t)(c / 128u) * 129ull;
        const float scale = arena_e8m0_to_f32(block[0]);
        const float w = arena_e4m3fn_to_f32(block[1u + (c % 128u)]) * scale;
        acc += w * x_row[c];
    }

    acc = arena_block_sum_256_f32(acc);
    if (threadIdx.x == 0) out[(uint64_t)tok * rows + r] = acc;
}

__global__ static void arena_f8_e4m3_b128_matmul_batch_add_kernel(
        float *out,
        const float *add,
        const uint8_t *base,
        const float *x,
        uint32_t rows,
        uint32_t cols,
        uint32_t row_stride_bytes) {
    const uint32_t r = blockIdx.x;
    const uint32_t tok = blockIdx.y;
    if (r >= rows) return;
    const uint8_t *row = base + (uint64_t)r * row_stride_bytes;
    const float *x_row = x + (uint64_t)tok * cols;
    float acc = 0.0f;
    for (uint32_t c = threadIdx.x; c < cols; c += blockDim.x) {
        const uint8_t *block = row + (uint64_t)(c / 128u) * 129ull;
        const float scale = arena_e8m0_to_f32(block[0]);
        const float w = arena_e4m3fn_to_f32(block[1u + (c % 128u)]) * scale;
        acc += w * x_row[c];
    }

    acc = arena_block_sum_256_f32(acc);
    if (threadIdx.x == 0) {
        const uint64_t idx = (uint64_t)tok * rows + r;
        out[idx] = acc + add[idx];
    }
}

__global__ static void arena_f8_e4m3_b128_matmul_batch_rows2_kernel(
        float *out,
        const uint8_t *base,
        const float *x,
        uint32_t rows,
        uint32_t cols,
        uint32_t row_stride_bytes) {
    const uint32_t r0 = blockIdx.x * 2u;
    const uint32_t r1 = r0 + 1u;
    const uint32_t tok = blockIdx.y;
    if (r0 >= rows) return;
    const int have_r1 = r1 < rows;
    const uint8_t *row0 = base + (uint64_t)r0 * row_stride_bytes;
    const uint8_t *row1 = have_r1 ? base + (uint64_t)r1 * row_stride_bytes : row0;
    const float *x_row = x + (uint64_t)tok * cols;
    float acc0 = 0.0f;
    float acc1 = 0.0f;
    for (uint32_t c = threadIdx.x; c < cols; c += blockDim.x) {
        const float xv = x_row[c];
        const uint64_t block_offset = (uint64_t)(c / 128u) * 129ull;
        const uint32_t block_lane = c % 128u;
        const uint8_t *block0 = row0 + block_offset;
        const float scale0 = arena_e8m0_to_f32(block0[0]);
        acc0 += arena_e4m3fn_to_f32(block0[1u + block_lane]) * scale0 * xv;
        if (have_r1) {
            const uint8_t *block1 = row1 + block_offset;
            const float scale1 = arena_e8m0_to_f32(block1[0]);
            acc1 += arena_e4m3fn_to_f32(block1[1u + block_lane]) * scale1 * xv;
        }
    }

    arena_block_sum2_256_f32(&acc0, &acc1);
    if (threadIdx.x == 0) {
        out[(uint64_t)tok * rows + r0] = acc0;
        if (have_r1) out[(uint64_t)tok * rows + r1] = acc1;
    }
}

__global__ static void arena_f8_e4m3_b128_matmul_batch_rows2_add_kernel(
        float *out,
        const float *add,
        const uint8_t *base,
        const float *x,
        uint32_t rows,
        uint32_t cols,
        uint32_t row_stride_bytes) {
    const uint32_t r0 = blockIdx.x * 2u;
    const uint32_t r1 = r0 + 1u;
    const uint32_t tok = blockIdx.y;
    if (r0 >= rows) return;
    const int have_r1 = r1 < rows;
    const uint8_t *row0 = base + (uint64_t)r0 * row_stride_bytes;
    const uint8_t *row1 = have_r1 ? base + (uint64_t)r1 * row_stride_bytes : row0;
    const float *x_row = x + (uint64_t)tok * cols;
    const uint64_t out_base = (uint64_t)tok * rows;
    float acc0 = 0.0f;
    float acc1 = 0.0f;
    for (uint32_t c = threadIdx.x; c < cols; c += blockDim.x) {
        const float xv = x_row[c];
        const uint64_t block_offset = (uint64_t)(c / 128u) * 129ull;
        const uint32_t block_lane = c % 128u;
        const uint8_t *block0 = row0 + block_offset;
        const float scale0 = arena_e8m0_to_f32(block0[0]);
        acc0 += arena_e4m3fn_to_f32(block0[1u + block_lane]) * scale0 * xv;
        if (have_r1) {
            const uint8_t *block1 = row1 + block_offset;
            const float scale1 = arena_e8m0_to_f32(block1[0]);
            acc1 += arena_e4m3fn_to_f32(block1[1u + block_lane]) * scale1 * xv;
        }
    }

    arena_block_sum2_256_f32(&acc0, &acc1);
    if (threadIdx.x == 0) {
        out[out_base + r0] = acc0 + add[out_base + r0];
        if (have_r1) out[out_base + r1] = acc1 + add[out_base + r1];
    }
}

__global__ static void arena_f8_e4m3_b128_matmul_batch_hmma_shared_down_kernel(
        float *out,
        const uint8_t *base,
        const float *x,
        uint32_t row_stride_bytes,
        uint32_t n_tokens) {
#if __CUDA_ARCH__ >= 700
    namespace wmma = nvcuda::wmma;
    enum {
        DS4_ROWS = 4096,
        DS4_COLS = 2048,
        WARPS_PER_BLOCK = 4,
        TILE_M = 16,
        TILE_N = 16,
        TILE_K = 16,
        ROWS_PER_BLOCK = WARPS_PER_BLOCK * TILE_N,
    };

    const uint32_t tid = threadIdx.x;
    const uint32_t warp = tid >> 5u;
    if (warp >= WARPS_PER_BLOCK) return;

    const uint32_t row_block = blockIdx.x * ROWS_PER_BLOCK;

    __shared__ __half a_sh[TILE_M * TILE_K];
    __shared__ __half b_sh[WARPS_PER_BLOCK * TILE_K * TILE_N];
    __shared__ float c_sh[WARPS_PER_BLOCK * TILE_M * TILE_N];

    wmma::fragment<wmma::matrix_a, TILE_M, TILE_N, TILE_K, __half, wmma::row_major> a_frag;
    wmma::fragment<wmma::matrix_b, TILE_M, TILE_N, TILE_K, __half, wmma::col_major> b_frag;
    wmma::fragment<wmma::accumulator, TILE_M, TILE_N, TILE_K, float> c_frag;
    wmma::fill_fragment(c_frag, 0.0f);

    for (uint32_t k0 = 0; k0 < DS4_COLS; k0 += TILE_K) {
        for (uint32_t i = tid; i < TILE_M * TILE_K; i += blockDim.x) {
            const uint32_t token = i >> 4u;
            const uint32_t k = i & 15u;
            float v = 0.0f;
            if (token < n_tokens) {
                v = x[(uint64_t)token * DS4_COLS + k0 + k];
            }
            a_sh[i] = __float2half_rn(v);
        }

        for (uint32_t i = tid; i < WARPS_PER_BLOCK * TILE_K * TILE_N; i += blockDim.x) {
            const uint32_t wtile = i >> 8u;
            const uint32_t local = i & 255u;
            const uint32_t out_col = local >> 4u;
            const uint32_t k = local & 15u;
            const uint32_t row = row_block + wtile * TILE_N + out_col;
            float w = 0.0f;
            if (row < DS4_ROWS) {
                const uint32_t col = k0 + k;
                const uint8_t *row_base = base + (uint64_t)row * row_stride_bytes;
                const uint8_t *block = row_base + (uint64_t)(col >> 7u) * 129ull;
                w = arena_e4m3fn_to_f32(block[1u + (col & 127u)]) *
                    arena_e8m0_to_f32(block[0]);
            }
            b_sh[i] = __float2half_rn(w);
        }
        __syncthreads();

        wmma::load_matrix_sync(a_frag, a_sh, TILE_K);
        wmma::load_matrix_sync(b_frag, b_sh + warp * TILE_K * TILE_N, TILE_K);
        wmma::mma_sync(c_frag, a_frag, b_frag, c_frag);
        __syncthreads();
    }

    wmma::store_matrix_sync(c_sh + warp * TILE_M * TILE_N,
                            c_frag,
                            TILE_N,
                            wmma::mem_row_major);
    __syncthreads();

    for (uint32_t i = tid; i < WARPS_PER_BLOCK * TILE_M * TILE_N; i += blockDim.x) {
        const uint32_t wtile = i >> 8u;
        const uint32_t local = i & 255u;
        const uint32_t token = local >> 4u;
        const uint32_t out_col = local & 15u;
        const uint32_t row = row_block + wtile * TILE_N + out_col;
        if (token < n_tokens && row < DS4_ROWS) {
            out[(uint64_t)token * DS4_ROWS + row] =
                c_sh[wtile * TILE_M * TILE_N + local];
        }
    }
#else
    (void)out;
    (void)base;
    (void)x;
    (void)row_stride_bytes;
    (void)n_tokens;
#endif
}

__global__ static void arena_f8_e4m3_b128_matmul_batch_hmma_shared_down_add_kernel(
        float *out,
        const float *add,
        const uint8_t *base,
        const float *x,
        uint32_t row_stride_bytes,
        uint32_t n_tokens) {
#if __CUDA_ARCH__ >= 700
    namespace wmma = nvcuda::wmma;
    enum {
        DS4_ROWS = 4096,
        DS4_COLS = 2048,
        WARPS_PER_BLOCK = 4,
        TILE_M = 16,
        TILE_N = 16,
        TILE_K = 16,
        ROWS_PER_BLOCK = WARPS_PER_BLOCK * TILE_N,
    };

    const uint32_t tid = threadIdx.x;
    const uint32_t warp = tid >> 5u;
    if (warp >= WARPS_PER_BLOCK) return;

    const uint32_t row_block = blockIdx.x * ROWS_PER_BLOCK;

    __shared__ __half a_sh[TILE_M * TILE_K];
    __shared__ __half b_sh[WARPS_PER_BLOCK * TILE_K * TILE_N];
    __shared__ float c_sh[WARPS_PER_BLOCK * TILE_M * TILE_N];

    wmma::fragment<wmma::matrix_a, TILE_M, TILE_N, TILE_K, __half, wmma::row_major> a_frag;
    wmma::fragment<wmma::matrix_b, TILE_M, TILE_N, TILE_K, __half, wmma::col_major> b_frag;
    wmma::fragment<wmma::accumulator, TILE_M, TILE_N, TILE_K, float> c_frag;
    wmma::fill_fragment(c_frag, 0.0f);

    for (uint32_t k0 = 0; k0 < DS4_COLS; k0 += TILE_K) {
        for (uint32_t i = tid; i < TILE_M * TILE_K; i += blockDim.x) {
            const uint32_t token = i >> 4u;
            const uint32_t k = i & 15u;
            float v = 0.0f;
            if (token < n_tokens) {
                v = x[(uint64_t)token * DS4_COLS + k0 + k];
            }
            a_sh[i] = __float2half_rn(v);
        }

        for (uint32_t i = tid; i < WARPS_PER_BLOCK * TILE_K * TILE_N; i += blockDim.x) {
            const uint32_t wtile = i >> 8u;
            const uint32_t local = i & 255u;
            const uint32_t out_col = local >> 4u;
            const uint32_t k = local & 15u;
            const uint32_t row = row_block + wtile * TILE_N + out_col;
            float w = 0.0f;
            if (row < DS4_ROWS) {
                const uint32_t col = k0 + k;
                const uint8_t *row_base = base + (uint64_t)row * row_stride_bytes;
                const uint8_t *block = row_base + (uint64_t)(col >> 7u) * 129ull;
                w = arena_e4m3fn_to_f32(block[1u + (col & 127u)]) *
                    arena_e8m0_to_f32(block[0]);
            }
            b_sh[i] = __float2half_rn(w);
        }
        __syncthreads();

        wmma::load_matrix_sync(a_frag, a_sh, TILE_K);
        wmma::load_matrix_sync(b_frag, b_sh + warp * TILE_K * TILE_N, TILE_K);
        wmma::mma_sync(c_frag, a_frag, b_frag, c_frag);
        __syncthreads();
    }

    wmma::store_matrix_sync(c_sh + warp * TILE_M * TILE_N,
                            c_frag,
                            TILE_N,
                            wmma::mem_row_major);
    __syncthreads();

    for (uint32_t i = tid; i < WARPS_PER_BLOCK * TILE_M * TILE_N; i += blockDim.x) {
        const uint32_t wtile = i >> 8u;
        const uint32_t local = i & 255u;
        const uint32_t token = local >> 4u;
        const uint32_t out_col = local & 15u;
        const uint32_t row = row_block + wtile * TILE_N + out_col;
        if (token < n_tokens && row < DS4_ROWS) {
            const uint64_t idx = (uint64_t)token * DS4_ROWS + row;
            out[idx] = c_sh[wtile * TILE_M * TILE_N + local] + add[idx];
        }
    }
#else
    (void)out;
    (void)add;
    (void)base;
    (void)x;
    (void)row_stride_bytes;
    (void)n_tokens;
#endif
}

__global__ static void arena_f8_e4m3_b128_matmul_batch_hmma_attn_kernel(
        float *out,
        const uint8_t *base,
        const float *x,
        uint32_t rows,
        uint32_t cols,
        uint32_t row_stride_bytes,
        uint32_t n_tokens) {
#if __CUDA_ARCH__ >= 700
    namespace wmma = nvcuda::wmma;
    enum {
        WARPS_PER_BLOCK = 4,
        TILE_M = 16,
        TILE_N = 16,
        TILE_K = 16,
        ROWS_PER_BLOCK = WARPS_PER_BLOCK * TILE_N,
    };

    const uint32_t tid = threadIdx.x;
    const uint32_t warp = tid >> 5u;
    if (warp >= WARPS_PER_BLOCK) return;

    const uint32_t row_block = blockIdx.x * ROWS_PER_BLOCK;

    __shared__ __half a_sh[TILE_M * TILE_K];
    __shared__ __half b_sh[WARPS_PER_BLOCK * TILE_K * TILE_N];
    __shared__ float c_sh[WARPS_PER_BLOCK * TILE_M * TILE_N];

    wmma::fragment<wmma::matrix_a, TILE_M, TILE_N, TILE_K, __half, wmma::row_major> a_frag;
    wmma::fragment<wmma::matrix_b, TILE_M, TILE_N, TILE_K, __half, wmma::col_major> b_frag;
    wmma::fragment<wmma::accumulator, TILE_M, TILE_N, TILE_K, float> c_frag;
    wmma::fill_fragment(c_frag, 0.0f);

    for (uint32_t k0 = 0; k0 < cols; k0 += TILE_K) {
        for (uint32_t i = tid; i < TILE_M * TILE_K; i += blockDim.x) {
            const uint32_t token = i >> 4u;
            const uint32_t k = i & 15u;
            const float v = token < n_tokens ? x[(uint64_t)token * cols + k0 + k] : 0.0f;
            a_sh[i] = __float2half_rn(v);
        }

        for (uint32_t i = tid; i < WARPS_PER_BLOCK * TILE_K * TILE_N; i += blockDim.x) {
            const uint32_t wtile = i >> 8u;
            const uint32_t local = i & 255u;
            const uint32_t out_col = local >> 4u;
            const uint32_t k = local & 15u;
            const uint32_t row = row_block + wtile * TILE_N + out_col;
            float w = 0.0f;
            if (row < rows) {
                const uint32_t col = k0 + k;
                const uint64_t block_offset = (uint64_t)(col >> 7u) * 129ull;
                const uint32_t block_lane = col & 127u;
                const uint8_t *row_base = base + (uint64_t)row * row_stride_bytes;
                const uint8_t *block = row_base + block_offset;
                w = arena_e4m3fn_to_f32(block[1u + block_lane]) *
                    arena_e8m0_to_f32(block[0]);
            }
            b_sh[i] = __float2half_rn(w);
        }
        __syncthreads();

        wmma::load_matrix_sync(a_frag, a_sh, TILE_K);
        wmma::load_matrix_sync(b_frag, b_sh + warp * TILE_K * TILE_N, TILE_K);
        wmma::mma_sync(c_frag, a_frag, b_frag, c_frag);
        __syncthreads();
    }

    wmma::store_matrix_sync(c_sh + warp * TILE_M * TILE_N,
                            c_frag,
                            TILE_N,
                            wmma::mem_row_major);
    __syncthreads();

    for (uint32_t i = tid; i < WARPS_PER_BLOCK * TILE_M * TILE_N; i += blockDim.x) {
        const uint32_t wtile = i >> 8u;
        const uint32_t local = i & 255u;
        const uint32_t token = local >> 4u;
        const uint32_t out_col = local & 15u;
        const uint32_t row = row_block + wtile * TILE_N + out_col;
        if (token < n_tokens && row < rows) {
            out[(uint64_t)token * rows + row] =
                c_sh[wtile * TILE_M * TILE_N + local];
        }
    }
#else
    (void)out;
    (void)base;
    (void)x;
    (void)rows;
    (void)cols;
    (void)row_stride_bytes;
    (void)n_tokens;
#endif
}

__global__ static void arena_f8_e4m3_b128_matmul_grouped_batch_hmma_ds4_attn_o_kernel(
        float *out,
        const uint8_t *base,
        const float *x,
        uint32_t row_stride_bytes,
        uint32_t n_tokens) {
#if __CUDA_ARCH__ >= 700
    namespace wmma = nvcuda::wmma;
    enum {
        DS4_GROUPS = 8,
        DS4_ROWS_PER_GROUP = 1024,
        DS4_COLS_PER_GROUP = 4096,
        DS4_ROWS = DS4_GROUPS * DS4_ROWS_PER_GROUP,
        DS4_INPUT_COLS = DS4_GROUPS * DS4_COLS_PER_GROUP,
        WARPS_PER_BLOCK = 4,
        TILE_M = 16,
        TILE_N = 16,
        TILE_K = 16,
        ROWS_PER_BLOCK = WARPS_PER_BLOCK * TILE_N,
    };

    const uint32_t tid = threadIdx.x;
    const uint32_t warp = tid >> 5u;
    if (warp >= WARPS_PER_BLOCK) return;

    const uint32_t row_block = blockIdx.x * ROWS_PER_BLOCK;
    const uint32_t group = row_block / DS4_ROWS_PER_GROUP;

    __shared__ __half a_sh[TILE_M * TILE_K];
    __shared__ __half b_sh[WARPS_PER_BLOCK * TILE_K * TILE_N];
    __shared__ float c_sh[WARPS_PER_BLOCK * TILE_M * TILE_N];

    wmma::fragment<wmma::matrix_a, TILE_M, TILE_N, TILE_K, __half, wmma::row_major> a_frag;
    wmma::fragment<wmma::matrix_b, TILE_M, TILE_N, TILE_K, __half, wmma::col_major> b_frag;
    wmma::fragment<wmma::accumulator, TILE_M, TILE_N, TILE_K, float> c_frag;
    wmma::fill_fragment(c_frag, 0.0f);

    for (uint32_t k0 = 0; k0 < DS4_COLS_PER_GROUP; k0 += TILE_K) {
        for (uint32_t i = tid; i < TILE_M * TILE_K; i += blockDim.x) {
            const uint32_t token = i >> 4u;
            const uint32_t k = i & 15u;
            float v = 0.0f;
            if (token < n_tokens && group < DS4_GROUPS) {
                v = x[(uint64_t)token * DS4_INPUT_COLS +
                      (uint64_t)group * DS4_COLS_PER_GROUP +
                      k0 + k];
            }
            a_sh[i] = __float2half_rn(v);
        }

        for (uint32_t i = tid; i < WARPS_PER_BLOCK * TILE_K * TILE_N; i += blockDim.x) {
            const uint32_t wtile = i >> 8u;
            const uint32_t local = i & 255u;
            const uint32_t out_col = local >> 4u;
            const uint32_t k = local & 15u;
            const uint32_t row = row_block + wtile * TILE_N + out_col;
            float w = 0.0f;
            if (row < DS4_ROWS) {
                const uint32_t col = k0 + k;
                const uint8_t *row_base = base + (uint64_t)row * row_stride_bytes;
                const uint8_t *block = row_base + (uint64_t)(col >> 7u) * 129ull;
                w = arena_e4m3fn_to_f32(block[1u + (col & 127u)]) *
                    arena_e8m0_to_f32(block[0]);
            }
            b_sh[i] = __float2half_rn(w);
        }
        __syncthreads();

        wmma::load_matrix_sync(a_frag, a_sh, TILE_K);
        wmma::load_matrix_sync(b_frag, b_sh + warp * TILE_K * TILE_N, TILE_K);
        wmma::mma_sync(c_frag, a_frag, b_frag, c_frag);
        __syncthreads();
    }

    wmma::store_matrix_sync(c_sh + warp * TILE_M * TILE_N,
                            c_frag,
                            TILE_N,
                            wmma::mem_row_major);
    __syncthreads();

    for (uint32_t i = tid; i < WARPS_PER_BLOCK * TILE_M * TILE_N; i += blockDim.x) {
        const uint32_t wtile = i >> 8u;
        const uint32_t local = i & 255u;
        const uint32_t token = local >> 4u;
        const uint32_t out_col = local & 15u;
        const uint32_t row = row_block + wtile * TILE_N + out_col;
        if (token < n_tokens && row < DS4_ROWS) {
            out[(uint64_t)token * DS4_ROWS + row] =
                c_sh[wtile * TILE_M * TILE_N + local];
        }
    }
#else
    (void)out;
    (void)base;
    (void)x;
    (void)row_stride_bytes;
    (void)n_tokens;
#endif
}

__global__ static void arena_f8_e4m3_b128_matmul_ptrs_kernel(
        float *out,
        const uint8_t *base,
        const float *const *x_row_ptrs,
        uint32_t rows,
        uint32_t cols,
        uint32_t row_stride_bytes) {
    const uint32_t r = blockIdx.x;
    const uint32_t tok = blockIdx.y;
    if (r >= rows) return;
    const uint8_t *row = base + (uint64_t)r * row_stride_bytes;
    const float *x_row = x_row_ptrs[tok];
    float acc = 0.0f;
    for (uint32_t c = threadIdx.x; c < cols; c += blockDim.x) {
        const uint8_t *block = row + (uint64_t)(c / 128u) * 129ull;
        const float scale = arena_e8m0_to_f32(block[0]);
        const float w = arena_e4m3fn_to_f32(block[1u + (c % 128u)]) * scale;
        acc += w * x_row[c];
    }

    acc = arena_block_sum_256_f32(acc);
    if (threadIdx.x == 0) out[(uint64_t)tok * rows + r] = acc;
}

__global__ static void arena_f8_e4m3_b128_matmul_ptrs_rows2_kernel(
        float *out,
        const uint8_t *base,
        const float *const *x_row_ptrs,
        uint32_t rows,
        uint32_t cols,
        uint32_t row_stride_bytes) {
    const uint32_t r0 = blockIdx.x * 2u;
    const uint32_t r1 = r0 + 1u;
    const uint32_t tok = blockIdx.y;
    if (r0 >= rows) return;
    const int have_r1 = r1 < rows;
    const uint8_t *row0 = base + (uint64_t)r0 * row_stride_bytes;
    const uint8_t *row1 = have_r1 ? base + (uint64_t)r1 * row_stride_bytes : row0;
    const float *x_row = x_row_ptrs[tok];
    float acc0 = 0.0f;
    float acc1 = 0.0f;
    for (uint32_t c = threadIdx.x; c < cols; c += blockDim.x) {
        const float xv = x_row[c];
        const uint64_t block_offset = (uint64_t)(c / 128u) * 129ull;
        const uint32_t block_lane = c % 128u;
        const uint8_t *block0 = row0 + block_offset;
        const float scale0 = arena_e8m0_to_f32(block0[0]);
        acc0 += arena_e4m3fn_to_f32(block0[1u + block_lane]) * scale0 * xv;
        if (have_r1) {
            const uint8_t *block1 = row1 + block_offset;
            const float scale1 = arena_e8m0_to_f32(block1[0]);
            acc1 += arena_e4m3fn_to_f32(block1[1u + block_lane]) * scale1 * xv;
        }
    }

    arena_block_sum2_256_f32(&acc0, &acc1);
    if (threadIdx.x == 0) {
        out[(uint64_t)tok * rows + r0] = acc0;
        if (have_r1) out[(uint64_t)tok * rows + r1] = acc1;
    }
}

__global__ static void arena_f8_e4m3_b128_matmul_ptr_table_hmma_attn_kernel(
        float *out,
        const uint8_t *base,
        const float *const *x_rows,
        uint32_t rows,
        uint32_t cols,
        uint32_t row_stride_bytes,
        uint32_t n_tokens) {
#if __CUDA_ARCH__ >= 700
    namespace wmma = nvcuda::wmma;
    enum {
        WARPS_PER_BLOCK = 4,
        TILE_M = 16,
        TILE_N = 16,
        TILE_K = 16,
        ROWS_PER_BLOCK = WARPS_PER_BLOCK * TILE_N,
    };

    const uint32_t tid = threadIdx.x;
    const uint32_t warp = tid >> 5u;
    if (warp >= WARPS_PER_BLOCK) return;

    const uint32_t row_block = blockIdx.x * ROWS_PER_BLOCK;

    __shared__ const float *x_ptrs[TILE_M];
    __shared__ __half a_sh[TILE_M * TILE_K];
    __shared__ __half b_sh[WARPS_PER_BLOCK * TILE_K * TILE_N];
    __shared__ float c_sh[WARPS_PER_BLOCK * TILE_M * TILE_N];

    for (uint32_t i = tid; i < TILE_M; i += blockDim.x) {
        x_ptrs[i] = i < n_tokens ? x_rows[i] : nullptr;
    }
    __syncthreads();

    wmma::fragment<wmma::matrix_a, TILE_M, TILE_N, TILE_K, __half, wmma::row_major> a_frag;
    wmma::fragment<wmma::matrix_b, TILE_M, TILE_N, TILE_K, __half, wmma::col_major> b_frag;
    wmma::fragment<wmma::accumulator, TILE_M, TILE_N, TILE_K, float> c_frag;
    wmma::fill_fragment(c_frag, 0.0f);

    for (uint32_t k0 = 0; k0 < cols; k0 += TILE_K) {
        for (uint32_t i = tid; i < TILE_M * TILE_K; i += blockDim.x) {
            const uint32_t token = i >> 4u;
            const uint32_t k = i & 15u;
            const float *x = x_ptrs[token];
            const float v = x ? x[k0 + k] : 0.0f;
            a_sh[i] = __float2half_rn(v);
        }

        for (uint32_t i = tid; i < WARPS_PER_BLOCK * TILE_K * TILE_N; i += blockDim.x) {
            const uint32_t wtile = i >> 8u;
            const uint32_t local = i & 255u;
            const uint32_t out_col = local >> 4u;
            const uint32_t k = local & 15u;
            const uint32_t row = row_block + wtile * TILE_N + out_col;
            float w = 0.0f;
            if (row < rows) {
                const uint32_t col = k0 + k;
                const uint64_t block_offset = (uint64_t)(col >> 7u) * 129ull;
                const uint32_t block_lane = col & 127u;
                const uint8_t *row_base = base + (uint64_t)row * row_stride_bytes;
                const uint8_t *block = row_base + block_offset;
                w = arena_e4m3fn_to_f32(block[1u + block_lane]) *
                    arena_e8m0_to_f32(block[0]);
            }
            b_sh[i] = __float2half_rn(w);
        }
        __syncthreads();

        wmma::load_matrix_sync(a_frag, a_sh, TILE_K);
        wmma::load_matrix_sync(b_frag, b_sh + warp * TILE_K * TILE_N, TILE_K);
        wmma::mma_sync(c_frag, a_frag, b_frag, c_frag);
        __syncthreads();
    }

    wmma::store_matrix_sync(c_sh + warp * TILE_M * TILE_N,
                            c_frag,
                            TILE_N,
                            wmma::mem_row_major);
    __syncthreads();

    for (uint32_t i = tid; i < WARPS_PER_BLOCK * TILE_M * TILE_N; i += blockDim.x) {
        const uint32_t wtile = i >> 8u;
        const uint32_t local = i & 255u;
        const uint32_t token = local >> 4u;
        const uint32_t out_col = local & 15u;
        const uint32_t row = row_block + wtile * TILE_N + out_col;
        if (token < n_tokens && row < rows) {
            out[(uint64_t)token * rows + row] =
                c_sh[wtile * TILE_M * TILE_N + local];
        }
    }
#else
    (void)out;
    (void)base;
    (void)x_rows;
    (void)rows;
    (void)cols;
    (void)row_stride_bytes;
    (void)n_tokens;
#endif
}

__global__ static void arena_f8_e4m3_b128_matmul_grouped_kernel(
        float *out,
        const uint8_t *base,
        const float *x,
        uint32_t groups,
        uint32_t rows_per_group,
        uint32_t cols_per_group,
        uint32_t row_stride_bytes) {
    const uint32_t r = blockIdx.x;
    const uint32_t rows = groups * rows_per_group;
    if (r >= rows) return;
    const uint32_t group = r / rows_per_group;
    const uint8_t *row = base + (uint64_t)r * row_stride_bytes;
    const float *x_row = x + (uint64_t)group * cols_per_group;
    float acc = 0.0f;
    for (uint32_t c = threadIdx.x; c < cols_per_group; c += blockDim.x) {
        const uint8_t *block = row + (uint64_t)(c / 128u) * 129ull;
        const float scale = arena_e8m0_to_f32(block[0]);
        const float w = arena_e4m3fn_to_f32(block[1u + (c % 128u)]) * scale;
        acc += w * x_row[c];
    }

    acc = arena_block_sum_256_f32(acc);
    if (threadIdx.x == 0) out[r] = acc;
}

__global__ static void arena_f8_e4m3_b128_matmul_grouped_rows2_kernel(
        float *out,
        const uint8_t *base,
        const float *x,
        uint32_t groups,
        uint32_t rows_per_group,
        uint32_t cols_per_group,
        uint32_t row_stride_bytes) {
    const uint32_t r0 = blockIdx.x * 2u;
    const uint32_t r1 = r0 + 1u;
    const uint32_t rows = groups * rows_per_group;
    if (r0 >= rows) return;
    const int have_r1 = r1 < rows;
    const uint32_t group0 = r0 / rows_per_group;
    const uint32_t group1 = have_r1 ? r1 / rows_per_group : group0;
    const uint8_t *row0 = base + (uint64_t)r0 * row_stride_bytes;
    const uint8_t *row1 = have_r1 ? base + (uint64_t)r1 * row_stride_bytes : row0;
    const float *x0 = x + (uint64_t)group0 * cols_per_group;
    const float *x1 = x + (uint64_t)group1 * cols_per_group;
    float acc0 = 0.0f;
    float acc1 = 0.0f;
    for (uint32_t c = threadIdx.x; c < cols_per_group; c += blockDim.x) {
        const uint64_t block_offset = (uint64_t)(c / 128u) * 129ull;
        const uint32_t block_lane = c % 128u;
        const uint8_t *block0 = row0 + block_offset;
        const float scale0 = arena_e8m0_to_f32(block0[0]);
        acc0 += arena_e4m3fn_to_f32(block0[1u + block_lane]) * scale0 * x0[c];
        if (have_r1) {
            const uint8_t *block1 = row1 + block_offset;
            const float scale1 = arena_e8m0_to_f32(block1[0]);
            acc1 += arena_e4m3fn_to_f32(block1[1u + block_lane]) * scale1 * x1[c];
        }
    }

    arena_block_sum2_256_f32(&acc0, &acc1);
    if (threadIdx.x == 0) {
        out[r0] = acc0;
        if (have_r1) out[r1] = acc1;
    }
}

__global__ static void arena_f8_e4m3_b128_matmul_grouped_batch_rows2_kernel(
        float *out,
        const uint8_t *base,
        const float *x,
        uint32_t groups,
        uint32_t rows_per_group,
        uint32_t cols_per_group,
        uint32_t row_stride_bytes) {
    const uint32_t r0 = blockIdx.x * 2u;
    const uint32_t r1 = r0 + 1u;
    const uint32_t tok = blockIdx.y;
    const uint32_t rows = groups * rows_per_group;
    if (r0 >= rows) return;
    const int have_r1 = r1 < rows;
    const uint32_t group0 = r0 / rows_per_group;
    const uint32_t group1 = have_r1 ? r1 / rows_per_group : group0;
    const uint8_t *row0 = base + (uint64_t)r0 * row_stride_bytes;
    const uint8_t *row1 = have_r1 ? base + (uint64_t)r1 * row_stride_bytes : row0;
    const float *x_base = x + (uint64_t)tok * groups * cols_per_group;
    const float *x0 = x_base + (uint64_t)group0 * cols_per_group;
    const float *x1 = x_base + (uint64_t)group1 * cols_per_group;
    const uint64_t out_base = (uint64_t)tok * rows;
    float acc0 = 0.0f;
    float acc1 = 0.0f;
    for (uint32_t c = threadIdx.x; c < cols_per_group; c += blockDim.x) {
        const uint64_t block_offset = (uint64_t)(c >> 7u) * 129ull;
        const uint32_t block_lane = c & 127u;
        const uint8_t *block0 = row0 + block_offset;
        const float scale0 = arena_e8m0_to_f32(block0[0]);
        acc0 += arena_e4m3fn_to_f32(block0[1u + block_lane]) * scale0 * x0[c];
        if (have_r1) {
            const uint8_t *block1 = row1 + block_offset;
            const float scale1 = arena_e8m0_to_f32(block1[0]);
            acc1 += arena_e4m3fn_to_f32(block1[1u + block_lane]) * scale1 * x1[c];
        }
    }

    arena_block_sum2_256_f32(&acc0, &acc1);
    if (threadIdx.x == 0) {
        out[out_base + r0] = acc0;
        if (have_r1) out[out_base + r1] = acc1;
    }
}

__global__ static void arena_f8_e4m3_b128_matmul_grouped_rows4_kernel(
        float *out,
        const uint8_t *base,
        const float *x,
        uint32_t groups,
        uint32_t rows_per_group,
        uint32_t cols_per_group,
        uint32_t row_stride_bytes) {
    const uint32_t r0 = blockIdx.x * 4u;
    const uint32_t r1 = r0 + 1u;
    const uint32_t r2 = r0 + 2u;
    const uint32_t r3 = r0 + 3u;
    const uint32_t rows = groups * rows_per_group;
    if (r0 >= rows) return;
    const int have_r1 = r1 < rows;
    const int have_r2 = r2 < rows;
    const int have_r3 = r3 < rows;
    const uint32_t group0 = r0 / rows_per_group;
    const uint32_t group1 = have_r1 ? r1 / rows_per_group : group0;
    const uint32_t group2 = have_r2 ? r2 / rows_per_group : group0;
    const uint32_t group3 = have_r3 ? r3 / rows_per_group : group0;
    const uint8_t *row0 = base + (uint64_t)r0 * row_stride_bytes;
    const uint8_t *row1 = have_r1 ? base + (uint64_t)r1 * row_stride_bytes : row0;
    const uint8_t *row2 = have_r2 ? base + (uint64_t)r2 * row_stride_bytes : row0;
    const uint8_t *row3 = have_r3 ? base + (uint64_t)r3 * row_stride_bytes : row0;
    const float *x0 = x + (uint64_t)group0 * cols_per_group;
    const float *x1 = x + (uint64_t)group1 * cols_per_group;
    const float *x2 = x + (uint64_t)group2 * cols_per_group;
    const float *x3 = x + (uint64_t)group3 * cols_per_group;
    float acc0 = 0.0f;
    float acc1 = 0.0f;
    float acc2 = 0.0f;
    float acc3 = 0.0f;
    for (uint32_t c = threadIdx.x; c < cols_per_group; c += blockDim.x) {
        const uint64_t block_offset = (uint64_t)(c / 128u) * 129ull;
        const uint32_t block_lane = c % 128u;
        const uint8_t *block0 = row0 + block_offset;
        const float scale0 = arena_e8m0_to_f32(block0[0]);
        acc0 += arena_e4m3fn_to_f32(block0[1u + block_lane]) * scale0 * x0[c];
        if (have_r1) {
            const uint8_t *block1 = row1 + block_offset;
            const float scale1 = arena_e8m0_to_f32(block1[0]);
            acc1 += arena_e4m3fn_to_f32(block1[1u + block_lane]) * scale1 * x1[c];
        }
        if (have_r2) {
            const uint8_t *block2 = row2 + block_offset;
            const float scale2 = arena_e8m0_to_f32(block2[0]);
            acc2 += arena_e4m3fn_to_f32(block2[1u + block_lane]) * scale2 * x2[c];
        }
        if (have_r3) {
            const uint8_t *block3 = row3 + block_offset;
            const float scale3 = arena_e8m0_to_f32(block3[0]);
            acc3 += arena_e4m3fn_to_f32(block3[1u + block_lane]) * scale3 * x3[c];
        }
    }

    arena_block_sum4_256_f32(&acc0, &acc1, &acc2, &acc3);
    if (threadIdx.x == 0) {
        out[r0] = acc0;
        if (have_r1) out[r1] = acc1;
        if (have_r2) out[r2] = acc2;
        if (have_r3) out[r3] = acc3;
    }
}

__global__ static void arena_f8_e4m3_b128_matmul_grouped_rows2_ds4_attn_o_kernel(
        float *out,
        const uint8_t *base,
        const float *x,
        uint32_t row_stride_bytes) {
    enum {
        DS4_GROUPS = 8,
        DS4_ROWS_PER_GROUP = 1024,
        DS4_COLS_PER_GROUP = 4096,
        DS4_ROWS = DS4_GROUPS * DS4_ROWS_PER_GROUP,
    };

    const uint32_t pair = blockIdx.x;
    const uint32_t r0 = pair * 2u;
    if (r0 >= DS4_ROWS) return;
    const uint32_t r1 = r0 + 1u;
    const uint32_t group = pair >> 9;
    const uint8_t *row0 = base + (uint64_t)r0 * row_stride_bytes;
    const uint8_t *row1 = base + (uint64_t)r1 * row_stride_bytes;
    const float *xg = x + (uint64_t)group * DS4_COLS_PER_GROUP;
    float acc0 = 0.0f;
    float acc1 = 0.0f;
    for (uint32_t c = threadIdx.x; c < DS4_COLS_PER_GROUP; c += blockDim.x) {
        const float xv = xg[c];
        const uint64_t block_offset = (uint64_t)(c >> 7) * 129ull;
        const uint32_t block_lane = c & 127u;
        const uint8_t *block0 = row0 + block_offset;
        const uint8_t *block1 = row1 + block_offset;
        const float scale0 = arena_e8m0_to_f32(block0[0]);
        const float scale1 = arena_e8m0_to_f32(block1[0]);
        acc0 += arena_e4m3fn_to_f32(block0[1u + block_lane]) * scale0 * xv;
        acc1 += arena_e4m3fn_to_f32(block1[1u + block_lane]) * scale1 * xv;
    }

    arena_block_sum2_256_f32(&acc0, &acc1);
    if (threadIdx.x == 0) {
        out[r0] = acc0;
        out[r1] = acc1;
    }
}

__global__ static void arena_f8_e4m3_b128_matmul_grouped_rows2_ds4_attn_o_warp_scale_kernel(
        float *out,
        const uint8_t *base,
        const float *x,
        uint32_t row_stride_bytes) {
    enum {
        DS4_GROUPS = 8,
        DS4_ROWS_PER_GROUP = 1024,
        DS4_COLS_PER_GROUP = 4096,
        DS4_ROWS = DS4_GROUPS * DS4_ROWS_PER_GROUP,
    };

    const uint32_t pair = blockIdx.x;
    const uint32_t r0 = pair * 2u;
    if (r0 >= DS4_ROWS) return;
    const uint32_t r1 = r0 + 1u;
    const uint32_t group = pair >> 9;
    const uint8_t *row0 = base + (uint64_t)r0 * row_stride_bytes;
    const uint8_t *row1 = base + (uint64_t)r1 * row_stride_bytes;
    const float *xg = x + (uint64_t)group * DS4_COLS_PER_GROUP;
    float acc0 = 0.0f;
    float acc1 = 0.0f;
    for (uint32_t c = threadIdx.x; c < DS4_COLS_PER_GROUP; c += blockDim.x) {
        const float xv = xg[c];
        const uint64_t block_offset = (uint64_t)(c >> 7) * 129ull;
        const uint32_t block_lane = c & 127u;
        const uint8_t *block0 = row0 + block_offset;
        const uint8_t *block1 = row1 + block_offset;
        const float scale0 = arena_f8_block_scale_warp(row0, c);
        const float scale1 = arena_f8_block_scale_warp(row1, c);
        acc0 += arena_e4m3fn_to_f32(block0[1u + block_lane]) * scale0 * xv;
        acc1 += arena_e4m3fn_to_f32(block1[1u + block_lane]) * scale1 * xv;
    }

    arena_block_sum2_256_f32(&acc0, &acc1);
    if (threadIdx.x == 0) {
        out[r0] = acc0;
        out[r1] = acc1;
    }
}

__global__ static void arena_f8_e4m3_b128_matmul_grouped_rows4_ds4_attn_o_kernel(
        float *out,
        const uint8_t *base,
        const float *x,
        uint32_t row_stride_bytes) {
    enum {
        DS4_GROUPS = 8,
        DS4_ROWS_PER_GROUP = 1024,
        DS4_COLS_PER_GROUP = 4096,
        DS4_ROWS = DS4_GROUPS * DS4_ROWS_PER_GROUP,
    };

    const uint32_t quad = blockIdx.x;
    const uint32_t r0 = quad * 4u;
    if (r0 >= DS4_ROWS) return;
    const uint32_t r1 = r0 + 1u;
    const uint32_t r2 = r0 + 2u;
    const uint32_t r3 = r0 + 3u;
    const uint32_t group = quad >> 8;
    const uint8_t *row0 = base + (uint64_t)r0 * row_stride_bytes;
    const uint8_t *row1 = base + (uint64_t)r1 * row_stride_bytes;
    const uint8_t *row2 = base + (uint64_t)r2 * row_stride_bytes;
    const uint8_t *row3 = base + (uint64_t)r3 * row_stride_bytes;
    const float *xg = x + (uint64_t)group * DS4_COLS_PER_GROUP;
    float acc0 = 0.0f;
    float acc1 = 0.0f;
    float acc2 = 0.0f;
    float acc3 = 0.0f;
    for (uint32_t c = threadIdx.x; c < DS4_COLS_PER_GROUP; c += blockDim.x) {
        const float xv = xg[c];
        const uint64_t block_offset = (uint64_t)(c >> 7) * 129ull;
        const uint32_t block_lane = c & 127u;
        const uint8_t *block0 = row0 + block_offset;
        const uint8_t *block1 = row1 + block_offset;
        const uint8_t *block2 = row2 + block_offset;
        const uint8_t *block3 = row3 + block_offset;
        const float scale0 = arena_e8m0_to_f32(block0[0]);
        const float scale1 = arena_e8m0_to_f32(block1[0]);
        const float scale2 = arena_e8m0_to_f32(block2[0]);
        const float scale3 = arena_e8m0_to_f32(block3[0]);
        acc0 += arena_e4m3fn_to_f32(block0[1u + block_lane]) * scale0 * xv;
        acc1 += arena_e4m3fn_to_f32(block1[1u + block_lane]) * scale1 * xv;
        acc2 += arena_e4m3fn_to_f32(block2[1u + block_lane]) * scale2 * xv;
        acc3 += arena_e4m3fn_to_f32(block3[1u + block_lane]) * scale3 * xv;
    }

    arena_block_sum4_256_f32(&acc0, &acc1, &acc2, &acc3);
    if (threadIdx.x == 0) {
        out[r0] = acc0;
        out[r1] = acc1;
        out[r2] = acc2;
        out[r3] = acc3;
    }
}

__global__ static void arena_f8_e4m3_b128_to_f16_kernel(
        __half *out,
        const uint8_t *base,
        uint32_t rows,
        uint32_t cols,
        uint32_t row_stride_bytes) {
    const uint64_t idx = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    const uint64_t n = (uint64_t)rows * cols;
    if (idx >= n) return;
    const uint32_t r = (uint32_t)(idx / cols);
    const uint32_t c = (uint32_t)(idx - (uint64_t)r * cols);
    const uint8_t *row = base + (uint64_t)r * row_stride_bytes;
    const uint8_t *block = row + (uint64_t)(c / 128u) * 129ull;
    const float scale = arena_e8m0_to_f32(block[0]);
    const float w = arena_e4m3fn_to_f32(block[1u + (c % 128u)]) * scale;
    out[idx] = __float2half_rn(w);
}

__global__ static void arena_f8_e4m3_b128_pair_swiglu_ptrs_kernel(
        float *out,
        const uint8_t *gate_base,
        const uint8_t *up_base,
        const float *const *x_rows,
        uint32_t rows,
        uint32_t cols,
        uint32_t gate_row_stride_bytes,
        uint32_t up_row_stride_bytes,
        float clamp,
        float weight) {
    const uint32_t r = blockIdx.x;
    const uint32_t tok = blockIdx.y;
    if (r >= rows) return;
    const float *x = x_rows[tok];
    if (!x) return;
    const uint8_t *gate_row = gate_base + (uint64_t)r * gate_row_stride_bytes;
    const uint8_t *up_row = up_base + (uint64_t)r * up_row_stride_bytes;
    float gate_acc = 0.0f;
    float up_acc = 0.0f;
    for (uint32_t c = threadIdx.x; c < cols; c += blockDim.x) {
        const uint8_t *gate_block = gate_row + (uint64_t)(c / 128u) * 129ull;
        const uint8_t *up_block = up_row + (uint64_t)(c / 128u) * 129ull;
        const uint32_t lane = c % 128u;
        const float xv = x[c];
        gate_acc += arena_e4m3fn_to_f32(gate_block[1u + lane]) *
                    arena_e8m0_to_f32(gate_block[0]) * xv;
        up_acc += arena_e4m3fn_to_f32(up_block[1u + lane]) *
                  arena_e8m0_to_f32(up_block[0]) * xv;
    }

    arena_block_sum2_256_f32(&gate_acc, &up_acc);
    if (threadIdx.x == 0) {
        float g = gate_acc;
        float u = up_acc;
        if (clamp > 1.0e-6f) {
            g = fminf(g, clamp);
            u = fminf(fmaxf(u, -clamp), clamp);
        }
        const float s = g / (1.0f + expf(-g));
        out[(uint64_t)tok * rows + r] = s * u * weight;
    }
}

__global__ static void arena_f8_e4m3_b128_pair_swiglu_kernel(
        float *out,
        const uint8_t *gate_base,
        const uint8_t *up_base,
        const float *x,
        uint32_t rows,
        uint32_t cols,
        uint32_t gate_row_stride_bytes,
        uint32_t up_row_stride_bytes,
        float clamp,
        float weight) {
    const uint32_t r = blockIdx.x;
    if (r >= rows || !x) return;
    const uint8_t *gate_row = gate_base + (uint64_t)r * gate_row_stride_bytes;
    const uint8_t *up_row = up_base + (uint64_t)r * up_row_stride_bytes;
    float gate_acc = 0.0f;
    float up_acc = 0.0f;
    for (uint32_t c = threadIdx.x; c < cols; c += blockDim.x) {
        const uint8_t *gate_block = gate_row + (uint64_t)(c / 128u) * 129ull;
        const uint8_t *up_block = up_row + (uint64_t)(c / 128u) * 129ull;
        const uint32_t lane = c % 128u;
        const float xv = x[c];
        gate_acc += arena_e4m3fn_to_f32(gate_block[1u + lane]) *
                    arena_e8m0_to_f32(gate_block[0]) * xv;
        up_acc += arena_e4m3fn_to_f32(up_block[1u + lane]) *
                  arena_e8m0_to_f32(up_block[0]) * xv;
    }

    arena_block_sum2_256_f32(&gate_acc, &up_acc);
    if (threadIdx.x == 0) {
        float g = gate_acc;
        float u = up_acc;
        if (clamp > 1.0e-6f) {
            g = fminf(g, clamp);
            u = fminf(fmaxf(u, -clamp), clamp);
        }
        const float s = g / (1.0f + expf(-g));
        out[r] = s * u * weight;
    }
}

__global__ static void arena_f8_e4m3_b128_pair_swiglu_rows2_kernel(
        float *out,
        const uint8_t *gate_base,
        const uint8_t *up_base,
        const float *x,
        uint32_t rows,
        uint32_t cols,
        uint32_t gate_row_stride_bytes,
        uint32_t up_row_stride_bytes,
        float clamp,
        float weight) {
    const uint32_t r0 = blockIdx.x * 2u;
    const uint32_t r1 = r0 + 1u;
    if (r0 >= rows || !x) return;
    const int have_r1 = r1 < rows;
    const uint8_t *gate_row0 = gate_base + (uint64_t)r0 * gate_row_stride_bytes;
    const uint8_t *up_row0 = up_base + (uint64_t)r0 * up_row_stride_bytes;
    const uint8_t *gate_row1 = have_r1 ? gate_base + (uint64_t)r1 * gate_row_stride_bytes : gate_row0;
    const uint8_t *up_row1 = have_r1 ? up_base + (uint64_t)r1 * up_row_stride_bytes : up_row0;
    float gate_acc0 = 0.0f;
    float up_acc0 = 0.0f;
    float gate_acc1 = 0.0f;
    float up_acc1 = 0.0f;
    for (uint32_t c = threadIdx.x; c < cols; c += blockDim.x) {
        const uint64_t block_offset = (uint64_t)(c >> 7u) * 129ull;
        const uint32_t lane = c & 127u;
        const float xv = x[c];
        const uint8_t *gate_block0 = gate_row0 + block_offset;
        const uint8_t *up_block0 = up_row0 + block_offset;
        gate_acc0 += arena_e4m3fn_to_f32(gate_block0[1u + lane]) *
                     arena_e8m0_to_f32(gate_block0[0]) * xv;
        up_acc0 += arena_e4m3fn_to_f32(up_block0[1u + lane]) *
                   arena_e8m0_to_f32(up_block0[0]) * xv;
        if (have_r1) {
            const uint8_t *gate_block1 = gate_row1 + block_offset;
            const uint8_t *up_block1 = up_row1 + block_offset;
            gate_acc1 += arena_e4m3fn_to_f32(gate_block1[1u + lane]) *
                         arena_e8m0_to_f32(gate_block1[0]) * xv;
            up_acc1 += arena_e4m3fn_to_f32(up_block1[1u + lane]) *
                       arena_e8m0_to_f32(up_block1[0]) * xv;
        }
    }

    arena_block_sum4_256_f32(&gate_acc0, &up_acc0, &gate_acc1, &up_acc1);
    if (threadIdx.x == 0) {
        float g0 = gate_acc0;
        float u0 = up_acc0;
        if (clamp > 1.0e-6f) {
            g0 = fminf(g0, clamp);
            u0 = fminf(fmaxf(u0, -clamp), clamp);
        }
        const float s0 = g0 / (1.0f + expf(-g0));
        out[r0] = s0 * u0 * weight;
        if (have_r1) {
            float g1 = gate_acc1;
            float u1 = up_acc1;
            if (clamp > 1.0e-6f) {
                g1 = fminf(g1, clamp);
                u1 = fminf(fmaxf(u1, -clamp), clamp);
            }
            const float s1 = g1 / (1.0f + expf(-g1));
            out[r1] = s1 * u1 * weight;
        }
    }
}

__global__ static void arena_f8_e4m3_b128_pair_swiglu_ptr_table_hmma_kernel(
        float *out,
        const uint8_t *gate_base,
        const uint8_t *up_base,
        const float *const *x_rows,
        uint32_t gate_row_stride_bytes,
        uint32_t up_row_stride_bytes,
        uint32_t n_tokens,
        float clamp,
        float weight) {
#if __CUDA_ARCH__ >= 700
    namespace wmma = nvcuda::wmma;
    enum {
        DS4_ROWS = 2048,
        DS4_COLS = 4096,
        WARPS_PER_BLOCK = 4,
        TILE_M = 16,
        TILE_N = 16,
        TILE_K = 16,
        ROWS_PER_BLOCK = WARPS_PER_BLOCK * TILE_N,
    };

    const uint32_t tid = threadIdx.x;
    const uint32_t warp = tid >> 5u;
    if (warp >= WARPS_PER_BLOCK) return;

    const uint32_t row_block = blockIdx.x * ROWS_PER_BLOCK;

    __shared__ const float *x_ptrs[TILE_M];
    __shared__ __half a_sh[TILE_M * TILE_K];
    __shared__ __half gate_b_sh[WARPS_PER_BLOCK * TILE_K * TILE_N];
    __shared__ __half up_b_sh[WARPS_PER_BLOCK * TILE_K * TILE_N];
    __shared__ float gate_c_sh[WARPS_PER_BLOCK * TILE_M * TILE_N];
    __shared__ float up_c_sh[WARPS_PER_BLOCK * TILE_M * TILE_N];

    for (uint32_t i = tid; i < TILE_M; i += blockDim.x) {
        x_ptrs[i] = i < n_tokens ? x_rows[i] : nullptr;
    }
    __syncthreads();

    wmma::fragment<wmma::matrix_a, TILE_M, TILE_N, TILE_K, __half, wmma::row_major> a_frag;
    wmma::fragment<wmma::matrix_b, TILE_M, TILE_N, TILE_K, __half, wmma::col_major> gate_b_frag;
    wmma::fragment<wmma::matrix_b, TILE_M, TILE_N, TILE_K, __half, wmma::col_major> up_b_frag;
    wmma::fragment<wmma::accumulator, TILE_M, TILE_N, TILE_K, float> gate_c_frag;
    wmma::fragment<wmma::accumulator, TILE_M, TILE_N, TILE_K, float> up_c_frag;
    wmma::fill_fragment(gate_c_frag, 0.0f);
    wmma::fill_fragment(up_c_frag, 0.0f);

    for (uint32_t k0 = 0; k0 < DS4_COLS; k0 += TILE_K) {
        for (uint32_t i = tid; i < TILE_M * TILE_K; i += blockDim.x) {
            const uint32_t token = i >> 4u;
            const uint32_t k = i & 15u;
            const float *x = x_ptrs[token];
            const float v = x ? x[k0 + k] : 0.0f;
            a_sh[i] = __float2half_rn(v);
        }

        for (uint32_t i = tid; i < WARPS_PER_BLOCK * TILE_K * TILE_N; i += blockDim.x) {
            const uint32_t wtile = i >> 8u;
            const uint32_t local = i & 255u;
            const uint32_t out_col = local >> 4u;
            const uint32_t k = local & 15u;
            const uint32_t row = row_block + wtile * TILE_N + out_col;
            float gate_w = 0.0f;
            float up_w = 0.0f;
            if (row < DS4_ROWS) {
                const uint32_t col = k0 + k;
                const uint64_t block_offset = (uint64_t)(col >> 7u) * 129ull;
                const uint32_t block_lane = col & 127u;
                const uint8_t *gate_row = gate_base + (uint64_t)row * gate_row_stride_bytes;
                const uint8_t *up_row = up_base + (uint64_t)row * up_row_stride_bytes;
                const uint8_t *gate_block = gate_row + block_offset;
                const uint8_t *up_block = up_row + block_offset;
                gate_w = arena_e4m3fn_to_f32(gate_block[1u + block_lane]) *
                         arena_e8m0_to_f32(gate_block[0]);
                up_w = arena_e4m3fn_to_f32(up_block[1u + block_lane]) *
                       arena_e8m0_to_f32(up_block[0]);
            }
            gate_b_sh[i] = __float2half_rn(gate_w);
            up_b_sh[i] = __float2half_rn(up_w);
        }
        __syncthreads();

        wmma::load_matrix_sync(a_frag, a_sh, TILE_K);
        wmma::load_matrix_sync(gate_b_frag, gate_b_sh + warp * TILE_K * TILE_N, TILE_K);
        wmma::load_matrix_sync(up_b_frag, up_b_sh + warp * TILE_K * TILE_N, TILE_K);
        wmma::mma_sync(gate_c_frag, a_frag, gate_b_frag, gate_c_frag);
        wmma::mma_sync(up_c_frag, a_frag, up_b_frag, up_c_frag);
        __syncthreads();
    }

    wmma::store_matrix_sync(gate_c_sh + warp * TILE_M * TILE_N,
                            gate_c_frag,
                            TILE_N,
                            wmma::mem_row_major);
    wmma::store_matrix_sync(up_c_sh + warp * TILE_M * TILE_N,
                            up_c_frag,
                            TILE_N,
                            wmma::mem_row_major);
    __syncthreads();

    for (uint32_t i = tid; i < WARPS_PER_BLOCK * TILE_M * TILE_N; i += blockDim.x) {
        const uint32_t wtile = i >> 8u;
        const uint32_t local = i & 255u;
        const uint32_t token = local >> 4u;
        const uint32_t out_col = local & 15u;
        const uint32_t row = row_block + wtile * TILE_N + out_col;
        if (token < n_tokens && row < DS4_ROWS) {
            float g = gate_c_sh[wtile * TILE_M * TILE_N + local];
            float u = up_c_sh[wtile * TILE_M * TILE_N + local];
            if (clamp > 1.0e-6f) {
                g = fminf(g, clamp);
                u = fminf(fmaxf(u, -clamp), clamp);
            }
            const float s = g / (1.0f + expf(-g));
            out[(uint64_t)token * DS4_ROWS + row] = s * u * weight;
        }
    }
#else
    (void)out;
    (void)gate_base;
    (void)up_base;
    (void)x_rows;
    (void)gate_row_stride_bytes;
    (void)up_row_stride_bytes;
    (void)n_tokens;
    (void)clamp;
    (void)weight;
#endif
}

__global__ static void arena_mxfp4_matmul_kernel(
        float *out,
        const uint8_t *base,
        const float *x,
        uint32_t rows,
        uint32_t cols,
        uint32_t row_stride_bytes) {
    const uint32_t r = blockIdx.x;
    if (r >= rows) return;
    const uint8_t *row = base + (uint64_t)r * row_stride_bytes;
    float acc = 0.0f;
    for (uint32_t c = threadIdx.x; c < cols; c += blockDim.x) {
        const uint8_t *block = row + (uint64_t)(c / 32u) * 17ull;
        const uint32_t lane = c % 32u;
        const uint8_t packed = block[1u + (lane % 16u)];
        const uint8_t q = lane < 16u ? (packed & 0x0fu) : ((packed >> 4) & 0x0fu);
        const float w = arena_mxfp4_nibble_to_f32(q) * arena_e8m0_to_f32(block[0]);
        acc += w * x[c];
    }

    __shared__ float partial[256];
    partial[threadIdx.x] = acc;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] += partial[threadIdx.x + stride];
        __syncthreads();
    }
    if (threadIdx.x == 0) out[r] = partial[0];
}

__global__ static void arena_mxfp4_pair_swiglu_kernel(
        float *out,
        const uint8_t *gate_base,
        const uint8_t *up_base,
        const float *x,
        uint32_t rows,
        uint32_t cols,
        uint32_t gate_row_stride_bytes,
        uint32_t up_row_stride_bytes,
        float clamp,
        float weight) {
    const uint32_t r = blockIdx.x;
    if (r >= rows) return;
    const uint8_t *gate_row = gate_base + (uint64_t)r * gate_row_stride_bytes;
    const uint8_t *up_row = up_base + (uint64_t)r * up_row_stride_bytes;
    float gate_acc = 0.0f;
    float up_acc = 0.0f;
    for (uint32_t c = threadIdx.x; c < cols; c += blockDim.x) {
        const uint32_t block_idx = c / 32u;
        const uint32_t lane = c % 32u;
        const uint32_t packed_idx = 1u + (lane % 16u);
        const uint8_t *gate_block = gate_row + (uint64_t)block_idx * 17ull;
        const uint8_t *up_block = up_row + (uint64_t)block_idx * 17ull;
        const uint8_t gate_packed = gate_block[packed_idx];
        const uint8_t up_packed = up_block[packed_idx];
        const uint8_t gate_q =
            lane < 16u ? (gate_packed & 0x0fu) : ((gate_packed >> 4) & 0x0fu);
        const uint8_t up_q =
            lane < 16u ? (up_packed & 0x0fu) : ((up_packed >> 4) & 0x0fu);
        const float xv = x[c];
        gate_acc += arena_mxfp4_nibble_to_f32(gate_q) *
                    arena_e8m0_to_f32(gate_block[0]) * xv;
        up_acc += arena_mxfp4_nibble_to_f32(up_q) *
                  arena_e8m0_to_f32(up_block[0]) * xv;
    }

    __shared__ float gate_partial[256];
    __shared__ float up_partial[256];
    gate_partial[threadIdx.x] = gate_acc;
    up_partial[threadIdx.x] = up_acc;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) {
            gate_partial[threadIdx.x] += gate_partial[threadIdx.x + stride];
            up_partial[threadIdx.x] += up_partial[threadIdx.x + stride];
        }
        __syncthreads();
    }
    if (threadIdx.x == 0) {
        float g = gate_partial[0];
        float u = up_partial[0];
        if (clamp > 1.0e-6f) {
            g = fminf(g, clamp);
            u = fminf(fmaxf(u, -clamp), clamp);
        }
        const float s = g / (1.0f + expf(-g));
        out[r] = s * u * weight;
    }
}

__global__ static void arena_mxfp4_matmul_add_kernel(
        float *out,
        const uint8_t *base,
        const float *x,
        const float *add,
        uint32_t rows,
        uint32_t cols,
        uint32_t row_stride_bytes) {
    const uint32_t r = blockIdx.x;
    if (r >= rows) return;
    const uint8_t *row = base + (uint64_t)r * row_stride_bytes;
    float acc = 0.0f;
    for (uint32_t c = threadIdx.x; c < cols; c += blockDim.x) {
        const uint8_t *block = row + (uint64_t)(c / 32u) * 17ull;
        const uint32_t lane = c % 32u;
        const uint8_t packed = block[1u + (lane % 16u)];
        const uint8_t q = lane < 16u ? (packed & 0x0fu) : ((packed >> 4) & 0x0fu);
        const float w = arena_mxfp4_nibble_to_f32(q) * arena_e8m0_to_f32(block[0]);
        acc += w * x[c];
    }

    __shared__ float partial[256];
    partial[threadIdx.x] = acc;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] += partial[threadIdx.x + stride];
        __syncthreads();
    }
    if (threadIdx.x == 0) out[r] = add[r] + partial[0];
}

__global__ static void arena_mxfp4_grouped_pair_swiglu_kernel(
        float *mid_out,
        const uint8_t *gate_base,
        const uint8_t *up_base,
        const int32_t *selected,
        const float *weights,
        const float *x,
        uint32_t hidden,
        uint32_t mid,
        uint32_t n_total_experts,
        uint32_t n_routes,
        uint64_t gate_expert_stride_bytes,
        uint32_t gate_row_stride_bytes,
        uint64_t up_expert_stride_bytes,
        uint32_t up_row_stride_bytes,
        float clamp) {
    const uint32_t r = blockIdx.x;
    const uint32_t route = blockIdx.y;
    const uint32_t tok = blockIdx.z;
    if (r >= mid || route >= n_routes) return;
    const int32_t expert_i = selected[(uint64_t)tok * n_routes + route];
    if (expert_i < 0 || (uint32_t)expert_i >= n_total_experts) return;
    const uint32_t expert = (uint32_t)expert_i;
    const uint8_t *gate_row =
        gate_base + (uint64_t)expert * gate_expert_stride_bytes +
        (uint64_t)r * gate_row_stride_bytes;
    const uint8_t *up_row =
        up_base + (uint64_t)expert * up_expert_stride_bytes +
        (uint64_t)r * up_row_stride_bytes;

    float gate_acc = 0.0f;
    float up_acc = 0.0f;
    for (uint32_t c = threadIdx.x; c < hidden; c += blockDim.x) {
        const uint32_t block_idx = c / 32u;
        const uint32_t lane = c % 32u;
        const uint32_t packed_idx = 1u + (lane % 16u);
        const uint8_t *gate_block = gate_row + (uint64_t)block_idx * 17ull;
        const uint8_t *up_block = up_row + (uint64_t)block_idx * 17ull;
        const uint8_t gate_packed = gate_block[packed_idx];
        const uint8_t up_packed = up_block[packed_idx];
        const uint8_t gate_q =
            lane < 16u ? (gate_packed & 0x0fu) : ((gate_packed >> 4) & 0x0fu);
        const uint8_t up_q =
            lane < 16u ? (up_packed & 0x0fu) : ((up_packed >> 4) & 0x0fu);
        const float xv = x[(uint64_t)tok * hidden + c];
        gate_acc += arena_mxfp4_nibble_to_f32(gate_q) *
                    arena_e8m0_to_f32(gate_block[0]) * xv;
        up_acc += arena_mxfp4_nibble_to_f32(up_q) *
                  arena_e8m0_to_f32(up_block[0]) * xv;
    }

    __shared__ float gate_partial[256];
    __shared__ float up_partial[256];
    gate_partial[threadIdx.x] = gate_acc;
    up_partial[threadIdx.x] = up_acc;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) {
            gate_partial[threadIdx.x] += gate_partial[threadIdx.x + stride];
            up_partial[threadIdx.x] += up_partial[threadIdx.x + stride];
        }
        __syncthreads();
    }
    if (threadIdx.x == 0) {
        float g = gate_partial[0];
        float u = up_partial[0];
        if (clamp > 1.0e-6f) {
            g = fminf(g, clamp);
            u = fminf(fmaxf(u, -clamp), clamp);
        }
        const float s = g / (1.0f + expf(-g));
        const uint64_t mid_index = ((uint64_t)tok * n_routes + route) * mid + r;
        mid_out[mid_index] = s * u * weights[(uint64_t)tok * n_routes + route];
    }
}

__global__ static void arena_mxfp4_grouped_pair_swiglu_ptrs_kernel(
        float *mid_out,
        const uint8_t *gate_base,
        const uint8_t *up_base,
        const int32_t *selected,
        const float *weights,
        const float *const *x_rows,
        uint32_t hidden,
        uint32_t mid,
        uint32_t n_total_experts,
        uint32_t n_routes,
        uint64_t gate_expert_stride_bytes,
        uint32_t gate_row_stride_bytes,
        uint64_t up_expert_stride_bytes,
        uint32_t up_row_stride_bytes,
        float clamp) {
    const uint32_t r = blockIdx.x;
    const uint32_t route = blockIdx.y;
    const uint32_t tok = blockIdx.z;
    if (r >= mid || route >= n_routes) return;
    const float *x = x_rows[tok];
    if (!x) return;
    const int32_t expert_i = selected[(uint64_t)tok * n_routes + route];
    if (expert_i < 0 || (uint32_t)expert_i >= n_total_experts) return;
    const uint32_t expert = (uint32_t)expert_i;
    const uint8_t *gate_row =
        gate_base + (uint64_t)expert * gate_expert_stride_bytes +
        (uint64_t)r * gate_row_stride_bytes;
    const uint8_t *up_row =
        up_base + (uint64_t)expert * up_expert_stride_bytes +
        (uint64_t)r * up_row_stride_bytes;

    float gate_acc = 0.0f;
    float up_acc = 0.0f;
    for (uint32_t c = threadIdx.x; c < hidden; c += blockDim.x) {
        const uint32_t block_idx = c / 32u;
        const uint32_t lane = c % 32u;
        const uint32_t packed_idx = 1u + (lane % 16u);
        const uint8_t *gate_block = gate_row + (uint64_t)block_idx * 17ull;
        const uint8_t *up_block = up_row + (uint64_t)block_idx * 17ull;
        const uint8_t gate_packed = gate_block[packed_idx];
        const uint8_t up_packed = up_block[packed_idx];
        const uint8_t gate_q =
            lane < 16u ? (gate_packed & 0x0fu) : ((gate_packed >> 4) & 0x0fu);
        const uint8_t up_q =
            lane < 16u ? (up_packed & 0x0fu) : ((up_packed >> 4) & 0x0fu);
        const float xv = x[c];
        gate_acc += arena_mxfp4_nibble_to_f32(gate_q) *
                    arena_e8m0_to_f32(gate_block[0]) * xv;
        up_acc += arena_mxfp4_nibble_to_f32(up_q) *
                  arena_e8m0_to_f32(up_block[0]) * xv;
    }

    __shared__ float gate_partial[256];
    __shared__ float up_partial[256];
    gate_partial[threadIdx.x] = gate_acc;
    up_partial[threadIdx.x] = up_acc;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) {
            gate_partial[threadIdx.x] += gate_partial[threadIdx.x + stride];
            up_partial[threadIdx.x] += up_partial[threadIdx.x + stride];
        }
        __syncthreads();
    }
    if (threadIdx.x == 0) {
        float g = gate_partial[0];
        float u = up_partial[0];
        if (clamp > 1.0e-6f) {
            g = fminf(g, clamp);
            u = fminf(fmaxf(u, -clamp), clamp);
        }
        const float s = g / (1.0f + expf(-g));
        const uint64_t mid_index = ((uint64_t)tok * n_routes + route) * mid + r;
        mid_out[mid_index] = s * u * weights[(uint64_t)tok * n_routes + route];
    }
}

__global__ static void arena_mxfp4_grouped_pair_swiglu_rows2_kernel(
        float *mid_out,
        const uint8_t *gate_base,
        const uint8_t *up_base,
        const int32_t *selected,
        const float *weights,
        const float *x,
        uint32_t hidden,
        uint32_t mid,
        uint32_t n_total_experts,
        uint32_t n_routes,
        uint64_t gate_expert_stride_bytes,
        uint32_t gate_row_stride_bytes,
        uint64_t up_expert_stride_bytes,
        uint32_t up_row_stride_bytes,
        float clamp) {
    const uint32_t r0 = blockIdx.x * 2u;
    const uint32_t r1 = r0 + 1u;
    const uint32_t route = blockIdx.y;
    const uint32_t tok = blockIdx.z;
    if (r0 >= mid || route >= n_routes) return;
    const int have_r1 = r1 < mid;
    const int32_t expert_i = selected[(uint64_t)tok * n_routes + route];
    if (expert_i < 0 || (uint32_t)expert_i >= n_total_experts) return;
    const uint32_t expert = (uint32_t)expert_i;

    const uint8_t *gate_row0 =
        gate_base + (uint64_t)expert * gate_expert_stride_bytes +
        (uint64_t)r0 * gate_row_stride_bytes;
    const uint8_t *up_row0 =
        up_base + (uint64_t)expert * up_expert_stride_bytes +
        (uint64_t)r0 * up_row_stride_bytes;
    const uint8_t *gate_row1 = have_r1
        ? gate_base + (uint64_t)expert * gate_expert_stride_bytes +
              (uint64_t)r1 * gate_row_stride_bytes
        : gate_row0;
    const uint8_t *up_row1 = have_r1
        ? up_base + (uint64_t)expert * up_expert_stride_bytes +
              (uint64_t)r1 * up_row_stride_bytes
        : up_row0;

    float gate0_acc = 0.0f;
    float up0_acc = 0.0f;
    float gate1_acc = 0.0f;
    float up1_acc = 0.0f;
    for (uint32_t c = threadIdx.x; c < hidden; c += blockDim.x) {
        const uint32_t block_idx = c / 32u;
        const uint32_t lane = c % 32u;
        const uint32_t packed_idx = 1u + (lane % 16u);
        const float xv = x[(uint64_t)tok * hidden + c];

        const uint8_t *gate_block0 = gate_row0 + (uint64_t)block_idx * 17ull;
        const uint8_t *up_block0 = up_row0 + (uint64_t)block_idx * 17ull;
        const uint8_t gate_packed0 = gate_block0[packed_idx];
        const uint8_t up_packed0 = up_block0[packed_idx];
        const uint8_t gate_q0 =
            lane < 16u ? (gate_packed0 & 0x0fu) : ((gate_packed0 >> 4) & 0x0fu);
        const uint8_t up_q0 =
            lane < 16u ? (up_packed0 & 0x0fu) : ((up_packed0 >> 4) & 0x0fu);
        gate0_acc += arena_mxfp4_nibble_to_f32(gate_q0) *
                     arena_e8m0_to_f32(gate_block0[0]) * xv;
        up0_acc += arena_mxfp4_nibble_to_f32(up_q0) *
                   arena_e8m0_to_f32(up_block0[0]) * xv;

        if (have_r1) {
            const uint8_t *gate_block1 = gate_row1 + (uint64_t)block_idx * 17ull;
            const uint8_t *up_block1 = up_row1 + (uint64_t)block_idx * 17ull;
            const uint8_t gate_packed1 = gate_block1[packed_idx];
            const uint8_t up_packed1 = up_block1[packed_idx];
            const uint8_t gate_q1 = lane < 16u
                ? (gate_packed1 & 0x0fu)
                : ((gate_packed1 >> 4) & 0x0fu);
            const uint8_t up_q1 = lane < 16u
                ? (up_packed1 & 0x0fu)
                : ((up_packed1 >> 4) & 0x0fu);
            gate1_acc += arena_mxfp4_nibble_to_f32(gate_q1) *
                         arena_e8m0_to_f32(gate_block1[0]) * xv;
            up1_acc += arena_mxfp4_nibble_to_f32(up_q1) *
                       arena_e8m0_to_f32(up_block1[0]) * xv;
        }
    }

    __shared__ float gate0_partial[256];
    __shared__ float up0_partial[256];
    __shared__ float gate1_partial[256];
    __shared__ float up1_partial[256];
    gate0_partial[threadIdx.x] = gate0_acc;
    up0_partial[threadIdx.x] = up0_acc;
    gate1_partial[threadIdx.x] = gate1_acc;
    up1_partial[threadIdx.x] = up1_acc;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) {
            gate0_partial[threadIdx.x] += gate0_partial[threadIdx.x + stride];
            up0_partial[threadIdx.x] += up0_partial[threadIdx.x + stride];
            gate1_partial[threadIdx.x] += gate1_partial[threadIdx.x + stride];
            up1_partial[threadIdx.x] += up1_partial[threadIdx.x + stride];
        }
        __syncthreads();
    }
    if (threadIdx.x == 0) {
        float g0 = gate0_partial[0];
        float u0 = up0_partial[0];
        float g1 = gate1_partial[0];
        float u1 = up1_partial[0];
        if (clamp > 1.0e-6f) {
            g0 = fminf(g0, clamp);
            u0 = fminf(fmaxf(u0, -clamp), clamp);
            g1 = fminf(g1, clamp);
            u1 = fminf(fmaxf(u1, -clamp), clamp);
        }
        const float route_weight = weights[(uint64_t)tok * n_routes + route];
        const uint64_t base_index = ((uint64_t)tok * n_routes + route) * mid;
        const float s0 = g0 / (1.0f + expf(-g0));
        mid_out[base_index + r0] = s0 * u0 * route_weight;
        if (have_r1) {
            const float s1 = g1 / (1.0f + expf(-g1));
            mid_out[base_index + r1] = s1 * u1 * route_weight;
        }
    }
}

__global__ static void arena_mxfp4_grouped_pair_swiglu_ptrs_rows2_kernel(
        float *mid_out,
        const uint8_t *gate_base,
        const uint8_t *up_base,
        const int32_t *selected,
        const float *weights,
        const float *const *x_rows,
        uint32_t hidden,
        uint32_t mid,
        uint32_t n_total_experts,
        uint32_t n_routes,
        uint64_t gate_expert_stride_bytes,
        uint32_t gate_row_stride_bytes,
        uint64_t up_expert_stride_bytes,
        uint32_t up_row_stride_bytes,
        float clamp) {
    const uint32_t r0 = blockIdx.x * 2u;
    const uint32_t r1 = r0 + 1u;
    const uint32_t route = blockIdx.y;
    const uint32_t tok = blockIdx.z;
    if (r0 >= mid || route >= n_routes) return;
    const float *x = x_rows[tok];
    if (!x) return;
    const int have_r1 = r1 < mid;
    const int32_t expert_i = selected[(uint64_t)tok * n_routes + route];
    if (expert_i < 0 || (uint32_t)expert_i >= n_total_experts) return;
    const uint32_t expert = (uint32_t)expert_i;

    const uint8_t *gate_row0 =
        gate_base + (uint64_t)expert * gate_expert_stride_bytes +
        (uint64_t)r0 * gate_row_stride_bytes;
    const uint8_t *up_row0 =
        up_base + (uint64_t)expert * up_expert_stride_bytes +
        (uint64_t)r0 * up_row_stride_bytes;
    const uint8_t *gate_row1 = have_r1
        ? gate_base + (uint64_t)expert * gate_expert_stride_bytes +
              (uint64_t)r1 * gate_row_stride_bytes
        : gate_row0;
    const uint8_t *up_row1 = have_r1
        ? up_base + (uint64_t)expert * up_expert_stride_bytes +
              (uint64_t)r1 * up_row_stride_bytes
        : up_row0;

    float gate0_acc = 0.0f;
    float up0_acc = 0.0f;
    float gate1_acc = 0.0f;
    float up1_acc = 0.0f;
    for (uint32_t c = threadIdx.x; c < hidden; c += blockDim.x) {
        const uint32_t block_idx = c / 32u;
        const uint32_t lane = c % 32u;
        const uint32_t packed_idx = 1u + (lane % 16u);
        const float xv = x[c];

        const uint8_t *gate_block0 = gate_row0 + (uint64_t)block_idx * 17ull;
        const uint8_t *up_block0 = up_row0 + (uint64_t)block_idx * 17ull;
        const uint8_t gate_packed0 = gate_block0[packed_idx];
        const uint8_t up_packed0 = up_block0[packed_idx];
        const uint8_t gate_q0 =
            lane < 16u ? (gate_packed0 & 0x0fu) : ((gate_packed0 >> 4) & 0x0fu);
        const uint8_t up_q0 =
            lane < 16u ? (up_packed0 & 0x0fu) : ((up_packed0 >> 4) & 0x0fu);
        gate0_acc += arena_mxfp4_nibble_to_f32(gate_q0) *
                     arena_e8m0_to_f32(gate_block0[0]) * xv;
        up0_acc += arena_mxfp4_nibble_to_f32(up_q0) *
                   arena_e8m0_to_f32(up_block0[0]) * xv;

        if (have_r1) {
            const uint8_t *gate_block1 = gate_row1 + (uint64_t)block_idx * 17ull;
            const uint8_t *up_block1 = up_row1 + (uint64_t)block_idx * 17ull;
            const uint8_t gate_packed1 = gate_block1[packed_idx];
            const uint8_t up_packed1 = up_block1[packed_idx];
            const uint8_t gate_q1 = lane < 16u
                ? (gate_packed1 & 0x0fu)
                : ((gate_packed1 >> 4) & 0x0fu);
            const uint8_t up_q1 = lane < 16u
                ? (up_packed1 & 0x0fu)
                : ((up_packed1 >> 4) & 0x0fu);
            gate1_acc += arena_mxfp4_nibble_to_f32(gate_q1) *
                         arena_e8m0_to_f32(gate_block1[0]) * xv;
            up1_acc += arena_mxfp4_nibble_to_f32(up_q1) *
                       arena_e8m0_to_f32(up_block1[0]) * xv;
        }
    }

    __shared__ float gate0_partial[256];
    __shared__ float up0_partial[256];
    __shared__ float gate1_partial[256];
    __shared__ float up1_partial[256];
    gate0_partial[threadIdx.x] = gate0_acc;
    up0_partial[threadIdx.x] = up0_acc;
    gate1_partial[threadIdx.x] = gate1_acc;
    up1_partial[threadIdx.x] = up1_acc;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) {
            gate0_partial[threadIdx.x] += gate0_partial[threadIdx.x + stride];
            up0_partial[threadIdx.x] += up0_partial[threadIdx.x + stride];
            gate1_partial[threadIdx.x] += gate1_partial[threadIdx.x + stride];
            up1_partial[threadIdx.x] += up1_partial[threadIdx.x + stride];
        }
        __syncthreads();
    }
    if (threadIdx.x == 0) {
        float g0 = gate0_partial[0];
        float u0 = up0_partial[0];
        float g1 = gate1_partial[0];
        float u1 = up1_partial[0];
        if (clamp > 1.0e-6f) {
            g0 = fminf(g0, clamp);
            u0 = fminf(fmaxf(u0, -clamp), clamp);
            g1 = fminf(g1, clamp);
            u1 = fminf(fmaxf(u1, -clamp), clamp);
        }
        const float route_weight = weights[(uint64_t)tok * n_routes + route];
        const uint64_t base_index = ((uint64_t)tok * n_routes + route) * mid;
        const float s0 = g0 / (1.0f + expf(-g0));
        mid_out[base_index + r0] = s0 * u0 * route_weight;
        if (have_r1) {
            const float s1 = g1 / (1.0f + expf(-g1));
            mid_out[base_index + r1] = s1 * u1 * route_weight;
        }
    }
}

__global__ static void arena_mxfp4_grouped_down_sum_kernel(
        float *out,
        const uint8_t *down_base,
        const int32_t *selected,
        const float *mid_in,
        uint32_t hidden,
        uint32_t mid,
        uint32_t n_total_experts,
        uint32_t n_routes,
        uint64_t down_expert_stride_bytes,
        uint32_t down_row_stride_bytes) {
    const uint32_t r = blockIdx.x;
    const uint32_t tok = blockIdx.y;
    if (r >= hidden) return;
    float acc = 0.0f;
    for (uint32_t route = 0; route < n_routes; route++) {
        const int32_t expert_i = selected[(uint64_t)tok * n_routes + route];
        if (expert_i < 0 || (uint32_t)expert_i >= n_total_experts) continue;
        const uint32_t expert = (uint32_t)expert_i;
        const uint8_t *row =
            down_base + (uint64_t)expert * down_expert_stride_bytes +
            (uint64_t)r * down_row_stride_bytes;
        const float *route_mid = mid_in + ((uint64_t)tok * n_routes + route) * mid;
        for (uint32_t c = threadIdx.x; c < mid; c += blockDim.x) {
            const uint8_t *block = row + (uint64_t)(c / 32u) * 17ull;
            const uint32_t lane = c % 32u;
            const uint8_t packed = block[1u + (lane % 16u)];
            const uint8_t q = lane < 16u ? (packed & 0x0fu) : ((packed >> 4) & 0x0fu);
            const float w = arena_mxfp4_nibble_to_f32(q) * arena_e8m0_to_f32(block[0]);
            acc += w * route_mid[c];
        }
    }

    __shared__ float partial[256];
    partial[threadIdx.x] = acc;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] += partial[threadIdx.x + stride];
        __syncthreads();
    }
    if (threadIdx.x == 0) out[(uint64_t)tok * hidden + r] = partial[0];
}

__global__ static void arena_mxfp4_grouped_down_sum_rows2_kernel(
        float *out,
        const uint8_t *down_base,
        const int32_t *selected,
        const float *mid_in,
        uint32_t hidden,
        uint32_t mid,
        uint32_t n_total_experts,
        uint32_t n_routes,
        uint64_t down_expert_stride_bytes,
        uint32_t down_row_stride_bytes) {
    const uint32_t r0 = blockIdx.x * 2u;
    const uint32_t r1 = r0 + 1u;
    const uint32_t tok = blockIdx.y;
    if (r0 >= hidden) return;
    const int have_r1 = r1 < hidden;
    float acc0 = 0.0f;
    float acc1 = 0.0f;
    for (uint32_t route = 0; route < n_routes; route++) {
        const int32_t expert_i = selected[(uint64_t)tok * n_routes + route];
        if (expert_i < 0 || (uint32_t)expert_i >= n_total_experts) continue;
        const uint32_t expert = (uint32_t)expert_i;
        const uint8_t *row0 =
            down_base + (uint64_t)expert * down_expert_stride_bytes +
            (uint64_t)r0 * down_row_stride_bytes;
        const uint8_t *row1 = have_r1
            ? down_base + (uint64_t)expert * down_expert_stride_bytes +
                  (uint64_t)r1 * down_row_stride_bytes
            : row0;
        const float *route_mid = mid_in + ((uint64_t)tok * n_routes + route) * mid;
        for (uint32_t c = threadIdx.x; c < mid; c += blockDim.x) {
            const uint32_t block_idx = c / 32u;
            const uint32_t lane = c % 32u;
            const uint32_t packed_idx = 1u + (lane % 16u);
            const float mv = route_mid[c];

            const uint8_t *block0 = row0 + (uint64_t)block_idx * 17ull;
            const uint8_t packed0 = block0[packed_idx];
            const uint8_t q0 =
                lane < 16u ? (packed0 & 0x0fu) : ((packed0 >> 4) & 0x0fu);
            const float w0 = arena_mxfp4_nibble_to_f32(q0) *
                             arena_e8m0_to_f32(block0[0]);
            acc0 += w0 * mv;

            if (have_r1) {
                const uint8_t *block1 = row1 + (uint64_t)block_idx * 17ull;
                const uint8_t packed1 = block1[packed_idx];
                const uint8_t q1 =
                    lane < 16u ? (packed1 & 0x0fu) : ((packed1 >> 4) & 0x0fu);
                const float w1 = arena_mxfp4_nibble_to_f32(q1) *
                                 arena_e8m0_to_f32(block1[0]);
                acc1 += w1 * mv;
            }
        }
    }

    __shared__ float partial0[256];
    __shared__ float partial1[256];
    partial0[threadIdx.x] = acc0;
    partial1[threadIdx.x] = acc1;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) {
            partial0[threadIdx.x] += partial0[threadIdx.x + stride];
            partial1[threadIdx.x] += partial1[threadIdx.x + stride];
        }
        __syncthreads();
    }
    if (threadIdx.x == 0) {
        out[(uint64_t)tok * hidden + r0] = partial0[0];
        if (have_r1) out[(uint64_t)tok * hidden + r1] = partial1[0];
    }
}

typedef struct alignas(16) {
    void *p;
    int stride;
    int pad;
} cuda_tm_strided_ptr;
static_assert(sizeof(cuda_tm_strided_ptr) == 16, "TurboMind StridedPtr table size mismatch");

typedef struct {
    void *weight_base;
    void *scale_base;
    cuda_tm_strided_ptr *d_weights;
    cuda_tm_strided_ptr *d_scales;
    size_t weight_bytes;
    size_t scale_bytes;
    int k_pack;
    uint32_t experts;
    int cached_tables;
} cuda_tm_matrix_pack;

typedef struct {
    void *arena_ptr;
    int gpu;
    uint64_t weight_offset;
    uint64_t scale_offset;
    uint64_t weight_bytes_per_expert;
    uint64_t scale_bytes_per_expert;
    uint32_t n;
    uint32_t k;
    uint32_t experts_packed;
    uint32_t n_total_experts;
    int k_pack;
    int weight_stride;
    int scale_stride;
} cuda_tm_matrix_table_key;

typedef struct {
    cuda_tm_matrix_table_key key;
    cuda_tm_strided_ptr *d_weights;
    cuda_tm_strided_ptr *d_scales;
} cuda_tm_matrix_table_cache_entry;

static std::mutex g_tm_matrix_table_cache_mutex;
static std::vector<cuda_tm_matrix_table_cache_entry> g_tm_matrix_table_cache;

static int cuda_tm_matrix_table_key_equal(
        const cuda_tm_matrix_table_key &a,
        const cuda_tm_matrix_table_key &b) {
    return a.arena_ptr == b.arena_ptr &&
        a.gpu == b.gpu &&
        a.weight_offset == b.weight_offset &&
        a.scale_offset == b.scale_offset &&
        a.weight_bytes_per_expert == b.weight_bytes_per_expert &&
        a.scale_bytes_per_expert == b.scale_bytes_per_expert &&
        a.n == b.n &&
        a.k == b.k &&
        a.experts_packed == b.experts_packed &&
        a.n_total_experts == b.n_total_experts &&
        a.k_pack == b.k_pack &&
        a.weight_stride == b.weight_stride &&
        a.scale_stride == b.scale_stride;
}

static void cuda_tm_matrix_table_cache_release_entry(
        const cuda_tm_matrix_table_cache_entry &e) {
    if (!e.d_weights && !e.d_scales) return;
    (void)cudaSetDevice(e.key.gpu);
    if (e.d_scales) (void)cudaFree(e.d_scales);
    if (e.d_weights) (void)cudaFree(e.d_weights);
}

static void cuda_tm_matrix_table_cache_release_arena(const ds4_gpu_arena *arena) {
    if (!arena) return;
    std::lock_guard<std::mutex> lk(g_tm_matrix_table_cache_mutex);
    for (size_t i = 0; i < g_tm_matrix_table_cache.size();) {
        const cuda_tm_matrix_table_cache_entry &e = g_tm_matrix_table_cache[i];
        if (e.key.arena_ptr == arena->ptr && e.key.gpu == arena->gpu) {
            cuda_tm_matrix_table_cache_release_entry(e);
            g_tm_matrix_table_cache.erase(g_tm_matrix_table_cache.begin() + i);
        } else {
            i++;
        }
    }
}

static void cuda_tm_matrix_table_cache_release_all(void) {
    std::lock_guard<std::mutex> lk(g_tm_matrix_table_cache_mutex);
    for (const cuda_tm_matrix_table_cache_entry &e : g_tm_matrix_table_cache) {
        cuda_tm_matrix_table_cache_release_entry(e);
    }
    g_tm_matrix_table_cache.clear();
}

__global__ static void tm_count_routes_kernel(
        int *counts,
        int *bad,
        const int32_t *selected,
        uint32_t total_routes,
        uint32_t n_total_experts) {
    const uint32_t idx = blockIdx.x * blockDim.x + threadIdx.x;
    if (idx >= total_routes) return;
    const int32_t e = selected[idx];
    if (e < 0 || (uint32_t)e >= n_total_experts) {
        atomicExch(bad, 1);
        return;
    }
    atomicAdd(&counts[(uint32_t)e], 1);
}

__global__ static void tm_prefix_offsets_kernel(
        int *offsets,
        int *cursors,
        const int *counts,
        uint32_t n_total_experts) {
    if (blockIdx.x || threadIdx.x) return;
    int sum = 0;
    for (uint32_t e = 0; e < n_total_experts; e++) {
        offsets[e] = sum;
        cursors[e] = sum;
        sum += counts[e];
    }
    offsets[n_total_experts] = sum;
}

__global__ static void tm_scatter_routes_kernel(
        int *sorted_pairs,
        float *sorted_weights,
        int *pair_rows,
        int *bad,
        int *cursors,
        const int32_t *selected,
        const float *weights,
        uint32_t total_routes,
        uint32_t n_total_experts) {
    const uint32_t pair = blockIdx.x * blockDim.x + threadIdx.x;
    if (pair >= total_routes) return;
    const int32_t e = selected[pair];
    if (e < 0 || (uint32_t)e >= n_total_experts) {
        atomicExch(bad, 1);
        return;
    }
    const int row = atomicAdd(&cursors[(uint32_t)e], 1);
    sorted_pairs[row] = (int)pair;
    sorted_weights[row] = weights[pair];
    if (pair_rows) pair_rows[pair] = row;
}

__global__ static void tm_build_routes_small_kernel(
        int *counts,
        int *cursors,
        int *offsets,
        int *sorted_pairs,
        float *sorted_weights,
        int *pair_rows,
        int *bad,
        const int32_t *selected,
        const float *weights,
        uint32_t total_routes,
        uint32_t n_total_experts) {
    const uint32_t tid = threadIdx.x;
    if (blockIdx.x != 0) return;
    if (tid == 0) *bad = 0;
    if (tid < n_total_experts) {
        counts[tid] = 0;
        cursors[tid] = 0;
        offsets[tid] = 0;
    }
    if (tid == n_total_experts) offsets[n_total_experts] = 0;
    __syncthreads();

    if (tid < total_routes) {
        const int32_t e = selected[tid];
        if (e < 0 || (uint32_t)e >= n_total_experts) {
            atomicExch(bad, 1);
        } else {
            atomicAdd(&counts[(uint32_t)e], 1);
        }
    }
    __syncthreads();

    if (tid == 0) {
        int sum = 0;
        for (uint32_t e = 0; e < n_total_experts; e++) {
            offsets[e] = sum;
            cursors[e] = sum;
            sum += counts[e];
        }
        offsets[n_total_experts] = sum;
    }
    __syncthreads();

    if (tid < total_routes) {
        const int32_t e = selected[tid];
        if (e < 0 || (uint32_t)e >= n_total_experts) return;
        const int row = atomicAdd(&cursors[(uint32_t)e], 1);
        sorted_pairs[row] = (int)tid;
        sorted_weights[row] = weights[tid];
        if (pair_rows) pair_rows[tid] = row;
    }
}

__global__ static void tm_gather_f32_to_f16_kernel(
        __half *out,
        const float *x,
        const float *const *x_row_ptrs,
        const int *sorted_pairs,
        uint32_t n_routes,
        uint32_t hidden,
        uint32_t total_routes) {
    const uint64_t idx = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    const uint64_t n = (uint64_t)total_routes * hidden;
    if (idx >= n) return;
    const uint32_t row = (uint32_t)(idx / hidden);
    const uint32_t col = (uint32_t)(idx - (uint64_t)row * hidden);
    const uint32_t pair = (uint32_t)sorted_pairs[row];
    const uint32_t tok = pair / n_routes;
    const float *src = x_row_ptrs ? x_row_ptrs[tok] : x + (uint64_t)tok * hidden;
    out[idx] = __float2half_rn(src[col]);
}

__global__ static void tm_swiglu_half_kernel(
        __half *out,
        const __half *gate,
        const __half *up,
        const float *weights,
        uint32_t total_routes,
        uint32_t cols,
        float clamp) {
    const uint64_t idx = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    const uint64_t n = (uint64_t)total_routes * cols;
    if (idx >= n) return;
    const uint32_t row = (uint32_t)(idx / cols);
    float g = __half2float(gate[idx]);
    float u = __half2float(up[idx]);
    if (clamp > 1.0e-6f) {
        g = fminf(g, clamp);
        u = fminf(fmaxf(u, -clamp), clamp);
    }
    const float s = g / (1.0f + expf(-g));
    out[idx] = __float2half_rn(s * u * weights[row]);
}

__global__ static void tm_swiglu_fused_gate_up_half_kernel(
        __half *out,
        const __half *gate_up,
        const float *weights,
        uint32_t total_routes,
        uint32_t cols,
        float clamp) {
    const uint64_t idx = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    const uint64_t n = (uint64_t)total_routes * cols;
    if (idx >= n) return;
    const uint32_t row = (uint32_t)(idx / cols);
    const uint32_t col = (uint32_t)(idx - (uint64_t)row * cols);
    const uint64_t base = (uint64_t)row * (uint64_t)cols * 2u + col;
    float g = __half2float(gate_up[base]);
    float u = __half2float(gate_up[base + cols]);
    if (clamp > 1.0e-6f) {
        g = fminf(g, clamp);
        u = fminf(fmaxf(u, -clamp), clamp);
    }
    const float s = g / (1.0f + expf(-g));
    out[idx] = __float2half_rn(s * u * weights[row]);
}

__global__ static void tm_scatter_sum_half_to_f32_kernel(
        float *out,
        const __half *routes,
        const int *sorted_pairs,
        uint32_t n_routes,
        uint32_t hidden,
        uint32_t total_routes) {
    const uint64_t idx = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    const uint64_t n = (uint64_t)total_routes * hidden;
    if (idx >= n) return;
    const uint32_t row = (uint32_t)(idx / hidden);
    const uint32_t col = (uint32_t)(idx - (uint64_t)row * hidden);
    const uint32_t pair = (uint32_t)sorted_pairs[row];
    const uint32_t tok = pair / n_routes;
    atomicAdd(out + (uint64_t)tok * hidden + col, __half2float(routes[idx]));
}

__global__ static void tm_reduce_sum_half_to_f32_by_pair_kernel(
        float *out,
        const __half *routes,
        const int *pair_rows,
        uint32_t n_routes,
        uint32_t hidden,
        uint32_t n_tokens,
        int accumulate_out) {
    const uint64_t idx = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    const uint64_t n = (uint64_t)n_tokens * hidden;
    if (idx >= n) return;
    const uint32_t tok = (uint32_t)(idx / hidden);
    const uint32_t col = (uint32_t)(idx - (uint64_t)tok * hidden);
    float acc = accumulate_out ? out[idx] : 0.0f;
    const uint32_t pair_base = tok * n_routes;
    for (uint32_t route = 0; route < n_routes; route++) {
        const uint32_t row = (uint32_t)pair_rows[pair_base + route];
        acc += __half2float(routes[(uint64_t)row * hidden + col]);
    }
    out[idx] = acc;
}

static uint64_t cuda_tm_align16(uint64_t v) {
    return (v + 15ull) & ~15ull;
}

static int cuda_tm_route_validation_sync_enabled(void) {
    return cuda_env_flag_enabled("DS4_V100_TURBOMIND_ROUTE_VALIDATE_SYNC") ||
           cuda_env_flag_enabled("DS4_V100_TURBOMIND_VALIDATE_ROUTES") ||
           cuda_env_flag_enabled("DS4_V100_TURBOMIND_STRICT");
}

static int cuda_tm_total_tokens_abi_enabled(void) {
    if (!g_tm_api.mul_mat_grouped_total_tokens) return 0;
    const char *disable = getenv("DS4_V100_DISABLE_TURBOMIND_TOTAL_TOKENS");
    if (!disable || !disable[0]) return 0;
    return !cuda_env_flag_enabled("DS4_V100_DISABLE_TURBOMIND_TOTAL_TOKENS");
}

static int cuda_tm_small_route_build_enabled(void) {
    const char *disable = getenv("DS4_V100_TURBOMIND_DISABLE_SMALL_ROUTE_BUILD");
    if (disable && disable[0]) {
        return !cuda_env_flag_enabled("DS4_V100_TURBOMIND_DISABLE_SMALL_ROUTE_BUILD");
    }
    const char *enable = getenv("DS4_V100_TURBOMIND_SMALL_ROUTE_BUILD");
    if (!enable || !enable[0]) return 0;
    return cuda_env_flag_enabled("DS4_V100_TURBOMIND_SMALL_ROUTE_BUILD");
}

static int cuda_tm_route_row_reduce_enabled(void) {
    return cuda_env_flag_enabled("DS4_V100_TURBOMIND_ROUTE_ROW_REDUCE");
}

static int cuda_tm_use_small_route_build(uint32_t total_routes, uint32_t n_total_experts) {
    return cuda_tm_small_route_build_enabled() &&
           total_routes <= 128u &&
           n_total_experts <= 256u;
}

static int cuda_tm_build_routes(
        int *counts,
        int *cursors,
        int *offsets,
        int *sorted_pairs,
        float *sorted_weights,
        int *pair_rows,
        int *bad,
        const int32_t *selected,
        const float *weights,
        uint32_t total_routes,
        uint32_t n_total_experts,
        const char *label) {
    if (cuda_tm_use_small_route_build(total_routes, n_total_experts)) {
        tm_build_routes_small_kernel<<<1, 256>>>(
            counts,
            cursors,
            offsets,
            sorted_pairs,
            sorted_weights,
            pair_rows,
            bad,
            selected,
            weights,
            total_routes,
            n_total_experts);
        return cuda_ok(cudaGetLastError(), label ? label : "turbomind small route build launch");
    }

    if (!cuda_ok(cudaMemset(counts, 0, (size_t)n_total_experts * sizeof(int)),
                 "turbomind route counts clear") ||
        !cuda_ok(cudaMemset(bad, 0, sizeof(int)), "turbomind bad flag clear")) {
        return 0;
    }
    tm_count_routes_kernel<<<(total_routes + 255u) / 256u, 256>>>(
        counts,
        bad,
        selected,
        total_routes,
        n_total_experts);
    if (!cuda_ok(cudaGetLastError(), "turbomind count routes launch")) return 0;
    tm_prefix_offsets_kernel<<<1, 1>>>(offsets, cursors, counts, n_total_experts);
    if (!cuda_ok(cudaGetLastError(), "turbomind prefix routes launch")) return 0;
    tm_scatter_routes_kernel<<<(total_routes + 255u) / 256u, 256>>>(
        sorted_pairs,
        sorted_weights,
        pair_rows,
        bad,
        cursors,
        selected,
        weights,
        total_routes,
        n_total_experts);
    return cuda_ok(cudaGetLastError(), "turbomind scatter routes launch");
}

static int cuda_tm_checked_total_routes(
        uint32_t n_tokens,
        uint32_t n_routes,
        uint32_t *out_total_routes) {
    if (!out_total_routes) return 0;
    const uint64_t total = (uint64_t)n_tokens * (uint64_t)n_routes;
    if (total == 0 || total > (uint64_t)INT_MAX) return 0;
    *out_total_routes = (uint32_t)total;
    return 1;
}

static void cuda_tm_matrix_pack_free(cuda_tm_matrix_pack *p) {
    if (!p) return;
    if (!p->cached_tables) {
        if (p->d_scales) (void)cudaFree(p->d_scales);
        if (p->d_weights) (void)cudaFree(p->d_weights);
    }
    if (p->scale_base) (void)cudaFree(p->scale_base);
    if (p->weight_base) (void)cudaFree(p->weight_base);
    memset(p, 0, sizeof(*p));
}

static void cuda_tm_matrix_pack_table_free(cuda_tm_matrix_pack *p) {
    if (!p) return;
    if (!p->cached_tables) {
        if (p->d_scales) (void)cudaFree(p->d_scales);
        if (p->d_weights) (void)cudaFree(p->d_weights);
    }
    memset(p, 0, sizeof(*p));
}

static int cuda_tm_matrix_pack_from_arena(
        cuda_tm_matrix_pack *out,
        const ds4_gpu_arena *arena,
        const ds4_gpu_turbomind_mxfp4_matrix_view *view,
        uint32_t n_total_experts,
        const char *label) {
    if (!out || !arena || !arena->ptr || !view ||
        n_total_experts == 0 ||
        view->experts_packed < n_total_experts ||
        view->weight_bytes_per_expert == 0 ||
        view->scale_bytes_per_expert == 0) {
        return 0;
    }
    memset(out, 0, sizeof(*out));
    const uint64_t weight_total =
        (uint64_t)n_total_experts * view->weight_bytes_per_expert;
    const uint64_t scale_total =
        (uint64_t)n_total_experts * view->scale_bytes_per_expert;
    if (!cuda_arena_range_ok(arena, view->weight_offset, weight_total) ||
        !cuda_arena_range_ok(arena, view->scale_offset, scale_total)) {
        fprintf(stderr,
                "ds4: TurboMind packed %s span outside arena\n",
                label ? label : "matrix");
        return 0;
    }

    cuda_tm_matrix_table_key key = {};
    key.arena_ptr = arena->ptr;
    key.gpu = arena->gpu;
    key.weight_offset = view->weight_offset;
    key.scale_offset = view->scale_offset;
    key.weight_bytes_per_expert = view->weight_bytes_per_expert;
    key.scale_bytes_per_expert = view->scale_bytes_per_expert;
    key.n = view->n;
    key.k = view->k;
    key.experts_packed = view->experts_packed;
    key.n_total_experts = n_total_experts;
    key.k_pack = view->k_pack;
    key.weight_stride = view->weight_stride;
    key.scale_stride = view->scale_stride;

    {
        std::lock_guard<std::mutex> lk(g_tm_matrix_table_cache_mutex);
        for (const cuda_tm_matrix_table_cache_entry &e : g_tm_matrix_table_cache) {
            if (cuda_tm_matrix_table_key_equal(e.key, key)) {
                out->d_weights = e.d_weights;
                out->d_scales = e.d_scales;
                out->weight_bytes = (size_t)view->weight_bytes_per_expert;
                out->scale_bytes = (size_t)view->scale_bytes_per_expert;
                out->k_pack = view->k_pack;
                out->experts = n_total_experts;
                out->cached_tables = 1;
                return 1;
            }
        }
    }

    std::vector<cuda_tm_strided_ptr> h_weights(n_total_experts);
    std::vector<cuda_tm_strided_ptr> h_scales(n_total_experts);
    for (uint32_t expert = 0; expert < n_total_experts; expert++) {
        h_weights[expert].p =
            (uint8_t *)arena->ptr + view->weight_offset +
            (uint64_t)expert * view->weight_bytes_per_expert;
        h_weights[expert].stride = view->weight_stride;
        h_weights[expert].pad = 0;
        h_scales[expert].p =
            (uint8_t *)arena->ptr + view->scale_offset +
            (uint64_t)expert * view->scale_bytes_per_expert;
        h_scales[expert].stride = view->scale_stride;
        h_scales[expert].pad = 0;
    }
    cuda_tm_strided_ptr *d_weights = nullptr;
    cuda_tm_strided_ptr *d_scales = nullptr;
    if (!cuda_ok(cudaSetDevice(arena->gpu), "turbomind packed table set device") ||
        !cuda_ok(cudaMalloc(&d_weights,
                            (size_t)n_total_experts * sizeof(cuda_tm_strided_ptr)),
                 "turbomind packed weight table alloc") ||
        !cuda_ok(cudaMalloc(&d_scales,
                            (size_t)n_total_experts * sizeof(cuda_tm_strided_ptr)),
                 "turbomind packed scale table alloc") ||
        !cuda_ok(cudaMemcpy(d_weights,
                            h_weights.data(),
                            (size_t)n_total_experts * sizeof(cuda_tm_strided_ptr),
                            cudaMemcpyHostToDevice),
                 "turbomind packed weight table upload") ||
        !cuda_ok(cudaMemcpy(d_scales,
                            h_scales.data(),
                            (size_t)n_total_experts * sizeof(cuda_tm_strided_ptr),
                            cudaMemcpyHostToDevice),
                 "turbomind packed scale table upload")) {
        if (d_scales) (void)cudaFree(d_scales);
        if (d_weights) (void)cudaFree(d_weights);
        return 0;
    }

    {
        std::lock_guard<std::mutex> lk(g_tm_matrix_table_cache_mutex);
        for (const cuda_tm_matrix_table_cache_entry &e : g_tm_matrix_table_cache) {
            if (cuda_tm_matrix_table_key_equal(e.key, key)) {
                if (d_scales) (void)cudaFree(d_scales);
                if (d_weights) (void)cudaFree(d_weights);
                out->d_weights = e.d_weights;
                out->d_scales = e.d_scales;
                out->weight_bytes = (size_t)view->weight_bytes_per_expert;
                out->scale_bytes = (size_t)view->scale_bytes_per_expert;
                out->k_pack = view->k_pack;
                out->experts = n_total_experts;
                out->cached_tables = 1;
                return 1;
            }
        }
        cuda_tm_matrix_table_cache_entry e = {};
        e.key = key;
        e.d_weights = d_weights;
        e.d_scales = d_scales;
        g_tm_matrix_table_cache.push_back(e);
    }

    out->d_weights = d_weights;
    out->d_scales = d_scales;
    out->weight_bytes = (size_t)view->weight_bytes_per_expert;
    out->scale_bytes = (size_t)view->scale_bytes_per_expert;
    out->k_pack = view->k_pack;
    out->experts = n_total_experts;
    out->cached_tables = 1;
    return 1;
}

static int cuda_tm_pack_matrix(
        cuda_tm_matrix_pack *out,
        const uint8_t *src_base,
        uint32_t n_total_experts,
        uint64_t expert_stride_bytes,
        int n,
        int k,
        const char *label) {
    if (!out || !src_base || n_total_experts == 0 || n <= 0 || k <= 0) return 0;
    memset(out, 0, sizeof(*out));

    size_t weight_bytes = 0;
    size_t scale_bytes = 0;
    if (g_tm_api.packed_bytes(GGML_TM_DTYPE_MXFP4,
                              n,
                              k,
                              DS4_SRC_MXFP4_BLOCK_ELEMS,
                              &weight_bytes,
                              &scale_bytes) != 0 ||
        weight_bytes == 0 || scale_bytes == 0) {
        cuda_tm_warn_once("packed_bytes failed");
        return 0;
    }
    if (weight_bytes > SIZE_MAX / n_total_experts ||
        scale_bytes > SIZE_MAX / n_total_experts) {
        cuda_tm_warn_once("packed expert byte size overflow");
        return 0;
    }
    const size_t total_weight_bytes = weight_bytes * (size_t)n_total_experts;
    const size_t total_scale_bytes = scale_bytes * (size_t)n_total_experts;
    if (!cuda_ok(cudaMalloc(&out->weight_base, total_weight_bytes), "turbomind weight pack alloc") ||
        !cuda_ok(cudaMalloc(&out->scale_base, total_scale_bytes), "turbomind scale pack alloc")) {
        cuda_tm_matrix_pack_free(out);
        return 0;
    }

    int expected_k_pack = 0;
    for (uint32_t expert = 0; expert < n_total_experts; expert++) {
        int this_k_pack = 0;
        const uint8_t *src = src_base + (uint64_t)expert * expert_stride_bytes;
        void *weight_dst = (uint8_t *)out->weight_base + (size_t)expert * weight_bytes;
        void *scale_dst = (uint8_t *)out->scale_base + (size_t)expert * scale_bytes;
        if (g_tm_api.pack_weight(src,
                                 GGML_TM_DTYPE_MXFP4,
                                 n,
                                 k,
                                 DS4_SRC_MXFP4_BLOCK_ELEMS,
                                 weight_dst,
                                 scale_dst,
                                 &this_k_pack,
                                 nullptr) != 0) {
            fprintf(stderr,
                    "ds4: TurboMind pack failed for %s expert %u\n",
                    label ? label : "matrix",
                    expert);
            cuda_tm_matrix_pack_free(out);
            return 0;
        }
        if (expert == 0) {
            expected_k_pack = this_k_pack;
        } else if (this_k_pack != expected_k_pack) {
            cuda_tm_warn_once("inconsistent TurboMind k_pack across experts");
            cuda_tm_matrix_pack_free(out);
            return 0;
        }
    }

    std::vector<cuda_tm_strided_ptr> h_weights(n_total_experts);
    std::vector<cuda_tm_strided_ptr> h_scales(n_total_experts);
    for (uint32_t expert = 0; expert < n_total_experts; expert++) {
        h_weights[expert].p = (uint8_t *)out->weight_base + (size_t)expert * weight_bytes;
        h_weights[expert].stride = k * 32;
        h_weights[expert].pad = 0;
        h_scales[expert].p = (uint8_t *)out->scale_base + (size_t)expert * scale_bytes;
        h_scales[expert].stride = n;
        h_scales[expert].pad = 0;
    }
    if (!cuda_ok(cudaMalloc(&out->d_weights,
                            (size_t)n_total_experts * sizeof(cuda_tm_strided_ptr)),
                 "turbomind weight table alloc") ||
        !cuda_ok(cudaMalloc(&out->d_scales,
                            (size_t)n_total_experts * sizeof(cuda_tm_strided_ptr)),
                 "turbomind scale table alloc") ||
        !cuda_ok(cudaMemcpy(out->d_weights,
                            h_weights.data(),
                            (size_t)n_total_experts * sizeof(cuda_tm_strided_ptr),
                            cudaMemcpyHostToDevice),
                 "turbomind weight table upload") ||
        !cuda_ok(cudaMemcpy(out->d_scales,
                            h_scales.data(),
                            (size_t)n_total_experts * sizeof(cuda_tm_strided_ptr),
                            cudaMemcpyHostToDevice),
                 "turbomind scale table upload")) {
        cuda_tm_matrix_pack_free(out);
        return 0;
    }
    out->weight_bytes = weight_bytes;
    out->scale_bytes = scale_bytes;
    out->k_pack = expected_k_pack;
    out->experts = n_total_experts;
    return 1;
}

static int cuda_tm_grouped_matmul(
        const __half *a,
        const int *offsets,
        const cuda_tm_matrix_pack *pack,
        uint32_t total_routes,
        uint32_t n_total_experts,
        int n,
        int k,
        __half *d,
        const char *label) {
    if (!pack || !pack->d_weights || !pack->d_scales) return 0;
    int rc = 0;
    if (cuda_tm_total_tokens_abi_enabled()) {
        rc = g_tm_api.mul_mat_grouped_total_tokens(
            a,
            nullptr,
            offsets,
            (int)n_total_experts,
            (int)total_routes,
            (const void * const *)pack->d_weights,
            (const void * const *)pack->d_scales,
            GGML_TM_DTYPE_MXFP4,
            n,
            k,
            DS4_SRC_MXFP4_BLOCK_ELEMS,
            pack->k_pack,
            d,
            nullptr);
    } else {
        rc = g_tm_api.mul_mat_grouped(
            a,
            nullptr,
            offsets,
            (int)n_total_experts,
            (const void * const *)pack->d_weights,
            (const void * const *)pack->d_scales,
            GGML_TM_DTYPE_MXFP4,
            n,
            k,
            DS4_SRC_MXFP4_BLOCK_ELEMS,
            pack->k_pack,
            d,
            nullptr);
    }
    if (rc != 0) {
        fprintf(stderr,
                "ds4: TurboMind grouped %s GEMM failed: rc=%d\n",
                label ? label : "matrix",
                rc);
        return 0;
    }
    return cuda_ok(cudaGetLastError(), label ? label : "turbomind grouped GEMM");
}

static int cuda_tm_routed_mxfp4_transient(
        const ds4_gpu_arena *arena,
        uint64_t gate_arena_offset,
        uint64_t up_arena_offset,
        uint64_t down_arena_offset,
        uint64_t gate_expert_stride_bytes,
        uint64_t down_expert_stride_bytes,
        uint32_t hidden,
        uint32_t mid,
        uint32_t n_total_experts,
        const ds4_gpu_tensor *selected_i32,
        const ds4_gpu_tensor *weights_f32,
        uint32_t n_routes,
        const ds4_gpu_tensor *x_f32,
        const ds4_gpu_tensor *x_row_ptrs,
        uint32_t n_tokens,
        ds4_gpu_tensor *out_f32) {
    if (!cuda_tm_load_api()) return 0;
    if (!arena || !arena->ptr || !out_f32 || !out_f32->ptr ||
        !selected_i32 || !selected_i32->ptr || !weights_f32 || !weights_f32->ptr ||
        (!x_f32 && !x_row_ptrs) || hidden == 0 || mid == 0 ||
        n_total_experts == 0 || n_routes == 0 || n_tokens == 0) {
        return 0;
    }
    if (!cuda_ok(cudaSetDevice(arena->gpu), "turbomind routed set device")) return 0;
    if (g_tm_api.init(arena->gpu) != 0) {
        cuda_tm_warn_once("ggml_turbomind_init failed");
        return 0;
    }

    uint32_t total_routes = 0;
    if (!cuda_tm_checked_total_routes(n_tokens, n_routes, &total_routes) ||
        n_total_experts > INT_MAX || hidden > INT_MAX || mid > INT_MAX) {
        cuda_tm_warn_once("unsupported routed shape");
        return 0;
    }

    uint64_t scratch_bytes = 0;
    const uint64_t counts_off = scratch_bytes = cuda_tm_align16(scratch_bytes);
    scratch_bytes += (uint64_t)n_total_experts * sizeof(int);
    const uint64_t cursors_off = scratch_bytes = cuda_tm_align16(scratch_bytes);
    scratch_bytes += (uint64_t)n_total_experts * sizeof(int);
    const uint64_t offsets_off = scratch_bytes = cuda_tm_align16(scratch_bytes);
    scratch_bytes += (uint64_t)(n_total_experts + 1u) * sizeof(int);
    const uint64_t sorted_pairs_off = scratch_bytes = cuda_tm_align16(scratch_bytes);
    scratch_bytes += (uint64_t)total_routes * sizeof(int);
    const uint64_t sorted_weights_off = scratch_bytes = cuda_tm_align16(scratch_bytes);
    scratch_bytes += (uint64_t)total_routes * sizeof(float);
    const uint64_t bad_off = scratch_bytes = cuda_tm_align16(scratch_bytes);
    scratch_bytes += sizeof(int);
    const uint64_t a_off = scratch_bytes = cuda_tm_align16(scratch_bytes);
    scratch_bytes += (uint64_t)total_routes * hidden * sizeof(__half);
    const uint64_t gate_out_off = scratch_bytes = cuda_tm_align16(scratch_bytes);
    scratch_bytes += (uint64_t)total_routes * mid * sizeof(__half);
    const uint64_t up_out_off = scratch_bytes = cuda_tm_align16(scratch_bytes);
    scratch_bytes += (uint64_t)total_routes * mid * sizeof(__half);
    const uint64_t mid_half_off = scratch_bytes = cuda_tm_align16(scratch_bytes);
    scratch_bytes += (uint64_t)total_routes * mid * sizeof(__half);
    const uint64_t down_routes_off = scratch_bytes = cuda_tm_align16(scratch_bytes);
    scratch_bytes += (uint64_t)total_routes * hidden * sizeof(__half);
    scratch_bytes = cuda_tm_align16(scratch_bytes);

    uint8_t *scratch = (uint8_t *)cuda_tmp_alloc(scratch_bytes, "turbomind routed FFN scratch");
    if (!scratch) return 0;
    int *counts = (int *)(scratch + counts_off);
    int *cursors = (int *)(scratch + cursors_off);
    int *offsets = (int *)(scratch + offsets_off);
    int *sorted_pairs = (int *)(scratch + sorted_pairs_off);
    float *sorted_weights = (float *)(scratch + sorted_weights_off);
    int *bad = (int *)(scratch + bad_off);
    __half *a_half = (__half *)(scratch + a_off);
    __half *gate_out = (__half *)(scratch + gate_out_off);
    __half *up_out = (__half *)(scratch + up_out_off);
    __half *mid_half = (__half *)(scratch + mid_half_off);
    __half *down_routes = (__half *)(scratch + down_routes_off);

    if (!cuda_tm_build_routes(counts,
                              cursors,
                              offsets,
                              sorted_pairs,
                              sorted_weights,
                              nullptr,
                              bad,
                              (const int32_t *)selected_i32->ptr,
                              (const float *)weights_f32->ptr,
                              total_routes,
                              n_total_experts,
                              "turbomind route build launch")) {
        return 0;
    }
    int h_bad = 0;
    if (!cuda_ok(cudaMemcpy(&h_bad, bad, sizeof(h_bad), cudaMemcpyDeviceToHost),
                 "turbomind route validation read")) {
        return 0;
    }
    if (h_bad) {
        cuda_tm_warn_once("invalid selected expert id");
        return 0;
    }

    const float *x_contig = x_f32 ? (const float *)x_f32->ptr : nullptr;
    const float *const *x_ptrs =
        x_row_ptrs ? (const float *const *)x_row_ptrs->ptr : nullptr;
    tm_gather_f32_to_f16_kernel<<<((uint64_t)total_routes * hidden + 255u) / 256u, 256>>>(
        a_half,
        x_contig,
        x_ptrs,
        sorted_pairs,
        n_routes,
        hidden,
        total_routes);
    if (!cuda_ok(cudaGetLastError(), "turbomind gather activations launch")) return 0;

    const uint8_t *gate_base =
        (const uint8_t *)((const char *)arena->ptr + gate_arena_offset);
    const uint8_t *up_base =
        (const uint8_t *)((const char *)arena->ptr + up_arena_offset);
    const uint8_t *down_base =
        (const uint8_t *)((const char *)arena->ptr + down_arena_offset);

    cuda_tm_matrix_pack gate_pack;
    if (!cuda_tm_pack_matrix(&gate_pack,
                             gate_base,
                             n_total_experts,
                             gate_expert_stride_bytes,
                             (int)mid,
                             (int)hidden,
                             "gate")) {
        return 0;
    }
    int ok = cuda_tm_grouped_matmul(
        a_half, offsets, &gate_pack, total_routes, n_total_experts, (int)mid, (int)hidden,
        gate_out, "gate");
    cuda_tm_matrix_pack_free(&gate_pack);
    if (!ok) return 0;

    cuda_tm_matrix_pack up_pack;
    if (!cuda_tm_pack_matrix(&up_pack,
                             up_base,
                             n_total_experts,
                             gate_expert_stride_bytes,
                             (int)mid,
                             (int)hidden,
                             "up")) {
        return 0;
    }
    ok = cuda_tm_grouped_matmul(
        a_half, offsets, &up_pack, total_routes, n_total_experts, (int)mid, (int)hidden,
        up_out, "up");
    cuda_tm_matrix_pack_free(&up_pack);
    if (!ok) return 0;

    tm_swiglu_half_kernel<<<((uint64_t)total_routes * mid + 255u) / 256u, 256>>>(
        mid_half,
        gate_out,
        up_out,
        sorted_weights,
        total_routes,
        mid,
        10.0f);
    if (!cuda_ok(cudaGetLastError(), "turbomind swiglu launch")) return 0;

    cuda_tm_matrix_pack down_pack;
    if (!cuda_tm_pack_matrix(&down_pack,
                             down_base,
                             n_total_experts,
                             down_expert_stride_bytes,
                             (int)hidden,
                             (int)mid,
                             "down")) {
        return 0;
    }
    ok = cuda_tm_grouped_matmul(
        mid_half, offsets, &down_pack, total_routes, n_total_experts, (int)hidden, (int)mid,
        down_routes, "down");
    cuda_tm_matrix_pack_free(&down_pack);
    if (!ok) return 0;

    if (!cuda_ok(cudaMemset(out_f32->ptr,
                            0,
                            (size_t)n_tokens * hidden * sizeof(float)),
                 "turbomind output clear")) {
        return 0;
    }
    tm_scatter_sum_half_to_f32_kernel<<<((uint64_t)total_routes * hidden + 255u) / 256u, 256>>>(
        (float *)out_f32->ptr,
        down_routes,
        sorted_pairs,
        n_routes,
        hidden,
        total_routes);
    return cuda_ok(cudaGetLastError(), "turbomind down scatter sum launch");
}

static int cuda_tm_routed_mxfp4_packed_impl(
        const ds4_gpu_arena *arena,
        const ds4_gpu_turbomind_mxfp4_matrix_view *gate,
        const ds4_gpu_turbomind_mxfp4_matrix_view *up,
        const ds4_gpu_turbomind_mxfp4_matrix_view *gate_up,
        const ds4_gpu_turbomind_mxfp4_matrix_view *down,
        uint32_t hidden,
        uint32_t mid,
        uint32_t n_total_experts,
        const ds4_gpu_tensor *selected_i32,
        const ds4_gpu_tensor *weights_f32,
        uint32_t n_routes,
        const ds4_gpu_tensor *x_f32,
        const ds4_gpu_tensor *x_row_ptrs,
        uint32_t n_tokens,
        ds4_gpu_tensor *out_f32,
        int accumulate_out) {
    if (!cuda_tm_load_api()) return 1;
    const int fused_gate_up = gate_up != nullptr;
    if (!arena || !arena->valid || !arena->ptr || !down ||
        !selected_i32 || !selected_i32->ptr || !weights_f32 || !weights_f32->ptr ||
        (!x_f32 && !x_row_ptrs) ||
        (x_f32 && !x_f32->ptr) ||
        (x_row_ptrs && (!x_row_ptrs->ptr || x_row_ptrs->device != arena->gpu)) ||
        !out_f32 || !out_f32->ptr ||
        hidden == 0 || mid == 0 || n_total_experts == 0 || n_routes == 0 ||
        n_tokens == 0) {
        return 1;
    }
    uint32_t total_routes = 0;
    const uint64_t fused_n_u64 = (uint64_t)mid * 2u;
    if (!cuda_tm_checked_total_routes(n_tokens, n_routes, &total_routes) ||
        n_total_experts > INT_MAX || hidden > INT_MAX || mid > INT_MAX ||
        (fused_gate_up && fused_n_u64 > INT_MAX)) {
        return 1;
    }
    if ((!fused_gate_up && (!gate || !up)) ||
        (fused_gate_up && (!gate_up || (uint64_t)gate_up->n != fused_n_u64 || gate_up->k != hidden)) ||
        (!fused_gate_up && (gate->n != mid || gate->k != hidden ||
                            up->n != mid || up->k != hidden)) ||
        down->n != hidden || down->k != mid ||
        (fused_gate_up ? gate_up->experts_packed : gate->experts_packed) < n_total_experts ||
        (!fused_gate_up && up->experts_packed < n_total_experts) ||
        down->experts_packed < n_total_experts) {
        return 1;
    }
    if (selected_i32->bytes < (uint64_t)n_tokens * n_routes * sizeof(int32_t) ||
        weights_f32->bytes < (uint64_t)n_tokens * n_routes * sizeof(float) ||
        (x_f32 && x_f32->bytes < (uint64_t)n_tokens * hidden * sizeof(float)) ||
        (x_row_ptrs && x_row_ptrs->bytes < (uint64_t)n_tokens * sizeof(float *)) ||
        out_f32->bytes < (uint64_t)n_tokens * hidden * sizeof(float)) {
        return 1;
    }
    if (!cuda_ok(cudaSetDevice(arena->gpu), "turbomind packed routed set device")) return 1;
    if (g_tm_api.init(arena->gpu) != 0) {
        cuda_tm_warn_once("ggml_turbomind_init failed");
        return 1;
    }
    cuda_tm_profile_call tm_prof;
    tm_prof.begin(arena->gpu);

    const int use_route_row_reduce = cuda_tm_route_row_reduce_enabled();
    uint64_t scratch_bytes = 0;
    const uint64_t counts_off = scratch_bytes = cuda_tm_align16(scratch_bytes);
    scratch_bytes += (uint64_t)n_total_experts * sizeof(int);
    const uint64_t cursors_off = scratch_bytes = cuda_tm_align16(scratch_bytes);
    scratch_bytes += (uint64_t)n_total_experts * sizeof(int);
    const uint64_t offsets_off = scratch_bytes = cuda_tm_align16(scratch_bytes);
    scratch_bytes += (uint64_t)(n_total_experts + 1u) * sizeof(int);
    const uint64_t sorted_pairs_off = scratch_bytes = cuda_tm_align16(scratch_bytes);
    scratch_bytes += (uint64_t)total_routes * sizeof(int);
    const uint64_t pair_rows_off = scratch_bytes = cuda_tm_align16(scratch_bytes);
    if (use_route_row_reduce) {
        scratch_bytes += (uint64_t)total_routes * sizeof(int);
    }
    const uint64_t sorted_weights_off = scratch_bytes = cuda_tm_align16(scratch_bytes);
    scratch_bytes += (uint64_t)total_routes * sizeof(float);
    const uint64_t bad_off = scratch_bytes = cuda_tm_align16(scratch_bytes);
    scratch_bytes += sizeof(int);
    const uint64_t a_off = scratch_bytes = cuda_tm_align16(scratch_bytes);
    scratch_bytes += (uint64_t)total_routes * hidden * sizeof(__half);
    const uint64_t gate_out_off = scratch_bytes = cuda_tm_align16(scratch_bytes);
    scratch_bytes += (uint64_t)total_routes * mid * (fused_gate_up ? 2u : 1u) * sizeof(__half);
    const uint64_t up_out_off = scratch_bytes = cuda_tm_align16(scratch_bytes);
    if (!fused_gate_up) {
        scratch_bytes += (uint64_t)total_routes * mid * sizeof(__half);
    }
    const uint64_t mid_half_off = scratch_bytes = cuda_tm_align16(scratch_bytes);
    scratch_bytes += (uint64_t)total_routes * mid * sizeof(__half);
    const uint64_t down_routes_off = scratch_bytes = cuda_tm_align16(scratch_bytes);
    scratch_bytes += (uint64_t)total_routes * hidden * sizeof(__half);
    scratch_bytes = cuda_tm_align16(scratch_bytes);

    uint8_t *scratch = (uint8_t *)cuda_tmp_alloc(scratch_bytes, "turbomind packed routed FFN scratch");
    if (!scratch) return 1;
    int *counts = (int *)(scratch + counts_off);
    int *cursors = (int *)(scratch + cursors_off);
    int *offsets = (int *)(scratch + offsets_off);
    int *sorted_pairs = (int *)(scratch + sorted_pairs_off);
    int *pair_rows = use_route_row_reduce ? (int *)(scratch + pair_rows_off) : nullptr;
    float *sorted_weights = (float *)(scratch + sorted_weights_off);
    int *bad = (int *)(scratch + bad_off);
    __half *a_half = (__half *)(scratch + a_off);
    __half *gate_out = (__half *)(scratch + gate_out_off);
    __half *up_out = fused_gate_up ? nullptr : (__half *)(scratch + up_out_off);
    __half *mid_half = (__half *)(scratch + mid_half_off);
    __half *down_routes = (__half *)(scratch + down_routes_off);

    if (!cuda_tm_build_routes(counts,
                              cursors,
                              offsets,
                              sorted_pairs,
                              sorted_weights,
                              pair_rows,
                              bad,
                              (const int32_t *)selected_i32->ptr,
                              (const float *)weights_f32->ptr,
                              total_routes,
                              n_total_experts,
                              "turbomind packed route build launch")) {
        return 1;
    }
    uint32_t tm_prof_active_experts = 0;
    uint32_t tm_prof_max_routes_per_expert = 0;
    if (tm_prof.enabled) {
        std::vector<int> h_offsets((size_t)n_total_experts + 1u);
        if (cudaMemcpy(h_offsets.data(),
                       offsets,
                       ((size_t)n_total_experts + 1u) * sizeof(int),
                       cudaMemcpyDeviceToHost) == cudaSuccess) {
            for (uint32_t e = 0; e < n_total_experts; e++) {
                const int count = h_offsets[e + 1u] - h_offsets[e];
                if (count > 0) {
                    tm_prof_active_experts++;
                    if ((uint32_t)count > tm_prof_max_routes_per_expert) {
                        tm_prof_max_routes_per_expert = (uint32_t)count;
                    }
                }
            }
        } else {
            (void)cudaGetLastError();
        }
    }
    if (cuda_tm_route_validation_sync_enabled()) {
        int h_bad = 0;
        if (!cuda_ok(cudaMemcpy(&h_bad, bad, sizeof(h_bad), cudaMemcpyDeviceToHost),
                     "turbomind packed route validation read")) {
            return 1;
        }
        if (h_bad) return 1;
    }
    tm_prof.mark(&tm_prof.route_ms);

    tm_gather_f32_to_f16_kernel<<<((uint64_t)total_routes * hidden + 255u) / 256u, 256>>>(
        a_half,
        x_f32 ? (const float *)x_f32->ptr : nullptr,
        x_row_ptrs ? (const float *const *)x_row_ptrs->ptr : nullptr,
        sorted_pairs,
        n_routes,
        hidden,
        total_routes);
    if (!cuda_ok(cudaGetLastError(), "turbomind packed gather activations launch")) return 1;
    tm_prof.mark(&tm_prof.gather_ms);

    cuda_tm_matrix_pack gate_pack = {};
    cuda_tm_matrix_pack up_pack = {};
    cuda_tm_matrix_pack gate_up_pack = {};
    cuda_tm_matrix_pack down_pack = {};
    if ((fused_gate_up
            ? !cuda_tm_matrix_pack_from_arena(&gate_up_pack, arena, gate_up, n_total_experts, "gate_up")
            : (!cuda_tm_matrix_pack_from_arena(&gate_pack, arena, gate, n_total_experts, "gate") ||
               !cuda_tm_matrix_pack_from_arena(&up_pack, arena, up, n_total_experts, "up"))) ||
        !cuda_tm_matrix_pack_from_arena(&down_pack, arena, down, n_total_experts, "down")) {
        cuda_tm_matrix_pack_table_free(&gate_up_pack);
        cuda_tm_matrix_pack_table_free(&gate_pack);
        cuda_tm_matrix_pack_table_free(&up_pack);
        cuda_tm_matrix_pack_table_free(&down_pack);
        return 1;
    }
    int ok = 1;
    if (fused_gate_up) {
        ok = cuda_tm_grouped_matmul(
            a_half, offsets, &gate_up_pack, total_routes, n_total_experts, (int)fused_n_u64, (int)hidden,
            gate_out, "packed gate_up");
    } else {
        ok = cuda_tm_grouped_matmul(
            a_half, offsets, &gate_pack, total_routes, n_total_experts, (int)mid, (int)hidden,
            gate_out, "packed gate");
        if (ok) {
            ok = cuda_tm_grouped_matmul(
                a_half, offsets, &up_pack, total_routes, n_total_experts, (int)mid, (int)hidden,
                up_out, "packed up");
        }
    }
    if (ok) tm_prof.mark(&tm_prof.gate_up_ms);
    if (ok) {
        if (fused_gate_up) {
            tm_swiglu_fused_gate_up_half_kernel<<<((uint64_t)total_routes * mid + 255u) / 256u, 256>>>(
                mid_half,
                gate_out,
                sorted_weights,
                total_routes,
                mid,
                10.0f);
        } else {
            tm_swiglu_half_kernel<<<((uint64_t)total_routes * mid + 255u) / 256u, 256>>>(
                mid_half,
                gate_out,
                up_out,
                sorted_weights,
                total_routes,
                mid,
                10.0f);
        }
        ok = cuda_ok(cudaGetLastError(), "turbomind packed swiglu launch");
    }
    if (ok) tm_prof.mark(&tm_prof.swiglu_ms);
    if (ok) {
        ok = cuda_tm_grouped_matmul(
            mid_half, offsets, &down_pack, total_routes, n_total_experts, (int)hidden, (int)mid,
            down_routes, "packed down");
    }
    if (ok) tm_prof.mark(&tm_prof.down_ms);
    cuda_tm_matrix_pack_table_free(&down_pack);
    cuda_tm_matrix_pack_table_free(&gate_up_pack);
    cuda_tm_matrix_pack_table_free(&up_pack);
    cuda_tm_matrix_pack_table_free(&gate_pack);
    if (!ok) return 1;

    if (use_route_row_reduce) {
        tm_reduce_sum_half_to_f32_by_pair_kernel<<<((uint64_t)n_tokens * hidden + 255u) / 256u, 256>>>(
            (float *)out_f32->ptr,
            down_routes,
            pair_rows,
            n_routes,
            hidden,
            n_tokens,
            accumulate_out);
        const int reduce_ok =
            cuda_ok(cudaGetLastError(), "turbomind packed route-row reduce launch") ? 1 : 0;
        if (reduce_ok) {
            tm_prof.mark(&tm_prof.scatter_ms);
            tm_prof.finish(n_tokens,
                           total_routes,
                           tm_prof_active_experts,
                           tm_prof_max_routes_per_expert,
                           fused_gate_up);
        }
        return reduce_ok ? 0 : 1;
    }

    if (!accumulate_out) {
        if (!cuda_ok(cudaMemset(out_f32->ptr,
                                0,
                                (size_t)n_tokens * hidden * sizeof(float)),
                     "turbomind packed output clear")) {
            return 1;
        }
    }
    tm_scatter_sum_half_to_f32_kernel<<<((uint64_t)total_routes * hidden + 255u) / 256u, 256>>>(
        (float *)out_f32->ptr,
        down_routes,
        sorted_pairs,
        n_routes,
        hidden,
        total_routes);
    const int scatter_ok =
        cuda_ok(cudaGetLastError(), "turbomind packed down scatter sum launch") ? 1 : 0;
    if (scatter_ok) {
        tm_prof.mark(&tm_prof.scatter_ms);
        tm_prof.finish(n_tokens,
                       total_routes,
                       tm_prof_active_experts,
                       tm_prof_max_routes_per_expert,
                       fused_gate_up);
    }
    return scatter_ok ? 0 : 1;
}

extern "C" int ds4_gpu_arena_turbomind_mxfp4_routed_swiglu_down_sum_f32(
        const ds4_gpu_arena *arena,
        const ds4_gpu_turbomind_mxfp4_matrix_view *gate,
        const ds4_gpu_turbomind_mxfp4_matrix_view *up,
        const ds4_gpu_turbomind_mxfp4_matrix_view *down,
        uint32_t hidden,
        uint32_t mid,
        uint32_t n_total_experts,
        const ds4_gpu_tensor *selected_i32,
        const ds4_gpu_tensor *weights_f32,
        uint32_t n_routes,
        const ds4_gpu_tensor *x_f32,
        uint32_t n_tokens,
        ds4_gpu_tensor *out_f32) {
    return cuda_tm_routed_mxfp4_packed_impl(arena,
                                           gate,
                                           up,
                                           nullptr,
                                           down,
                                           hidden,
                                           mid,
                                           n_total_experts,
                                           selected_i32,
                                           weights_f32,
                                           n_routes,
                                           x_f32,
                                           nullptr,
                                           n_tokens,
                                           out_f32,
                                           0);
}

extern "C" int ds4_gpu_arena_turbomind_mxfp4_routed_swiglu_down_sum_batch_ptr_table_f32(
        const ds4_gpu_arena *arena,
        const ds4_gpu_turbomind_mxfp4_matrix_view *gate,
        const ds4_gpu_turbomind_mxfp4_matrix_view *up,
        const ds4_gpu_turbomind_mxfp4_matrix_view *down,
        uint32_t hidden,
        uint32_t mid,
        uint32_t n_total_experts,
        const ds4_gpu_tensor *selected_i32,
        const ds4_gpu_tensor *weights_f32,
        uint32_t n_routes,
        const ds4_gpu_tensor *x_row_ptrs,
        uint32_t n_tokens,
        ds4_gpu_tensor *out_f32);

extern "C" int ds4_gpu_arena_turbomind_mxfp4_routed_swiglu_down_sum_batch_ptrs_f32(
        const ds4_gpu_arena *arena,
        const ds4_gpu_turbomind_mxfp4_matrix_view *gate,
        const ds4_gpu_turbomind_mxfp4_matrix_view *up,
        const ds4_gpu_turbomind_mxfp4_matrix_view *down,
        uint32_t hidden,
        uint32_t mid,
        uint32_t n_total_experts,
        const ds4_gpu_tensor *selected_i32,
        const ds4_gpu_tensor *weights_f32,
        uint32_t n_routes,
        ds4_gpu_tensor *x_row_ptrs,
        const ds4_gpu_tensor *const *x_rows_f32,
        uint32_t n_tokens,
        ds4_gpu_tensor *out_f32) {
    if (!arena || !x_row_ptrs || !x_rows_f32 || !x_row_ptrs->ptr ||
        x_row_ptrs->device != arena->gpu ||
        x_row_ptrs->bytes < (uint64_t)n_tokens * sizeof(float *) ||
        n_tokens == 0) {
        return 1;
    }
    std::vector<const float *> row_ptrs(n_tokens);
    for (uint32_t tok = 0; tok < n_tokens; tok++) {
        const ds4_gpu_tensor *x = x_rows_f32[tok];
        if (!x || !x->ptr || x->device != arena->gpu ||
            x->bytes < (uint64_t)hidden * sizeof(float)) {
            return 1;
        }
        row_ptrs[tok] = (const float *)x->ptr;
    }
    if (!cuda_ok(cudaSetDevice(arena->gpu), "turbomind packed batch ptr set device") ||
        !cuda_ok(cudaMemcpy(x_row_ptrs->ptr,
                            row_ptrs.data(),
                            (size_t)n_tokens * sizeof(float *),
                            cudaMemcpyHostToDevice),
                 "turbomind packed batch row ptr upload")) {
        return 1;
    }
    return ds4_gpu_arena_turbomind_mxfp4_routed_swiglu_down_sum_batch_ptr_table_f32(
        arena,
        gate,
        up,
        down,
        hidden,
        mid,
        n_total_experts,
        selected_i32,
        weights_f32,
        n_routes,
        x_row_ptrs,
        n_tokens,
        out_f32);
}

extern "C" int ds4_gpu_arena_turbomind_mxfp4_routed_swiglu_down_sum_batch_ptr_table_f32(
        const ds4_gpu_arena *arena,
        const ds4_gpu_turbomind_mxfp4_matrix_view *gate,
        const ds4_gpu_turbomind_mxfp4_matrix_view *up,
        const ds4_gpu_turbomind_mxfp4_matrix_view *down,
        uint32_t hidden,
        uint32_t mid,
        uint32_t n_total_experts,
        const ds4_gpu_tensor *selected_i32,
        const ds4_gpu_tensor *weights_f32,
        uint32_t n_routes,
        const ds4_gpu_tensor *x_row_ptrs,
        uint32_t n_tokens,
        ds4_gpu_tensor *out_f32) {
    if (!arena || !x_row_ptrs || !x_row_ptrs->ptr ||
        x_row_ptrs->device != arena->gpu ||
        x_row_ptrs->bytes < (uint64_t)n_tokens * sizeof(float *) ||
        n_tokens == 0) {
        return 1;
    }
    return cuda_tm_routed_mxfp4_packed_impl(arena,
                                           gate,
                                           up,
                                           nullptr,
                                           down,
                                           hidden,
                                           mid,
                                           n_total_experts,
                                           selected_i32,
                                           weights_f32,
                                           n_routes,
                                           nullptr,
                                           x_row_ptrs,
                                           n_tokens,
                                           out_f32,
                                           0);
}

extern "C" int ds4_gpu_arena_turbomind_mxfp4_routed_swiglu_down_sum_accum_f32(
        const ds4_gpu_arena *arena,
        const ds4_gpu_turbomind_mxfp4_matrix_view *gate,
        const ds4_gpu_turbomind_mxfp4_matrix_view *up,
        const ds4_gpu_turbomind_mxfp4_matrix_view *down,
        uint32_t hidden,
        uint32_t mid,
        uint32_t n_total_experts,
        const ds4_gpu_tensor *selected_i32,
        const ds4_gpu_tensor *weights_f32,
        uint32_t n_routes,
        const ds4_gpu_tensor *x_f32,
        uint32_t n_tokens,
        ds4_gpu_tensor *out_f32) {
    return cuda_tm_routed_mxfp4_packed_impl(arena,
                                           gate,
                                           up,
                                           nullptr,
                                           down,
                                           hidden,
                                           mid,
                                           n_total_experts,
                                           selected_i32,
                                           weights_f32,
                                           n_routes,
                                           x_f32,
                                           nullptr,
                                           n_tokens,
                                           out_f32,
                                           1);
}

extern "C" int ds4_gpu_arena_turbomind_mxfp4_routed_gate_up_swiglu_down_sum_f32(
        const ds4_gpu_arena *arena,
        const ds4_gpu_turbomind_mxfp4_matrix_view *gate_up,
        const ds4_gpu_turbomind_mxfp4_matrix_view *down,
        uint32_t hidden,
        uint32_t mid,
        uint32_t n_total_experts,
        const ds4_gpu_tensor *selected_i32,
        const ds4_gpu_tensor *weights_f32,
        uint32_t n_routes,
        const ds4_gpu_tensor *x_f32,
        uint32_t n_tokens,
        ds4_gpu_tensor *out_f32) {
    return cuda_tm_routed_mxfp4_packed_impl(arena,
                                           nullptr,
                                           nullptr,
                                           gate_up,
                                           down,
                                           hidden,
                                           mid,
                                           n_total_experts,
                                           selected_i32,
                                           weights_f32,
                                           n_routes,
                                           x_f32,
                                           nullptr,
                                           n_tokens,
                                           out_f32,
                                           0);
}

extern "C" int ds4_gpu_arena_turbomind_mxfp4_routed_gate_up_swiglu_down_sum_accum_f32(
        const ds4_gpu_arena *arena,
        const ds4_gpu_turbomind_mxfp4_matrix_view *gate_up,
        const ds4_gpu_turbomind_mxfp4_matrix_view *down,
        uint32_t hidden,
        uint32_t mid,
        uint32_t n_total_experts,
        const ds4_gpu_tensor *selected_i32,
        const ds4_gpu_tensor *weights_f32,
        uint32_t n_routes,
        const ds4_gpu_tensor *x_f32,
        uint32_t n_tokens,
        ds4_gpu_tensor *out_f32) {
    return cuda_tm_routed_mxfp4_packed_impl(arena,
                                           nullptr,
                                           nullptr,
                                           gate_up,
                                           down,
                                           hidden,
                                           mid,
                                           n_total_experts,
                                           selected_i32,
                                           weights_f32,
                                           n_routes,
                                           x_f32,
                                           nullptr,
                                           n_tokens,
                                           out_f32,
                                           1);
}

extern "C" int ds4_gpu_arena_turbomind_mxfp4_routed_gate_up_swiglu_down_sum_batch_ptr_table_f32(
        const ds4_gpu_arena *arena,
        const ds4_gpu_turbomind_mxfp4_matrix_view *gate_up,
        const ds4_gpu_turbomind_mxfp4_matrix_view *down,
        uint32_t hidden,
        uint32_t mid,
        uint32_t n_total_experts,
        const ds4_gpu_tensor *selected_i32,
        const ds4_gpu_tensor *weights_f32,
        uint32_t n_routes,
        const ds4_gpu_tensor *x_row_ptrs,
        uint32_t n_tokens,
        ds4_gpu_tensor *out_f32);

extern "C" int ds4_gpu_arena_turbomind_mxfp4_routed_gate_up_swiglu_down_sum_batch_ptrs_f32(
        const ds4_gpu_arena *arena,
        const ds4_gpu_turbomind_mxfp4_matrix_view *gate_up,
        const ds4_gpu_turbomind_mxfp4_matrix_view *down,
        uint32_t hidden,
        uint32_t mid,
        uint32_t n_total_experts,
        const ds4_gpu_tensor *selected_i32,
        const ds4_gpu_tensor *weights_f32,
        uint32_t n_routes,
        ds4_gpu_tensor *x_row_ptrs,
        const ds4_gpu_tensor *const *x_rows_f32,
        uint32_t n_tokens,
        ds4_gpu_tensor *out_f32) {
    if (!arena || !x_row_ptrs || !x_rows_f32 || !x_row_ptrs->ptr ||
        x_row_ptrs->device != arena->gpu ||
        x_row_ptrs->bytes < (uint64_t)n_tokens * sizeof(float *) ||
        n_tokens == 0) {
        return 1;
    }
    std::vector<const float *> row_ptrs(n_tokens);
    for (uint32_t tok = 0; tok < n_tokens; tok++) {
        const ds4_gpu_tensor *x = x_rows_f32[tok];
        if (!x || !x->ptr || x->device != arena->gpu ||
            x->bytes < (uint64_t)hidden * sizeof(float)) {
            return 1;
        }
        row_ptrs[tok] = (const float *)x->ptr;
    }
    if (!cuda_ok(cudaSetDevice(arena->gpu), "turbomind fused batch ptr set device") ||
        !cuda_ok(cudaMemcpy(x_row_ptrs->ptr,
                            row_ptrs.data(),
                            (size_t)n_tokens * sizeof(float *),
                            cudaMemcpyHostToDevice),
                 "turbomind fused batch row ptr upload")) {
        return 1;
    }
    return ds4_gpu_arena_turbomind_mxfp4_routed_gate_up_swiglu_down_sum_batch_ptr_table_f32(
        arena,
        gate_up,
        down,
        hidden,
        mid,
        n_total_experts,
        selected_i32,
        weights_f32,
        n_routes,
        x_row_ptrs,
        n_tokens,
        out_f32);
}

extern "C" int ds4_gpu_arena_turbomind_mxfp4_routed_gate_up_swiglu_down_sum_batch_ptr_table_f32(
        const ds4_gpu_arena *arena,
        const ds4_gpu_turbomind_mxfp4_matrix_view *gate_up,
        const ds4_gpu_turbomind_mxfp4_matrix_view *down,
        uint32_t hidden,
        uint32_t mid,
        uint32_t n_total_experts,
        const ds4_gpu_tensor *selected_i32,
        const ds4_gpu_tensor *weights_f32,
        uint32_t n_routes,
        const ds4_gpu_tensor *x_row_ptrs,
        uint32_t n_tokens,
        ds4_gpu_tensor *out_f32) {
    if (!arena || !x_row_ptrs || !x_row_ptrs->ptr ||
        x_row_ptrs->device != arena->gpu ||
        x_row_ptrs->bytes < (uint64_t)n_tokens * sizeof(float *) ||
        n_tokens == 0) {
        return 1;
    }
    return cuda_tm_routed_mxfp4_packed_impl(arena,
                                           nullptr,
                                           nullptr,
                                           gate_up,
                                           down,
                                           hidden,
                                           mid,
                                           n_total_experts,
                                           selected_i32,
                                           weights_f32,
                                           n_routes,
                                           nullptr,
                                           x_row_ptrs,
                                           n_tokens,
                                           out_f32,
                                           0);
}

extern "C" int ds4_gpu_arena_open(ds4_gpu_arena **out, int gpu, uint64_t bytes) {
    if (!out || gpu < 0) return 1;
    *out = NULL;
    int n_dev = ds4_gpu_device_count();
    if (gpu >= n_dev) {
        fprintf(stderr, "ds4: arena GPU %d is outside visible device count %d\n", gpu, n_dev);
        return 1;
    }
    ds4_gpu_arena *a = (ds4_gpu_arena *)calloc(1, sizeof(*a));
    if (!a) return 1;
    a->gpu = gpu;
    a->bytes = bytes;
    a->valid = 1;
    if (!cuda_ok(cudaSetDevice(gpu), "arena set device")) {
        free(a);
        return 1;
    }
    (void)cudaMemGetInfo(&a->free_before, &a->total_before);
    uint64_t alloc_bytes = bytes ? bytes : 1;
    if (!cuda_ok(cudaMalloc(&a->ptr, (size_t)alloc_bytes), "arena alloc")) {
        free(a);
        return 1;
    }
    (void)cudaMemGetInfo(&a->free_after_alloc, &a->total_after_alloc);
    cudaPointerAttributes attr;
    memset(&attr, 0, sizeof(attr));
    cudaError_t attr_err = cudaPointerGetAttributes(&attr, a->ptr);
    if (attr_err != cudaSuccess || attr.type != cudaMemoryTypeDevice) {
        fprintf(stderr, "ds4: arena allocation is not device memory on gpu %d\n", gpu);
        if (attr_err != cudaSuccess) (void)cudaGetLastError();
        (void)cudaFree(a->ptr);
        free(a);
        return 1;
    }
    *out = a;
    return 0;
}

extern "C" void ds4_gpu_arena_close(ds4_gpu_arena *arena) {
    if (!arena) return;
    if (arena->ptr) {
        (void)cudaSetDevice(arena->gpu);
        cuda_tm_matrix_table_cache_release_arena(arena);
        cuda_f8_f16_arena_cache_release_arena(arena);
        (void)cudaFree(arena->ptr);
    }
    free(arena);
}

extern "C" int ds4_gpu_arena_upload(ds4_gpu_arena *arena,
                                    uint64_t offset,
                                    const void *host_src,
                                    uint64_t bytes) {
    if (!cuda_arena_range_ok(arena, offset, bytes) || (bytes && !host_src)) {
        if (arena) arena->valid = 0;
        return 1;
    }
    if (bytes == 0) return 0;
    if (!cuda_ok(cudaSetDevice(arena->gpu), "arena upload set device") ||
        !cuda_ok(cudaMemcpy((char *)arena->ptr + offset,
                            host_src,
                            (size_t)bytes,
                            cudaMemcpyHostToDevice),
                 "arena upload")) {
        arena->valid = 0;
        if (arena->ptr) {
            (void)cudaFree(arena->ptr);
            arena->ptr = NULL;
        }
        return 1;
    }
    if (offset + bytes > arena->used) arena->used = offset + bytes;
    if (arena->used > arena->peak_used) arena->peak_used = arena->used;
    (void)cudaMemGetInfo(&arena->free_after_upload, &arena->total_after_upload);
    return 0;
}

extern "C" int ds4_gpu_arena_read(const ds4_gpu_arena *arena,
                                  uint64_t offset,
                                  void *dst,
                                  uint64_t bytes) {
    if (!cuda_arena_range_ok(arena, offset, bytes) || (bytes && !dst)) return 1;
    if (bytes == 0) return 0;
    if (!cuda_ok(cudaSetDevice(arena->gpu), "arena read set device") ||
        !cuda_ok(cudaMemcpy(dst,
                            (const char *)arena->ptr + offset,
                            (size_t)bytes,
                            cudaMemcpyDeviceToHost),
                 "arena read")) {
        return 1;
    }
    return 0;
}

extern "C" uint64_t ds4_gpu_arena_bytes(const ds4_gpu_arena *arena) {
    return arena ? arena->bytes : 0;
}

extern "C" uint64_t ds4_gpu_arena_used(const ds4_gpu_arena *arena) {
    return arena ? arena->used : 0;
}

extern "C" uint64_t ds4_gpu_arena_free_after_upload_bytes(const ds4_gpu_arena *arena) {
    return arena ? (uint64_t)arena->free_after_upload : 0;
}

extern "C" int ds4_gpu_arena_gpu(const ds4_gpu_arena *arena) {
    return arena ? arena->gpu : -1;
}

extern "C" const char *ds4_gpu_arena_memory_kind(const ds4_gpu_arena *arena) {
    (void)arena;
    return "device";
}

extern "C" int ds4_gpu_arena_is_device_memory(const ds4_gpu_arena *arena) {
    if (!arena || !arena->ptr) return 0;
    cudaPointerAttributes attr;
    memset(&attr, 0, sizeof(attr));
    cudaError_t err = cudaPointerGetAttributes(&attr, arena->ptr);
    if (err != cudaSuccess) {
        (void)cudaGetLastError();
        return 0;
    }
    return attr.type == cudaMemoryTypeDevice;
}

extern "C" void ds4_gpu_arena_print_memory_report(FILE *fp,
                                                  ds4_gpu_arena * const *arenas,
                                                  int n_arenas) {
    if (!fp) fp = stderr;
    fprintf(fp,
            "gpu\tarena_bytes\tused_bytes\tpeak_used\tmemory_kind\t"
            "free_before\ttotal_before\tfree_after_alloc\ttotal_after_alloc\t"
            "free_after_upload\ttotal_after_upload\tvalid\n");
    for (int i = 0; i < n_arenas; i++) {
        ds4_gpu_arena *a = arenas ? arenas[i] : NULL;
        if (!a) continue;
        size_t free_now = 0;
        size_t total_now = 0;
        (void)cudaSetDevice(a->gpu);
        (void)cudaMemGetInfo(&free_now, &total_now);
        if (a->free_after_upload == 0) {
            a->free_after_upload = free_now;
            a->total_after_upload = total_now;
        }
        fprintf(fp,
                "%d\t%llu\t%llu\t%llu\t%s\t"
                "%zu\t%zu\t%zu\t%zu\t%zu\t%zu\t%d\n",
                a->gpu,
                (unsigned long long)a->bytes,
                (unsigned long long)a->used,
                (unsigned long long)a->peak_used,
                ds4_gpu_arena_memory_kind(a),
                a->free_before,
                a->total_before,
                a->free_after_alloc,
                a->total_after_alloc,
                a->free_after_upload,
                a->total_after_upload,
                a->valid);
    }
}

extern "C" void ds4_gpu_print_topology_report(FILE *fp) {
    if (!fp) fp = stderr;
    int n = ds4_gpu_device_count();
    fprintf(fp, "gpu_topology\tdevice_count\t%d\n", n);
    fprintf(fp, "gpu\tname\tpci_bus_id\ttotal_global_mem\n");
    for (int i = 0; i < n; i++) {
        cudaDeviceProp prop;
        memset(&prop, 0, sizeof(prop));
        if (cudaGetDeviceProperties(&prop, i) != cudaSuccess) {
            (void)cudaGetLastError();
            continue;
        }
        char pci[32] = {0};
        (void)cudaDeviceGetPCIBusId(pci, sizeof(pci), i);
        fprintf(fp, "%d\t%s\t%s\t%zu\n", i, prop.name, pci, prop.totalGlobalMem);
    }
    fprintf(fp, "p2p_from\\to");
    for (int j = 0; j < n; j++) fprintf(fp, "\t%d", j);
    fputc('\n', fp);
    for (int i = 0; i < n; i++) {
        fprintf(fp, "%d", i);
        for (int j = 0; j < n; j++) {
            int can = (i == j) ? 1 : 0;
            if (i != j) {
                cudaError_t err = cudaDeviceCanAccessPeer(&can, i, j);
                if (err != cudaSuccess) {
                    (void)cudaGetLastError();
                    can = 0;
                }
            }
            fprintf(fp, "\t%d", can);
        }
        fputc('\n', fp);
    }
}

extern "C" int ds4_gpu_arena_bf16_row_gather_f32(
        const ds4_gpu_arena            *arena,
        const ds4_gpu_bf16_matrix_view *view,
        const uint32_t                 *row_ids,
        uint32_t                        n_rows,
        float                          *out_f32,
        uint64_t                        out_bytes) {
    uint64_t values = 0;
    uint64_t row_id_bytes = 0;
    if (!cuda_bf16_view_range_ok(arena, view, row_ids, n_rows, out_f32,
                                 out_bytes, &values, &row_id_bytes)) {
        return 1;
    }
    uint64_t output_bytes = values * sizeof(float);
    if (output_bytes > (uint64_t)SIZE_MAX || row_id_bytes > (uint64_t)SIZE_MAX) return 1;

    if (!cuda_ok(cudaSetDevice(arena->gpu), "bf16 probe set device")) return 1;

    uint32_t *dev_rows = NULL;
    float *dev_out = NULL;
    if (!cuda_ok(cudaMalloc(&dev_rows, (size_t)row_id_bytes), "bf16 probe row ids alloc")) return 1;
    if (!cuda_ok(cudaMalloc(&dev_out, (size_t)output_bytes), "bf16 probe output alloc")) {
        (void)cudaFree(dev_rows);
        return 1;
    }
    int ok = 1;
    if (!cuda_ok(cudaMemcpy(dev_rows, row_ids, (size_t)row_id_bytes, cudaMemcpyHostToDevice),
                 "bf16 probe row ids upload")) {
        ok = 0;
    }
    if (ok) {
        const uint16_t *base =
            (const uint16_t *)((const char *)arena->ptr + view->arena_offset);
        uint64_t blocks = (values + 255) / 256;
        if (blocks > (uint64_t)UINT32_MAX) {
            ok = 0;
        } else {
            arena_bf16_row_gather_kernel<<<(unsigned int)blocks, 256>>>(
                dev_out,
                base,
                dev_rows,
                n_rows,
                view->cols,
                view->row_stride_elements);
            if (!cuda_ok(cudaGetLastError(), "bf16 probe launch") ||
                !cuda_ok(cudaDeviceSynchronize(), "bf16 probe synchronize")) {
                ok = 0;
            }
        }
    }
    if (ok &&
        !cuda_ok(cudaMemcpy(out_f32, dev_out, (size_t)output_bytes, cudaMemcpyDeviceToHost),
                 "bf16 probe output read")) {
        ok = 0;
    }
    (void)cudaFree(dev_out);
    (void)cudaFree(dev_rows);
    return ok ? 0 : 1;
}

static int cuda_bf16_matmul_view_ok(const ds4_gpu_arena *arena,
                                    const ds4_gpu_bf16_matrix_view *view,
                                    const ds4_gpu_tensor *x_f32,
                                    const ds4_gpu_tensor *out_f32) {
    if (!arena || !view || !x_f32 || !out_f32 || !arena->valid ||
        !arena->ptr || !x_f32->ptr || !out_f32->ptr) {
        return 0;
    }
    if (view->rows == 0 || view->cols == 0) return 0;
    if (view->row_stride_elements < view->cols) return 0;
    if ((view->arena_offset & 1ull) != 0 || (view->byte_length & 1ull) != 0) return 0;
    if (!cuda_arena_range_ok(arena, view->arena_offset, view->byte_length)) return 0;

    const uint64_t total_elements = view->byte_length / sizeof(uint16_t);
    uint64_t last_start = 0;
    if (checked_mul_u64((uint64_t)view->rows - 1u,
                        (uint64_t)view->row_stride_elements,
                        &last_start)) {
        return 0;
    }
    if ((uint64_t)view->cols > total_elements ||
        last_start > total_elements - (uint64_t)view->cols) {
        return 0;
    }

    uint64_t x_bytes = 0;
    uint64_t out_bytes = 0;
    if (checked_mul_u64((uint64_t)view->cols, sizeof(float), &x_bytes)) return 0;
    if (checked_mul_u64((uint64_t)view->rows, sizeof(float), &out_bytes)) return 0;
    if (x_f32->bytes < x_bytes || out_f32->bytes < out_bytes) return 0;
    return 1;
}

extern "C" int ds4_gpu_arena_bf16_matmul_f32(
        const ds4_gpu_arena            *arena,
        const ds4_gpu_bf16_matrix_view *view,
        const ds4_gpu_tensor           *x_f32,
        ds4_gpu_tensor                 *out_f32) {
    if (!cuda_bf16_matmul_view_ok(arena, view, x_f32, out_f32)) return 1;
    if (!cuda_ok(cudaSetDevice(arena->gpu), "bf16 source matmul set device")) return 1;
    const uint16_t *base = (const uint16_t *)((const char *)arena->ptr + view->arena_offset);
    arena_bf16_matmul_kernel<<<view->rows, 256>>>(
        (float *)out_f32->ptr,
        base,
        (const float *)x_f32->ptr,
        view->rows,
        view->cols,
        view->row_stride_elements);
    return cuda_ok(cudaGetLastError(), "bf16 source matmul launch") ? 0 : 1;
}

extern "C" int ds4_gpu_arena_bf16_matmul_f32_rows(
        const ds4_gpu_arena            *arena,
        const ds4_gpu_bf16_matrix_view *view,
        const ds4_gpu_tensor           *x_f32,
        uint32_t                        n_rows,
        ds4_gpu_tensor                 *out_f32) {
    if (!arena || !view || !x_f32 || !out_f32 || !arena->valid ||
        !arena->ptr || !x_f32->ptr || !out_f32->ptr || n_rows == 0) {
        return 1;
    }
    if (view->rows == 0 || view->cols == 0 ||
        view->row_stride_elements < view->cols ||
        (view->arena_offset & 1ull) != 0 ||
        (view->byte_length & 1ull) != 0 ||
        !cuda_arena_range_ok(arena, view->arena_offset, view->byte_length)) {
        return 1;
    }
    const uint64_t total_elements = view->byte_length / sizeof(uint16_t);
    uint64_t last_start = 0;
    if (checked_mul_u64((uint64_t)view->rows - 1u,
                        (uint64_t)view->row_stride_elements,
                        &last_start) ||
        (uint64_t)view->cols > total_elements ||
        last_start > total_elements - (uint64_t)view->cols) {
        return 1;
    }
    uint64_t x_elems = 0;
    uint64_t out_elems = 0;
    if (checked_mul_u64((uint64_t)view->cols, n_rows, &x_elems) ||
        checked_mul_u64((uint64_t)view->rows, n_rows, &out_elems) ||
        x_elems > UINT64_MAX / sizeof(float) ||
        out_elems > UINT64_MAX / sizeof(float) ||
        x_f32->bytes < x_elems * sizeof(float) ||
        out_f32->bytes < out_elems * sizeof(float)) {
        return 1;
    }
    if (!cuda_ok(cudaSetDevice(arena->gpu), "bf16 source rows matmul set device")) return 1;
    const uint16_t *base = (const uint16_t *)((const char *)arena->ptr + view->arena_offset);
    dim3 grid(view->rows, n_rows, 1);
    arena_bf16_matmul_rows_kernel<<<grid, 256>>>(
        (float *)out_f32->ptr,
        base,
        (const float *)x_f32->ptr,
        view->rows,
        view->cols,
        view->row_stride_elements,
        n_rows);
    return cuda_ok(cudaGetLastError(), "bf16 source rows matmul launch") ? 0 : 1;
}

static int cuda_f32_matmul_view_ok(const ds4_gpu_arena *arena,
                                   const ds4_gpu_source_row_view *view,
                                   const ds4_gpu_tensor *x_f32,
                                   const ds4_gpu_tensor *out_f32) {
    if (!arena || !view || !x_f32 || !out_f32 || !arena->valid ||
        !arena->ptr || !x_f32->ptr || !out_f32->ptr) {
        return 0;
    }
    if (view->rows == 0 || view->cols == 0 || view->row_stride_bytes == 0) return 0;
    if ((view->arena_offset & 3ull) != 0 ||
        (view->byte_length & 3ull) != 0 ||
        (view->row_stride_bytes & 3u) != 0) {
        return 0;
    }
    if (!cuda_arena_range_ok(arena, view->arena_offset, view->byte_length)) return 0;
    const uint64_t row_bytes = (uint64_t)view->cols * sizeof(float);
    if ((uint64_t)view->row_stride_bytes < row_bytes) return 0;
    uint64_t last_start = 0;
    if (checked_mul_u64((uint64_t)view->rows - 1u,
                        (uint64_t)view->row_stride_bytes,
                        &last_start)) {
        return 0;
    }
    if (last_start > view->byte_length || row_bytes > view->byte_length - last_start) {
        return 0;
    }
    uint64_t x_bytes = 0;
    uint64_t out_bytes = 0;
    if (checked_mul_u64((uint64_t)view->cols, sizeof(float), &x_bytes)) return 0;
    if (checked_mul_u64((uint64_t)view->rows, sizeof(float), &out_bytes)) return 0;
    if (x_f32->bytes < x_bytes || out_f32->bytes < out_bytes) return 0;
    return 1;
}

extern "C" int ds4_gpu_arena_f32_matmul_f32(
        const ds4_gpu_arena           *arena,
        const ds4_gpu_source_row_view *view,
        const ds4_gpu_tensor          *x_f32,
        ds4_gpu_tensor                *out_f32) {
    if (!cuda_f32_matmul_view_ok(arena, view, x_f32, out_f32)) return 1;
    if (!cuda_ok(cudaSetDevice(arena->gpu), "f32 source matmul set device")) return 1;
    const float *base = (const float *)((const char *)arena->ptr + view->arena_offset);
    arena_f32_matmul_kernel<<<view->rows, 256>>>(
        (float *)out_f32->ptr,
        base,
        (const float *)x_f32->ptr,
        view->rows,
        view->cols,
        view->row_stride_bytes);
    return cuda_ok(cudaGetLastError(), "f32 source matmul launch") ? 0 : 1;
}

extern "C" int ds4_gpu_arena_f8_e4m3_b128_row_decode_f32(
        const ds4_gpu_arena           *arena,
        const ds4_gpu_source_row_view *view,
        const uint32_t                *row_ids,
        uint32_t                       n_rows,
        float                         *out_f32,
        uint64_t                       out_bytes) {
    uint64_t values = 0;
    uint64_t row_id_bytes = 0;
    if (!cuda_f8_e4m3_b128_view_range_ok(arena, view, row_ids, n_rows, out_f32,
                                         out_bytes, &values, &row_id_bytes)) {
        return 1;
    }
    uint64_t output_bytes = values * sizeof(float);
    if (output_bytes > (uint64_t)SIZE_MAX || row_id_bytes > (uint64_t)SIZE_MAX) return 1;

    if (!cuda_ok(cudaSetDevice(arena->gpu), "f8 source probe set device")) return 1;

    uint32_t *dev_rows = NULL;
    float *dev_out = NULL;
    if (!cuda_ok(cudaMalloc(&dev_rows, (size_t)row_id_bytes), "f8 source probe row ids alloc")) return 1;
    if (!cuda_ok(cudaMalloc(&dev_out, (size_t)output_bytes), "f8 source probe output alloc")) {
        (void)cudaFree(dev_rows);
        return 1;
    }
    int ok = 1;
    if (!cuda_ok(cudaMemcpy(dev_rows, row_ids, (size_t)row_id_bytes, cudaMemcpyHostToDevice),
                 "f8 source probe row ids upload")) {
        ok = 0;
    }
    if (ok) {
        const uint8_t *base = (const uint8_t *)((const char *)arena->ptr + view->arena_offset);
        uint64_t blocks = (values + 255) / 256;
        if (blocks > (uint64_t)UINT32_MAX) {
            ok = 0;
        } else {
            arena_f8_e4m3_b128_row_decode_kernel<<<(unsigned int)blocks, 256>>>(
                dev_out,
                base,
                dev_rows,
                n_rows,
                view->cols,
                view->row_stride_bytes);
            if (!cuda_ok(cudaGetLastError(), "f8 source probe launch") ||
                !cuda_ok(cudaDeviceSynchronize(), "f8 source probe synchronize")) {
                ok = 0;
            }
        }
    }
    if (ok &&
        !cuda_ok(cudaMemcpy(out_f32, dev_out, (size_t)output_bytes, cudaMemcpyDeviceToHost),
                 "f8 source probe output read")) {
        ok = 0;
    }
    (void)cudaFree(dev_out);
    (void)cudaFree(dev_rows);
    return ok ? 0 : 1;
}

static int cuda_f8_e4m3_b128_view_layout_ok(const ds4_gpu_arena *arena,
                                            const ds4_gpu_source_row_view *view) {
    if (!arena || !view || !arena->valid || !arena->ptr) {
        return 0;
    }
    if (view->rows == 0 || view->cols == 0 || view->row_stride_bytes == 0) return 0;
    if (!cuda_arena_range_ok(arena, view->arena_offset, view->byte_length)) return 0;
    uint64_t row_bytes = 0;
    if (f8_e4m3_b128_row_bytes(view->cols, &row_bytes)) return 0;
    if ((uint64_t)view->row_stride_bytes < row_bytes) return 0;
    uint64_t last_start = 0;
    if (checked_mul_u64((uint64_t)view->rows - 1u,
                        (uint64_t)view->row_stride_bytes,
                        &last_start)) {
        return 0;
    }
    if (last_start > view->byte_length || row_bytes > view->byte_length - last_start) {
        return 0;
    }
    return 1;
}

static int cuda_f8_e4m3_b128_matmul_view_ok(const ds4_gpu_arena *arena,
                                            const ds4_gpu_source_row_view *view,
                                            const ds4_gpu_tensor *x_f32,
                                            const ds4_gpu_tensor *out_f32) {
    if (!x_f32 || !out_f32 || !x_f32->ptr || !out_f32->ptr) {
        return 0;
    }
    if (!cuda_f8_e4m3_b128_view_layout_ok(arena, view)) {
        return 0;
    }
    if (x_f32->bytes < (uint64_t)view->cols * sizeof(float) ||
        out_f32->bytes < (uint64_t)view->rows * sizeof(float)) {
        return 0;
    }
    return 1;
}

static int cuda_f8_f16_arena_cache_enabled(void) {
    const char *v = getenv("DS4_CUDA_F8_F16_CACHE");
    return v && v[0] &&
           strcmp(v, "0") != 0 &&
           strcmp(v, "false") != 0 &&
           strcmp(v, "FALSE") != 0 &&
           strcmp(v, "off") != 0 &&
           strcmp(v, "OFF") != 0;
}

static int cuda_f8_rowpair_enabled(void) {
    const char *v = getenv("DS4_CUDA_F8_ROWPAIR");
    return v && v[0] &&
           strcmp(v, "0") != 0 &&
           strcmp(v, "false") != 0 &&
           strcmp(v, "FALSE") != 0 &&
           strcmp(v, "off") != 0 &&
           strcmp(v, "OFF") != 0;
}

static int cuda_f8_row4_enabled(void) {
    const char *v = getenv("DS4_CUDA_F8_ROW4");
    return v && v[0] &&
           strcmp(v, "0") != 0 &&
           strcmp(v, "false") != 0 &&
           strcmp(v, "FALSE") != 0 &&
           strcmp(v, "off") != 0 &&
           strcmp(v, "OFF") != 0;
}

static int cuda_f8_warp_scale_enabled(void) {
    const char *v = getenv("DS4_CUDA_F8_WARP_SCALE");
    return v && v[0] &&
           strcmp(v, "0") != 0 &&
           strcmp(v, "false") != 0 &&
           strcmp(v, "FALSE") != 0 &&
           strcmp(v, "off") != 0 &&
           strcmp(v, "OFF") != 0;
}

static int cuda_f8_row4_shape_ok(uint64_t rows, uint32_t cols) {
    if (rows < 2048u || (rows & 3u) != 0u) return 0;
    return cols == 1024u || cols == 2048u || cols == 4096u || cols == 8192u;
}

static int cuda_f8_grouped_ds4_fast_enabled(void) {
    const char *v = getenv("DS4_CUDA_F8_GROUPED_DS4_FAST");
    return !v || !v[0] ||
           (strcmp(v, "0") != 0 &&
            strcmp(v, "false") != 0 &&
            strcmp(v, "FALSE") != 0 &&
            strcmp(v, "off") != 0 &&
            strcmp(v, "OFF") != 0);
}

static int cuda_f8_hmma_shared_down_enabled(void) {
    return cuda_env_flag_enabled("DS4_CUDA_F8_HMMA_SHARED_DOWN");
}

static int cuda_f8_hmma_shared_down_shape_ok(uint32_t rows, uint32_t cols, uint32_t n_tokens) {
    return rows == 4096u &&
           cols == 2048u &&
           (n_tokens == 4u || n_tokens == 8u || n_tokens == 16u);
}

static int cuda_f8_hmma_pair_swiglu_enabled(void) {
    return cuda_env_flag_enabled("DS4_CUDA_F8_HMMA_PAIR_SWIGLU");
}

static int cuda_f8_pair_swiglu_single_rows2_enabled(void) {
    return cuda_env_flag_enabled("DS4_CUDA_F8_PAIR_SWIGLU_SINGLE_ROWS2");
}

static int cuda_f8_hmma_pair_swiglu_shape_ok(uint32_t rows, uint32_t cols, uint32_t n_tokens) {
    return rows == 2048u &&
           cols == 4096u &&
           (n_tokens == 4u || n_tokens == 8u || n_tokens == 16u);
}

static int cuda_f8_hmma_attn_batch_enabled(void) {
    return cuda_env_flag_enabled("DS4_CUDA_F8_HMMA_ATTN_BATCH");
}

static int cuda_f8_hmma_grouped_attn_o_batch_enabled(void) {
    return cuda_env_flag_enabled("DS4_CUDA_F8_HMMA_GROUPED_ATTN_O_BATCH");
}

static int cuda_f8_hmma_single_enabled(void) {
    return cuda_env_flag_enabled("DS4_CUDA_F8_HMMA_SINGLE");
}

static int cuda_f8_hmma_single_shape_ok(uint32_t rows, uint32_t cols) {
    return rows == 4096u && cols == 8192u;
}

static int cuda_f8_hmma_attn_batch_shape_ok(uint32_t rows, uint32_t cols, uint32_t n_tokens) {
    if (n_tokens != 4u && n_tokens != 8u && n_tokens != 16u) return 0;
    return (rows == 1024u && cols == 4096u) ||
           (rows == 512u && cols == 4096u) ||
           (rows == 32768u && cols == 1024u) ||
           (rows == 4096u && cols == 8192u);
}

static int cuda_f8_hmma_grouped_attn_o_batch_shape_ok(
        uint32_t groups,
        uint32_t rows_per_group,
        uint32_t cols_per_group,
        uint32_t n_tokens) {
    return groups == 8u &&
           rows_per_group == 1024u &&
           cols_per_group == 4096u &&
           n_tokens == 16u;
}

static uint64_t cuda_f8_f16_arena_cache_reserve_bytes(void) {
    int present = 0;
    const uint64_t mib = cuda_parse_mib_env("DS4_CUDA_F8_F16_CACHE_RESERVE_MIB", &present);
    return (present ? mib : 4096ull) * 1048576ull;
}

static const __half *cuda_f8_f16_arena_ptr(
        const ds4_gpu_arena *arena,
        const ds4_gpu_source_row_view *view,
        const char *label) {
    if (!cuda_f8_f16_arena_cache_enabled() ||
        !arena || !view || !arena->ptr || view->rows == 0 || view->cols == 0) {
        return nullptr;
    }
    {
        std::lock_guard<std::mutex> lk(g_f8_f16_arena_mutex);
        for (const cuda_f8_f16_arena_range &r : g_f8_f16_arena_ranges) {
            if (r.arena_ptr == arena->ptr &&
                r.gpu == arena->gpu &&
                r.arena_offset == view->arena_offset &&
                r.byte_length == view->byte_length &&
                r.rows == view->rows &&
                r.cols == view->cols &&
                r.row_stride_bytes == view->row_stride_bytes) {
                return r.device_ptr;
            }
        }
    }

    if ((uint64_t)view->rows > UINT64_MAX / view->cols / sizeof(__half)) return nullptr;
    const uint64_t out_bytes = (uint64_t)view->rows * view->cols * sizeof(__half);
    size_t free_bytes = 0;
    size_t total_bytes = 0;
    (void)cudaMemGetInfo(&free_bytes, &total_bytes);
    const uint64_t reserve_bytes = cuda_f8_f16_arena_cache_reserve_bytes();
    if (free_bytes <= reserve_bytes || out_bytes > (uint64_t)free_bytes - reserve_bytes) {
        if (getenv("DS4_CUDA_WEIGHT_CACHE_VERBOSE")) {
            fprintf(stderr,
                    "ds4: CUDA F8 fp16 arena cache skipped for %s %.2f MiB (free %.2f GiB reserve %.2f GiB)\n",
                    label ? label : "f8",
                    (double)out_bytes / 1048576.0,
                    (double)free_bytes / 1073741824.0,
                    (double)reserve_bytes / 1073741824.0);
        }
        return nullptr;
    }

    __half *dev = nullptr;
    cudaError_t err = cudaMalloc(&dev, (size_t)out_bytes);
    if (err != cudaSuccess) {
        fprintf(stderr, "ds4: CUDA F8 fp16 arena cache alloc failed for %s (%.2f MiB): %s\n",
                label ? label : "f8",
                (double)out_bytes / 1048576.0,
                cudaGetErrorString(err));
        (void)cudaGetLastError();
        return nullptr;
    }
    const uint8_t *base = (const uint8_t *)((const char *)arena->ptr + view->arena_offset);
    const uint64_t n = (uint64_t)view->rows * view->cols;
    arena_f8_e4m3_b128_to_f16_kernel<<<(n + 255u) / 256u, 256>>>(
        dev,
        base,
        view->rows,
        view->cols,
        view->row_stride_bytes);
    if (!cuda_ok(cudaGetLastError(), "f8 fp16 arena dequant launch")) {
        (void)cudaFree(dev);
        return nullptr;
    }

    cuda_f8_f16_arena_range inserted = {};
    inserted.arena_ptr = arena->ptr;
    inserted.gpu = arena->gpu;
    inserted.arena_offset = view->arena_offset;
    inserted.byte_length = view->byte_length;
    inserted.rows = view->rows;
    inserted.cols = view->cols;
    inserted.row_stride_bytes = view->row_stride_bytes;
    inserted.device_ptr = dev;
    inserted.bytes = out_bytes;
    {
        std::lock_guard<std::mutex> lk(g_f8_f16_arena_mutex);
        for (const cuda_f8_f16_arena_range &r : g_f8_f16_arena_ranges) {
            if (r.arena_ptr == inserted.arena_ptr &&
                r.gpu == inserted.gpu &&
                r.arena_offset == inserted.arena_offset &&
                r.byte_length == inserted.byte_length &&
                r.rows == inserted.rows &&
                r.cols == inserted.cols &&
                r.row_stride_bytes == inserted.row_stride_bytes) {
                (void)cudaFree(dev);
                return r.device_ptr;
            }
        }
        g_f8_f16_arena_ranges.push_back(inserted);
        g_f8_f16_arena_bytes += out_bytes;
    }
    if (getenv("DS4_CUDA_WEIGHT_CACHE_VERBOSE")) {
        fprintf(stderr,
                "ds4: CUDA cached F8 fp16 arena %s %.2f MiB (total %.2f GiB)\n",
                label ? label : "f8",
                (double)out_bytes / 1048576.0,
                (double)g_f8_f16_arena_bytes / 1073741824.0);
    }
    return dev;
}

extern "C" int ds4_gpu_arena_f8_e4m3_b128_matmul_f32(
        const ds4_gpu_arena           *arena,
        const ds4_gpu_source_row_view *view,
        const ds4_gpu_tensor          *x_f32,
        ds4_gpu_tensor                *out_f32) {
    if (!cuda_f8_e4m3_b128_matmul_view_ok(arena, view, x_f32, out_f32)) return 1;
    if (!cuda_ok(cudaSetDevice(arena->gpu), "f8 source matmul set device")) return 1;
    const uint8_t *base = (const uint8_t *)((const char *)arena->ptr + view->arena_offset);
    if (cuda_f8_hmma_single_enabled() &&
        cuda_f8_hmma_single_shape_ok(view->rows, view->cols)) {
        cuda_f8_shape_trace("plain", "hmma_single", arena->gpu,
                            view->rows, view->cols, 1u, 0, 0, 0);
        dim3 grid((view->rows + 63u) / 64u, 1, 1);
        arena_f8_e4m3_b128_matmul_batch_hmma_attn_kernel<<<grid, 128>>>(
            (float *)out_f32->ptr,
            base,
            (const float *)x_f32->ptr,
            view->rows,
            view->cols,
            view->row_stride_bytes,
            1u);
    } else if (cuda_f8_row4_enabled() &&
        cuda_f8_rowpair_enabled() &&
        cuda_f8_row4_shape_ok(view->rows, view->cols)) {
        cuda_f8_shape_trace("plain", "rows4", arena->gpu,
                            view->rows, view->cols, 1u, 0, 0, 0);
        arena_f8_e4m3_b128_matmul_rows4_kernel<<<(view->rows + 3u) / 4u, 256>>>(
            (float *)out_f32->ptr,
            base,
            (const float *)x_f32->ptr,
            view->rows,
            view->cols,
            view->row_stride_bytes);
    } else if (cuda_f8_rowpair_enabled() && view->rows > 1u) {
        if (cuda_f8_warp_scale_enabled()) {
            cuda_f8_shape_trace("plain", "rows2_warp_scale", arena->gpu,
                                view->rows, view->cols, 1u, 0, 0, 0);
            arena_f8_e4m3_b128_matmul_rows2_warp_scale_kernel<<<(view->rows + 1u) / 2u, 256>>>(
                (float *)out_f32->ptr,
                base,
                (const float *)x_f32->ptr,
                view->rows,
                view->cols,
                view->row_stride_bytes);
        } else {
            cuda_f8_shape_trace("plain", "rows2", arena->gpu,
                                view->rows, view->cols, 1u, 0, 0, 0);
            arena_f8_e4m3_b128_matmul_rows2_kernel<<<(view->rows + 1u) / 2u, 256>>>(
                (float *)out_f32->ptr,
                base,
                (const float *)x_f32->ptr,
                view->rows,
                view->cols,
                view->row_stride_bytes);
        }
    } else {
        cuda_f8_shape_trace("plain", "rows1", arena->gpu,
                            view->rows, view->cols, 1u, 0, 0, 0);
        arena_f8_e4m3_b128_matmul_kernel<<<view->rows, 256>>>(
            (float *)out_f32->ptr,
            base,
            (const float *)x_f32->ptr,
            view->rows,
            view->cols,
            view->row_stride_bytes);
    }
    return cuda_ok(cudaGetLastError(), "f8 source matmul launch") ? 0 : 1;
}

extern "C" int ds4_gpu_arena_f8_e4m3_b128_matmul_batch_f32(
        const ds4_gpu_arena           *arena,
        const ds4_gpu_source_row_view *view,
        const ds4_gpu_tensor          *x_f32,
        uint32_t                       n_tokens,
        ds4_gpu_tensor                *out_f32) {
    if (!x_f32 || !out_f32 || !x_f32->ptr || !out_f32->ptr || n_tokens == 0 ||
        !cuda_f8_e4m3_b128_view_layout_ok(arena, view) ||
        x_f32->bytes < (uint64_t)n_tokens * view->cols * sizeof(float) ||
        out_f32->bytes < (uint64_t)n_tokens * view->rows * sizeof(float)) {
        return 1;
    }
    if (!cuda_ok(cudaSetDevice(arena->gpu), "f8 source batch matmul set device")) return 1;
    const int use_hmma_attn_batch =
        cuda_f8_hmma_attn_batch_enabled() &&
        cuda_f8_hmma_attn_batch_shape_ok(view->rows, view->cols, n_tokens);
    const int use_hmma_shared_down =
        cuda_f8_hmma_shared_down_enabled() &&
        cuda_f8_hmma_shared_down_shape_ok(view->rows, view->cols, n_tokens);
    if (!use_hmma_attn_batch && !use_hmma_shared_down && g_cublas_ready && n_tokens > 1) {
        const __half *w_f16 = cuda_f8_f16_arena_ptr(arena, view, "f8_arena_batch");
        if (w_f16) {
            const uint64_t xh_count = (uint64_t)n_tokens * view->cols;
            __half *xh = (__half *)cuda_tmp_alloc(xh_count * sizeof(__half), "f8 arena f16 activations");
            if (!xh) return 1;
            f32_to_f16_kernel<<<(xh_count + 255u) / 256u, 256>>>(xh, (const float *)x_f32->ptr, xh_count);
            if (!cuda_ok(cudaGetLastError(), "f8 arena f16 activation convert launch")) return 1;
            const float alpha = 1.0f;
            const float beta = 0.0f;
            cublasStatus_t st = cublasGemmEx(g_cublas,
                                             CUBLAS_OP_T,
                                             CUBLAS_OP_N,
                                             (int)view->rows,
                                             (int)n_tokens,
                                             (int)view->cols,
                                             &alpha,
                                             w_f16,
                                             CUDA_R_16F,
                                             (int)view->cols,
                                             xh,
                                             CUDA_R_16F,
                                             (int)view->cols,
                                             &beta,
                                             out_f32->ptr,
                                             CUDA_R_32F,
                                             (int)view->rows,
                                             CUDA_R_32F,
                                             CUBLAS_GEMM_DEFAULT);
            if (st == CUBLAS_STATUS_SUCCESS) {
                cuda_f8_shape_trace("batch", "cublas_f16", arena->gpu,
                                    view->rows, view->cols, n_tokens, 0, 0, 0);
                return 0;
            }
            fprintf(stderr, "ds4: cuBLAS f8 arena f16 batch matmul failed: status %d\n", (int)st);
        }
    }
    const uint8_t *base = (const uint8_t *)((const char *)arena->ptr + view->arena_offset);
    if (use_hmma_attn_batch) {
        cuda_f8_shape_trace("batch", "hmma_attn", arena->gpu,
                            view->rows, view->cols, n_tokens, 0, 0, 0);
        dim3 grid((view->rows + 63u) / 64u, 1, 1);
        arena_f8_e4m3_b128_matmul_batch_hmma_attn_kernel<<<grid, 128>>>(
            (float *)out_f32->ptr,
            base,
            (const float *)x_f32->ptr,
            view->rows,
            view->cols,
            view->row_stride_bytes,
            n_tokens);
    } else if (use_hmma_shared_down) {
        cuda_f8_shape_trace("batch", "hmma_shared_down", arena->gpu,
                            view->rows, view->cols, n_tokens, 0, 0, 0);
        dim3 grid((view->rows + 63u) / 64u, 1, 1);
        arena_f8_e4m3_b128_matmul_batch_hmma_shared_down_kernel<<<grid, 128>>>(
            (float *)out_f32->ptr,
            base,
            (const float *)x_f32->ptr,
            view->row_stride_bytes,
            n_tokens);
    } else if (cuda_f8_rowpair_enabled() && view->rows > 1u) {
        cuda_f8_shape_trace("batch", "rows2", arena->gpu,
                            view->rows, view->cols, n_tokens, 0, 0, 0);
        dim3 grid((view->rows + 1u) / 2u, n_tokens, 1);
        arena_f8_e4m3_b128_matmul_batch_rows2_kernel<<<grid, 256>>>(
            (float *)out_f32->ptr,
            base,
            (const float *)x_f32->ptr,
            view->rows,
            view->cols,
            view->row_stride_bytes);
    } else {
        cuda_f8_shape_trace("batch", "rows1", arena->gpu,
                            view->rows, view->cols, n_tokens, 0, 0, 0);
        dim3 grid(view->rows, n_tokens, 1);
        arena_f8_e4m3_b128_matmul_batch_kernel<<<grid, 256>>>(
            (float *)out_f32->ptr,
            base,
            (const float *)x_f32->ptr,
            view->rows,
            view->cols,
            view->row_stride_bytes);
    }
    return cuda_ok(cudaGetLastError(), "f8 source batch matmul launch") ? 0 : 1;
}

extern "C" int ds4_gpu_arena_f8_e4m3_b128_matmul_batch_add_f32(
        const ds4_gpu_arena           *arena,
        const ds4_gpu_source_row_view *view,
        const ds4_gpu_tensor          *x_f32,
        uint32_t                       n_tokens,
        const ds4_gpu_tensor          *add_f32,
        ds4_gpu_tensor                *out_f32) {
    if (!x_f32 || !add_f32 || !out_f32 ||
        !x_f32->ptr || !add_f32->ptr || !out_f32->ptr ||
        n_tokens == 0 ||
        !cuda_f8_e4m3_b128_view_layout_ok(arena, view) ||
        x_f32->device != arena->gpu ||
        add_f32->device != arena->gpu ||
        out_f32->device != arena->gpu ||
        x_f32->bytes < (uint64_t)n_tokens * view->cols * sizeof(float) ||
        add_f32->bytes < (uint64_t)n_tokens * view->rows * sizeof(float) ||
        out_f32->bytes < (uint64_t)n_tokens * view->rows * sizeof(float)) {
        return 1;
    }
    if (!cuda_ok(cudaSetDevice(arena->gpu), "f8 source batch matmul add set device")) return 1;
    const uint8_t *base = (const uint8_t *)((const char *)arena->ptr + view->arena_offset);
    const int use_hmma_shared_down =
        cuda_f8_hmma_shared_down_enabled() &&
        cuda_f8_hmma_shared_down_shape_ok(view->rows, view->cols, n_tokens);
    if (use_hmma_shared_down) {
        cuda_f8_shape_trace("batch_add", "hmma_shared_down", arena->gpu,
                            view->rows, view->cols, n_tokens, 0, 0, 0);
        dim3 grid((view->rows + 63u) / 64u, 1, 1);
        arena_f8_e4m3_b128_matmul_batch_hmma_shared_down_add_kernel<<<grid, 128>>>(
            (float *)out_f32->ptr,
            (const float *)add_f32->ptr,
            base,
            (const float *)x_f32->ptr,
            view->row_stride_bytes,
            n_tokens);
    } else if (cuda_f8_rowpair_enabled() && view->rows > 1u) {
        cuda_f8_shape_trace("batch_add", "rows2", arena->gpu,
                            view->rows, view->cols, n_tokens, 0, 0, 0);
        dim3 grid((view->rows + 1u) / 2u, n_tokens, 1);
        arena_f8_e4m3_b128_matmul_batch_rows2_add_kernel<<<grid, 256>>>(
            (float *)out_f32->ptr,
            (const float *)add_f32->ptr,
            base,
            (const float *)x_f32->ptr,
            view->rows,
            view->cols,
            view->row_stride_bytes);
    } else {
        cuda_f8_shape_trace("batch_add", "rows1", arena->gpu,
                            view->rows, view->cols, n_tokens, 0, 0, 0);
        dim3 grid(view->rows, n_tokens, 1);
        arena_f8_e4m3_b128_matmul_batch_add_kernel<<<grid, 256>>>(
            (float *)out_f32->ptr,
            (const float *)add_f32->ptr,
            base,
            (const float *)x_f32->ptr,
            view->rows,
            view->cols,
            view->row_stride_bytes);
    }
    return cuda_ok(cudaGetLastError(), "f8 source batch matmul add launch") ? 0 : 1;
}

extern "C" int ds4_gpu_arena_f8_e4m3_b128_matmul_add_f32(
        const ds4_gpu_arena           *arena,
        const ds4_gpu_source_row_view *view,
        const ds4_gpu_tensor          *x_f32,
        const ds4_gpu_tensor          *add_f32,
        ds4_gpu_tensor                *out_f32) {
    return ds4_gpu_arena_f8_e4m3_b128_matmul_batch_add_f32(arena,
                                                           view,
                                                           x_f32,
                                                           1u,
                                                           add_f32,
                                                           out_f32);
}

extern "C" int ds4_gpu_arena_f8_e4m3_b128_matmul_batch_ptr_table_f32(
        const ds4_gpu_arena           *arena,
        const ds4_gpu_source_row_view *view,
        const ds4_gpu_tensor          *x_row_ptrs,
        uint32_t                       n_tokens,
        ds4_gpu_tensor                *out_f32) {
    if (!x_row_ptrs || !out_f32 || !x_row_ptrs->ptr || !out_f32->ptr ||
        n_tokens == 0 ||
        !cuda_f8_e4m3_b128_view_layout_ok(arena, view) ||
        x_row_ptrs->device != arena->gpu ||
        x_row_ptrs->bytes < (uint64_t)n_tokens * sizeof(float *) ||
        out_f32->bytes < (uint64_t)n_tokens * view->rows * sizeof(float)) {
        return 1;
    }
    if (!cuda_ok(cudaSetDevice(arena->gpu), "f8 source ptr table matmul set device")) return 1;
    const uint8_t *base = (const uint8_t *)((const char *)arena->ptr + view->arena_offset);
    if (cuda_f8_hmma_attn_batch_enabled() &&
        cuda_f8_hmma_attn_batch_shape_ok(view->rows, view->cols, n_tokens)) {
        cuda_f8_shape_trace("ptr_table", "hmma_attn", arena->gpu,
                            view->rows, view->cols, n_tokens, 0, 0, 0);
        dim3 grid((view->rows + 63u) / 64u, 1, 1);
        arena_f8_e4m3_b128_matmul_ptr_table_hmma_attn_kernel<<<grid, 128>>>(
            (float *)out_f32->ptr,
            base,
            (const float *const *)x_row_ptrs->ptr,
            view->rows,
            view->cols,
            view->row_stride_bytes,
            n_tokens);
    } else if (cuda_f8_rowpair_enabled() && view->rows > 1u) {
        cuda_f8_shape_trace("ptr_table", "rows2", arena->gpu,
                            view->rows, view->cols, n_tokens, 0, 0, 0);
        dim3 grid((view->rows + 1u) / 2u, n_tokens, 1);
        arena_f8_e4m3_b128_matmul_ptrs_rows2_kernel<<<grid, 256>>>(
            (float *)out_f32->ptr,
            base,
            (const float *const *)x_row_ptrs->ptr,
            view->rows,
            view->cols,
            view->row_stride_bytes);
    } else {
        cuda_f8_shape_trace("ptr_table", "rows1", arena->gpu,
                            view->rows, view->cols, n_tokens, 0, 0, 0);
        dim3 grid(view->rows, n_tokens, 1);
        arena_f8_e4m3_b128_matmul_ptrs_kernel<<<grid, 256>>>(
            (float *)out_f32->ptr,
            base,
            (const float *const *)x_row_ptrs->ptr,
            view->rows,
            view->cols,
            view->row_stride_bytes);
    }
    return cuda_ok(cudaGetLastError(), "f8 source ptr table matmul launch") ? 0 : 1;
}

extern "C" int ds4_gpu_arena_f8_e4m3_b128_matmul_grouped_f32(
        const ds4_gpu_arena           *arena,
        const ds4_gpu_source_row_view *view,
        const ds4_gpu_tensor          *x_f32,
        uint32_t                       groups,
        uint32_t                       rows_per_group,
        uint32_t                       cols_per_group,
        ds4_gpu_tensor                *out_f32) {
    if (!x_f32 || !out_f32 || !x_f32->ptr || !out_f32->ptr ||
        groups == 0 || rows_per_group == 0 || cols_per_group == 0 ||
        !cuda_f8_e4m3_b128_view_layout_ok(arena, view)) {
        return 1;
    }
    const uint64_t rows = (uint64_t)groups * rows_per_group;
    if (rows != view->rows || cols_per_group != view->cols ||
        x_f32->bytes < (uint64_t)groups * cols_per_group * sizeof(float) ||
        out_f32->bytes < rows * sizeof(float)) {
        return 1;
    }
    if (!cuda_ok(cudaSetDevice(arena->gpu), "f8 source grouped matmul set device")) return 1;
    const uint8_t *base = (const uint8_t *)((const char *)arena->ptr + view->arena_offset);
    if (cuda_f8_row4_enabled() &&
        cuda_f8_rowpair_enabled() &&
        cuda_f8_row4_shape_ok(rows, cols_per_group)) {
        if (cuda_f8_grouped_ds4_fast_enabled() &&
            groups == 8u && rows_per_group == 1024u && cols_per_group == 4096u) {
            cuda_f8_shape_trace("grouped", "rows4_ds4_attn_o", arena->gpu,
                                (uint32_t)rows, view->cols, 1u,
                                groups, rows_per_group, cols_per_group);
            arena_f8_e4m3_b128_matmul_grouped_rows4_ds4_attn_o_kernel<<<2048u, 256>>>(
                (float *)out_f32->ptr,
                base,
                (const float *)x_f32->ptr,
                view->row_stride_bytes);
        } else {
            cuda_f8_shape_trace("grouped", "rows4", arena->gpu,
                                (uint32_t)rows, view->cols, 1u,
                                groups, rows_per_group, cols_per_group);
            arena_f8_e4m3_b128_matmul_grouped_rows4_kernel<<<(unsigned int)((rows + 3u) / 4u), 256>>>(
                (float *)out_f32->ptr,
                base,
                (const float *)x_f32->ptr,
                groups,
                rows_per_group,
                cols_per_group,
                view->row_stride_bytes);
        }
    } else if (cuda_f8_rowpair_enabled() && rows > 1u) {
        if (cuda_f8_grouped_ds4_fast_enabled() &&
            groups == 8u && rows_per_group == 1024u && cols_per_group == 4096u) {
            if (cuda_f8_warp_scale_enabled()) {
                cuda_f8_shape_trace("grouped", "rows2_ds4_attn_o_warp_scale", arena->gpu,
                                    (uint32_t)rows, view->cols, 1u,
                                    groups, rows_per_group, cols_per_group);
                arena_f8_e4m3_b128_matmul_grouped_rows2_ds4_attn_o_warp_scale_kernel<<<4096u, 256>>>(
                    (float *)out_f32->ptr,
                    base,
                    (const float *)x_f32->ptr,
                    view->row_stride_bytes);
            } else {
                cuda_f8_shape_trace("grouped", "rows2_ds4_attn_o", arena->gpu,
                                    (uint32_t)rows, view->cols, 1u,
                                    groups, rows_per_group, cols_per_group);
                arena_f8_e4m3_b128_matmul_grouped_rows2_ds4_attn_o_kernel<<<4096u, 256>>>(
                    (float *)out_f32->ptr,
                    base,
                    (const float *)x_f32->ptr,
                    view->row_stride_bytes);
            }
        } else {
            cuda_f8_shape_trace("grouped", "rows2", arena->gpu,
                                (uint32_t)rows, view->cols, 1u,
                                groups, rows_per_group, cols_per_group);
            arena_f8_e4m3_b128_matmul_grouped_rows2_kernel<<<(unsigned int)((rows + 1u) / 2u), 256>>>(
                (float *)out_f32->ptr,
                base,
                (const float *)x_f32->ptr,
                groups,
                rows_per_group,
                cols_per_group,
            view->row_stride_bytes);
        }
    } else {
        cuda_f8_shape_trace("grouped", "rows1", arena->gpu,
                            (uint32_t)rows, view->cols, 1u,
                            groups, rows_per_group, cols_per_group);
        arena_f8_e4m3_b128_matmul_grouped_kernel<<<(unsigned int)rows, 256>>>(
            (float *)out_f32->ptr,
            base,
            (const float *)x_f32->ptr,
            groups,
            rows_per_group,
            cols_per_group,
            view->row_stride_bytes);
    }
    return cuda_ok(cudaGetLastError(), "f8 source grouped matmul launch") ? 0 : 1;
}

extern "C" int ds4_gpu_arena_f8_e4m3_b128_matmul_grouped_batch_f32(
        const ds4_gpu_arena           *arena,
        const ds4_gpu_source_row_view *view,
        const ds4_gpu_tensor          *x_f32,
        uint32_t                       n_tokens,
        uint32_t                       groups,
        uint32_t                       rows_per_group,
        uint32_t                       cols_per_group,
        ds4_gpu_tensor                *out_f32) {
    if (!x_f32 || !out_f32 || !x_f32->ptr || !out_f32->ptr ||
        n_tokens == 0 || groups == 0 || rows_per_group == 0 || cols_per_group == 0 ||
        !cuda_f8_e4m3_b128_view_layout_ok(arena, view)) {
        return 1;
    }
    const uint64_t rows = (uint64_t)groups * rows_per_group;
    const uint64_t input_cols = (uint64_t)groups * cols_per_group;
    if (rows != view->rows || cols_per_group != view->cols ||
        x_f32->device != arena->gpu ||
        out_f32->device != arena->gpu ||
        x_f32->bytes < (uint64_t)n_tokens * input_cols * sizeof(float) ||
        out_f32->bytes < (uint64_t)n_tokens * rows * sizeof(float)) {
        return 1;
    }
    if (!cuda_ok(cudaSetDevice(arena->gpu), "f8 source grouped batch matmul set device")) return 1;
    const uint8_t *base = (const uint8_t *)((const char *)arena->ptr + view->arena_offset);
    if (cuda_f8_hmma_grouped_attn_o_batch_enabled() &&
        cuda_f8_hmma_grouped_attn_o_batch_shape_ok(groups,
                                                   rows_per_group,
                                                   cols_per_group,
                                                   n_tokens)) {
        cuda_f8_shape_trace("grouped_batch", "hmma_ds4_attn_o", arena->gpu,
                            (uint32_t)rows, view->cols, n_tokens,
                            groups, rows_per_group, cols_per_group);
        arena_f8_e4m3_b128_matmul_grouped_batch_hmma_ds4_attn_o_kernel<<<128u, 128>>>(
            (float *)out_f32->ptr,
            base,
            (const float *)x_f32->ptr,
            view->row_stride_bytes,
            n_tokens);
    } else {
        cuda_f8_shape_trace("grouped_batch", "rows2", arena->gpu,
                            (uint32_t)rows, view->cols, n_tokens,
                            groups, rows_per_group, cols_per_group);
        dim3 grid((unsigned int)((rows + 1u) / 2u), n_tokens, 1);
        arena_f8_e4m3_b128_matmul_grouped_batch_rows2_kernel<<<grid, 256>>>(
            (float *)out_f32->ptr,
            base,
            (const float *)x_f32->ptr,
            groups,
            rows_per_group,
            cols_per_group,
            view->row_stride_bytes);
    }
    return cuda_ok(cudaGetLastError(), "f8 source grouped batch matmul launch") ? 0 : 1;
}

extern "C" int ds4_gpu_arena_f8_e4m3_b128_pair_swiglu_batch_ptrs_f32(
        const ds4_gpu_arena           *arena,
        const ds4_gpu_source_row_view *gate,
        const ds4_gpu_source_row_view *up,
        ds4_gpu_tensor                *x_row_ptrs,
        const ds4_gpu_tensor *const   *x_rows_f32,
        uint32_t                       n_tokens,
        ds4_gpu_tensor                *out_f32,
        float                          clamp,
        float                          weight) {
    if (!gate || !up || !x_row_ptrs || !x_rows_f32 || !out_f32 ||
        !x_row_ptrs->ptr || !out_f32->ptr || n_tokens == 0 ||
        gate->rows != up->rows || gate->cols != up->cols ||
        !cuda_f8_e4m3_b128_view_layout_ok(arena, gate) ||
        !cuda_f8_e4m3_b128_view_layout_ok(arena, up) ||
        x_row_ptrs->device != arena->gpu ||
        x_row_ptrs->bytes < (uint64_t)n_tokens * sizeof(float *) ||
        out_f32->bytes < (uint64_t)n_tokens * gate->rows * sizeof(float)) {
        return 1;
    }
    std::vector<const float *> row_ptrs(n_tokens);
    for (uint32_t tok = 0; tok < n_tokens; tok++) {
        const ds4_gpu_tensor *x = x_rows_f32[tok];
        if (!x || !x->ptr || x->device != arena->gpu ||
            x->bytes < (uint64_t)gate->cols * sizeof(float)) {
            return 1;
        }
        row_ptrs[tok] = (const float *)x->ptr;
    }
    if (!cuda_ok(cudaSetDevice(arena->gpu), "f8 source pair ptr swiglu set device")) return 1;
    if (!cuda_ok(cudaMemcpy(x_row_ptrs->ptr,
                            row_ptrs.data(),
                            (size_t)n_tokens * sizeof(float *),
                            cudaMemcpyHostToDevice),
                 "f8 source pair row ptr upload")) {
        return 1;
    }
    const uint8_t *gate_base =
        (const uint8_t *)((const char *)arena->ptr + gate->arena_offset);
    const uint8_t *up_base =
        (const uint8_t *)((const char *)arena->ptr + up->arena_offset);
    cuda_f8_shape_trace("pair_swiglu_ptrs", "scalar", arena->gpu,
                        gate->rows, gate->cols, n_tokens, 0, 0, 0);
    dim3 grid(gate->rows, n_tokens, 1);
    arena_f8_e4m3_b128_pair_swiglu_ptrs_kernel<<<grid, 256>>>(
        (float *)out_f32->ptr,
        gate_base,
        up_base,
        (const float *const *)x_row_ptrs->ptr,
        gate->rows,
        gate->cols,
        gate->row_stride_bytes,
        up->row_stride_bytes,
        clamp,
        weight);
    return cuda_ok(cudaGetLastError(), "f8 source pair ptr swiglu launch") ? 0 : 1;
}

extern "C" int ds4_gpu_arena_f8_e4m3_b128_pair_swiglu_f32(
        const ds4_gpu_arena           *arena,
        const ds4_gpu_source_row_view *gate,
        const ds4_gpu_source_row_view *up,
        const ds4_gpu_tensor          *x_f32,
        ds4_gpu_tensor                *out_f32,
        float                          clamp,
        float                          weight) {
    if (!gate || !up || !x_f32 || !out_f32 ||
        !x_f32->ptr || !out_f32->ptr ||
        gate->rows != up->rows || gate->cols != up->cols ||
        !cuda_f8_e4m3_b128_view_layout_ok(arena, gate) ||
        !cuda_f8_e4m3_b128_view_layout_ok(arena, up) ||
        x_f32->bytes < (uint64_t)gate->cols * sizeof(float) ||
        out_f32->bytes < (uint64_t)gate->rows * sizeof(float)) {
        return 1;
    }
    if (!cuda_ok(cudaSetDevice(arena->gpu), "f8 source pair swiglu set device")) return 1;
    const uint8_t *gate_base =
        (const uint8_t *)((const char *)arena->ptr + gate->arena_offset);
    const uint8_t *up_base =
        (const uint8_t *)((const char *)arena->ptr + up->arena_offset);
    if (cuda_f8_pair_swiglu_single_rows2_enabled() && gate->rows > 1u) {
        cuda_f8_shape_trace("pair_swiglu_single", "rows2", arena->gpu,
                            gate->rows, gate->cols, 1u, 0, 0, 0);
        arena_f8_e4m3_b128_pair_swiglu_rows2_kernel<<<(gate->rows + 1u) / 2u, 256>>>(
            (float *)out_f32->ptr,
            gate_base,
            up_base,
            (const float *)x_f32->ptr,
            gate->rows,
            gate->cols,
            gate->row_stride_bytes,
            up->row_stride_bytes,
            clamp,
            weight);
    } else {
        cuda_f8_shape_trace("pair_swiglu_single", "scalar", arena->gpu,
                            gate->rows, gate->cols, 1u, 0, 0, 0);
        arena_f8_e4m3_b128_pair_swiglu_kernel<<<gate->rows, 256>>>(
            (float *)out_f32->ptr,
            gate_base,
            up_base,
            (const float *)x_f32->ptr,
            gate->rows,
            gate->cols,
            gate->row_stride_bytes,
            up->row_stride_bytes,
            clamp,
            weight);
    }
    return cuda_ok(cudaGetLastError(), "f8 source pair swiglu launch") ? 0 : 1;
}

extern "C" int ds4_gpu_arena_f8_e4m3_b128_pair_swiglu_batch_ptr_table_f32(
        const ds4_gpu_arena           *arena,
        const ds4_gpu_source_row_view *gate,
        const ds4_gpu_source_row_view *up,
        const ds4_gpu_tensor          *x_row_ptrs,
        uint32_t                       n_tokens,
        ds4_gpu_tensor                *out_f32,
        float                          clamp,
        float                          weight) {
    if (!gate || !up || !x_row_ptrs || !out_f32 ||
        !x_row_ptrs->ptr || !out_f32->ptr || n_tokens == 0 ||
        gate->rows != up->rows || gate->cols != up->cols ||
        !cuda_f8_e4m3_b128_view_layout_ok(arena, gate) ||
        !cuda_f8_e4m3_b128_view_layout_ok(arena, up) ||
        x_row_ptrs->device != arena->gpu ||
        x_row_ptrs->bytes < (uint64_t)n_tokens * sizeof(float *) ||
        out_f32->bytes < (uint64_t)n_tokens * gate->rows * sizeof(float)) {
        return 1;
    }
    if (!cuda_ok(cudaSetDevice(arena->gpu), "f8 source pair ptr table swiglu set device")) return 1;
    const uint8_t *gate_base =
        (const uint8_t *)((const char *)arena->ptr + gate->arena_offset);
    const uint8_t *up_base =
        (const uint8_t *)((const char *)arena->ptr + up->arena_offset);
    if (cuda_f8_hmma_pair_swiglu_enabled() &&
        cuda_f8_hmma_pair_swiglu_shape_ok(gate->rows, gate->cols, n_tokens)) {
        cuda_f8_shape_trace("pair_swiglu_ptr_table", "hmma", arena->gpu,
                            gate->rows, gate->cols, n_tokens, 0, 0, 0);
        dim3 grid((gate->rows + 63u) / 64u, 1, 1);
        arena_f8_e4m3_b128_pair_swiglu_ptr_table_hmma_kernel<<<grid, 128>>>(
            (float *)out_f32->ptr,
            gate_base,
            up_base,
            (const float *const *)x_row_ptrs->ptr,
            gate->row_stride_bytes,
            up->row_stride_bytes,
            n_tokens,
            clamp,
            weight);
    } else {
        cuda_f8_shape_trace("pair_swiglu_ptr_table", "scalar", arena->gpu,
                            gate->rows, gate->cols, n_tokens, 0, 0, 0);
        dim3 grid(gate->rows, n_tokens, 1);
        arena_f8_e4m3_b128_pair_swiglu_ptrs_kernel<<<grid, 256>>>(
            (float *)out_f32->ptr,
            gate_base,
            up_base,
            (const float *const *)x_row_ptrs->ptr,
            gate->rows,
            gate->cols,
            gate->row_stride_bytes,
            up->row_stride_bytes,
            clamp,
            weight);
    }
    return cuda_ok(cudaGetLastError(), "f8 source pair ptr table swiglu launch") ? 0 : 1;
}

static int cuda_mxfp4_matmul_view_ok(const ds4_gpu_arena *arena,
                                     const ds4_gpu_source_row_view *view,
                                     const ds4_gpu_tensor *x_f32,
                                     const ds4_gpu_tensor *out_f32) {
    if (!arena || !view || !x_f32 || !out_f32 || !arena->valid ||
        !arena->ptr || !x_f32->ptr || !out_f32->ptr) {
        return 0;
    }
    if (view->rows == 0 || view->cols == 0 || view->row_stride_bytes == 0) return 0;
    if (!cuda_arena_range_ok(arena, view->arena_offset, view->byte_length)) return 0;
    uint64_t row_bytes = 0;
    if (mxfp4_row_bytes(view->cols, &row_bytes)) return 0;
    if ((uint64_t)view->row_stride_bytes < row_bytes) return 0;
    uint64_t last_start = 0;
    if (checked_mul_u64((uint64_t)view->rows - 1u,
                        (uint64_t)view->row_stride_bytes,
                        &last_start)) {
        return 0;
    }
    if (last_start > view->byte_length || row_bytes > view->byte_length - last_start) {
        return 0;
    }
    uint64_t x_bytes = 0;
    uint64_t out_bytes = 0;
    if (checked_mul_u64((uint64_t)view->cols, sizeof(float), &x_bytes)) return 0;
    if (checked_mul_u64((uint64_t)view->rows, sizeof(float), &out_bytes)) return 0;
    if (x_f32->bytes < x_bytes || out_f32->bytes < out_bytes) return 0;
    return 1;
}

extern "C" int ds4_gpu_arena_mxfp4_matmul_f32(
        const ds4_gpu_arena           *arena,
        const ds4_gpu_source_row_view *view,
        const ds4_gpu_tensor          *x_f32,
        ds4_gpu_tensor                *out_f32) {
    if (!cuda_mxfp4_matmul_view_ok(arena, view, x_f32, out_f32)) return 1;
    if (!cuda_ok(cudaSetDevice(arena->gpu), "mxfp4 source matmul set device")) return 1;
    const uint8_t *base = (const uint8_t *)((const char *)arena->ptr + view->arena_offset);
    arena_mxfp4_matmul_kernel<<<view->rows, 256>>>(
        (float *)out_f32->ptr,
        base,
        (const float *)x_f32->ptr,
        view->rows,
        view->cols,
        view->row_stride_bytes);
    return cuda_ok(cudaGetLastError(), "mxfp4 source matmul launch") ? 0 : 1;
}

extern "C" int ds4_gpu_arena_mxfp4_pair_swiglu_f32(
        const ds4_gpu_arena           *arena,
        const ds4_gpu_source_row_view *gate,
        const ds4_gpu_source_row_view *up,
        const ds4_gpu_tensor          *x_f32,
        ds4_gpu_tensor                *out_f32,
        float                          clamp,
        float                          weight) {
    if (!gate || !up || gate->rows != up->rows || gate->cols != up->cols) return 1;
    if (!cuda_mxfp4_matmul_view_ok(arena, gate, x_f32, out_f32) ||
        !cuda_mxfp4_matmul_view_ok(arena, up, x_f32, out_f32)) {
        return 1;
    }
    if (!cuda_ok(cudaSetDevice(arena->gpu), "mxfp4 pair swiglu set device")) return 1;
    const uint8_t *gate_base =
        (const uint8_t *)((const char *)arena->ptr + gate->arena_offset);
    const uint8_t *up_base =
        (const uint8_t *)((const char *)arena->ptr + up->arena_offset);
    arena_mxfp4_pair_swiglu_kernel<<<gate->rows, 256>>>(
        (float *)out_f32->ptr,
        gate_base,
        up_base,
        (const float *)x_f32->ptr,
        gate->rows,
        gate->cols,
        gate->row_stride_bytes,
        up->row_stride_bytes,
        clamp,
        weight);
    return cuda_ok(cudaGetLastError(), "mxfp4 pair swiglu launch") ? 0 : 1;
}

extern "C" int ds4_gpu_arena_mxfp4_matmul_add_f32(
        const ds4_gpu_arena           *arena,
        const ds4_gpu_source_row_view *view,
        const ds4_gpu_tensor          *x_f32,
        const ds4_gpu_tensor          *add_f32,
        ds4_gpu_tensor                *out_f32) {
    if (!cuda_mxfp4_matmul_view_ok(arena, view, x_f32, out_f32) ||
        !add_f32 || !add_f32->ptr ||
        add_f32->bytes < (uint64_t)view->rows * sizeof(float)) {
        return 1;
    }
    if (!cuda_ok(cudaSetDevice(arena->gpu), "mxfp4 matmul add set device")) return 1;
    const uint8_t *base = (const uint8_t *)((const char *)arena->ptr + view->arena_offset);
    arena_mxfp4_matmul_add_kernel<<<view->rows, 256>>>(
        (float *)out_f32->ptr,
        base,
        (const float *)x_f32->ptr,
        (const float *)add_f32->ptr,
        view->rows,
        view->cols,
        view->row_stride_bytes);
    return cuda_ok(cudaGetLastError(), "mxfp4 matmul add launch") ? 0 : 1;
}

extern "C" int ds4_gpu_arena_mxfp4_routed_swiglu_down_sum_batch_f32(
        const ds4_gpu_arena *arena,
        uint64_t gate_arena_offset,
        uint64_t gate_byte_length,
        uint64_t up_arena_offset,
        uint64_t up_byte_length,
        uint64_t down_arena_offset,
        uint64_t down_byte_length,
        uint64_t gate_expert_stride_bytes,
        uint32_t gate_row_stride_bytes,
        uint64_t down_expert_stride_bytes,
        uint32_t down_row_stride_bytes,
        uint32_t hidden,
        uint32_t mid,
        uint32_t n_total_experts,
        const ds4_gpu_tensor *selected_i32,
        const ds4_gpu_tensor *weights_f32,
        uint32_t n_routes,
        const ds4_gpu_tensor *x_f32,
        uint32_t n_tokens,
        ds4_gpu_tensor *mid_tmp_f32,
        ds4_gpu_tensor *out_f32) {
    if (!arena || !arena->valid || !arena->ptr || !selected_i32 || !weights_f32 ||
        !x_f32 || !mid_tmp_f32 || !out_f32 || !selected_i32->ptr ||
        !weights_f32->ptr || !x_f32->ptr || !mid_tmp_f32->ptr || !out_f32->ptr ||
        hidden == 0 || mid == 0 || n_total_experts == 0 || n_routes == 0 ||
        n_tokens == 0) {
        return 1;
    }
    uint64_t gate_row_bytes = 0;
    uint64_t down_row_bytes = 0;
    if (mxfp4_row_bytes(hidden, &gate_row_bytes) ||
        mxfp4_row_bytes(mid, &down_row_bytes)) {
        return 1;
    }
    if ((uint64_t)gate_row_stride_bytes < gate_row_bytes ||
        (uint64_t)down_row_stride_bytes < down_row_bytes) {
        return 1;
    }
    const uint64_t gate_min_expert = (uint64_t)mid * gate_row_stride_bytes;
    const uint64_t down_min_expert = (uint64_t)hidden * down_row_stride_bytes;
    if (gate_expert_stride_bytes < gate_min_expert ||
        down_expert_stride_bytes < down_min_expert) {
        return 1;
    }
    if (gate_byte_length / gate_expert_stride_bytes < n_total_experts ||
        up_byte_length / gate_expert_stride_bytes < n_total_experts ||
        down_byte_length / down_expert_stride_bytes < n_total_experts) {
        return 1;
    }
    if (!cuda_arena_range_ok(arena, gate_arena_offset, gate_byte_length) ||
        !cuda_arena_range_ok(arena, up_arena_offset, up_byte_length) ||
        !cuda_arena_range_ok(arena, down_arena_offset, down_byte_length)) {
        return 1;
    }
    if (selected_i32->bytes < (uint64_t)n_tokens * n_routes * sizeof(int32_t) ||
        weights_f32->bytes < (uint64_t)n_tokens * n_routes * sizeof(float) ||
        x_f32->bytes < (uint64_t)n_tokens * hidden * sizeof(float) ||
        mid_tmp_f32->bytes < (uint64_t)n_tokens * n_routes * mid * sizeof(float) ||
        out_f32->bytes < (uint64_t)n_tokens * hidden * sizeof(float)) {
        return 1;
    }
    if (!cuda_ok(cudaSetDevice(arena->gpu), "mxfp4 grouped route set device")) return 1;
    const uint8_t *gate_base =
        (const uint8_t *)((const char *)arena->ptr + gate_arena_offset);
    const uint8_t *up_base =
        (const uint8_t *)((const char *)arena->ptr + up_arena_offset);
    const uint8_t *down_base =
        (const uint8_t *)((const char *)arena->ptr + down_arena_offset);
    if (cuda_env_flag_enabled("DS4_V100_TURBOMIND_ROUTED_FFN")) {
        if (cuda_tm_routed_mxfp4_transient(
                arena,
                gate_arena_offset,
                up_arena_offset,
                down_arena_offset,
                gate_expert_stride_bytes,
                down_expert_stride_bytes,
                hidden,
                mid,
                n_total_experts,
                selected_i32,
                weights_f32,
                n_routes,
                x_f32,
                nullptr,
                n_tokens,
                out_f32)) {
            return 0;
        }
        cuda_tm_warn_once("falling back to source MXFP4 arena path");
        if (cuda_env_flag_enabled("DS4_V100_TURBOMIND_STRICT")) return 1;
    }
    const int rows2 = cuda_env_flag_enabled("DS4_CUDA_MXFP4_ROUTE_ROWS2");
    if (rows2) {
        dim3 mid_grid((mid + 1u) / 2u, n_routes, n_tokens);
        arena_mxfp4_grouped_pair_swiglu_rows2_kernel<<<mid_grid, 256>>>(
            (float *)mid_tmp_f32->ptr,
            gate_base,
            up_base,
            (const int32_t *)selected_i32->ptr,
            (const float *)weights_f32->ptr,
            (const float *)x_f32->ptr,
            hidden,
            mid,
            n_total_experts,
            n_routes,
            gate_expert_stride_bytes,
            gate_row_stride_bytes,
            gate_expert_stride_bytes,
            gate_row_stride_bytes,
            10.0f);
    } else {
        dim3 mid_grid(mid, n_routes, n_tokens);
        arena_mxfp4_grouped_pair_swiglu_kernel<<<mid_grid, 256>>>(
            (float *)mid_tmp_f32->ptr,
            gate_base,
            up_base,
            (const int32_t *)selected_i32->ptr,
            (const float *)weights_f32->ptr,
            (const float *)x_f32->ptr,
            hidden,
            mid,
            n_total_experts,
            n_routes,
            gate_expert_stride_bytes,
            gate_row_stride_bytes,
            gate_expert_stride_bytes,
            gate_row_stride_bytes,
            10.0f);
    }
    if (!cuda_ok(cudaGetLastError(), "mxfp4 grouped gate/up launch")) return 1;
    if (rows2) {
        dim3 down_grid((hidden + 1u) / 2u, n_tokens, 1);
        arena_mxfp4_grouped_down_sum_rows2_kernel<<<down_grid, 256>>>(
            (float *)out_f32->ptr,
            down_base,
            (const int32_t *)selected_i32->ptr,
            (const float *)mid_tmp_f32->ptr,
            hidden,
            mid,
            n_total_experts,
            n_routes,
            down_expert_stride_bytes,
            down_row_stride_bytes);
    } else {
        dim3 down_grid(hidden, n_tokens, 1);
        arena_mxfp4_grouped_down_sum_kernel<<<down_grid, 256>>>(
            (float *)out_f32->ptr,
            down_base,
            (const int32_t *)selected_i32->ptr,
            (const float *)mid_tmp_f32->ptr,
            hidden,
            mid,
            n_total_experts,
            n_routes,
            down_expert_stride_bytes,
            down_row_stride_bytes);
    }
    return cuda_ok(cudaGetLastError(), "mxfp4 grouped down sum launch") ? 0 : 1;
}

extern "C" int ds4_gpu_arena_mxfp4_routed_swiglu_down_sum_batch_ptrs_f32(
        const ds4_gpu_arena *arena,
        uint64_t gate_arena_offset,
        uint64_t gate_byte_length,
        uint64_t up_arena_offset,
        uint64_t up_byte_length,
        uint64_t down_arena_offset,
        uint64_t down_byte_length,
        uint64_t gate_expert_stride_bytes,
        uint32_t gate_row_stride_bytes,
        uint64_t down_expert_stride_bytes,
        uint32_t down_row_stride_bytes,
        uint32_t hidden,
        uint32_t mid,
        uint32_t n_total_experts,
        const ds4_gpu_tensor *selected_i32,
        const ds4_gpu_tensor *weights_f32,
        uint32_t n_routes,
        ds4_gpu_tensor *x_row_ptrs,
        const ds4_gpu_tensor *const *x_rows_f32,
        uint32_t n_tokens,
        ds4_gpu_tensor *mid_tmp_f32,
        ds4_gpu_tensor *out_f32) {
    if (!arena || !arena->valid || !arena->ptr || !selected_i32 || !weights_f32 ||
        !x_row_ptrs || !x_rows_f32 || !mid_tmp_f32 || !out_f32 || !selected_i32->ptr ||
        !weights_f32->ptr || !x_row_ptrs->ptr || !mid_tmp_f32->ptr || !out_f32->ptr ||
        hidden == 0 || mid == 0 || n_total_experts == 0 || n_routes == 0 ||
        n_tokens == 0) {
        return 1;
    }
    uint64_t gate_row_bytes = 0;
    uint64_t down_row_bytes = 0;
    if (mxfp4_row_bytes(hidden, &gate_row_bytes) ||
        mxfp4_row_bytes(mid, &down_row_bytes)) {
        return 1;
    }
    if ((uint64_t)gate_row_stride_bytes < gate_row_bytes ||
        (uint64_t)down_row_stride_bytes < down_row_bytes) {
        return 1;
    }
    const uint64_t gate_min_expert = (uint64_t)mid * gate_row_stride_bytes;
    const uint64_t down_min_expert = (uint64_t)hidden * down_row_stride_bytes;
    if (gate_expert_stride_bytes < gate_min_expert ||
        down_expert_stride_bytes < down_min_expert) {
        return 1;
    }
    if (gate_byte_length / gate_expert_stride_bytes < n_total_experts ||
        up_byte_length / gate_expert_stride_bytes < n_total_experts ||
        down_byte_length / down_expert_stride_bytes < n_total_experts) {
        return 1;
    }
    if (!cuda_arena_range_ok(arena, gate_arena_offset, gate_byte_length) ||
        !cuda_arena_range_ok(arena, up_arena_offset, up_byte_length) ||
        !cuda_arena_range_ok(arena, down_arena_offset, down_byte_length)) {
        return 1;
    }
    if (selected_i32->bytes < (uint64_t)n_tokens * n_routes * sizeof(int32_t) ||
        weights_f32->bytes < (uint64_t)n_tokens * n_routes * sizeof(float) ||
        x_row_ptrs->bytes < (uint64_t)n_tokens * sizeof(float *) ||
        mid_tmp_f32->bytes < (uint64_t)n_tokens * n_routes * mid * sizeof(float) ||
        out_f32->bytes < (uint64_t)n_tokens * hidden * sizeof(float)) {
        return 1;
    }
    if (x_row_ptrs->device != arena->gpu) return 1;
    std::vector<const float *> row_ptrs(n_tokens);
    for (uint32_t tok = 0; tok < n_tokens; tok++) {
        const ds4_gpu_tensor *x = x_rows_f32[tok];
        if (!x || !x->ptr || x->device != arena->gpu ||
            x->bytes < (uint64_t)hidden * sizeof(float)) {
            return 1;
        }
        row_ptrs[tok] = (const float *)x->ptr;
    }
    if (!cuda_ok(cudaSetDevice(arena->gpu), "mxfp4 grouped ptr route set device")) return 1;
    if (!cuda_ok(cudaMemcpy(x_row_ptrs->ptr,
                            row_ptrs.data(),
                            (size_t)n_tokens * sizeof(float *),
                            cudaMemcpyHostToDevice),
                 "mxfp4 grouped row ptr upload")) {
        return 1;
    }
    const uint8_t *gate_base =
        (const uint8_t *)((const char *)arena->ptr + gate_arena_offset);
    const uint8_t *up_base =
        (const uint8_t *)((const char *)arena->ptr + up_arena_offset);
    const uint8_t *down_base =
        (const uint8_t *)((const char *)arena->ptr + down_arena_offset);
    if (cuda_env_flag_enabled("DS4_V100_TURBOMIND_ROUTED_FFN")) {
        if (cuda_tm_routed_mxfp4_transient(
                arena,
                gate_arena_offset,
                up_arena_offset,
                down_arena_offset,
                gate_expert_stride_bytes,
                down_expert_stride_bytes,
                hidden,
                mid,
                n_total_experts,
                selected_i32,
                weights_f32,
                n_routes,
                nullptr,
                x_row_ptrs,
                n_tokens,
                out_f32)) {
            return 0;
        }
        cuda_tm_warn_once("falling back to source MXFP4 arena ptr path");
        if (cuda_env_flag_enabled("DS4_V100_TURBOMIND_STRICT")) return 1;
    }
    const int rows2 = cuda_env_flag_enabled("DS4_CUDA_MXFP4_ROUTE_ROWS2");
    if (rows2) {
        dim3 mid_grid((mid + 1u) / 2u, n_routes, n_tokens);
        arena_mxfp4_grouped_pair_swiglu_ptrs_rows2_kernel<<<mid_grid, 256>>>(
            (float *)mid_tmp_f32->ptr,
            gate_base,
            up_base,
            (const int32_t *)selected_i32->ptr,
            (const float *)weights_f32->ptr,
            (const float *const *)x_row_ptrs->ptr,
            hidden,
            mid,
            n_total_experts,
            n_routes,
            gate_expert_stride_bytes,
            gate_row_stride_bytes,
            gate_expert_stride_bytes,
            gate_row_stride_bytes,
            10.0f);
    } else {
        dim3 mid_grid(mid, n_routes, n_tokens);
        arena_mxfp4_grouped_pair_swiglu_ptrs_kernel<<<mid_grid, 256>>>(
            (float *)mid_tmp_f32->ptr,
            gate_base,
            up_base,
            (const int32_t *)selected_i32->ptr,
            (const float *)weights_f32->ptr,
            (const float *const *)x_row_ptrs->ptr,
            hidden,
            mid,
            n_total_experts,
            n_routes,
            gate_expert_stride_bytes,
            gate_row_stride_bytes,
            gate_expert_stride_bytes,
            gate_row_stride_bytes,
            10.0f);
    }
    if (!cuda_ok(cudaGetLastError(), "mxfp4 grouped ptr gate/up launch")) return 1;
    if (rows2) {
        dim3 down_grid((hidden + 1u) / 2u, n_tokens, 1);
        arena_mxfp4_grouped_down_sum_rows2_kernel<<<down_grid, 256>>>(
            (float *)out_f32->ptr,
            down_base,
            (const int32_t *)selected_i32->ptr,
            (const float *)mid_tmp_f32->ptr,
            hidden,
            mid,
            n_total_experts,
            n_routes,
            down_expert_stride_bytes,
            down_row_stride_bytes);
    } else {
        dim3 down_grid(hidden, n_tokens, 1);
        arena_mxfp4_grouped_down_sum_kernel<<<down_grid, 256>>>(
            (float *)out_f32->ptr,
            down_base,
            (const int32_t *)selected_i32->ptr,
            (const float *)mid_tmp_f32->ptr,
            hidden,
            mid,
            n_total_experts,
            n_routes,
            down_expert_stride_bytes,
            down_row_stride_bytes);
    }
    return cuda_ok(cudaGetLastError(), "mxfp4 grouped ptr down sum launch") ? 0 : 1;
}

extern "C" int ds4_gpu_arena_mxfp4_routed_swiglu_down_sum_f32(
        const ds4_gpu_arena *arena,
        uint64_t gate_arena_offset,
        uint64_t gate_byte_length,
        uint64_t up_arena_offset,
        uint64_t up_byte_length,
        uint64_t down_arena_offset,
        uint64_t down_byte_length,
        uint64_t gate_expert_stride_bytes,
        uint32_t gate_row_stride_bytes,
        uint64_t down_expert_stride_bytes,
        uint32_t down_row_stride_bytes,
        uint32_t hidden,
        uint32_t mid,
        uint32_t n_total_experts,
        const ds4_gpu_tensor *selected_i32,
        const ds4_gpu_tensor *weights_f32,
        uint32_t n_routes,
        const ds4_gpu_tensor *x_f32,
        ds4_gpu_tensor *mid_tmp_f32,
        ds4_gpu_tensor *out_f32) {
    return ds4_gpu_arena_mxfp4_routed_swiglu_down_sum_batch_f32(
        arena,
        gate_arena_offset,
        gate_byte_length,
        up_arena_offset,
        up_byte_length,
        down_arena_offset,
        down_byte_length,
        gate_expert_stride_bytes,
        gate_row_stride_bytes,
        down_expert_stride_bytes,
        down_row_stride_bytes,
        hidden,
        mid,
        n_total_experts,
        selected_i32,
        weights_f32,
        n_routes,
        x_f32,
        1,
        mid_tmp_f32,
        out_f32);
}

extern "C" void ds4_gpu_set_quality(bool quality) {
    g_quality_mode = quality ? 1 : 0;
    if (g_cublas_ready) {
        const cublasMath_t math_mode =
            (g_quality_mode || getenv("DS4_CUDA_NO_TF32") != NULL)
                ? CUBLAS_DEFAULT_MATH
                : CUBLAS_TF32_TENSOR_OP_MATH;
        (void)cublasSetMathMode(g_cublas, math_mode);
    }
}

__global__ static void embed_token_hc_kernel(float *out, const unsigned short *w, uint32_t token, uint32_t n_embd, uint32_t n_hc) {
    uint32_t i = blockIdx.x * blockDim.x + threadIdx.x;
    uint32_t n = n_embd * n_hc;
    if (i >= n) return;
    uint32_t e = i % n_embd;
    out[i] = arena_bf16_to_f32(w[(uint64_t)token * n_embd + e]);
}

__global__ static void embed_tokens_hc_kernel(
        float *out,
        const int32_t *tokens,
        const unsigned short *w,
        uint32_t n_vocab,
        uint32_t n_tokens,
        uint32_t n_embd,
        uint32_t n_hc) {
    uint64_t gid = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    uint64_t n = (uint64_t)n_tokens * n_hc * n_embd;
    if (gid >= n) return;
    uint32_t d = gid % n_embd;
    uint64_t tmp = gid / n_embd;
    uint32_t t = tmp / n_hc;
    int32_t tok_i = tokens[t];
    uint32_t tok = tok_i < 0 ? 0u : (uint32_t)tok_i;
    if (tok >= n_vocab) tok = 0;
    out[gid] = arena_bf16_to_f32(w[(uint64_t)tok * n_embd + d]);
}

__global__ static void matmul_f16_kernel(
        float *out,
        const __half *w,
        const float *x,
        uint64_t in_dim,
        uint64_t out_dim,
        uint64_t n_tok) {
    uint64_t row = (uint64_t)blockIdx.x;
    uint64_t tok = (uint64_t)blockIdx.y;
    if (row >= out_dim || tok >= n_tok) return;

    float sum = 0.0f;
    const __half *wr = w + row * in_dim;
    const float *xr = x + tok * in_dim;
    for (uint64_t i = threadIdx.x; i < in_dim; i += blockDim.x) {
        sum += __half2float(wr[i]) * xr[i];
    }

    __shared__ float partial[256];
    partial[threadIdx.x] = sum;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] += partial[threadIdx.x + stride];
        __syncthreads();
    }
    if (threadIdx.x == 0) out[tok * out_dim + row] = partial[0];
}

__global__ static void matmul_f16_serial_kernel(
        float *out,
        const __half *w,
        const float *x,
        uint64_t in_dim,
        uint64_t out_dim,
        uint64_t n_tok) {
    uint64_t row = (uint64_t)blockIdx.x;
    uint64_t tok = (uint64_t)blockIdx.y;
    if (row >= out_dim || tok >= n_tok || threadIdx.x != 0) return;

    float sum = 0.0f;
    const __half *wr = w + row * in_dim;
    const float *xr = x + tok * in_dim;
    for (uint64_t i = 0; i < in_dim; i++) {
        sum += __half2float(wr[i]) * xr[i];
    }
    out[tok * out_dim + row] = sum;
}

__global__ static void matmul_f16_ordered_chunks_kernel(
        float *out,
        const __half *w,
        const float *x,
        uint64_t in_dim,
        uint64_t out_dim,
        uint64_t n_tok) {
    uint64_t row = (uint64_t)blockIdx.x;
    uint64_t tok = (uint64_t)blockIdx.y;
    if (row >= out_dim || tok >= n_tok) return;

    __shared__ float partial[32];
    const uint32_t tid = threadIdx.x;
    float sum = 0.0f;
    const uint64_t chunk = (in_dim + 31u) / 32u;
    const uint64_t k0 = (uint64_t)tid * chunk;
    uint64_t k1 = k0 + chunk;
    if (k1 > in_dim) k1 = in_dim;
    const __half *wr = w + row * in_dim;
    const float *xr = x + tok * in_dim;
    for (uint64_t i = k0; i < k1; i++) {
        sum += __half2float(wr[i]) * xr[i];
    }
    partial[tid] = sum;
    __syncthreads();
    if (tid == 0) {
        float total = 0.0f;
        for (uint32_t i = 0; i < 32u; i++) total += partial[i];
        out[tok * out_dim + row] = total;
    }
}

__global__ static void matmul_f16_pair_ordered_chunks_kernel(
        float *out0,
        float *out1,
        const __half *w0,
        const __half *w1,
        const float *x,
        uint64_t in_dim,
        uint64_t out0_dim,
        uint64_t out1_dim) {
    uint64_t row = (uint64_t)blockIdx.x;
    if (row >= out0_dim && row >= out1_dim) return;

    __shared__ float partial0[32];
    __shared__ float partial1[32];
    const uint32_t tid = threadIdx.x;
    float sum0 = 0.0f;
    float sum1 = 0.0f;
    const uint64_t chunk = (in_dim + 31u) / 32u;
    const uint64_t k0 = (uint64_t)tid * chunk;
    uint64_t k1 = k0 + chunk;
    if (k1 > in_dim) k1 = in_dim;
    const __half *wr0 = row < out0_dim ? w0 + row * in_dim : w0;
    const __half *wr1 = row < out1_dim ? w1 + row * in_dim : w1;
    for (uint64_t i = k0; i < k1; i++) {
        const float xv = x[i];
        if (row < out0_dim) sum0 += __half2float(wr0[i]) * xv;
        if (row < out1_dim) sum1 += __half2float(wr1[i]) * xv;
    }
    partial0[tid] = sum0;
    partial1[tid] = sum1;
    __syncthreads();
    if (tid == 0) {
        float total0 = 0.0f;
        float total1 = 0.0f;
        for (uint32_t i = 0; i < 32u; i++) {
            total0 += partial0[i];
            total1 += partial1[i];
        }
        if (row < out0_dim) out0[row] = total0;
        if (row < out1_dim) out1[row] = total1;
    }
}

__global__ static void matmul_f32_kernel(
        float *out,
        const float *w,
        const float *x,
        uint64_t in_dim,
        uint64_t out_dim,
        uint64_t n_tok) {
    uint64_t row = (uint64_t)blockIdx.x;
    uint64_t tok = (uint64_t)blockIdx.y;
    if (row >= out_dim || tok >= n_tok) return;

    float sum = 0.0f;
    const float *wr = w + row * in_dim;
    const float *xr = x + tok * in_dim;
    for (uint64_t i = threadIdx.x; i < in_dim; i += blockDim.x) {
        sum += wr[i] * xr[i];
    }

    __shared__ float partial[256];
    partial[threadIdx.x] = sum;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] += partial[threadIdx.x + stride];
        __syncthreads();
    }
    if (threadIdx.x == 0) out[tok * out_dim + row] = partial[0];
}

__global__ static void repeat_hc_kernel(float *out, const float *row, uint32_t n_embd, uint32_t n_hc) {
    uint64_t i = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    uint64_t n = (uint64_t)n_embd * n_hc;
    if (i >= n) return;
    out[i] = row[i % n_embd];
}

__global__ static void f32_to_f16_kernel(__half *out, const float *x, uint64_t n) {
    uint64_t i = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    if (i < n) out[i] = __float2half(x[i]);
}

__global__ static void f32_f16_round_kernel(float *x, uint64_t n) {
    uint64_t i = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    if (i < n) x[i] = __half2float(__float2half_rn(x[i]));
}

__device__ static float warp_sum_f32(float v) {
    for (int offset = 16; offset > 0; offset >>= 1) {
        v += __shfl_down_sync(0xffffffffu, v, offset);
    }
    return v;
}

__device__ static float warp_max_f32(float v) {
    for (int offset = 16; offset > 0; offset >>= 1) {
        v = fmaxf(v, __shfl_down_sync(0xffffffffu, v, offset));
    }
    return v;
}

__device__ static float dot4_f32(float4 a, float4 b) {
    return a.x * b.x + a.y * b.y + a.z * b.z + a.w * b.w;
}

__device__ __forceinline__ static int32_t load_i8x4_i32_aligned(const int8_t *p) {
    return *(const int32_t *)p;
}

__device__ __forceinline__ static int32_t load_i8x4_i32_unaligned(const int8_t *p) {
    const uint8_t *u = (const uint8_t *)p;
    return (int32_t)((uint32_t)u[0] |
                     ((uint32_t)u[1] << 8) |
                     ((uint32_t)u[2] << 16) |
                     ((uint32_t)u[3] << 24));
}

__device__ __forceinline__ static int32_t dot_i8x32_dp4a(const int8_t *a, const int8_t *b) {
    int32_t dot = 0;
#pragma unroll
    for (uint32_t i = 0; i < 32u; i += 4u) {
        dot = __dp4a(load_i8x4_i32_unaligned(a + i), load_i8x4_i32_aligned(b + i), dot);
    }
    return dot;
}

__device__ __forceinline__ static int32_t dot_i8_block(const int8_t *a, const int8_t *b, uint64_t n, int use_dp4a) {
    if (use_dp4a && n == 32u) return dot_i8x32_dp4a(a, b);
    int32_t dot = 0;
    for (uint64_t i = 0; i < n; i++) dot += (int32_t)a[i] * (int32_t)b[i];
    return dot;
}

__global__ static DS4_CUDA_UNUSED void matmul_q8_0_kernel(
        float *out,
        const unsigned char *w,
        const float *x,
        uint64_t in_dim,
        uint64_t out_dim,
        uint64_t n_tok) {
    uint64_t row = (uint64_t)blockIdx.x;
    uint64_t tok = (uint64_t)blockIdx.y;
    if (row >= out_dim || tok >= n_tok) return;
    const uint64_t blocks = (in_dim + 31) / 32;
    const unsigned char *wr = w + row * blocks * 34;
    const float *xr = x + tok * in_dim;
    float acc = 0.0f;

    for (uint64_t b = threadIdx.x; b < blocks; b += blockDim.x) {
        uint64_t i0 = b * 32;
        uint64_t bn = in_dim - i0 < 32 ? in_dim - i0 : 32;
        float amax = 0.0f;
        for (uint64_t i = 0; i < bn; i++) amax = fmaxf(amax, fabsf(xr[i0 + i]));
        float d = amax / 127.0f;
        float id = d != 0.0f ? 1.0f / d : 0.0f;
        const __half *scale_h = (const __half *)(wr + b * 34);
        const int8_t *qs = (const int8_t *)(wr + b * 34 + 2);
        int dot = 0;
        for (uint64_t i = 0; i < bn; i++) {
            int q = (int)lrintf(xr[i0 + i] * id);
            q = q > 127 ? 127 : (q < -128 ? -128 : q);
            dot += (int)qs[i] * q;
        }
        acc += __half2float(*scale_h) * d * (float)dot;
    }

    __shared__ float partial[256];
    partial[threadIdx.x] = acc;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] += partial[threadIdx.x + stride];
        __syncthreads();
    }
    if (threadIdx.x == 0) out[tok * out_dim + row] = partial[0];
}

__global__ static void quantize_q8_0_f32_kernel(
        int8_t *xq,
        float *xscale,
        const float *x,
        uint64_t in_dim,
        uint64_t blocks) {
    uint64_t b = blockIdx.x;
    uint64_t tok = blockIdx.y;
    if (b >= blocks) return;
    uint64_t i0 = b * 32;
    uint64_t bn = in_dim - i0 < 32 ? in_dim - i0 : 32;
    const float *xr = x + tok * in_dim + i0;

    float a = 0.0f;
    if (threadIdx.x < bn) a = fabsf(xr[threadIdx.x]);
    __shared__ float vals[32];
    vals[threadIdx.x] = a;
    __syncthreads();
    for (uint32_t stride = 16; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) vals[threadIdx.x] = fmaxf(vals[threadIdx.x], vals[threadIdx.x + stride]);
        __syncthreads();
    }
    const float d = vals[0] / 127.0f;
    const float id = d != 0.0f ? 1.0f / d : 0.0f;
    if (threadIdx.x == 0) xscale[tok * blocks + b] = d;
    int8_t *dst = xq + (tok * blocks + b) * 32;
    if (threadIdx.x < bn) {
        int v = (int)lrintf(xr[threadIdx.x] * id);
        v = v > 127 ? 127 : (v < -128 ? -128 : v);
        dst[threadIdx.x] = (int8_t)v;
    } else {
        dst[threadIdx.x] = 0;
    }
}

__global__ static void matmul_q8_0_preq_kernel(
        float *out,
        const unsigned char *w,
        const int8_t *xq,
        const float *xscale,
        uint64_t in_dim,
        uint64_t out_dim,
        uint64_t n_tok,
        uint64_t blocks,
        int use_dp4a) {
    uint64_t row = (uint64_t)blockIdx.x;
    uint64_t tok = (uint64_t)blockIdx.y;
    if (row >= out_dim || tok >= n_tok) return;
    const unsigned char *wr = w + row * blocks * 34;
    const int8_t *xqr = xq + tok * blocks * 32;
    const float *xsr = xscale + tok * blocks;
    float acc = 0.0f;
    for (uint64_t b = threadIdx.x; b < blocks; b += blockDim.x) {
        uint64_t i0 = b * 32;
        uint64_t bn = in_dim - i0 < 32 ? in_dim - i0 : 32;
        const __half *scale_h = (const __half *)(wr + b * 34);
        const int8_t *qs = (const int8_t *)(wr + b * 34 + 2);
        const int8_t *xqb = xqr + b * 32;
        int dot = dot_i8_block(qs, xqb, bn, use_dp4a);
        acc += __half2float(*scale_h) * xsr[b] * (float)dot;
    }
    __shared__ float partial[256];
    partial[threadIdx.x] = acc;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] += partial[threadIdx.x + stride];
        __syncthreads();
    }
    if (threadIdx.x == 0) out[tok * out_dim + row] = partial[0];
}

__global__ static void matmul_q8_0_preq_warp8_kernel(
        float *out,
        const unsigned char *w,
        const int8_t *xq,
        const float *xscale,
        uint64_t in_dim,
        uint64_t out_dim,
        uint64_t blocks,
        int use_dp4a) {
    uint64_t row = (uint64_t)blockIdx.x * 8u + (threadIdx.x >> 5u);
    uint32_t lane = threadIdx.x & 31u;
    if (row >= out_dim) return;
    const unsigned char *wr = w + row * blocks * 34;
    float acc = 0.0f;
    for (uint64_t b = lane; b < blocks; b += 32u) {
        uint64_t i0 = b * 32;
        uint64_t bn = in_dim - i0 < 32 ? in_dim - i0 : 32;
        const __half *scale_h = (const __half *)(wr + b * 34);
        const int8_t *qs = (const int8_t *)(wr + b * 34 + 2);
        const int8_t *xqb = xq + b * 32;
        int dot = dot_i8_block(qs, xqb, bn, use_dp4a);
        acc += __half2float(*scale_h) * xscale[b] * (float)dot;
    }
    acc = warp_sum_f32(acc);
    if (lane == 0) out[row] = acc;
}

__global__ static void matmul_q8_0_pair_preq_warp8_kernel(
        float *out0,
        float *out1,
        const unsigned char *w0,
        const unsigned char *w1,
        const int8_t *xq,
        const float *xscale,
        uint64_t in_dim,
        uint64_t out0_dim,
        uint64_t out1_dim,
        uint64_t blocks,
        int use_dp4a) {
    uint64_t row = (uint64_t)blockIdx.x * 8u + (threadIdx.x >> 5u);
    uint32_t lane = threadIdx.x & 31u;
    if (row >= out0_dim && row >= out1_dim) return;
    float acc0 = 0.0f;
    float acc1 = 0.0f;
    const unsigned char *wr0 = row < out0_dim ? w0 + row * blocks * 34 : NULL;
    const unsigned char *wr1 = row < out1_dim ? w1 + row * blocks * 34 : NULL;
    for (uint64_t b = lane; b < blocks; b += 32u) {
        uint64_t i0 = b * 32;
        uint64_t bn = in_dim - i0 < 32 ? in_dim - i0 : 32;
        const int8_t *xqb = xq + b * 32;
        const float xs = xscale[b];
        if (wr0) {
            const __half *scale_h = (const __half *)(wr0 + b * 34);
            const int8_t *qs = (const int8_t *)(wr0 + b * 34 + 2);
            int dot = dot_i8_block(qs, xqb, bn, use_dp4a);
            acc0 += __half2float(*scale_h) * xs * (float)dot;
        }
        if (wr1) {
            const __half *scale_h = (const __half *)(wr1 + b * 34);
            const int8_t *qs = (const int8_t *)(wr1 + b * 34 + 2);
            int dot = dot_i8_block(qs, xqb, bn, use_dp4a);
            acc1 += __half2float(*scale_h) * xs * (float)dot;
        }
    }
    acc0 = warp_sum_f32(acc0);
    acc1 = warp_sum_f32(acc1);
    if (lane == 0) {
        if (row < out0_dim) out0[row] = acc0;
        if (row < out1_dim) out1[row] = acc1;
    }
}

__global__ static void matmul_q8_0_hc_expand_preq_warp8_kernel(
        float *out_hc,
        float *block_out,
        const float *block_add,
        const float *residual_hc,
        const float *split,
        const unsigned char *w,
        const int8_t *xq,
        const float *xscale,
        uint64_t in_dim,
        uint64_t out_dim,
        uint32_t n_embd,
        uint32_t n_hc,
        uint64_t blocks,
        int has_add,
        int use_dp4a) {
    const uint64_t row = (uint64_t)blockIdx.x * 8u + (threadIdx.x >> 5u);
    const uint32_t lane = threadIdx.x & 31u;
    if (row >= out_dim) return;
    const unsigned char *wr = w + row * blocks * 34;
    float acc = 0.0f;
    for (uint64_t b = lane; b < blocks; b += 32u) {
        const uint64_t i0 = b * 32;
        const uint64_t bn = in_dim - i0 < 32 ? in_dim - i0 : 32;
        const __half *scale_h = (const __half *)(wr + b * 34);
        const int8_t *qs = (const int8_t *)(wr + b * 34 + 2);
        const int8_t *xqb = xq + b * 32;
        int dot = dot_i8_block(qs, xqb, bn, use_dp4a);
        acc += __half2float(*scale_h) * xscale[b] * (float)dot;
    }
    acc = warp_sum_f32(acc);
    if (lane == 0) {
        const uint32_t d = (uint32_t)row;
        block_out[d] = acc;
        float block_v = acc;
        if (has_add) block_v += block_add[d];
        const float *post = split + n_hc;
        const float *comb = split + 2u * n_hc;
        for (uint32_t dst_hc = 0; dst_hc < n_hc; dst_hc++) {
            float hc_acc = block_v * post[dst_hc];
            for (uint32_t src_hc = 0; src_hc < n_hc; src_hc++) {
                const float comb_v = comb[dst_hc + (uint64_t)src_hc * n_hc];
                const float res_v = residual_hc[(uint64_t)src_hc * n_embd + d];
                hc_acc += comb_v * res_v;
            }
            out_hc[(uint64_t)dst_hc * n_embd + d] = hc_acc;
        }
    }
}

__global__ static void matmul_q8_0_preq_batch_warp8_kernel(
        float *out,
        const unsigned char *w,
        const int8_t *xq,
        const float *xscale,
        uint64_t in_dim,
        uint64_t out_dim,
        uint64_t n_tok,
        uint64_t blocks,
        int use_dp4a) {
    const uint64_t row = (uint64_t)blockIdx.x * 8u + (threadIdx.x >> 5u);
    const uint64_t tok = (uint64_t)blockIdx.y;
    const uint32_t lane = threadIdx.x & 31u;
    if (row >= out_dim || tok >= n_tok) return;

    const unsigned char *wr = w + row * blocks * 34;
    const int8_t *xqr = xq + tok * blocks * 32;
    const float *xsr = xscale + tok * blocks;
    float acc = 0.0f;
    for (uint64_t b = lane; b < blocks; b += 32u) {
        const uint64_t i0 = b * 32;
        const uint64_t bn = in_dim - i0 < 32 ? in_dim - i0 : 32;
        const __half *scale_h = (const __half *)(wr + b * 34);
        const int8_t *qs = (const int8_t *)(wr + b * 34 + 2);
        const int8_t *xqb = xqr + b * 32;
        int dot = dot_i8_block(qs, xqb, bn, use_dp4a);
        acc += __half2float(*scale_h) * xsr[b] * (float)dot;
    }
    acc = warp_sum_f32(acc);
    if (lane == 0) out[tok * out_dim + row] = acc;
}

__global__ static void dequant_q8_0_to_f16_kernel(
        __half *out,
        const unsigned char *w,
        uint64_t in_dim,
        uint64_t out_dim,
        uint64_t blocks) {
    uint64_t gid = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    uint64_t n = in_dim * out_dim;
    if (gid >= n) return;
    uint64_t row = gid / in_dim;
    uint64_t i = gid - row * in_dim;
    uint64_t b = i / 32;
    uint64_t j = i - b * 32;
    const unsigned char *blk = w + (row * blocks + b) * 34;
    const __half scale = *(const __half *)blk;
    const int8_t q = *(const int8_t *)(blk + 2 + j);
    out[gid] = __hmul(scale, __float2half((float)q));
}

__global__ static void dequant_q8_0_to_f32_kernel(
        float *out,
        const unsigned char *w,
        uint64_t in_dim,
        uint64_t out_dim,
        uint64_t blocks) {
    uint64_t gid = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    uint64_t n = in_dim * out_dim;
    if (gid >= n) return;
    uint64_t row = gid / in_dim;
    uint64_t i = gid - row * in_dim;
    uint64_t b = i / 32;
    uint64_t j = i - b * 32;
    const unsigned char *blk = w + (row * blocks + b) * 34;
    const float scale = __half2float(*(const __half *)blk);
    const int8_t q = *(const int8_t *)(blk + 2 + j);
    out[gid] = scale * (float)q;
}

__global__ static void grouped_q8_0_a_preq_warp8_kernel(
        float *low,
        const unsigned char *w,
        const int8_t *xq,
        const float *xscale,
        uint64_t group_dim,
        uint64_t rank,
        uint32_t n_groups,
        uint32_t n_tokens,
        uint64_t blocks,
        int use_dp4a) {
    const uint64_t row = (uint64_t)blockIdx.x * 8u + (threadIdx.x >> 5u);
    const uint64_t tok = (uint64_t)blockIdx.y;
    const uint32_t lane = threadIdx.x & 31u;
    const uint64_t low_dim = (uint64_t)n_groups * rank;
    if (row >= low_dim || tok >= n_tokens) return;

    const uint64_t group = row / rank;
    const uint64_t row_in_group = row - group * rank;
    const unsigned char *wr = w + (group * rank + row_in_group) * blocks * 34;
    const uint64_t xrow = tok * (uint64_t)n_groups + group;
    const int8_t *xqr = xq + xrow * blocks * 32;
    const float *xsr = xscale + xrow * blocks;
    float acc = 0.0f;

    for (uint64_t b = lane; b < blocks; b += 32u) {
        const uint64_t i0 = b * 32;
        const uint64_t bn = group_dim - i0 < 32 ? group_dim - i0 : 32;
        const __half *scale_h = (const __half *)(wr + b * 34);
        const int8_t *qs = (const int8_t *)(wr + b * 34 + 2);
        const int8_t *xqb = xqr + b * 32;
        int dot = dot_i8_block(qs, xqb, bn, use_dp4a);
        acc += __half2float(*scale_h) * xsr[b] * (float)dot;
    }
    acc = warp_sum_f32(acc);
    if (lane == 0) low[tok * low_dim + row] = acc;
}

__global__ static void rms_norm_plain_kernel(float *out, const float *x, uint32_t n, uint32_t rows, float eps) {
    uint32_t row = blockIdx.x;
    if (row >= rows) return;
    const float *xr = x + (uint64_t)row * n;
    float *orow = out + (uint64_t)row * n;
    float sum = 0.0f;
    for (uint32_t i = threadIdx.x; i < n; i += blockDim.x) {
        float v = xr[i];
        sum += v * v;
    }
    __shared__ float partial[256];
    partial[threadIdx.x] = sum;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] += partial[threadIdx.x + stride];
        __syncthreads();
    }
    float scale = rsqrtf(partial[0] / (float)n + eps);
    for (uint32_t i = threadIdx.x; i < n; i += blockDim.x) {
        orow[i] = xr[i] * scale;
    }
}

__global__ static void rms_norm_weight_kernel(float *out, const float *x, const float *w, uint32_t n, uint32_t rows, float eps) {
    uint32_t row = blockIdx.x;
    if (row >= rows) return;
    const float *xr = x + (uint64_t)row * n;
    float *orow = out + (uint64_t)row * n;
    float sum = 0.0f;
    for (uint32_t i = threadIdx.x; i < n; i += blockDim.x) {
        float v = xr[i];
        sum += v * v;
    }
    __shared__ float partial[256];
    partial[threadIdx.x] = sum;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] += partial[threadIdx.x + stride];
        __syncthreads();
    }
    float scale = rsqrtf(partial[0] / (float)n + eps);
    for (uint32_t i = threadIdx.x; i < n; i += blockDim.x) {
        orow[i] = xr[i] * scale * w[i];
    }
}

__global__ static void dsv4_qkv_rms_norm_rows_kernel(
        float *q_out,
        const float *q,
        const float *q_w,
        uint32_t q_n,
        float *kv_out,
        const float *kv,
        const float *kv_w,
        uint32_t kv_n,
        uint32_t rows,
        float eps) {
    const uint32_t row = blockIdx.x;
    const uint32_t which = blockIdx.y;
    if (row >= rows || which > 1u) return;
    const uint32_t n = which == 0u ? q_n : kv_n;
    const float *xr = (which == 0u ? q : kv) + (uint64_t)row * n;
    float *orow = (which == 0u ? q_out : kv_out) + (uint64_t)row * n;
    const float *w = which == 0u ? q_w : kv_w;
    float sum = 0.0f;
    for (uint32_t i = threadIdx.x; i < n; i += blockDim.x) {
        const float v = xr[i];
        sum += v * v;
    }
    __shared__ float partial[256];
    partial[threadIdx.x] = sum;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] += partial[threadIdx.x + stride];
        __syncthreads();
    }
    const float scale = rsqrtf(partial[0] / (float)n + eps);
    for (uint32_t i = threadIdx.x; i < n; i += blockDim.x) {
        orow[i] = xr[i] * scale * w[i];
    }
}

__global__ static void head_rms_norm_kernel(float *x, uint32_t n_tok, uint32_t n_head, uint32_t head_dim, float eps) {
    uint32_t row = blockIdx.x;
    if (row >= n_tok * n_head) return;
    float *xr = x + (uint64_t)row * head_dim;
    float sum = 0.0f;
    for (uint32_t i = threadIdx.x; i < head_dim; i += blockDim.x) {
        float v = xr[i];
        sum += v * v;
    }
    __shared__ float partial[256];
    partial[threadIdx.x] = sum;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] += partial[threadIdx.x + stride];
        __syncthreads();
    }
    float scale = rsqrtf(partial[0] / (float)head_dim + eps);
    for (uint32_t i = threadIdx.x; i < head_dim; i += blockDim.x) xr[i] *= scale;
}

__device__ static float rope_yarn_ramp_dev(float low, float high, int i0);

__global__ static void head_rms_norm_rope_tail_kernel(
        float *x,
        uint32_t n_tok,
        uint32_t n_head,
        uint32_t head_dim,
        uint32_t n_rot,
        uint32_t pos0,
        uint32_t n_ctx_orig,
        int inverse,
        float freq_base,
        float freq_scale,
        float ext_factor,
        float attn_factor,
        float beta_fast,
        float beta_slow,
        float eps) {
    uint32_t row = blockIdx.x;
    if (row >= n_tok * n_head) return;
    uint32_t t = row / n_head;
    float *xr = x + (uint64_t)row * head_dim;
    float sum = 0.0f;
    for (uint32_t i = threadIdx.x; i < head_dim; i += blockDim.x) {
        float v = xr[i];
        sum += v * v;
    }
    __shared__ float partial[256];
    partial[threadIdx.x] = sum;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] += partial[threadIdx.x + stride];
        __syncthreads();
    }
    const float scale = rsqrtf(partial[0] / (float)head_dim + eps);
    const uint32_t n_nope = head_dim - n_rot;
    for (uint32_t i = threadIdx.x; i < n_nope; i += blockDim.x) {
        xr[i] *= scale;
    }

    float corr0 = 0.0f, corr1 = 0.0f;
    if (ext_factor != 0.0f) {
        float denom = 2.0f * logf(freq_base);
        corr0 = floorf((float)n_rot * logf((float)n_ctx_orig / (beta_fast * 2.0f * (float)M_PI)) / denom);
        corr1 = ceilf((float)n_rot * logf((float)n_ctx_orig / (beta_slow * 2.0f * (float)M_PI)) / denom);
        corr0 = fmaxf(0.0f, corr0);
        corr1 = fminf((float)(n_rot - 1), corr1);
    }
    for (uint32_t pair = threadIdx.x; pair < n_rot / 2; pair += blockDim.x) {
        uint32_t i = pair * 2u;
        float theta_extrap = (float)(pos0 + t) * powf(freq_base, -((float)i) / (float)n_rot);
        float theta_interp = freq_scale * theta_extrap;
        float theta = theta_interp;
        float mscale = attn_factor;
        if (ext_factor != 0.0f) {
            float ramp_mix = rope_yarn_ramp_dev(corr0, corr1, (int)i) * ext_factor;
            theta = theta_interp * (1.0f - ramp_mix) + theta_extrap * ramp_mix;
            mscale *= 1.0f + 0.1f * logf(1.0f / freq_scale);
        }
        float c = cosf(theta) * mscale;
        float s = sinf(theta) * mscale;
        if (inverse) s = -s;
        float *tail = xr + n_nope;
        float x0 = tail[i] * scale;
        float x1 = tail[i + 1] * scale;
        tail[i] = x0 * c - x1 * s;
        tail[i + 1] = x0 * s + x1 * c;
    }
}

__device__ static float rope_yarn_ramp_dev(float low, float high, int i0) {
    float y = ((float)(i0 / 2) - low) / fmaxf(0.001f, high - low);
    return 1.0f - fminf(1.0f, fmaxf(0.0f, y));
}

__global__ static void rope_tail_kernel(
        float *x,
        uint32_t n_tok,
        uint32_t n_head,
        uint32_t head_dim,
        uint32_t n_rot,
        uint32_t pos0,
        uint32_t n_ctx_orig,
        int inverse,
        float freq_base,
        float freq_scale,
        float ext_factor,
        float attn_factor,
        float beta_fast,
        float beta_slow) {
    uint32_t gid = blockIdx.x * blockDim.x + threadIdx.x;
    uint32_t pairs = n_tok * n_head * (n_rot / 2);
    if (gid >= pairs) return;
    uint32_t pair = gid % (n_rot / 2);
    uint32_t tmp = gid / (n_rot / 2);
    uint32_t h = tmp % n_head;
    uint32_t t = tmp / n_head;
    uint32_t n_nope = head_dim - n_rot;
    uint32_t i = pair * 2;

    float corr0 = 0.0f, corr1 = 0.0f;
    if (ext_factor != 0.0f) {
        float denom = 2.0f * logf(freq_base);
        corr0 = floorf((float)n_rot * logf((float)n_ctx_orig / (beta_fast * 2.0f * (float)M_PI)) / denom);
        corr1 = ceilf((float)n_rot * logf((float)n_ctx_orig / (beta_slow * 2.0f * (float)M_PI)) / denom);
        corr0 = fmaxf(0.0f, corr0);
        corr1 = fminf((float)(n_rot - 1), corr1);
    }

    float theta_extrap = (float)(pos0 + t) * powf(freq_base, -((float)i) / (float)n_rot);
    float theta_interp = freq_scale * theta_extrap;
    float theta = theta_interp;
    float mscale = attn_factor;
    if (ext_factor != 0.0f) {
        float ramp_mix = rope_yarn_ramp_dev(corr0, corr1, (int)i) * ext_factor;
        theta = theta_interp * (1.0f - ramp_mix) + theta_extrap * ramp_mix;
        mscale *= 1.0f + 0.1f * logf(1.0f / freq_scale);
    }
    float c = cosf(theta) * mscale;
    float s = sinf(theta) * mscale;
    if (inverse) s = -s;

    float *tail = x + ((uint64_t)t * n_head + h) * head_dim + n_nope;
    float x0 = tail[i];
    float x1 = tail[i + 1];
    tail[i] = x0 * c - x1 * s;
    tail[i + 1] = x0 * s + x1 * c;
}

__device__ static float dsv4_e4m3fn_value_dev(int i) {
    int exp = (i >> 3) & 15;
    int mant = i & 7;
    if (exp == 0) return (float)mant * 0.001953125f;
    return (1.0f + (float)mant * 0.125f) * exp2f((float)exp - 7.0f);
}

__device__ static float dsv4_e4m3fn_dequant_dev(float x) {
    float sign = x < 0.0f ? -1.0f : 1.0f;
    float ax = fminf(fabsf(x), 448.0f);
    int lo = 0, hi = 126;
    while (lo < hi) {
        int mid = (lo + hi + 1) >> 1;
        if (dsv4_e4m3fn_value_dev(mid) <= ax) lo = mid;
        else hi = mid - 1;
    }
    int best = lo;
    if (best < 126) {
        float bd = fabsf(ax - dsv4_e4m3fn_value_dev(best));
        float nd = fabsf(ax - dsv4_e4m3fn_value_dev(best + 1));
        if (nd < bd || (nd == bd && (((best + 1) & 1) == 0) && ((best & 1) != 0))) best++;
    }
    return sign * dsv4_e4m3fn_value_dev(best);
}

__device__ static float model_scalar_dev(const void *base, uint64_t offset, uint32_t type, uint64_t idx) {
    const char *p = (const char *)base + offset;
    if (type == 1u) return __half2float(((const __half *)p)[idx]);
    return ((const float *)p)[idx];
}

__device__ static float rope_yarn_ramp_cpu_equiv_dev(float low, float high, int i0) {
    float y = ((float)(i0 / 2) - low) / fmaxf(0.001f, high - low);
    return 1.0f - fminf(1.0f, fmaxf(0.0f, y));
}

__device__ static DS4_CUDA_UNUSED void rope_tail_one_dev(float *x, uint32_t head_dim, uint32_t n_rot, uint32_t pos, uint32_t n_ctx_orig, float freq_base, float freq_scale, float ext_factor, float attn_factor, float beta_fast, float beta_slow) {
    uint32_t n_nope = head_dim - n_rot;
    float corr0 = 0.0f, corr1 = 0.0f;
    if (ext_factor != 0.0f) {
        float denom = 2.0f * logf(freq_base);
        corr0 = fmaxf(0.0f, floorf((float)n_rot * logf((float)n_ctx_orig / (beta_fast * 2.0f * (float)M_PI)) / denom));
        corr1 = fminf((float)(n_rot - 1), ceilf((float)n_rot * logf((float)n_ctx_orig / (beta_slow * 2.0f * (float)M_PI)) / denom));
    }
    for (uint32_t i = 0; i < n_rot; i += 2) {
        float theta_extrap = (float)pos * powf(freq_base, -((float)i) / (float)n_rot);
        float theta_interp = freq_scale * theta_extrap;
        float theta = theta_interp;
        float mscale = attn_factor;
        if (ext_factor != 0.0f) {
            float mix = rope_yarn_ramp_cpu_equiv_dev(corr0, corr1, (int)i) * ext_factor;
            theta = theta_interp * (1.0f - mix) + theta_extrap * mix;
            mscale *= 1.0f + 0.1f * logf(1.0f / freq_scale);
        }
        float c = cosf(theta) * mscale;
        float s = sinf(theta) * mscale;
        float x0 = x[n_nope + i];
        float x1 = x[n_nope + i + 1];
        x[n_nope + i] = x0 * c - x1 * s;
        x[n_nope + i + 1] = x0 * s + x1 * c;
    }
}

__global__ static void fp8_kv_quantize_kernel(float *x, uint32_t n_tok, uint32_t head_dim, uint32_t n_rot) {
    uint32_t row = blockIdx.x;
    uint32_t tid = threadIdx.x;
    uint32_t n_nope = head_dim - n_rot;
    float *xr = x + (uint64_t)row * head_dim;
    __shared__ float scratch[64];
    for (uint32_t off = 0; off < n_nope; off += 64) {
        float v = 0.0f;
        if (off + tid < n_nope) v = xr[off + tid];
        scratch[tid] = off + tid < n_nope ? fabsf(v) : 0.0f;
        __syncthreads();
        for (uint32_t stride = 32; stride > 0; stride >>= 1) {
            if (tid < stride) scratch[tid] = fmaxf(scratch[tid], scratch[tid + stride]);
            __syncthreads();
        }
        float scale = exp2f(ceilf(log2f(fmaxf(scratch[0], 1.0e-4f) / 448.0f)));
        if (off + tid < n_nope) {
            float q = dsv4_e4m3fn_dequant_dev(fminf(448.0f, fmaxf(-448.0f, v / scale))) * scale;
            xr[off + tid] = q;
        }
        __syncthreads();
    }
}

__global__ static void store_raw_kv_batch_kernel(float *raw, const float *kv, uint32_t raw_cap, uint32_t pos0, uint32_t n_tokens, uint32_t head_dim) {
    uint64_t gid = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    uint64_t n = (uint64_t)n_tokens * head_dim;
    if (gid >= n) return;
    uint32_t d = gid % head_dim;
    uint32_t t = gid / head_dim;
    uint32_t row = (pos0 + t) % raw_cap;
    raw[(uint64_t)row * head_dim + d] = __half2float(__float2half(kv[(uint64_t)t * head_dim + d]));
}

__global__ static void attention_prefill_raw_kernel(
        float *heads,
        const float *sinks,
        const float *q,
        const float *raw_kv,
        uint32_t n_tokens,
        uint32_t window,
        uint32_t n_head,
        uint32_t head_dim) {
    uint32_t t = blockIdx.x;
    uint32_t h = blockIdx.y;
    if (t >= n_tokens || h >= n_head) return;
    uint32_t raw_count = t + 1 < window ? t + 1 : window;
    uint32_t raw_start = t + 1 - raw_count;
    const float *qh = q + ((uint64_t)t * n_head + h) * head_dim;
    __shared__ float scores[256];
    __shared__ float partial[128];
    __shared__ float max_s;
    __shared__ float denom;
    float scale = rsqrtf((float)head_dim);
    float local_max = sinks[h];
    __syncthreads();
    for (uint32_t r = threadIdx.x; r < raw_count; r += blockDim.x) {
        const float *kv = raw_kv + (uint64_t)(raw_start + r) * head_dim;
        float dot = 0.0f;
        for (uint32_t d = 0; d < head_dim; d++) dot += qh[d] * kv[d];
        scores[r] = dot * scale;
        local_max = fmaxf(local_max, scores[r]);
    }
    partial[threadIdx.x] = local_max;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] = fmaxf(partial[threadIdx.x], partial[threadIdx.x + stride]);
        __syncthreads();
    }
    if (threadIdx.x == 0) max_s = partial[0];
    __syncthreads();
    if (threadIdx.x == 0) {
        float den = expf(sinks[h] - max_s);
        for (uint32_t r = 0; r < raw_count; r++) {
            scores[r] = expf(scores[r] - max_s);
            den += scores[r];
        }
        denom = den;
    }
    __syncthreads();
    float *oh = heads + ((uint64_t)t * n_head + h) * head_dim;
    for (uint32_t d = threadIdx.x; d < head_dim; d += blockDim.x) {
        float acc = 0.0f;
        for (uint32_t r = 0; r < raw_count; r++) {
            acc += raw_kv[(uint64_t)(raw_start + r) * head_dim + d] * scores[r];
        }
        oh[d] = acc / denom;
    }
}

__global__ static void attention_prefill_mixed_kernel(
        float *heads,
        const float *sinks,
        const float *q,
        const float *raw_kv,
        const float *comp_kv,
        const float *comp_mask,
        uint32_t use_comp_mask,
        uint32_t n_tokens,
        uint32_t n_comp,
        uint32_t window,
        uint32_t ratio,
        uint32_t n_head,
        uint32_t head_dim) {
    uint32_t t = blockIdx.x;
    uint32_t h = blockIdx.y;
    if (t >= n_tokens || h >= n_head) return;
    const float *qh = q + ((uint64_t)t * n_head + h) * head_dim;
    uint32_t raw_start = (window != 0 && t + 1u > window) ? t + 1u - window : 0u;
    uint32_t raw_count = t + 1u - raw_start;
    uint32_t visible_comp = (t + 1u) / ratio;
    if (visible_comp > n_comp) visible_comp = n_comp;
    __shared__ float scores[512];
    __shared__ float partial[256];
    __shared__ float max_s;
    __shared__ float denom;
    float scale = rsqrtf((float)head_dim);
    float local_max = sinks[h];
    uint32_t n_score = raw_count + visible_comp;

    for (uint32_t r = threadIdx.x; r < raw_count; r += blockDim.x) {
        const float *kvrow = raw_kv + (uint64_t)(raw_start + r) * head_dim;
        float dot = 0.0f;
        for (uint32_t d = 0; d < head_dim; d++) dot += qh[d] * kvrow[d];
        scores[r] = dot * scale;
        local_max = fmaxf(local_max, scores[r]);
    }
    for (uint32_t c = threadIdx.x; c < visible_comp; c += blockDim.x) {
        float add = use_comp_mask ? comp_mask[(uint64_t)t * n_comp + c] : 0.0f;
        float s = -INFINITY;
        if (add > -1.0e20f) {
            const float *kvrow = comp_kv + (uint64_t)c * head_dim;
            float dot = 0.0f;
            for (uint32_t d = 0; d < head_dim; d++) dot += qh[d] * kvrow[d];
            s = dot * scale + add;
        }
        scores[raw_count + c] = s;
        local_max = fmaxf(local_max, s);
    }
    partial[threadIdx.x] = local_max;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] = fmaxf(partial[threadIdx.x], partial[threadIdx.x + stride]);
        __syncthreads();
    }
    if (threadIdx.x == 0) max_s = partial[0];
    __syncthreads();
    float den_local = 0.0f;
    for (uint32_t i = threadIdx.x; i < n_score; i += blockDim.x) {
        scores[i] = expf(scores[i] - max_s);
        den_local += scores[i];
    }
    partial[threadIdx.x] = den_local;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] += partial[threadIdx.x + stride];
        __syncthreads();
    }
    if (threadIdx.x == 0) denom = partial[0] + expf(sinks[h] - max_s);
    __syncthreads();
    float *oh = heads + ((uint64_t)t * n_head + h) * head_dim;
    for (uint32_t d = threadIdx.x; d < head_dim; d += blockDim.x) {
        float acc = 0.0f;
        for (uint32_t r = 0; r < raw_count; r++) acc += raw_kv[(uint64_t)(raw_start + r) * head_dim + d] * scores[r];
        for (uint32_t c = 0; c < visible_comp; c++) acc += comp_kv[(uint64_t)c * head_dim + d] * scores[raw_count + c];
        oh[d] = acc / denom;
    }
}

__global__ static void attention_prefill_raw_softmax_kernel(
        float *scores,
        const float *sinks,
        uint32_t n_tokens,
        uint32_t window,
        uint32_t n_keys) {
    uint32_t t = blockIdx.x;
    uint32_t h = blockIdx.y;
    if (t >= n_tokens) return;
    float *row = scores + ((uint64_t)h * n_tokens + t) * n_keys;
    __shared__ float partial[256];
    __shared__ float max_s;
    __shared__ float denom;
    float local_max = sinks[h];
    for (uint32_t k = threadIdx.x; k < n_keys; k += blockDim.x) {
        bool valid = k <= t && (window == 0 || t - k < window);
        float s = valid ? row[k] : -INFINITY;
        row[k] = s;
        local_max = fmaxf(local_max, s);
    }
    partial[threadIdx.x] = local_max;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] = fmaxf(partial[threadIdx.x], partial[threadIdx.x + stride]);
        __syncthreads();
    }
    if (threadIdx.x == 0) max_s = partial[0];
    __syncthreads();
    float den_local = 0.0f;
    for (uint32_t k = threadIdx.x; k < n_keys; k += blockDim.x) {
        float p = isfinite(row[k]) ? expf(row[k] - max_s) : 0.0f;
        row[k] = p;
        den_local += p;
    }
    partial[threadIdx.x] = den_local;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] += partial[threadIdx.x + stride];
        __syncthreads();
    }
    if (threadIdx.x == 0) denom = partial[0] + expf(sinks[h] - max_s);
    __syncthreads();
    for (uint32_t k = threadIdx.x; k < n_keys; k += blockDim.x) row[k] /= denom;
}

__global__ static void attention_prefill_mixed_softmax_kernel(
        float *scores,
        const float *sinks,
        const float *comp_mask,
        uint32_t use_comp_mask,
        uint32_t n_tokens,
        uint32_t n_comp,
        uint32_t window,
        uint32_t ratio,
        uint32_t n_keys) {
    uint32_t t = blockIdx.x;
    uint32_t h = blockIdx.y;
    if (t >= n_tokens || ratio == 0) return;
    float *row = scores + ((uint64_t)h * n_tokens + t) * n_keys;
    __shared__ float partial[256];
    __shared__ float max_s;
    __shared__ float denom;
    float local_max = sinks[h];
    const uint32_t visible_comp = (t + 1u) / ratio;
    for (uint32_t k = threadIdx.x; k < n_keys; k += blockDim.x) {
        float s = -INFINITY;
        if (k < n_tokens) {
            if (k <= t && (window == 0 || t - k < window)) s = row[k];
        } else {
            uint32_t c = k - n_tokens;
            if (c < n_comp && c < visible_comp) {
                float add = use_comp_mask ? comp_mask[(uint64_t)t * n_comp + c] : 0.0f;
                if (add > -1.0e20f) s = row[k] + add;
            }
        }
        row[k] = s;
        local_max = fmaxf(local_max, s);
    }
    partial[threadIdx.x] = local_max;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] = fmaxf(partial[threadIdx.x], partial[threadIdx.x + stride]);
        __syncthreads();
    }
    if (threadIdx.x == 0) max_s = partial[0];
    __syncthreads();
    float den_local = 0.0f;
    for (uint32_t k = threadIdx.x; k < n_keys; k += blockDim.x) {
        float p = isfinite(row[k]) ? expf(row[k] - max_s) : 0.0f;
        row[k] = p;
        den_local += p;
    }
    partial[threadIdx.x] = den_local;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] += partial[threadIdx.x + stride];
        __syncthreads();
    }
    if (threadIdx.x == 0) denom = partial[0] + expf(sinks[h] - max_s);
    __syncthreads();
    for (uint32_t k = threadIdx.x; k < n_keys; k += blockDim.x) row[k] /= denom;
}

__global__ static void attention_prefill_pack_mixed_kv_kernel(
        float *dst,
        const float *raw_kv,
        const float *comp_kv,
        uint32_t n_tokens,
        uint32_t n_comp,
        uint32_t head_dim) {
    uint64_t gid = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    uint64_t n = (uint64_t)(n_tokens + n_comp) * head_dim;
    if (gid >= n) return;
    uint32_t d = gid % head_dim;
    uint32_t r = gid / head_dim;
    dst[gid] = r < n_tokens ? raw_kv[(uint64_t)r * head_dim + d]
                             : comp_kv[(uint64_t)(r - n_tokens) * head_dim + d];
}

__global__ static void attention_prefill_unpack_heads_kernel(
        float *heads,
        const float *tmp,
        uint32_t n_tokens,
        uint32_t n_head,
        uint32_t head_dim) {
    uint64_t gid = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    uint64_t n = (uint64_t)n_tokens * n_head * head_dim;
    if (gid >= n) return;
    uint32_t d = gid % head_dim;
    uint64_t q = gid / head_dim;
    uint32_t h = q % n_head;
    uint32_t t = q / n_head;
    heads[gid] = tmp[((uint64_t)h * n_tokens + t) * head_dim + d];
}

__global__ static void attention_pack_group_heads_f16_kernel(
        __half *dst,
        const float *heads,
        uint32_t n_tokens,
        uint32_t n_groups,
        uint32_t group_dim) {
    uint64_t gid = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    uint64_t n = (uint64_t)n_groups * n_tokens * group_dim;
    if (gid >= n) return;
    uint32_t d = gid % group_dim;
    uint64_t q = gid / group_dim;
    uint32_t t = q % n_tokens;
    uint32_t g = q / n_tokens;
    dst[gid] = __float2half(heads[((uint64_t)t * n_groups + g) * group_dim + d]);
}

__global__ static void attention_unpack_group_low_kernel(
        float *low,
        const float *tmp,
        uint32_t n_tokens,
        uint32_t n_groups,
        uint32_t rank) {
    uint64_t gid = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    uint64_t n = (uint64_t)n_groups * n_tokens * rank;
    if (gid >= n) return;
    uint32_t r = gid % rank;
    uint64_t q = gid / rank;
    uint32_t t = q % n_tokens;
    uint32_t g = q / n_tokens;
    uint32_t low_dim = n_groups * rank;
    low[(uint64_t)t * low_dim + (uint64_t)g * rank + r] = tmp[gid];
}

__global__ static void attention_decode_mixed_kernel(
        float *heads,
        const float *sinks,
        const float *q,
        const float *raw_kv,
        const float *comp_kv,
        const float *comp_mask,
        uint32_t use_comp_mask,
        uint32_t n_tokens,
        uint32_t pos0,
        uint32_t n_raw,
        uint32_t raw_cap,
        uint32_t raw_start,
        uint32_t n_comp,
        uint32_t window,
        uint32_t ratio,
        uint32_t n_head,
        uint32_t head_dim) {
    uint32_t t = blockIdx.x;
    uint32_t h = blockIdx.y;
    if (t >= n_tokens || h >= n_head) return;
    const bool single_all = (n_tokens == 1u && ratio == 0u);
    uint32_t qpos = pos0 + t;
    uint32_t first_raw_pos = pos0 + n_tokens - n_raw;
    uint32_t visible_comp = single_all ? n_comp : (n_comp ? (qpos + 1u) / ratio : 0u);
    if (visible_comp > n_comp) visible_comp = n_comp;
    const float *qh = q + ((uint64_t)t * n_head + h) * head_dim;
    __shared__ float scores[DS4_CUDA_ATTENTION_SCORE_CAP];
    __shared__ uint32_t raw_rows[256];
    __shared__ float partial[256];
    __shared__ float max_s;
    __shared__ float denom;
    __shared__ uint32_t raw_count;
    __shared__ uint32_t raw_first_idx;
    float scale = rsqrtf((float)head_dim);
    if (threadIdx.x == 0) {
        raw_count = 0;
        raw_first_idx = 0;
        if (n_raw != 0) {
            const uint32_t raw_last_pos = first_raw_pos + n_raw - 1u;
            if (single_all) {
                raw_count = n_raw > 256u ? 256u : n_raw;
            } else if (qpos >= first_raw_pos) {
                uint32_t lo = first_raw_pos;
                if (window != 0 && qpos + 1u > window) {
                    const uint32_t wlo = qpos + 1u - window;
                    if (wlo > lo) lo = wlo;
                }
                const uint32_t hi = qpos < raw_last_pos ? qpos : raw_last_pos;
                if (hi >= lo) {
                    raw_first_idx = lo - first_raw_pos;
                    raw_count = hi - lo + 1u;
                    if (raw_count > 256u) raw_count = 256u;
                }
            }
        }
    }
    __syncthreads();
    for (uint32_t r = threadIdx.x; r < raw_count; r += blockDim.x) {
        raw_rows[r] = (raw_start + raw_first_idx + r) % raw_cap;
    }
    __syncthreads();
    uint32_t n_score = raw_count + visible_comp;
    float local_max = sinks[h];
    if (visible_comp == 0 || n_tokens == 1u) {
        for (uint32_t r = threadIdx.x; r < raw_count; r += blockDim.x) {
            const float *kvrow = raw_kv + (uint64_t)raw_rows[r] * head_dim;
            float dot = 0.0f;
            for (uint32_t d = 0; d < head_dim; d++) dot += qh[d] * kvrow[d];
            scores[r] = dot * scale;
            local_max = fmaxf(local_max, scores[r]);
        }
        for (uint32_t c = threadIdx.x; c < visible_comp; c += blockDim.x) {
            float add = use_comp_mask ? comp_mask[(uint64_t)t * n_comp + c] : 0.0f;
            float s = -INFINITY;
            if (add > -1.0e20f) {
                const float *kvrow = comp_kv + (uint64_t)c * head_dim;
                float dot = 0.0f;
                for (uint32_t d = 0; d < head_dim; d++) dot += qh[d] * kvrow[d];
                s = dot * scale + add;
            }
            scores[raw_count + c] = s;
            local_max = fmaxf(local_max, s);
        }
    } else {
        uint32_t qlane = threadIdx.x & 7u;
        uint32_t qgroup = threadIdx.x >> 3u;
        for (uint32_t row0 = 0; row0 < n_score; row0 += 32u) {
            uint32_t row = row0 + qgroup;
            if (row < n_score) {
                float add = 0.0f;
                const float *kvrow = NULL;
                if (row < raw_count) {
                    kvrow = raw_kv + (uint64_t)raw_rows[row] * head_dim;
                } else {
                    uint32_t c = row - raw_count;
                    add = use_comp_mask ? comp_mask[(uint64_t)t * n_comp + c] : 0.0f;
                    if (add > -1.0e20f) kvrow = comp_kv + (uint64_t)c * head_dim;
                }
                float s = -INFINITY;
                if (kvrow) {
                    float dot = 0.0f;
                    for (uint32_t d = qlane; d < head_dim; d += 8u) dot += qh[d] * kvrow[d];
                    const uint32_t mask = 0xffu << (threadIdx.x & 24u);
                    for (uint32_t off = 4u; off > 0u; off >>= 1u) {
                        dot += __shfl_down_sync(mask, dot, off, 8);
                    }
                    s = dot * scale + add;
                }
                if (qlane == 0) scores[row] = s;
            }
        }
        __syncthreads();
        for (uint32_t i = threadIdx.x; i < n_score; i += blockDim.x) {
            local_max = fmaxf(local_max, scores[i]);
        }
    }
    partial[threadIdx.x] = local_max;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] = fmaxf(partial[threadIdx.x], partial[threadIdx.x + stride]);
        __syncthreads();
    }
    if (threadIdx.x == 0) max_s = partial[0];
    __syncthreads();
    float den_local = 0.0f;
    for (uint32_t i = threadIdx.x; i < n_score; i += blockDim.x) {
        scores[i] = expf(scores[i] - max_s);
        den_local += scores[i];
    }
    partial[threadIdx.x] = den_local;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] += partial[threadIdx.x + stride];
        __syncthreads();
    }
    if (threadIdx.x == 0) denom = partial[0] + expf(sinks[h] - max_s);
    __syncthreads();
    float *oh = heads + ((uint64_t)t * n_head + h) * head_dim;
    if (head_dim == 512u && blockDim.x == 256u) {
        uint32_t d0 = threadIdx.x;
        uint32_t d1 = d0 + 256u;
        float acc0 = 0.0f;
        float acc1 = 0.0f;
        for (uint32_t r = 0; r < raw_count; r++) {
            float s = scores[r];
            const float *kv = raw_kv + (uint64_t)raw_rows[r] * head_dim;
            acc0 += kv[d0] * s;
            acc1 += kv[d1] * s;
        }
        for (uint32_t c = 0; c < visible_comp; c++) {
            float s = scores[raw_count + c];
            const float *kv = comp_kv + (uint64_t)c * head_dim;
            acc0 += kv[d0] * s;
            acc1 += kv[d1] * s;
        }
        oh[d0] = acc0 / denom;
        oh[d1] = acc1 / denom;
    } else {
        for (uint32_t d = threadIdx.x; d < head_dim; d += blockDim.x) {
            float acc = 0.0f;
            for (uint32_t r = 0; r < raw_count; r++) acc += raw_kv[(uint64_t)raw_rows[r] * head_dim + d] * scores[r];
            for (uint32_t c = 0; c < visible_comp; c++) acc += comp_kv[(uint64_t)c * head_dim + d] * scores[raw_count + c];
            oh[d] = acc / denom;
        }
    }
}

__global__ static void attention_indexed_mixed_kernel(
        float *heads,
        const float *sinks,
        const float *q,
        const float *raw_kv,
        const float *comp_kv,
        const int32_t *topk,
        uint32_t n_tokens,
        uint32_t pos0,
        uint32_t n_raw,
        uint32_t raw_cap,
        uint32_t raw_start,
        uint32_t n_comp,
        uint32_t top_k,
        uint32_t window,
        uint32_t ratio,
        uint32_t n_head,
        uint32_t head_dim) {
    uint32_t t = blockIdx.x;
    uint32_t h = blockIdx.y;
    if (t >= n_tokens || h >= n_head) return;
    uint32_t qpos = pos0 + t;
    uint32_t first_raw_pos = pos0 + n_tokens - n_raw;
    uint32_t visible_comp = n_comp;
    if (ratio != 0) {
        visible_comp = (qpos + 1u) / ratio;
        if (visible_comp > n_comp) visible_comp = n_comp;
    }
    const float *qh = q + ((uint64_t)t * n_head + h) * head_dim;
    __shared__ float scores[768];
    __shared__ uint32_t raw_rows[256];
    __shared__ uint32_t comp_rows[512];
    __shared__ float partial[256];
    __shared__ float max_s;
    __shared__ float denom;
    __shared__ uint32_t raw_count;
    __shared__ uint32_t raw_first_idx;
    __shared__ uint32_t comp_count;
    float scale = rsqrtf((float)head_dim);
    if (threadIdx.x == 0) {
        raw_count = 0;
        raw_first_idx = 0;
        comp_count = 0;
        if (n_raw != 0) {
            const uint32_t raw_last_pos = first_raw_pos + n_raw - 1u;
            if (qpos >= first_raw_pos) {
                uint32_t lo = first_raw_pos;
                if (window != 0 && qpos + 1u > window) {
                    const uint32_t wlo = qpos + 1u - window;
                    if (wlo > lo) lo = wlo;
                }
                const uint32_t hi = qpos < raw_last_pos ? qpos : raw_last_pos;
                if (hi >= lo) {
                    raw_first_idx = lo - first_raw_pos;
                    raw_count = hi - lo + 1u;
                    if (raw_count > 256u) raw_count = 256u;
                }
            }
        }
    }
    __syncthreads();
    for (uint32_t r = threadIdx.x; r < raw_count; r += blockDim.x) {
        raw_rows[r] = (raw_start + raw_first_idx + r) % raw_cap;
    }
    for (uint32_t i = threadIdx.x; i < top_k; i += blockDim.x) {
        int32_t c = topk[(uint64_t)t * top_k + i];
        if (c >= 0 && (uint32_t)c < visible_comp) {
            uint32_t slot = atomicAdd(&comp_count, 1u);
            if (slot < 512u) comp_rows[slot] = (uint32_t)c;
        }
    }
    __syncthreads();
    if (threadIdx.x == 0) {
        if (comp_count > 512u) comp_count = 512u;
    }
    __syncthreads();
    uint32_t n_score = raw_count + comp_count;
    float local_max = sinks[h];
    if (comp_count == 0) {
        for (uint32_t r = threadIdx.x; r < raw_count; r += blockDim.x) {
            const float *kvrow = raw_kv + (uint64_t)raw_rows[r] * head_dim;
            float dot = 0.0f;
            for (uint32_t d = 0; d < head_dim; d++) dot += qh[d] * kvrow[d];
            scores[r] = dot * scale;
            local_max = fmaxf(local_max, scores[r]);
        }
    } else {
        uint32_t qlane = threadIdx.x & 7u;
        uint32_t qgroup = threadIdx.x >> 3u;
        for (uint32_t row0 = 0; row0 < n_score; row0 += 32u) {
            uint32_t row = row0 + qgroup;
            if (row < n_score) {
                const float *kvrow = row < raw_count
                    ? raw_kv + (uint64_t)raw_rows[row] * head_dim
                    : comp_kv + (uint64_t)comp_rows[row - raw_count] * head_dim;
                float dot = 0.0f;
                for (uint32_t d = qlane; d < head_dim; d += 8u) dot += qh[d] * kvrow[d];
                const uint32_t mask = 0xffu << (threadIdx.x & 24u);
                for (uint32_t off = 4u; off > 0u; off >>= 1u) {
                    dot += __shfl_down_sync(mask, dot, off, 8);
                }
                if (qlane == 0) scores[row] = dot * scale;
            }
        }
        __syncthreads();
        for (uint32_t i = threadIdx.x; i < n_score; i += blockDim.x) {
            local_max = fmaxf(local_max, scores[i]);
        }
    }
    partial[threadIdx.x] = local_max;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] = fmaxf(partial[threadIdx.x], partial[threadIdx.x + stride]);
        __syncthreads();
    }
    if (threadIdx.x == 0) max_s = partial[0];
    __syncthreads();
    float den_local = 0.0f;
    for (uint32_t i = threadIdx.x; i < n_score; i += blockDim.x) {
        scores[i] = expf(scores[i] - max_s);
        den_local += scores[i];
    }
    partial[threadIdx.x] = den_local;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] += partial[threadIdx.x + stride];
        __syncthreads();
    }
    if (threadIdx.x == 0) denom = partial[0] + expf(sinks[h] - max_s);
    __syncthreads();
    float *oh = heads + ((uint64_t)t * n_head + h) * head_dim;
    if (head_dim == 512u && blockDim.x == 256u) {
        uint32_t d0 = threadIdx.x;
        uint32_t d1 = d0 + 256u;
        float acc0 = 0.0f;
        float acc1 = 0.0f;
        for (uint32_t r = 0; r < raw_count; r++) {
            float s = scores[r];
            const float *kv = raw_kv + (uint64_t)raw_rows[r] * head_dim;
            acc0 += kv[d0] * s;
            acc1 += kv[d1] * s;
        }
        for (uint32_t c = 0; c < comp_count; c++) {
            float s = scores[raw_count + c];
            const float *kv = comp_kv + (uint64_t)comp_rows[c] * head_dim;
            acc0 += kv[d0] * s;
            acc1 += kv[d1] * s;
        }
        oh[d0] = acc0 / denom;
        oh[d1] = acc1 / denom;
    } else {
        for (uint32_t d = threadIdx.x; d < head_dim; d += blockDim.x) {
            float acc = 0.0f;
            for (uint32_t r = 0; r < raw_count; r++) acc += raw_kv[(uint64_t)raw_rows[r] * head_dim + d] * scores[r];
            for (uint32_t s = 0; s < comp_count; s++) acc += comp_kv[(uint64_t)comp_rows[s] * head_dim + d] * scores[raw_count + s];
            oh[d] = acc / denom;
        }
    }
}

__global__ static void attention_indexed_mixed_heads8_rb4_kernel(
        float *heads,
        const float *sinks,
        const float *q,
        const float *raw_kv,
        const float *comp_kv,
        const int32_t *topk,
        uint32_t n_tokens,
        uint32_t pos0,
        uint32_t n_raw,
        uint32_t raw_cap,
        uint32_t raw_start,
        uint32_t n_comp,
        uint32_t top_k,
        uint32_t window,
        uint32_t ratio,
        uint32_t n_head,
        uint32_t head_dim) {
    uint32_t t = blockIdx.x;
    uint32_t head_group = blockIdx.y;
    if (t >= n_tokens || head_dim != 512u) return;
    const uint32_t lane = threadIdx.x & 31u;
    const uint32_t warp = threadIdx.x >> 5u;
    const uint32_t head = head_group * 8u + warp;
    const bool valid_head = head < n_head;

    __shared__ uint32_t raw_rows[256];
    __shared__ uint32_t comp_rows[512];
    __shared__ uint32_t raw_count;
    __shared__ uint32_t raw_first_idx;
    __shared__ uint32_t comp_count;
    __shared__ float4 kv_shared[4 * 128];
    __shared__ float scores[8 * 768];

    uint32_t qpos = pos0 + t;
    uint32_t first_raw_pos = pos0 + n_tokens - n_raw;
    uint32_t visible_comp = n_comp;
    if (ratio != 0) {
        visible_comp = (qpos + 1u) / ratio;
        if (visible_comp > n_comp) visible_comp = n_comp;
    }

    if (threadIdx.x == 0) {
        raw_count = 0;
        raw_first_idx = 0;
        comp_count = 0;
        if (n_raw != 0) {
            const uint32_t raw_last_pos = first_raw_pos + n_raw - 1u;
            if (qpos >= first_raw_pos) {
                uint32_t lo = first_raw_pos;
                if (window != 0 && qpos + 1u > window) {
                    const uint32_t wlo = qpos + 1u - window;
                    if (wlo > lo) lo = wlo;
                }
                const uint32_t hi = qpos < raw_last_pos ? qpos : raw_last_pos;
                if (hi >= lo) {
                    raw_first_idx = lo - first_raw_pos;
                    raw_count = hi - lo + 1u;
                    if (raw_count > 256u) raw_count = 256u;
                }
            }
        }
    }
    __syncthreads();
    for (uint32_t r = threadIdx.x; r < raw_count; r += blockDim.x) {
        raw_rows[r] = (raw_start + raw_first_idx + r) % raw_cap;
    }
    if (threadIdx.x == 0) {
        for (uint32_t i = 0; i < top_k && comp_count < 512u; i++) {
            int32_t c = topk[(uint64_t)t * top_k + i];
            if (c >= 0 && (uint32_t)c < visible_comp) comp_rows[comp_count++] = (uint32_t)c;
        }
    }
    __syncthreads();

    const uint32_t n_score = raw_count + comp_count;
    const float scale = rsqrtf((float)head_dim);
    const float4 *q4 = valid_head
        ? (const float4 *)(q + ((uint64_t)t * n_head + head) * head_dim)
        : NULL;
    float4 q0 = make_float4(0.0f, 0.0f, 0.0f, 0.0f);
    float4 q1 = q0, q2 = q0, q3 = q0;
    if (valid_head) {
        q0 = q4[lane +  0u];
        q1 = q4[lane + 32u];
        q2 = q4[lane + 64u];
        q3 = q4[lane + 96u];
    }

    for (uint32_t row0 = 0; row0 < n_score; row0 += 4u) {
        const uint32_t nr = n_score - row0 < 4u ? n_score - row0 : 4u;
        for (uint32_t off = threadIdx.x; off < nr * 128u; off += blockDim.x) {
            const uint32_t rr = off >> 7u;
            const uint32_t c4 = off & 127u;
            const uint32_t sr = row0 + rr;
            const float4 *src = sr < raw_count
                ? (const float4 *)(raw_kv + (uint64_t)raw_rows[sr] * head_dim)
                : (const float4 *)(comp_kv + (uint64_t)comp_rows[sr - raw_count] * head_dim);
            kv_shared[off] = src[c4];
        }
        __syncthreads();
        if (valid_head) {
            for (uint32_t rr = 0; rr < nr; rr++) {
                const float4 *kv4 = kv_shared + rr * 128u;
                float dot = dot4_f32(q0, kv4[lane +  0u]) +
                            dot4_f32(q1, kv4[lane + 32u]) +
                            dot4_f32(q2, kv4[lane + 64u]) +
                            dot4_f32(q3, kv4[lane + 96u]);
                dot = warp_sum_f32(dot);
                if (lane == 0) scores[warp * 768u + row0 + rr] = dot * scale;
            }
        }
        __syncthreads();
    }

    float max_s = valid_head ? sinks[head] : -INFINITY;
    if (valid_head) {
        const float *score_row = scores + warp * 768u;
        for (uint32_t i = lane; i < n_score; i += 32u) max_s = fmaxf(max_s, score_row[i]);
        max_s = warp_max_f32(max_s);
        max_s = __shfl_sync(0xffffffffu, max_s, 0);
    }
    float den = 0.0f;
    if (valid_head) {
        float *score_row = scores + warp * 768u;
        for (uint32_t i = lane; i < n_score; i += 32u) {
            float p = expf(score_row[i] - max_s);
            score_row[i] = p;
            den += p;
        }
        den = warp_sum_f32(den);
        den += expf(sinks[head] - max_s);
        den = __shfl_sync(0xffffffffu, den, 0);
    }

    float4 o0 = make_float4(0.0f, 0.0f, 0.0f, 0.0f);
    float4 o1 = o0, o2 = o0, o3 = o0;
    for (uint32_t row0 = 0; row0 < n_score; row0 += 4u) {
        const uint32_t nr = n_score - row0 < 4u ? n_score - row0 : 4u;
        for (uint32_t off = threadIdx.x; off < nr * 128u; off += blockDim.x) {
            const uint32_t rr = off >> 7u;
            const uint32_t c4 = off & 127u;
            const uint32_t sr = row0 + rr;
            const float4 *src = sr < raw_count
                ? (const float4 *)(raw_kv + (uint64_t)raw_rows[sr] * head_dim)
                : (const float4 *)(comp_kv + (uint64_t)comp_rows[sr - raw_count] * head_dim);
            kv_shared[off] = src[c4];
        }
        __syncthreads();
        if (valid_head) {
            const float *score_row = scores + warp * 768u;
            for (uint32_t rr = 0; rr < nr; rr++) {
                const float p = den == 0.0f ? 0.0f : score_row[row0 + rr] / den;
                const float4 *kv4 = kv_shared + rr * 128u;
                float4 k0 = kv4[lane +  0u];
                float4 k1 = kv4[lane + 32u];
                float4 k2 = kv4[lane + 64u];
                float4 k3 = kv4[lane + 96u];
                o0.x += k0.x * p; o0.y += k0.y * p; o0.z += k0.z * p; o0.w += k0.w * p;
                o1.x += k1.x * p; o1.y += k1.y * p; o1.z += k1.z * p; o1.w += k1.w * p;
                o2.x += k2.x * p; o2.y += k2.y * p; o2.z += k2.z * p; o2.w += k2.w * p;
                o3.x += k3.x * p; o3.y += k3.y * p; o3.z += k3.z * p; o3.w += k3.w * p;
            }
        }
        __syncthreads();
    }
    if (valid_head) {
        float4 *out4 = (float4 *)(heads + ((uint64_t)t * n_head + head) * head_dim);
        out4[lane +  0u] = o0;
        out4[lane + 32u] = o1;
        out4[lane + 64u] = o2;
        out4[lane + 96u] = o3;
    }
}

template <uint32_t ROWS_PER_STAGE, uint32_t HEADS_PER_GROUP>
__global__ static void attention_indexed_mixed_heads8_online_kernel(
        float *heads,
        const float *sinks,
        const float *q,
        const float *raw_kv,
        const float *comp_kv,
        const int32_t *topk,
        uint32_t n_tokens,
        uint32_t pos0,
        uint32_t n_raw,
        uint32_t raw_cap,
        uint32_t raw_start,
        uint32_t n_comp,
        uint32_t top_k,
        uint32_t window,
        uint32_t ratio,
        uint32_t n_head,
        uint32_t head_dim) {
    uint32_t t = blockIdx.x;
    uint32_t head_group = blockIdx.y;
    if (t >= n_tokens || head_dim != 512u) return;
    const uint32_t lane = threadIdx.x & 31u;
    const uint32_t warp = threadIdx.x >> 5u;
    const uint32_t head = head_group * HEADS_PER_GROUP + warp;
    const bool valid_head = head < n_head;

    __shared__ uint32_t raw_rows[256];
    __shared__ uint32_t raw_count;
    __shared__ uint32_t raw_first_idx;
    __shared__ float4 kv_shared[ROWS_PER_STAGE * 128];

    uint32_t qpos = pos0 + t;
    uint32_t first_raw_pos = pos0 + n_tokens - n_raw;
    uint32_t visible_comp = n_comp;
    if (ratio != 0) {
        visible_comp = (qpos + 1u) / ratio;
        if (visible_comp > n_comp) visible_comp = n_comp;
    }

    if (threadIdx.x == 0) {
        raw_count = 0;
        raw_first_idx = 0;
        if (n_raw != 0) {
            const uint32_t raw_last_pos = first_raw_pos + n_raw - 1u;
            if (qpos >= first_raw_pos) {
                uint32_t lo = first_raw_pos;
                if (window != 0 && qpos + 1u > window) {
                    const uint32_t wlo = qpos + 1u - window;
                    if (wlo > lo) lo = wlo;
                }
                const uint32_t hi = qpos < raw_last_pos ? qpos : raw_last_pos;
                if (hi >= lo) {
                    raw_first_idx = lo - first_raw_pos;
                    raw_count = hi - lo + 1u;
                    if (raw_count > 256u) raw_count = 256u;
                }
            }
        }
    }
    __syncthreads();
    for (uint32_t r = threadIdx.x; r < raw_count; r += blockDim.x) {
        raw_rows[r] = (raw_start + raw_first_idx + r) % raw_cap;
    }
    __syncthreads();

    uint32_t comp_count = top_k < visible_comp ? top_k : visible_comp;
    if (comp_count > 512u) comp_count = 512u;
    const uint32_t n_score = raw_count + comp_count;
    const float scale = rsqrtf((float)head_dim);
    const float4 *q4 = valid_head
        ? (const float4 *)(q + ((uint64_t)t * n_head + head) * head_dim)
        : NULL;
    float4 q0 = make_float4(0.0f, 0.0f, 0.0f, 0.0f);
    float4 q1 = q0, q2 = q0, q3 = q0;
    if (valid_head) {
        q0 = q4[lane +  0u];
        q1 = q4[lane + 32u];
        q2 = q4[lane + 64u];
        q3 = q4[lane + 96u];
    }

    float max_s = -INFINITY;
    float sum_s = 0.0f;
    float4 o0 = make_float4(0.0f, 0.0f, 0.0f, 0.0f);
    float4 o1 = o0, o2 = o0, o3 = o0;

    for (uint32_t row0 = 0; row0 < n_score; row0 += ROWS_PER_STAGE) {
        const uint32_t nr = n_score - row0 < ROWS_PER_STAGE ? n_score - row0 : ROWS_PER_STAGE;
        for (uint32_t off = threadIdx.x; off < nr * 128u; off += blockDim.x) {
            const uint32_t rr = off >> 7u;
            const uint32_t c4 = off & 127u;
            const uint32_t sr = row0 + rr;
            const uint32_t comp_idx = sr < raw_count
                ? 0u
                : (uint32_t)topk[(uint64_t)t * top_k + (sr - raw_count)];
            const float4 *src = sr < raw_count
                ? (const float4 *)(raw_kv + (uint64_t)raw_rows[sr] * head_dim)
                : (const float4 *)(comp_kv + (uint64_t)comp_idx * head_dim);
            kv_shared[off] = src[c4];
        }
        __syncthreads();
        if (valid_head) {
            for (uint32_t rr = 0; rr < nr; rr++) {
                const float4 *kv4 = kv_shared + rr * 128u;
                float4 k0 = kv4[lane +  0u];
                float4 k1 = kv4[lane + 32u];
                float4 k2 = kv4[lane + 64u];
                float4 k3 = kv4[lane + 96u];
                float score = dot4_f32(q0, k0) +
                              dot4_f32(q1, k1) +
                              dot4_f32(q2, k2) +
                              dot4_f32(q3, k3);
                score = warp_sum_f32(score) * scale;
                score = __shfl_sync(0xffffffffu, score, 0);

                const float new_m = fmaxf(max_s, score);
                const float old_scale = expf(max_s - new_m);
                const float row_scale = expf(score - new_m);
                sum_s = sum_s * old_scale + row_scale;
                o0.x = o0.x * old_scale + k0.x * row_scale;
                o0.y = o0.y * old_scale + k0.y * row_scale;
                o0.z = o0.z * old_scale + k0.z * row_scale;
                o0.w = o0.w * old_scale + k0.w * row_scale;
                o1.x = o1.x * old_scale + k1.x * row_scale;
                o1.y = o1.y * old_scale + k1.y * row_scale;
                o1.z = o1.z * old_scale + k1.z * row_scale;
                o1.w = o1.w * old_scale + k1.w * row_scale;
                o2.x = o2.x * old_scale + k2.x * row_scale;
                o2.y = o2.y * old_scale + k2.y * row_scale;
                o2.z = o2.z * old_scale + k2.z * row_scale;
                o2.w = o2.w * old_scale + k2.w * row_scale;
                o3.x = o3.x * old_scale + k3.x * row_scale;
                o3.y = o3.y * old_scale + k3.y * row_scale;
                o3.z = o3.z * old_scale + k3.z * row_scale;
                o3.w = o3.w * old_scale + k3.w * row_scale;
                max_s = new_m;
            }
        }
        __syncthreads();
    }

    if (valid_head) {
        const float sink = sinks[head];
        const float new_m = fmaxf(max_s, sink);
        const float old_scale = expf(max_s - new_m);
        const float sink_scale = expf(sink - new_m);
        sum_s = sum_s * old_scale + sink_scale;
        o0.x *= old_scale; o0.y *= old_scale; o0.z *= old_scale; o0.w *= old_scale;
        o1.x *= old_scale; o1.y *= old_scale; o1.z *= old_scale; o1.w *= old_scale;
        o2.x *= old_scale; o2.y *= old_scale; o2.z *= old_scale; o2.w *= old_scale;
        o3.x *= old_scale; o3.y *= old_scale; o3.z *= old_scale; o3.w *= old_scale;

        const float inv_s = sum_s == 0.0f ? 0.0f : 1.0f / sum_s;
        o0.x *= inv_s; o0.y *= inv_s; o0.z *= inv_s; o0.w *= inv_s;
        o1.x *= inv_s; o1.y *= inv_s; o1.z *= inv_s; o1.w *= inv_s;
        o2.x *= inv_s; o2.y *= inv_s; o2.z *= inv_s; o2.w *= inv_s;
        o3.x *= inv_s; o3.y *= inv_s; o3.z *= inv_s; o3.w *= inv_s;
        float4 *out4 = (float4 *)(heads + ((uint64_t)t * n_head + head) * head_dim);
        out4[lane +  0u] = o0;
        out4[lane + 32u] = o1;
        out4[lane + 64u] = o2;
        out4[lane + 96u] = o3;
    }
}

__global__ static void attention_static_mixed_heads8_online_kernel(
        float *heads,
        const float *sinks,
        const float *q,
        const float *raw_kv,
        const float *comp_kv,
        uint32_t n_tokens,
        uint32_t n_comp,
        uint32_t window,
        uint32_t ratio,
        uint32_t n_head,
        uint32_t head_dim) {
    uint32_t t = blockIdx.x;
    uint32_t head_group = blockIdx.y;
    if (t >= n_tokens || head_dim != 512u) return;
    const uint32_t lane = threadIdx.x & 31u;
    const uint32_t warp = threadIdx.x >> 5u;
    const uint32_t head = head_group * 8u + warp;
    const bool valid_head = head < n_head;

    __shared__ float4 kv_shared[4 * 128];

    const uint32_t raw_count = window != 0u && t + 1u > window ? window : t + 1u;
    const uint32_t raw_start = t + 1u - raw_count;
    uint32_t comp_count = 0;
    if (n_comp != 0u && ratio != 0u) {
        comp_count = (t + 1u) / ratio;
        if (comp_count > n_comp) comp_count = n_comp;
    }
    const uint32_t n_score = raw_count + comp_count;
    const float scale = rsqrtf((float)head_dim);
    const float4 *q4 = valid_head
        ? (const float4 *)(q + ((uint64_t)t * n_head + head) * head_dim)
        : NULL;
    float4 q0 = make_float4(0.0f, 0.0f, 0.0f, 0.0f);
    float4 q1 = q0, q2 = q0, q3 = q0;
    if (valid_head) {
        q0 = q4[lane +  0u];
        q1 = q4[lane + 32u];
        q2 = q4[lane + 64u];
        q3 = q4[lane + 96u];
    }

    float max_s = -INFINITY;
    float sum_s = 0.0f;
    float4 o0 = make_float4(0.0f, 0.0f, 0.0f, 0.0f);
    float4 o1 = o0, o2 = o0, o3 = o0;

    for (uint32_t row0 = 0; row0 < n_score; row0 += 4u) {
        const uint32_t nr = n_score - row0 < 4u ? n_score - row0 : 4u;
        for (uint32_t off = threadIdx.x; off < nr * 128u; off += blockDim.x) {
            const uint32_t rr = off >> 7u;
            const uint32_t c4 = off & 127u;
            const uint32_t sr = row0 + rr;
            const float4 *src = sr < raw_count
                ? (const float4 *)(raw_kv + (uint64_t)(raw_start + sr) * head_dim)
                : (const float4 *)(comp_kv + (uint64_t)(sr - raw_count) * head_dim);
            kv_shared[off] = src[c4];
        }
        __syncthreads();
        if (valid_head) {
            for (uint32_t rr = 0; rr < nr; rr++) {
                const float4 *kv4 = kv_shared + rr * 128u;
                float4 k0 = kv4[lane +  0u];
                float4 k1 = kv4[lane + 32u];
                float4 k2 = kv4[lane + 64u];
                float4 k3 = kv4[lane + 96u];
                float score = dot4_f32(q0, k0) +
                              dot4_f32(q1, k1) +
                              dot4_f32(q2, k2) +
                              dot4_f32(q3, k3);
                score = warp_sum_f32(score) * scale;
                score = __shfl_sync(0xffffffffu, score, 0);

                const float new_m = fmaxf(max_s, score);
                const float old_scale = expf(max_s - new_m);
                const float row_scale = expf(score - new_m);
                sum_s = sum_s * old_scale + row_scale;
                o0.x = o0.x * old_scale + k0.x * row_scale;
                o0.y = o0.y * old_scale + k0.y * row_scale;
                o0.z = o0.z * old_scale + k0.z * row_scale;
                o0.w = o0.w * old_scale + k0.w * row_scale;
                o1.x = o1.x * old_scale + k1.x * row_scale;
                o1.y = o1.y * old_scale + k1.y * row_scale;
                o1.z = o1.z * old_scale + k1.z * row_scale;
                o1.w = o1.w * old_scale + k1.w * row_scale;
                o2.x = o2.x * old_scale + k2.x * row_scale;
                o2.y = o2.y * old_scale + k2.y * row_scale;
                o2.z = o2.z * old_scale + k2.z * row_scale;
                o2.w = o2.w * old_scale + k2.w * row_scale;
                o3.x = o3.x * old_scale + k3.x * row_scale;
                o3.y = o3.y * old_scale + k3.y * row_scale;
                o3.z = o3.z * old_scale + k3.z * row_scale;
                o3.w = o3.w * old_scale + k3.w * row_scale;
                max_s = new_m;
            }
        }
        __syncthreads();
    }

    if (valid_head) {
        const float sink = sinks[head];
        const float new_m = fmaxf(max_s, sink);
        const float old_scale = expf(max_s - new_m);
        const float sink_scale = expf(sink - new_m);
        sum_s = sum_s * old_scale + sink_scale;
        o0.x *= old_scale; o0.y *= old_scale; o0.z *= old_scale; o0.w *= old_scale;
        o1.x *= old_scale; o1.y *= old_scale; o1.z *= old_scale; o1.w *= old_scale;
        o2.x *= old_scale; o2.y *= old_scale; o2.z *= old_scale; o2.w *= old_scale;
        o3.x *= old_scale; o3.y *= old_scale; o3.z *= old_scale; o3.w *= old_scale;

        const float inv_s = sum_s == 0.0f ? 0.0f : 1.0f / sum_s;
        o0.x *= inv_s; o0.y *= inv_s; o0.z *= inv_s; o0.w *= inv_s;
        o1.x *= inv_s; o1.y *= inv_s; o1.z *= inv_s; o1.w *= inv_s;
        o2.x *= inv_s; o2.y *= inv_s; o2.z *= inv_s; o2.w *= inv_s;
        o3.x *= inv_s; o3.y *= inv_s; o3.z *= inv_s; o3.w *= inv_s;
        float4 *out4 = (float4 *)(heads + ((uint64_t)t * n_head + head) * head_dim);
        out4[lane +  0u] = o0;
        out4[lane + 32u] = o1;
        out4[lane + 64u] = o2;
        out4[lane + 96u] = o3;
    }
}

__global__ static void attention_decode_mixed_heads8_online_kernel(
        float *heads,
        const float *sinks,
        const float *q,
        const float *raw_kv,
        const float *comp_kv,
        uint32_t n_tokens,
        uint32_t pos0,
        uint32_t n_raw,
        uint32_t raw_cap,
        uint32_t raw_start,
        uint32_t n_comp,
        uint32_t window,
        uint32_t ratio,
        uint32_t n_head,
        uint32_t head_dim) {
    uint32_t t = blockIdx.x;
    uint32_t head_group = blockIdx.y;
    if (t >= n_tokens || head_dim != 512u) return;
    const uint32_t lane = threadIdx.x & 31u;
    const uint32_t warp = threadIdx.x >> 5u;
    const uint32_t head = head_group * 8u + warp;
    const bool valid_head = head < n_head;

    __shared__ uint32_t raw_rows[256];
    __shared__ uint32_t raw_count_s;
    __shared__ uint32_t raw_first_idx_s;
    __shared__ float4 kv_shared[4 * 128];

    const uint32_t qpos = pos0 + t;
    const uint32_t first_raw_pos = pos0 + n_tokens - n_raw;
    uint32_t comp_count = 0;
    if (n_comp != 0u) {
        if (n_tokens == 1u && ratio == 0u) {
            comp_count = n_comp;
        } else if (ratio != 0u) {
            comp_count = (qpos + 1u) / ratio;
            if (comp_count > n_comp) comp_count = n_comp;
        }
    }
    if (threadIdx.x == 0) {
        uint32_t raw_count = 0;
        uint32_t raw_first_idx = 0;
        if (n_raw != 0u) {
            const uint32_t raw_last_pos = first_raw_pos + n_raw - 1u;
            if (qpos >= first_raw_pos) {
                uint32_t lo = first_raw_pos;
                if (window != 0u && qpos + 1u > window) {
                    const uint32_t wlo = qpos + 1u - window;
                    if (wlo > lo) lo = wlo;
                }
                const uint32_t hi = qpos < raw_last_pos ? qpos : raw_last_pos;
                if (hi >= lo) {
                    raw_first_idx = lo - first_raw_pos;
                    raw_count = hi - lo + 1u;
                    if (raw_count > 256u) raw_count = 256u;
                }
            }
        }
        raw_count_s = raw_count;
        raw_first_idx_s = raw_first_idx;
    }
    __syncthreads();
    const uint32_t raw_count = raw_count_s;
    const uint32_t raw_first_idx = raw_first_idx_s;
    for (uint32_t r = threadIdx.x; r < raw_count; r += blockDim.x) {
        raw_rows[r] = (raw_start + raw_first_idx + r) % raw_cap;
    }
    __syncthreads();

    const uint32_t n_score = raw_count + comp_count;
    const float scale = rsqrtf((float)head_dim);
    const float4 *q4 = valid_head
        ? (const float4 *)(q + ((uint64_t)t * n_head + head) * head_dim)
        : NULL;
    float4 q0 = make_float4(0.0f, 0.0f, 0.0f, 0.0f);
    float4 q1 = q0, q2 = q0, q3 = q0;
    if (valid_head) {
        q0 = q4[lane +  0u];
        q1 = q4[lane + 32u];
        q2 = q4[lane + 64u];
        q3 = q4[lane + 96u];
    }

    float max_s = -INFINITY;
    float sum_s = 0.0f;
    float4 o0 = make_float4(0.0f, 0.0f, 0.0f, 0.0f);
    float4 o1 = o0, o2 = o0, o3 = o0;

    for (uint32_t row0 = 0; row0 < n_score; row0 += 4u) {
        const uint32_t nr = n_score - row0 < 4u ? n_score - row0 : 4u;
        for (uint32_t off = threadIdx.x; off < nr * 128u; off += blockDim.x) {
            const uint32_t rr = off >> 7u;
            const uint32_t c4 = off & 127u;
            const uint32_t sr = row0 + rr;
            const float4 *src = sr < raw_count
                ? (const float4 *)(raw_kv + (uint64_t)raw_rows[sr] * head_dim)
                : (const float4 *)(comp_kv + (uint64_t)(sr - raw_count) * head_dim);
            kv_shared[off] = src[c4];
        }
        __syncthreads();
        if (valid_head) {
            for (uint32_t rr = 0; rr < nr; rr++) {
                const float4 *kv4 = kv_shared + rr * 128u;
                float4 k0 = kv4[lane +  0u];
                float4 k1 = kv4[lane + 32u];
                float4 k2 = kv4[lane + 64u];
                float4 k3 = kv4[lane + 96u];
                float score = dot4_f32(q0, k0) +
                              dot4_f32(q1, k1) +
                              dot4_f32(q2, k2) +
                              dot4_f32(q3, k3);
                score = warp_sum_f32(score) * scale;
                score = __shfl_sync(0xffffffffu, score, 0);

                const float new_m = fmaxf(max_s, score);
                const float old_scale = expf(max_s - new_m);
                const float row_scale = expf(score - new_m);
                sum_s = sum_s * old_scale + row_scale;
                o0.x = o0.x * old_scale + k0.x * row_scale;
                o0.y = o0.y * old_scale + k0.y * row_scale;
                o0.z = o0.z * old_scale + k0.z * row_scale;
                o0.w = o0.w * old_scale + k0.w * row_scale;
                o1.x = o1.x * old_scale + k1.x * row_scale;
                o1.y = o1.y * old_scale + k1.y * row_scale;
                o1.z = o1.z * old_scale + k1.z * row_scale;
                o1.w = o1.w * old_scale + k1.w * row_scale;
                o2.x = o2.x * old_scale + k2.x * row_scale;
                o2.y = o2.y * old_scale + k2.y * row_scale;
                o2.z = o2.z * old_scale + k2.z * row_scale;
                o2.w = o2.w * old_scale + k2.w * row_scale;
                o3.x = o3.x * old_scale + k3.x * row_scale;
                o3.y = o3.y * old_scale + k3.y * row_scale;
                o3.z = o3.z * old_scale + k3.z * row_scale;
                o3.w = o3.w * old_scale + k3.w * row_scale;
                max_s = new_m;
            }
        }
        __syncthreads();
    }

    if (valid_head) {
        const float sink = sinks[head];
        const float new_m = fmaxf(max_s, sink);
        const float old_scale = expf(max_s - new_m);
        const float sink_scale = expf(sink - new_m);
        sum_s = sum_s * old_scale + sink_scale;
        o0.x *= old_scale; o0.y *= old_scale; o0.z *= old_scale; o0.w *= old_scale;
        o1.x *= old_scale; o1.y *= old_scale; o1.z *= old_scale; o1.w *= old_scale;
        o2.x *= old_scale; o2.y *= old_scale; o2.z *= old_scale; o2.w *= old_scale;
        o3.x *= old_scale; o3.y *= old_scale; o3.z *= old_scale; o3.w *= old_scale;

        const float inv_s = sum_s == 0.0f ? 0.0f : 1.0f / sum_s;
        o0.x *= inv_s; o0.y *= inv_s; o0.z *= inv_s; o0.w *= inv_s;
        o1.x *= inv_s; o1.y *= inv_s; o1.z *= inv_s; o1.w *= inv_s;
        o2.x *= inv_s; o2.y *= inv_s; o2.z *= inv_s; o2.w *= inv_s;
        o3.x *= inv_s; o3.y *= inv_s; o3.z *= inv_s; o3.w *= inv_s;
        float4 *out4 = (float4 *)(heads + ((uint64_t)t * n_head + head) * head_dim);
        out4[lane +  0u] = o0;
        out4[lane + 32u] = o1;
        out4[lane + 64u] = o2;
        out4[lane + 96u] = o3;
    }
}

__device__ static void hc4_split_one(float *out, const float *mix, const float *scale, const float *base, uint32_t sinkhorn_iters, float epsv) {
    const float pre_scale = scale[0];
    const float post_scale = scale[1];
    const float comb_scale = scale[2];
    for (int i = 0; i < 4; i++) {
        float z = mix[i] * pre_scale + base[i];
        out[i] = 1.0f / (1.0f + expf(-z)) + epsv;
    }
    for (int i = 0; i < 4; i++) {
        float z = mix[4 + i] * post_scale + base[4 + i];
        out[4 + i] = 2.0f / (1.0f + expf(-z));
    }
    float c[16];
    for (int r = 0; r < 4; r++) {
        float m = -INFINITY;
        for (int col = 0; col < 4; col++) {
            float v = mix[8 + r * 4 + col] * comb_scale + base[8 + r * 4 + col];
            c[r * 4 + col] = v;
            m = fmaxf(m, v);
        }
        float s = 0.0f;
        for (int col = 0; col < 4; col++) {
            float v = expf(c[r * 4 + col] - m);
            c[r * 4 + col] = v;
            s += v;
        }
        for (int col = 0; col < 4; col++) c[r * 4 + col] = c[r * 4 + col] / s + epsv;
    }
    for (int col = 0; col < 4; col++) {
        float s = epsv;
        for (int r = 0; r < 4; r++) s += c[r * 4 + col];
        for (int r = 0; r < 4; r++) c[r * 4 + col] /= s;
    }
    for (uint32_t iter = 1; iter < sinkhorn_iters; iter++) {
        for (int r = 0; r < 4; r++) {
            float s = epsv;
            for (int col = 0; col < 4; col++) s += c[r * 4 + col];
            for (int col = 0; col < 4; col++) c[r * 4 + col] /= s;
        }
        for (int col = 0; col < 4; col++) {
            float s = epsv;
            for (int r = 0; r < 4; r++) s += c[r * 4 + col];
            for (int r = 0; r < 4; r++) c[r * 4 + col] /= s;
        }
    }
    for (int i = 0; i < 16; i++) out[8 + i] = c[i];
}

__global__ static void hc_split_sinkhorn_kernel(float *out, const float *mix, const float *scale, const float *base, uint32_t n_rows, uint32_t sinkhorn_iters, float epsv) {
    uint32_t row = blockIdx.x * blockDim.x + threadIdx.x;
    if (row >= n_rows) return;
    hc4_split_one(out + (uint64_t)row * 24, mix + (uint64_t)row * 24, scale, base, sinkhorn_iters, epsv);
}

__global__ static void hc_weighted_sum_kernel(float *out, const float *x, const float *w, uint32_t n_embd, uint32_t n_hc, uint32_t n_tokens, uint32_t weight_stride_f32) {
    uint64_t gid = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    uint64_t n = (uint64_t)n_embd * n_tokens;
    if (gid >= n) return;
    uint32_t d = gid % n_embd;
    uint32_t t = gid / n_embd;
    float acc = 0.0f;
    for (uint32_t h = 0; h < n_hc; h++) {
        acc += x[(uint64_t)t * n_hc * n_embd + (uint64_t)h * n_embd + d] *
               w[(uint64_t)t * weight_stride_f32 + h];
    }
    out[(uint64_t)t * n_embd + d] = acc;
}

__global__ static void hc_expand_kernel(
        float *out_hc,
        const float *block_out,
        const float *block_add,
        const float *residual_hc,
        const float *post,
        const float *comb,
        uint32_t n_embd,
        uint32_t n_hc,
        uint32_t n_tokens,
        uint32_t post_stride,
        uint32_t comb_stride,
        int has_add) {
    uint64_t gid = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    uint64_t n_elem = (uint64_t)n_tokens * n_hc * n_embd;
    if (gid >= n_elem) return;
    uint32_t d = gid % n_embd;
    uint64_t tmp = gid / n_embd;
    uint32_t dst_hc = tmp % n_hc;
    uint32_t t = tmp / n_hc;

    float block_v = block_out[(uint64_t)t * n_embd + d];
    if (has_add) block_v += block_add[(uint64_t)t * n_embd + d];
    float acc = block_v * post[(uint64_t)t * post_stride + dst_hc];
    for (uint32_t src_hc = 0; src_hc < n_hc; src_hc++) {
        float comb_v = comb[(uint64_t)t * comb_stride + dst_hc + (uint64_t)src_hc * n_hc];
        float res_v = residual_hc[(uint64_t)t * n_hc * n_embd + (uint64_t)src_hc * n_embd + d];
        acc += comb_v * res_v;
    }
    out_hc[(uint64_t)t * n_hc * n_embd + (uint64_t)dst_hc * n_embd + d] = acc;
}

__global__ static void hc_split_weighted_sum_fused_kernel(
        float *out,
        float *split,
        const float *mix,
        const float *residual_hc,
        const float *scale,
        const float *base,
        uint32_t n_embd,
        uint32_t n_hc,
        uint32_t n_rows,
        uint32_t sinkhorn_iters,
        float epsv) {
    uint32_t t = blockIdx.x;
    uint32_t d = threadIdx.x;
    if (t >= n_rows || n_hc != 4) return;
    const uint32_t mix_hc = 24;
    float *sp = split + (uint64_t)t * mix_hc;
    if (d == 0) hc4_split_one(sp, mix + (uint64_t)t * mix_hc, scale, base, sinkhorn_iters, epsv);
    __syncthreads();
    for (uint32_t col = d; col < n_embd; col += blockDim.x) {
        float acc = 0.0f;
        for (uint32_t h = 0; h < 4; h++) {
            acc += residual_hc[(uint64_t)t * 4u * n_embd + (uint64_t)h * n_embd + col] * sp[h];
        }
        out[(uint64_t)t * n_embd + col] = acc;
    }
}

__global__ static void hc_split_weighted_sum_norm_fused_kernel(
        float *out,
        float *norm_out,
        float *split,
        const float *mix,
        const float *residual_hc,
        const float *scale,
        const float *base,
        const float *norm_w,
        uint32_t n_embd,
        uint32_t n_hc,
        uint32_t n_rows,
        uint32_t sinkhorn_iters,
        float epsv,
        float norm_eps) {
    const uint32_t t = blockIdx.x;
    const uint32_t d = threadIdx.x;
    if (t >= n_rows || n_hc != 4) return;
    const uint32_t mix_hc = 24;
    float *sp = split + (uint64_t)t * mix_hc;
    if (d == 0) hc4_split_one(sp, mix + (uint64_t)t * mix_hc, scale, base, sinkhorn_iters, epsv);
    __syncthreads();

    float sum = 0.0f;
    for (uint32_t col = d; col < n_embd; col += blockDim.x) {
        float acc = 0.0f;
        for (uint32_t h = 0; h < 4; h++) {
            acc += residual_hc[(uint64_t)t * 4u * n_embd + (uint64_t)h * n_embd + col] * sp[h];
        }
        out[(uint64_t)t * n_embd + col] = acc;
        sum += acc * acc;
    }

    __shared__ float partial[256];
    partial[d] = sum;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (d < stride) partial[d] += partial[d + stride];
        __syncthreads();
    }
    const float norm_scale = rsqrtf(partial[0] / (float)n_embd + norm_eps);
    for (uint32_t col = d; col < n_embd; col += blockDim.x) {
        const float v = out[(uint64_t)t * n_embd + col];
        norm_out[(uint64_t)t * n_embd + col] = v * norm_scale * norm_w[col];
    }
}

__global__ static void output_hc_weights_kernel(
        float *out,
        const float *pre,
        const float *scale,
        const float *base,
        uint32_t n_hc,
        uint32_t n_tokens,
        float epsv) {
    uint32_t gid = blockIdx.x * blockDim.x + threadIdx.x;
    uint32_t n = n_tokens * n_hc;
    if (gid >= n) return;
    uint32_t h = gid % n_hc;
    float z = pre[gid] * scale[0] + base[h];
    out[gid] = 1.0f / (1.0f + expf(-z)) + epsv;
}

__global__ static void fill_f32_kernel(float *x, uint64_t n, float v) {
    uint64_t i = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    if (i < n) x[i] = v;
}

__global__ static void compressor_store_kernel(
        const float *kv,
        const float *sc,
        float *state_kv,
        float *state_score,
        const void *model_map,
        uint64_t ape_offset,
        uint32_t ape_type,
        uint32_t head_dim,
        uint32_t ratio,
        uint32_t pos0,
        uint32_t n_tokens) {
    uint32_t coff = ratio == 4u ? 2u : 1u;
    uint32_t width = coff * head_dim;
    uint64_t gid = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    uint64_t n = (uint64_t)n_tokens * width;
    if (gid >= n) return;
    uint32_t t = gid / width;
    uint32_t j = gid - (uint64_t)t * width;
    uint32_t pos_mod = (pos0 + t) % ratio;
    uint32_t dst_row = ratio == 4u ? ratio + pos_mod : pos_mod;
    state_kv[(uint64_t)dst_row * width + j] = kv[(uint64_t)t * width + j];
    state_score[(uint64_t)dst_row * width + j] =
        sc[(uint64_t)t * width + j] + model_scalar_dev(model_map, ape_offset, ape_type, (uint64_t)pos_mod * width + j);
}

__global__ static void compressor_set_rows_kernel(
        float *state_kv,
        float *state_score,
        const float *kv,
        const float *sc,
        const void *model_map,
        uint64_t ape_offset,
        uint32_t ape_type,
        uint32_t width,
        uint32_t ratio,
        uint32_t pos0,
        uint32_t src0,
        uint32_t dst0,
        uint32_t rows) {
    uint64_t gid = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    uint64_t n = (uint64_t)rows * width;
    if (gid >= n) return;
    uint32_t r = gid / width;
    uint32_t j = gid - (uint64_t)r * width;
    uint32_t src = src0 + r;
    uint32_t dst = dst0 + r;
    uint32_t phase = (pos0 + src) % ratio;
    state_kv[(uint64_t)dst * width + j] = kv[(uint64_t)src * width + j];
    state_score[(uint64_t)dst * width + j] =
        sc[(uint64_t)src * width + j] + model_scalar_dev(model_map, ape_offset, ape_type, (uint64_t)phase * width + j);
}

__global__ static void compressor_prefill_pool_kernel(
        float *comp,
        const float *kv,
        const float *sc,
        const float *state_kv,
        const float *state_score,
        const void *model_map,
        uint64_t ape_offset,
        uint32_t ape_type,
        uint32_t head_dim,
        uint32_t ratio,
        uint32_t pos0,
        uint32_t n_comp,
        uint32_t replay) {
    uint32_t d = blockIdx.x * blockDim.x + threadIdx.x;
    uint32_t c = blockIdx.y;
    if (d >= head_dim || c >= n_comp) return;
    uint32_t coff = ratio == 4u ? 2u : 1u;
    uint32_t width = coff * head_dim;
    float vals[128];
    float scores[128];
    float max_s = -INFINITY;
    uint32_t n_cand = 0;
    if (ratio == 4u) {
        if (replay && c == 0) {
            for (uint32_t r = 0; r < 4; r++) {
                vals[n_cand] = state_kv[(uint64_t)r * width + d];
                scores[n_cand] = state_score[(uint64_t)r * width + d];
                max_s = fmaxf(max_s, scores[n_cand++]);
            }
        } else if (c > 0) {
            uint32_t base = (c - 1u) * ratio;
            for (uint32_t r = 0; r < 4; r++) {
                uint32_t t = base + r;
                float ape = model_scalar_dev(model_map, ape_offset, ape_type, (uint64_t)((pos0 + t) % ratio) * width + d);
                vals[n_cand] = kv[(uint64_t)t * width + d];
                scores[n_cand] = sc[(uint64_t)t * width + d] + ape;
                max_s = fmaxf(max_s, scores[n_cand++]);
            }
        }
        uint32_t base = c * ratio;
        for (uint32_t r = 0; r < 4; r++) {
            uint32_t t = base + r;
            float ape = model_scalar_dev(model_map, ape_offset, ape_type, (uint64_t)((pos0 + t) % ratio) * width + head_dim + d);
            vals[n_cand] = kv[(uint64_t)t * width + head_dim + d];
            scores[n_cand] = sc[(uint64_t)t * width + head_dim + d] + ape;
            max_s = fmaxf(max_s, scores[n_cand++]);
        }
    } else {
        uint32_t base = c * ratio;
        for (uint32_t r = 0; r < ratio; r++) {
            uint32_t t = base + r;
            float ape = model_scalar_dev(model_map, ape_offset, ape_type, (uint64_t)((pos0 + t) % ratio) * width + d);
            vals[n_cand] = kv[(uint64_t)t * width + d];
            scores[n_cand] = sc[(uint64_t)t * width + d] + ape;
            max_s = fmaxf(max_s, scores[n_cand++]);
        }
    }
    float den = 0.0f, acc = 0.0f;
    for (uint32_t i = 0; i < n_cand; i++) {
        float w = expf(scores[i] - max_s);
        den += w;
        acc += vals[i] * w;
    }
    comp[(uint64_t)c * head_dim + d] = den != 0.0f ? acc / den : 0.0f;
}

__global__ static void compressor_update_pool_kernel(
        float *row,
        const float *state_kv,
        const float *state_score,
        uint32_t head_dim,
        uint32_t ratio) {
    uint32_t d = blockIdx.x * blockDim.x + threadIdx.x;
    if (d >= head_dim) return;
    uint32_t coff = ratio == 4u ? 2u : 1u;
    uint32_t width = coff * head_dim;
    float vals[128];
    float scores[128];
    float max_s = -INFINITY;
    uint32_t n_cand = 0;
    if (ratio == 4u) {
        for (uint32_t r = 0; r < 4; r++) {
            vals[n_cand] = state_kv[(uint64_t)r * width + d];
            scores[n_cand] = state_score[(uint64_t)r * width + d];
            max_s = fmaxf(max_s, scores[n_cand++]);
        }
        for (uint32_t r = 0; r < 4; r++) {
            vals[n_cand] = state_kv[(uint64_t)(ratio + r) * width + head_dim + d];
            scores[n_cand] = state_score[(uint64_t)(ratio + r) * width + head_dim + d];
            max_s = fmaxf(max_s, scores[n_cand++]);
        }
    } else {
        for (uint32_t r = 0; r < ratio; r++) {
            vals[n_cand] = state_kv[(uint64_t)r * width + d];
            scores[n_cand] = state_score[(uint64_t)r * width + d];
            max_s = fmaxf(max_s, scores[n_cand++]);
        }
    }
    float den = 0.0f, acc = 0.0f;
    for (uint32_t i = 0; i < n_cand; i++) {
        float w = expf(scores[i] - max_s);
        den += w;
        acc += vals[i] * w;
    }
    row[d] = den != 0.0f ? acc / den : 0.0f;
}

__global__ static void compressor_shift_ratio4_kernel(float *state_kv, float *state_score, uint32_t width) {
    uint64_t i = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    uint64_t half = 4ull * width;
    if (i >= half) return;
    float v = state_kv[half + i];
    float s = state_score[half + i];
    state_kv[i] = v;
    state_score[i] = s;
    state_kv[half + i] = v;
    state_score[half + i] = s;
}

__device__ static float softplus_dev(float x) {
    if (x > 20.0f) return x;
    if (x < -20.0f) return expf(x);
    return log1pf(expf(x));
}

__global__ static void router_select_kernel(
        int32_t *selected,
        float *weights,
        float *probs,
        const float *bias,
        const int32_t *hash,
        const float *logits,
        const int32_t *tokens,
        int32_t token_scalar,
        uint32_t hash_rows,
        uint32_t n_tokens,
        int has_bias,
        int hash_mode) {
    uint32_t t = blockIdx.x;
    if (t >= n_tokens || threadIdx.x != 0) return;
    const float *log = logits + (uint64_t)t * 256;
    float *prob = probs + (uint64_t)t * 256;
    int32_t *sel = selected + (uint64_t)t * 6;
    float *w = weights + (uint64_t)t * 6;

    for (int i = 0; i < 256; i++) prob[i] = sqrtf(softplus_dev(log[i]));

    if (hash_mode) {
        int32_t tok = tokens ? tokens[t] : token_scalar;
        if (tok < 0 || (uint32_t)tok >= hash_rows) tok = 0;
        const int32_t *row = hash + (uint64_t)tok * 6;
        for (int i = 0; i < 6; i++) sel[i] = row[i];
    } else {
        for (int i = 0; i < 6; i++) sel[i] = -1;
        for (int i = 0; i < 256; i++) {
            float score = prob[i] + (has_bias ? bias[i] : 0.0f);
            for (int j = 0; j < 6; j++) {
                if (sel[j] < 0 || score > prob[sel[j]] + (has_bias ? bias[sel[j]] : 0.0f)) {
                    for (int k = 5; k > j; k--) sel[k] = sel[k - 1];
                    sel[j] = i;
                    break;
                }
            }
        }
    }

    float sum = 0.0f;
    for (int i = 0; i < 6; i++) {
        int e = sel[i];
        float v = (e >= 0 && e < 256) ? prob[e] : 0.0f;
        w[i] = v;
        sum += v;
    }
    sum = fmaxf(sum, 6.103515625e-5f);
    for (int i = 0; i < 6; i++) w[i] = w[i] / sum * 1.5f;
}

__global__ static void router_select_parallel_kernel(
        int32_t *selected,
        float *weights,
        float *probs,
        const float *bias,
        const int32_t *hash,
        const float *logits,
        const int32_t *tokens,
        int32_t token_scalar,
        uint32_t hash_rows,
        uint32_t n_tokens,
        int has_bias,
        int hash_mode) {
    uint32_t t = blockIdx.x;
    uint32_t i = threadIdx.x;
    if (t >= n_tokens || i >= 256u) return;
    const float *log = logits + (uint64_t)t * 256;
    float *prob = probs + (uint64_t)t * 256;
    int32_t *sel = selected + (uint64_t)t * 6;
    float *w = weights + (uint64_t)t * 6;
    __shared__ float sprob[256];

    const float p = sqrtf(softplus_dev(log[i]));
    sprob[i] = p;
    prob[i] = p;
    __syncthreads();

    if (i != 0) return;
    if (hash_mode) {
        int32_t tok = tokens ? tokens[t] : token_scalar;
        if (tok < 0 || (uint32_t)tok >= hash_rows) tok = 0;
        const int32_t *row = hash + (uint64_t)tok * 6;
        for (int j = 0; j < 6; j++) sel[j] = row[j];
    } else {
        for (int j = 0; j < 6; j++) sel[j] = -1;
        for (int e = 0; e < 256; e++) {
            float score = sprob[e] + (has_bias ? bias[e] : 0.0f);
            for (int j = 0; j < 6; j++) {
                if (sel[j] < 0 || score > sprob[sel[j]] + (has_bias ? bias[sel[j]] : 0.0f)) {
                    for (int k = 5; k > j; k--) sel[k] = sel[k - 1];
                    sel[j] = e;
                    break;
                }
            }
        }
    }

    float sum = 0.0f;
    for (int j = 0; j < 6; j++) {
        int e = sel[j];
        float v = (e >= 0 && e < 256) ? sprob[e] : 0.0f;
        w[j] = v;
        sum += v;
    }
    sum = fmaxf(sum, 6.103515625e-5f);
    for (int j = 0; j < 6; j++) w[j] = w[j] / sum * 1.5f;
}

__device__ __forceinline__ static bool router_score_better(float av, uint32_t ai, float bv, uint32_t bi) {
    return av > bv || (av == bv && ai < bi);
}

__global__ static void router_select_warp_topk_kernel(
        int32_t *selected,
        float *weights,
        float *probs,
        const float *bias,
        const int32_t *hash,
        const float *logits,
        const int32_t *tokens,
        int32_t token_scalar,
        uint32_t hash_rows,
        uint32_t n_tokens,
        int has_bias,
        int hash_mode) {
    const uint32_t lane = threadIdx.x;
    const uint32_t row_in_block = threadIdx.y;
    const uint32_t t = blockIdx.x * blockDim.y + row_in_block;
    if (t >= n_tokens || lane >= 32u) return;

    const float *log = logits + (uint64_t)t * 256u;
    float *prob = probs + (uint64_t)t * 256u;
    int32_t *sel = selected + (uint64_t)t * 6u;
    float *w = weights + (uint64_t)t * 6u;
    __shared__ float sprob[4][256];
    float local_prob[8];
    float local_score[8];

    #pragma unroll
    for (uint32_t j = 0; j < 8u; j++) {
        const uint32_t e = lane + j * 32u;
        const float p = sqrtf(softplus_dev(log[e]));
        local_prob[j] = p;
        local_score[j] = p + (has_bias ? bias[e] : 0.0f);
        sprob[row_in_block][e] = p;
        prob[e] = p;
    }
    __syncwarp();

    if (hash_mode) {
        if (lane == 0) {
            int32_t tok = tokens ? tokens[t] : token_scalar;
            if (tok < 0 || (uint32_t)tok >= hash_rows) tok = 0;
            const int32_t *row = hash + (uint64_t)tok * 6u;
            float sum = 0.0f;
            #pragma unroll
            for (uint32_t j = 0; j < 6u; j++) {
                const int32_t e = row[j];
                sel[j] = e;
                const float v = (e >= 0 && e < 256) ? sprob[row_in_block][(uint32_t)e] : 0.0f;
                w[j] = v;
                sum += v;
            }
            sum = fmaxf(sum, 6.103515625e-5f);
            #pragma unroll
            for (uint32_t j = 0; j < 6u; j++) w[j] = w[j] / sum * 1.5f;
        }
        return;
    }

    float out_prob[6] = {0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f};
    uint32_t out_idx[6] = {0, 0, 0, 0, 0, 0};
    #pragma unroll
    for (uint32_t k = 0; k < 6u; k++) {
        float best_score = -INFINITY;
        float best_prob = 0.0f;
        uint32_t best_idx = UINT32_MAX;
        #pragma unroll
        for (uint32_t j = 0; j < 8u; j++) {
            const uint32_t e = lane + j * 32u;
            const float s = local_score[j];
            if (router_score_better(s, e, best_score, best_idx)) {
                best_score = s;
                best_prob = local_prob[j];
                best_idx = e;
            }
        }
        #pragma unroll
        for (uint32_t mask = 16u; mask > 0u; mask >>= 1u) {
            const float other_score = __shfl_xor_sync(0xffffffffu, best_score, mask);
            const float other_prob = __shfl_xor_sync(0xffffffffu, best_prob, mask);
            const uint32_t other_idx = __shfl_xor_sync(0xffffffffu, best_idx, mask);
            if (router_score_better(other_score, other_idx, best_score, best_idx)) {
                best_score = other_score;
                best_prob = other_prob;
                best_idx = other_idx;
            }
        }
        #pragma unroll
        for (uint32_t j = 0; j < 8u; j++) {
            const uint32_t e = lane + j * 32u;
            if (e == best_idx) local_score[j] = -INFINITY;
        }
        if (lane == 0) {
            out_idx[k] = best_idx;
            out_prob[k] = best_prob;
        }
    }

    if (lane == 0) {
        float sum = 0.0f;
        #pragma unroll
        for (uint32_t j = 0; j < 6u; j++) {
            sel[j] = (int32_t)out_idx[j];
            w[j] = out_prob[j];
            sum += out_prob[j];
        }
        sum = fmaxf(sum, 6.103515625e-5f);
        #pragma unroll
        for (uint32_t j = 0; j < 6u; j++) w[j] = w[j] / sum * 1.5f;
    }
}

__global__ static void swiglu_kernel(float *out, const float *gate, const float *up, uint32_t n, float clamp, float weight) {
    uint32_t i = blockIdx.x * blockDim.x + threadIdx.x;
    if (i >= n) return;
    float g = gate[i];
    float u = up[i];
    if (clamp > 1.0e-6f) {
        g = fminf(g, clamp);
        u = fminf(fmaxf(u, -clamp), clamp);
    }
    float s = g / (1.0f + expf(-g));
    out[i] = s * u * weight;
}

__global__ static void add_kernel(float *out, const float *a, const float *b, uint32_t n) {
    uint32_t i = blockIdx.x * blockDim.x + threadIdx.x;
    if (i >= n) return;
    out[i] = a[i] + b[i];
}

__global__ static void directional_steering_project_kernel(
        float       *x,
        const float *directions,
        uint32_t     layer,
        uint32_t     width,
        uint32_t     rows,
        float        scale) {
    const uint32_t row = blockIdx.x;
    if (row >= rows || width == 0) return;

    float *xr = x + (uint64_t)row * width;
    const float *dir = directions + (uint64_t)layer * width;
    float sum = 0.0f;
    for (uint32_t i = threadIdx.x; i < width; i += blockDim.x) {
        sum += xr[i] * dir[i];
    }

    __shared__ float partial[256];
    partial[threadIdx.x] = sum;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] += partial[threadIdx.x + stride];
        __syncthreads();
    }

    const float coeff = scale * partial[0];
    for (uint32_t i = threadIdx.x; i < width; i += blockDim.x) {
        xr[i] -= coeff * dir[i];
    }
}

__global__ static void zero_kernel(float *out, uint64_t n) {
    uint64_t i = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    if (i < n) out[i] = 0.0f;
}

__global__ static void indexer_scores_kernel(
        float *scores,
        const float *q,
        const float *weights,
        const float *index_comp,
        uint32_t n_comp,
        uint32_t n_tokens,
        uint32_t pos0,
        uint32_t n_head,
        uint32_t head_dim,
        uint32_t ratio,
        float scale,
        int causal) {
    uint32_t c = blockIdx.x;
    uint32_t t = blockIdx.y;
    if (c >= n_comp || t >= n_tokens) return;
    if (causal) {
        uint32_t n_visible = (pos0 + t + 1u) / ratio;
        if (c >= n_visible) {
            if (threadIdx.x == 0) scores[(uint64_t)t * n_comp + c] = -INFINITY;
            return;
        }
    }
    float total = 0.0f;
    for (uint32_t h = 0; h < n_head; h++) {
        const float *qh = q + ((uint64_t)t * n_head + h) * head_dim;
        const float *kh = index_comp + (uint64_t)c * head_dim;
        float dot = 0.0f;
        for (uint32_t d = threadIdx.x; d < head_dim; d += blockDim.x) dot += qh[d] * kh[d];
        __shared__ float partial[256];
        partial[threadIdx.x] = dot;
        __syncthreads();
        for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
            if (threadIdx.x < stride) partial[threadIdx.x] += partial[threadIdx.x + stride];
            __syncthreads();
        }
        total += fmaxf(partial[0], 0.0f) * weights[(uint64_t)t * n_head + h];
        __syncthreads();
    }
    if (threadIdx.x == 0) scores[(uint64_t)t * n_comp + c] = total * scale;
}

__global__ static void indexer_score_one_direct_kernel(
        float *scores,
        const float *q,
        const float *weights,
        const float *index_comp,
        uint32_t n_comp,
        uint32_t pos0,
        uint32_t ratio,
        float scale,
        int causal) {
    const uint32_t c = blockIdx.x;
    const uint32_t tid = threadIdx.x;
    const uint32_t lane = tid & 31u;
    const uint32_t warp = tid >> 5u;
    if (c >= n_comp || tid >= 128u) return;
    if (causal) {
        const uint32_t visible = ratio ? (pos0 + 1u) / ratio : n_comp;
        if (c >= visible) {
            if (tid == 0) scores[c] = -INFINITY;
            return;
        }
    }

    __shared__ float krow[128];
    __shared__ float partial[4];
    if (tid < 128u) krow[tid] = index_comp[(uint64_t)c * 128u + tid];
    __syncthreads();

    float total = 0.0f;
    for (uint32_t h0 = 0; h0 < 64u; h0 += 4u) {
        const uint32_t h = h0 + warp;
        const float4 qv = ((const float4 *)(q + (uint64_t)h * 128u))[lane];
        const float4 kv = ((const float4 *)krow)[lane];
        float dot = qv.x * kv.x + qv.y * kv.y + qv.z * kv.z + qv.w * kv.w;
        dot = warp_sum_f32(dot);
        if (lane == 0) partial[warp] = fmaxf(dot, 0.0f) * weights[h] * scale;
        __syncthreads();
        if (tid == 0) total += partial[0] + partial[1] + partial[2] + partial[3];
        __syncthreads();
    }
    if (tid == 0) scores[c] = total;
}

__global__ static void indexer_scores_wmma_kernel(
        float *scores,
        const float *q,
        const float *weights,
        const float *index_comp,
        uint32_t n_comp,
        uint32_t n_tokens,
        uint32_t pos0,
        uint32_t n_head,
        uint32_t head_dim,
        uint32_t ratio,
        float scale,
        int causal) {
#if __CUDA_ARCH__ >= 700
    namespace wmma = nvcuda::wmma;
    const uint32_t tile_c = blockIdx.x * 16u;
    const uint32_t tile_t = blockIdx.y * 16u;
    const uint32_t tid = threadIdx.x;
    if (tid >= 32u || head_dim != 128u) return;

    if (causal) {
        const uint32_t last_token = min(tile_t + 16u, n_tokens);
        const uint32_t max_visible = last_token > tile_t
            ? min((pos0 + last_token) / ratio, n_comp)
            : 0u;
        if (tile_c >= max_visible) {
            for (uint32_t i = tid; i < 16u * 16u; i += 32u) {
                const uint32_t r = i >> 4u;
                const uint32_t c = i & 15u;
                const uint32_t token = tile_t + r;
                const uint32_t comp = tile_c + c;
                if (token < n_tokens && comp < n_comp) {
                    scores[(uint64_t)token * n_comp + comp] = -INFINITY;
                }
            }
            return;
        }
    }

    __shared__ __half a_sh[16 * 128];
    __shared__ __half b_sh[16 * 128];
    __shared__ float c_sh[16 * 16];
    __shared__ float acc_sh[16 * 16];

    for (uint32_t i = tid; i < 16u * 16u; i += 32u) acc_sh[i] = 0.0f;
    for (uint32_t i = tid; i < 16u * 128u; i += 32u) {
        const uint32_t c = i >> 7u;
        const uint32_t d = i & 127u;
        const uint32_t comp = tile_c + c;
        float v = 0.0f;
        if (comp < n_comp) v = index_comp[(uint64_t)comp * head_dim + d];
        b_sh[d + c * 128u] = __float2half(v);
    }
    __syncthreads();

    for (uint32_t h = 0; h < n_head; h++) {
        for (uint32_t i = tid; i < 16u * 128u; i += 32u) {
            const uint32_t r = i >> 7u;
            const uint32_t d = i & 127u;
            const uint32_t token = tile_t + r;
            float v = 0.0f;
            if (token < n_tokens) {
                v = q[((uint64_t)token * n_head + h) * head_dim + d];
            }
            a_sh[i] = __float2half(v);
        }
        __syncthreads();

        wmma::fragment<wmma::matrix_a, 16, 16, 16, __half, wmma::row_major> a_frag;
        wmma::fragment<wmma::matrix_b, 16, 16, 16, __half, wmma::col_major> b_frag;
        wmma::fragment<wmma::accumulator, 16, 16, 16, float> c_frag;
        wmma::fill_fragment(c_frag, 0.0f);
        for (uint32_t k0 = 0; k0 < 128u; k0 += 16u) {
            wmma::load_matrix_sync(a_frag, a_sh + k0, 128);
            wmma::load_matrix_sync(b_frag, b_sh + k0, 128);
            wmma::mma_sync(c_frag, a_frag, b_frag, c_frag);
        }
        wmma::store_matrix_sync(c_sh, c_frag, 16, wmma::mem_row_major);
        __syncthreads();

        for (uint32_t i = tid; i < 16u * 16u; i += 32u) {
            const uint32_t r = i >> 4u;
            const uint32_t token = tile_t + r;
            if (token < n_tokens) {
                const float w = weights[(uint64_t)token * n_head + h];
                acc_sh[i] += fmaxf(c_sh[i], 0.0f) * w;
            }
        }
        __syncthreads();
    }

    for (uint32_t i = tid; i < 16u * 16u; i += 32u) {
        const uint32_t r = i >> 4u;
        const uint32_t c = i & 15u;
        const uint32_t token = tile_t + r;
        const uint32_t comp = tile_c + c;
        if (token < n_tokens && comp < n_comp) {
            float out = acc_sh[i] * scale;
            if (causal) {
                const uint32_t visible = (pos0 + token + 1u) / ratio;
                if (comp >= visible) out = -INFINITY;
            }
            scores[(uint64_t)token * n_comp + comp] = out;
        }
    }
#endif
}

__global__ static void indexer_scores_wmma32_kernel(
        float *scores,
        const float *q,
        const float *weights,
        const float *index_comp,
        uint32_t n_comp,
        uint32_t n_tokens,
        uint32_t pos0,
        uint32_t n_head,
        uint32_t head_dim,
        uint32_t ratio,
        float scale,
        int causal) {
#if __CUDA_ARCH__ >= 700
    namespace wmma = nvcuda::wmma;
    const uint32_t tile_c = blockIdx.x * 32u;
    const uint32_t tile_t = blockIdx.y * 16u;
    const uint32_t tid = threadIdx.x;
    const uint32_t warp = tid >> 5u;
    if (tid >= 64u || head_dim != 128u) return;

    if (causal) {
        const uint32_t last_token = min(tile_t + 16u, n_tokens);
        const uint32_t max_visible = last_token > tile_t
            ? min((pos0 + last_token) / ratio, n_comp)
            : 0u;
        if (tile_c >= max_visible) {
            for (uint32_t i = tid; i < 16u * 32u; i += 64u) {
                const uint32_t r = i >> 5u;
                const uint32_t c = i & 31u;
                const uint32_t token = tile_t + r;
                const uint32_t comp = tile_c + c;
                if (token < n_tokens && comp < n_comp) {
                    scores[(uint64_t)token * n_comp + comp] = -INFINITY;
                }
            }
            return;
        }
    }

    __shared__ __half a_sh[16 * 128];
    __shared__ __half b_sh[32 * 128];
    __shared__ float c_sh[2 * 16 * 16];
    __shared__ float acc_sh[2 * 16 * 16];

    for (uint32_t i = tid; i < 2u * 16u * 16u; i += 64u) acc_sh[i] = 0.0f;
    for (uint32_t i = tid; i < 32u * 128u; i += 64u) {
        const uint32_t c = i >> 7u;
        const uint32_t d = i & 127u;
        const uint32_t comp = tile_c + c;
        float v = 0.0f;
        if (comp < n_comp) v = index_comp[(uint64_t)comp * head_dim + d];
        b_sh[d + c * 128u] = __float2half(v);
    }
    __syncthreads();

    for (uint32_t h = 0; h < n_head; h++) {
        for (uint32_t i = tid; i < 16u * 128u; i += 64u) {
            const uint32_t r = i >> 7u;
            const uint32_t d = i & 127u;
            const uint32_t token = tile_t + r;
            float v = 0.0f;
            if (token < n_tokens) {
                v = q[((uint64_t)token * n_head + h) * head_dim + d];
            }
            a_sh[i] = __float2half(v);
        }
        __syncthreads();

        wmma::fragment<wmma::matrix_a, 16, 16, 16, __half, wmma::row_major> a_frag;
        wmma::fragment<wmma::matrix_b, 16, 16, 16, __half, wmma::col_major> b_frag;
        wmma::fragment<wmma::accumulator, 16, 16, 16, float> c_frag;
        wmma::fill_fragment(c_frag, 0.0f);
        const uint32_t col0 = warp * 16u;
        for (uint32_t k0 = 0; k0 < 128u; k0 += 16u) {
            wmma::load_matrix_sync(a_frag, a_sh + k0, 128);
            wmma::load_matrix_sync(b_frag, b_sh + col0 * 128u + k0, 128);
            wmma::mma_sync(c_frag, a_frag, b_frag, c_frag);
        }
        wmma::store_matrix_sync(c_sh + warp * 16u * 16u, c_frag, 16, wmma::mem_row_major);
        __syncthreads();

        for (uint32_t i = tid; i < 2u * 16u * 16u; i += 64u) {
            const uint32_t wtile = i >> 8u;
            const uint32_t local = i & 255u;
            const uint32_t r = local >> 4u;
            const uint32_t c = local & 15u;
            const uint32_t token = tile_t + r;
            const uint32_t comp = tile_c + wtile * 16u + c;
            if (token < n_tokens && comp < n_comp) {
                const float w = weights[(uint64_t)token * n_head + h];
                acc_sh[i] += fmaxf(c_sh[i], 0.0f) * w;
            }
        }
        __syncthreads();
    }

    for (uint32_t i = tid; i < 2u * 16u * 16u; i += 64u) {
        const uint32_t wtile = i >> 8u;
        const uint32_t local = i & 255u;
        const uint32_t r = local >> 4u;
        const uint32_t c = local & 15u;
        const uint32_t token = tile_t + r;
        const uint32_t comp = tile_c + wtile * 16u + c;
        if (token < n_tokens && comp < n_comp) {
            float out = acc_sh[i] * scale;
            if (causal) {
                const uint32_t visible = (pos0 + token + 1u) / ratio;
                if (comp >= visible) out = -INFINITY;
            }
            scores[(uint64_t)token * n_comp + comp] = out;
        }
    }
#endif
}

__global__ static void indexer_scores_wmma64_kernel(
        float *scores,
        const float *q,
        const float *weights,
        const float *index_comp,
        uint32_t n_comp,
        uint32_t n_tokens,
        uint32_t pos0,
        uint32_t n_head,
        uint32_t head_dim,
        uint32_t ratio,
        float scale,
        int causal) {
#if __CUDA_ARCH__ >= 700
    namespace wmma = nvcuda::wmma;
    const uint32_t tile_c = blockIdx.x * 64u;
    const uint32_t tile_t = blockIdx.y * 16u;
    const uint32_t tid = threadIdx.x;
    const uint32_t warp = tid >> 5u;
    if (tid >= 128u || head_dim != 128u) return;

    if (causal) {
        const uint32_t last_token = min(tile_t + 16u, n_tokens);
        const uint32_t max_visible = last_token > tile_t
            ? min((pos0 + last_token) / ratio, n_comp)
            : 0u;
        if (tile_c >= max_visible) {
            for (uint32_t i = tid; i < 16u * 64u; i += 128u) {
                const uint32_t r = i >> 6u;
                const uint32_t c = i & 63u;
                const uint32_t token = tile_t + r;
                const uint32_t comp = tile_c + c;
                if (token < n_tokens && comp < n_comp) {
                    scores[(uint64_t)token * n_comp + comp] = -INFINITY;
                }
            }
            return;
        }
    }

    __shared__ __half a_sh[16 * 128];
    __shared__ __half b_sh[64 * 128];
    __shared__ float c_sh[4 * 16 * 16];
    __shared__ float acc_sh[4 * 16 * 16];

    for (uint32_t i = tid; i < 4u * 16u * 16u; i += 128u) acc_sh[i] = 0.0f;
    for (uint32_t i = tid; i < 64u * 128u; i += 128u) {
        const uint32_t c = i >> 7u;
        const uint32_t d = i & 127u;
        const uint32_t comp = tile_c + c;
        float v = 0.0f;
        if (comp < n_comp) v = index_comp[(uint64_t)comp * head_dim + d];
        b_sh[d + c * 128u] = __float2half(v);
    }
    __syncthreads();

    for (uint32_t h = 0; h < n_head; h++) {
        for (uint32_t i = tid; i < 16u * 128u; i += 128u) {
            const uint32_t r = i >> 7u;
            const uint32_t d = i & 127u;
            const uint32_t token = tile_t + r;
            float v = 0.0f;
            if (token < n_tokens) {
                v = q[((uint64_t)token * n_head + h) * head_dim + d];
            }
            a_sh[i] = __float2half(v);
        }
        __syncthreads();

        wmma::fragment<wmma::matrix_a, 16, 16, 16, __half, wmma::row_major> a_frag;
        wmma::fragment<wmma::matrix_b, 16, 16, 16, __half, wmma::col_major> b_frag;
        wmma::fragment<wmma::accumulator, 16, 16, 16, float> c_frag;
        wmma::fill_fragment(c_frag, 0.0f);
        const uint32_t col0 = warp * 16u;
        for (uint32_t k0 = 0; k0 < 128u; k0 += 16u) {
            wmma::load_matrix_sync(a_frag, a_sh + k0, 128);
            wmma::load_matrix_sync(b_frag, b_sh + col0 * 128u + k0, 128);
            wmma::mma_sync(c_frag, a_frag, b_frag, c_frag);
        }
        wmma::store_matrix_sync(c_sh + warp * 16u * 16u, c_frag, 16, wmma::mem_row_major);
        __syncthreads();

        for (uint32_t i = tid; i < 4u * 16u * 16u; i += 128u) {
            const uint32_t wtile = i >> 8u;
            const uint32_t local = i & 255u;
            const uint32_t r = local >> 4u;
            const uint32_t c = local & 15u;
            const uint32_t token = tile_t + r;
            const uint32_t comp = tile_c + wtile * 16u + c;
            if (token < n_tokens && comp < n_comp) {
                const float w = weights[(uint64_t)token * n_head + h];
                acc_sh[i] += fmaxf(c_sh[i], 0.0f) * w;
            }
        }
        __syncthreads();
    }

    for (uint32_t i = tid; i < 4u * 16u * 16u; i += 128u) {
        const uint32_t wtile = i >> 8u;
        const uint32_t local = i & 255u;
        const uint32_t r = local >> 4u;
        const uint32_t c = local & 15u;
        const uint32_t token = tile_t + r;
        const uint32_t comp = tile_c + wtile * 16u + c;
        if (token < n_tokens && comp < n_comp) {
            float out = acc_sh[i] * scale;
            if (causal) {
                const uint32_t visible = (pos0 + token + 1u) / ratio;
                if (comp >= visible) out = -INFINITY;
            }
            scores[(uint64_t)token * n_comp + comp] = out;
        }
    }
#endif
}

__global__ static void indexer_scores_wmma128_kernel(
        float *scores,
        const float *q,
        const float *weights,
        const float *index_comp,
        uint32_t n_comp,
        uint32_t n_tokens,
        uint32_t pos0,
        uint32_t n_head,
        uint32_t head_dim,
        uint32_t ratio,
        float scale,
        int causal) {
#if __CUDA_ARCH__ >= 700
    namespace wmma = nvcuda::wmma;
    const uint32_t tile_c = blockIdx.x * 128u;
    const uint32_t tile_t = blockIdx.y * 16u;
    const uint32_t tid = threadIdx.x;
    const uint32_t warp = tid >> 5u;
    if (tid >= 256u || head_dim != 128u) return;

    if (causal) {
        const uint32_t last_token = min(tile_t + 16u, n_tokens);
        const uint32_t max_visible = last_token > tile_t
            ? min((pos0 + last_token) / ratio, n_comp)
            : 0u;
        if (tile_c >= max_visible) {
            for (uint32_t i = tid; i < 16u * 128u; i += 256u) {
                const uint32_t r = i >> 7u;
                const uint32_t c = i & 127u;
                const uint32_t token = tile_t + r;
                const uint32_t comp = tile_c + c;
                if (token < n_tokens && comp < n_comp) {
                    scores[(uint64_t)token * n_comp + comp] = -INFINITY;
                }
            }
            return;
        }
    }

    __shared__ __half a_sh[16 * 128];
    __shared__ __half b_sh[128 * 128];
    __shared__ float c_sh[8 * 16 * 16];

    float acc[8];
#pragma unroll
    for (uint32_t i = 0; i < 8u; i++) acc[i] = 0.0f;

    for (uint32_t i = tid; i < 128u * 128u; i += 256u) {
        const uint32_t c = i >> 7u;
        const uint32_t d = i & 127u;
        const uint32_t comp = tile_c + c;
        float v = 0.0f;
        if (comp < n_comp) v = index_comp[(uint64_t)comp * head_dim + d];
        b_sh[d + c * 128u] = __float2half(v);
    }
    __syncthreads();

    for (uint32_t h = 0; h < n_head; h++) {
        for (uint32_t i = tid; i < 16u * 128u; i += 256u) {
            const uint32_t r = i >> 7u;
            const uint32_t d = i & 127u;
            const uint32_t token = tile_t + r;
            float v = 0.0f;
            if (token < n_tokens) {
                v = q[((uint64_t)token * n_head + h) * head_dim + d];
            }
            a_sh[i] = __float2half(v);
        }
        __syncthreads();

        wmma::fragment<wmma::matrix_a, 16, 16, 16, __half, wmma::row_major> a_frag;
        wmma::fragment<wmma::matrix_b, 16, 16, 16, __half, wmma::col_major> b_frag;
        wmma::fragment<wmma::accumulator, 16, 16, 16, float> c_frag;
        wmma::fill_fragment(c_frag, 0.0f);
        const uint32_t col0 = warp * 16u;
        for (uint32_t k0 = 0; k0 < 128u; k0 += 16u) {
            wmma::load_matrix_sync(a_frag, a_sh + k0, 128);
            wmma::load_matrix_sync(b_frag, b_sh + col0 * 128u + k0, 128);
            wmma::mma_sync(c_frag, a_frag, b_frag, c_frag);
        }
        wmma::store_matrix_sync(c_sh + warp * 16u * 16u, c_frag, 16, wmma::mem_row_major);
        __syncthreads();

        const uint32_t local0 = tid & 255u;
        const uint32_t token0 = tile_t + (local0 >> 4u);
        const float w0 = token0 < n_tokens ? weights[(uint64_t)token0 * n_head + h] : 0.0f;
        uint32_t slot = 0;
        for (uint32_t i = tid; i < 8u * 16u * 16u; i += 256u, slot++) {
            const uint32_t wtile = i >> 8u;
            const uint32_t local = i & 255u;
            const uint32_t r = local >> 4u;
            const uint32_t c = local & 15u;
            const uint32_t token = tile_t + r;
            const uint32_t comp = tile_c + wtile * 16u + c;
            if (token < n_tokens && comp < n_comp) {
                acc[slot] += fmaxf(c_sh[i], 0.0f) * w0;
            }
        }
        __syncthreads();
    }

    uint32_t slot = 0;
    for (uint32_t i = tid; i < 8u * 16u * 16u; i += 256u, slot++) {
        const uint32_t wtile = i >> 8u;
        const uint32_t local = i & 255u;
        const uint32_t r = local >> 4u;
        const uint32_t c = local & 15u;
        const uint32_t token = tile_t + r;
        const uint32_t comp = tile_c + wtile * 16u + c;
        if (token < n_tokens && comp < n_comp) {
            float out = acc[slot] * scale;
            if (causal) {
                const uint32_t visible = (pos0 + token + 1u) / ratio;
                if (comp >= visible) out = -INFINITY;
            }
            scores[(uint64_t)token * n_comp + comp] = out;
        }
    }
#endif
}

__global__ static void indexer_topk_kernel(uint32_t *selected, const float *scores, uint32_t n_comp, uint32_t n_tokens, uint32_t top_k) {
    uint32_t t = blockIdx.x;
    if (t >= n_tokens || threadIdx.x != 0) return;
    const float *row = scores + (uint64_t)t * n_comp;
    uint32_t *sel = selected + (uint64_t)t * top_k;
    for (uint32_t k = 0; k < top_k; k++) sel[k] = 0;
    for (uint32_t c = 0; c < n_comp; c++) {
        float v = row[c];
        for (uint32_t k = 0; k < top_k; k++) {
            if ((k >= c) || v > row[sel[k]]) {
                for (uint32_t j = top_k - 1; j > k; j--) sel[j] = sel[j - 1];
                sel[k] = c;
                break;
            }
        }
    }
}

__device__ __forceinline__ static bool topk_score_better(float av, uint32_t ai, float bv, uint32_t bi) {
    return av > bv || (av == bv && ai < bi);
}

__device__ __forceinline__ static uint32_t topk_float_ordered_key(float v) {
    const uint32_t u = __float_as_uint(v);
    return (u & 0x80000000u) ? ~u : (u ^ 0x80000000u);
}

__device__ __forceinline__ static uint64_t topk_pack_key(float v, uint32_t idx) {
    return ((uint64_t)topk_float_ordered_key(v) << 32u) | (uint64_t)(0xffffffffu - idx);
}

__global__ static void indexer_topk_8192_cub_kernel(
        uint32_t *selected,
        const float *scores,
        uint32_t n_comp,
        uint32_t n_tokens,
        uint32_t top_k) {
    constexpr uint32_t BLOCK_THREADS = 512u;
    constexpr uint32_t ITEMS_PER_THREAD = 16u;
    using BlockSort = cub::BlockRadixSort<uint64_t, BLOCK_THREADS, ITEMS_PER_THREAD>;
    extern __shared__ __align__(16) unsigned char sort_smem[];
    typename BlockSort::TempStorage &sort_storage =
        *reinterpret_cast<typename BlockSort::TempStorage *>(sort_smem);

    const uint32_t t = blockIdx.x;
    const uint32_t tid = threadIdx.x;
    if (t >= n_tokens || tid >= BLOCK_THREADS) return;

    const float *row = scores + (uint64_t)t * n_comp;
    uint64_t keys[ITEMS_PER_THREAD];
#pragma unroll
    for (uint32_t item = 0; item < ITEMS_PER_THREAD; item++) {
        const uint32_t i = tid * ITEMS_PER_THREAD + item;
        if (i < n_comp) {
            keys[item] = topk_pack_key(row[i], i);
        } else {
            keys[item] = topk_pack_key(-INFINITY, UINT32_MAX);
        }
    }

    BlockSort(sort_storage).SortDescending(keys);

#pragma unroll
    for (uint32_t item = 0; item < ITEMS_PER_THREAD; item++) {
        const uint32_t i = tid * ITEMS_PER_THREAD + item;
        if (i < top_k) {
            selected[(uint64_t)t * top_k + i] = 0xffffffffu - (uint32_t)keys[item];
        }
    }
}

__global__ static void indexer_topk_1024_kernel(
        uint32_t *selected,
        const float *scores,
        uint32_t n_comp,
        uint32_t n_tokens,
        uint32_t top_k) {
    uint32_t t = blockIdx.x;
    uint32_t tid = threadIdx.x;
    if (t >= n_tokens || tid >= 1024u) return;
    __shared__ float vals[1024];
    __shared__ uint32_t idxs[1024];

    const float *row = scores + (uint64_t)t * n_comp;
    if (tid < n_comp) {
        vals[tid] = row[tid];
        idxs[tid] = tid;
    } else {
        vals[tid] = -INFINITY;
        idxs[tid] = UINT32_MAX;
    }
    __syncthreads();

    for (uint32_t k = 2u; k <= 1024u; k <<= 1u) {
        for (uint32_t j = k >> 1u; j > 0u; j >>= 1u) {
            uint32_t other = tid ^ j;
            if (other > tid && other < 1024u) {
                const float av = vals[tid];
                const float bv = vals[other];
                const uint32_t ai = idxs[tid];
                const uint32_t bi = idxs[other];
                const bool desc_half = (tid & k) == 0u;
                const bool swap = desc_half
                    ? topk_score_better(bv, bi, av, ai)
                    : topk_score_better(av, ai, bv, bi);
                if (swap) {
                    vals[tid] = bv;
                    idxs[tid] = bi;
                    vals[other] = av;
                    idxs[other] = ai;
                }
            }
            __syncthreads();
        }
    }

    if (tid < top_k) selected[(uint64_t)t * top_k + tid] = idxs[tid];
}

template <uint32_t SORT_N>
__global__ static void indexer_topk_pow2_kernel(
        uint32_t *selected,
        const float *scores,
        uint32_t n_comp,
        uint32_t n_tokens,
        uint32_t top_k) {
    uint32_t t = blockIdx.x;
    uint32_t tid = threadIdx.x;
    if (t >= n_tokens) return;
    __shared__ float vals[SORT_N];
    __shared__ uint32_t idxs[SORT_N];

    const float *row = scores + (uint64_t)t * n_comp;
    for (uint32_t i = tid; i < SORT_N; i += blockDim.x) {
        if (i < n_comp) {
            vals[i] = row[i];
            idxs[i] = i;
        } else {
            vals[i] = -INFINITY;
            idxs[i] = UINT32_MAX;
        }
    }
    __syncthreads();

    for (uint32_t k = 2u; k <= SORT_N; k <<= 1u) {
        for (uint32_t j = k >> 1u; j > 0u; j >>= 1u) {
            for (uint32_t i = tid; i < SORT_N; i += blockDim.x) {
                uint32_t other = i ^ j;
                if (other > i && other < SORT_N) {
                    const float av = vals[i];
                    const float bv = vals[other];
                    const uint32_t ai = idxs[i];
                    const uint32_t bi = idxs[other];
                    const bool desc_half = (i & k) == 0u;
                    const bool swap = desc_half
                        ? topk_score_better(bv, bi, av, ai)
                        : topk_score_better(av, ai, bv, bi);
                    if (swap) {
                        vals[i] = bv;
                        idxs[i] = bi;
                        vals[other] = av;
                        idxs[other] = ai;
                    }
                }
            }
            __syncthreads();
        }
    }

    for (uint32_t i = tid; i < top_k; i += blockDim.x) {
        selected[(uint64_t)t * top_k + i] = idxs[i];
    }
}

template <uint32_t SORT_N>
__global__ static void indexer_topk_pow2_u16_kernel(
        uint32_t *selected,
        const float *scores,
        uint32_t n_comp,
        uint32_t n_tokens,
        uint32_t top_k) {
    uint32_t t = blockIdx.x;
    uint32_t tid = threadIdx.x;
    if (t >= n_tokens) return;
    __shared__ float vals[SORT_N];
    __shared__ uint16_t idxs[SORT_N];

    const float *row = scores + (uint64_t)t * n_comp;
    for (uint32_t i = tid; i < SORT_N; i += blockDim.x) {
        if (i < n_comp) {
            vals[i] = row[i];
            idxs[i] = (uint16_t)i;
        } else {
            vals[i] = -INFINITY;
            idxs[i] = UINT16_MAX;
        }
    }
    __syncthreads();

    for (uint32_t k = 2u; k <= SORT_N; k <<= 1u) {
        for (uint32_t j = k >> 1u; j > 0u; j >>= 1u) {
            for (uint32_t i = tid; i < SORT_N; i += blockDim.x) {
                uint32_t other = i ^ j;
                if (other > i && other < SORT_N) {
                    const float av = vals[i];
                    const float bv = vals[other];
                    const uint32_t ai = idxs[i];
                    const uint32_t bi = idxs[other];
                    const bool desc_half = (i & k) == 0u;
                    const bool swap = desc_half
                        ? topk_score_better(bv, bi, av, ai)
                        : topk_score_better(av, ai, bv, bi);
                    if (swap) {
                        vals[i] = bv;
                        idxs[i] = (uint16_t)bi;
                        vals[other] = av;
                        idxs[other] = (uint16_t)ai;
                    }
                }
            }
            __syncthreads();
        }
    }

    for (uint32_t i = tid; i < top_k; i += blockDim.x) {
        selected[(uint64_t)t * top_k + i] = idxs[i];
    }
}

template <uint32_t SORT_N>
__global__ static void indexer_topk_chunk_pow2_kernel(
        uint32_t *candidates,
        const float *scores,
        uint32_t n_comp,
        uint32_t n_tokens,
        uint32_t top_k,
        uint32_t candidate_stride) {
    uint32_t t = blockIdx.x;
    uint32_t chunk = blockIdx.y;
    uint32_t tid = threadIdx.x;
    if (t >= n_tokens) return;

    const uint32_t chunk_start = chunk * SORT_N;
    if (chunk_start >= n_comp) return;
    const uint32_t chunk_n = n_comp - chunk_start < SORT_N ? n_comp - chunk_start : SORT_N;
    __shared__ float vals[SORT_N];
    __shared__ uint32_t idxs[SORT_N];

    const float *row = scores + (uint64_t)t * n_comp;
    for (uint32_t i = tid; i < SORT_N; i += blockDim.x) {
        if (i < chunk_n) {
            vals[i] = row[chunk_start + i];
            idxs[i] = chunk_start + i;
        } else {
            vals[i] = -INFINITY;
            idxs[i] = UINT32_MAX;
        }
    }
    __syncthreads();

    for (uint32_t k = 2u; k <= SORT_N; k <<= 1u) {
        for (uint32_t j = k >> 1u; j > 0u; j >>= 1u) {
            for (uint32_t i = tid; i < SORT_N; i += blockDim.x) {
                uint32_t other = i ^ j;
                if (other > i && other < SORT_N) {
                    const float av = vals[i];
                    const float bv = vals[other];
                    const uint32_t ai = idxs[i];
                    const uint32_t bi = idxs[other];
                    const bool desc_half = (i & k) == 0u;
                    const bool swap = desc_half
                        ? topk_score_better(bv, bi, av, ai)
                        : topk_score_better(av, ai, bv, bi);
                    if (swap) {
                        vals[i] = bv;
                        idxs[i] = bi;
                        vals[other] = av;
                        idxs[other] = ai;
                    }
                }
            }
            __syncthreads();
        }
    }

    uint32_t *out = candidates + (uint64_t)t * candidate_stride + chunk * top_k;
    for (uint32_t i = tid; i < top_k; i += blockDim.x) {
        out[i] = idxs[i];
    }
}

template <uint32_t SORT_N>
__global__ static void indexer_topk_merge_pow2_kernel(
        uint32_t *selected,
        const uint32_t *candidates,
        const float *scores,
        uint32_t n_comp,
        uint32_t n_tokens,
        uint32_t top_k,
        uint32_t candidate_count,
        uint32_t candidate_stride) {
    uint32_t t = blockIdx.x;
    uint32_t tid = threadIdx.x;
    if (t >= n_tokens) return;
    __shared__ float vals[SORT_N];
    __shared__ uint32_t idxs[SORT_N];

    const float *row = scores + (uint64_t)t * n_comp;
    const uint32_t *cand = candidates + (uint64_t)t * candidate_stride;
    for (uint32_t i = tid; i < SORT_N; i += blockDim.x) {
        uint32_t idx = UINT32_MAX;
        float v = -INFINITY;
        if (i < candidate_count) {
            idx = cand[i];
            if (idx < n_comp) v = row[idx];
        }
        vals[i] = v;
        idxs[i] = idx;
    }
    __syncthreads();

    for (uint32_t k = 2u; k <= SORT_N; k <<= 1u) {
        for (uint32_t j = k >> 1u; j > 0u; j >>= 1u) {
            for (uint32_t i = tid; i < SORT_N; i += blockDim.x) {
                uint32_t other = i ^ j;
                if (other > i && other < SORT_N) {
                    const float av = vals[i];
                    const float bv = vals[other];
                    const uint32_t ai = idxs[i];
                    const uint32_t bi = idxs[other];
                    const bool desc_half = (i & k) == 0u;
                    const bool swap = desc_half
                        ? topk_score_better(bv, bi, av, ai)
                        : topk_score_better(av, ai, bv, bi);
                    if (swap) {
                        vals[i] = bv;
                        idxs[i] = bi;
                        vals[other] = av;
                        idxs[other] = ai;
                    }
                }
            }
            __syncthreads();
        }
    }

    for (uint32_t i = tid; i < top_k; i += blockDim.x) {
        selected[(uint64_t)t * top_k + i] = idxs[i];
    }
}

template <uint32_t SORT_N>
__global__ static void indexer_topk_tree_merge_pow2_kernel(
        uint32_t *out,
        const uint32_t *candidates,
        const float *scores,
        uint32_t n_comp,
        uint32_t n_tokens,
        uint32_t top_k,
        uint32_t n_sets,
        uint32_t merge_group,
        uint32_t candidate_stride,
        uint32_t out_stride) {
    uint32_t t = blockIdx.x;
    uint32_t group = blockIdx.y;
    uint32_t tid = threadIdx.x;
    if (t >= n_tokens) return;

    const uint32_t set0 = group * merge_group;
    if (set0 >= n_sets) return;
    uint32_t set_count = n_sets - set0;
    if (set_count > merge_group) set_count = merge_group;
    const uint32_t candidate_count = set_count * top_k;

    __shared__ float vals[SORT_N];
    __shared__ uint32_t idxs[SORT_N];

    const float *row = scores + (uint64_t)t * n_comp;
    const uint32_t *cand = candidates + (uint64_t)t * candidate_stride + set0 * top_k;
    for (uint32_t i = tid; i < SORT_N; i += blockDim.x) {
        uint32_t idx = UINT32_MAX;
        float v = -INFINITY;
        if (i < candidate_count) {
            idx = cand[i];
            if (idx < n_comp) v = row[idx];
        }
        vals[i] = v;
        idxs[i] = idx;
    }
    __syncthreads();

    for (uint32_t k = 2u; k <= SORT_N; k <<= 1u) {
        for (uint32_t j = k >> 1u; j > 0u; j >>= 1u) {
            for (uint32_t i = tid; i < SORT_N; i += blockDim.x) {
                uint32_t other = i ^ j;
                if (other > i && other < SORT_N) {
                    const float av = vals[i];
                    const float bv = vals[other];
                    const uint32_t ai = idxs[i];
                    const uint32_t bi = idxs[other];
                    const bool desc_half = (i & k) == 0u;
                    const bool swap = desc_half
                        ? topk_score_better(bv, bi, av, ai)
                        : topk_score_better(av, ai, bv, bi);
                    if (swap) {
                        vals[i] = bv;
                        idxs[i] = bi;
                        vals[other] = av;
                        idxs[other] = ai;
                    }
                }
            }
            __syncthreads();
        }
    }

    uint32_t *dst = out + (uint64_t)t * out_stride + group * top_k;
    for (uint32_t i = tid; i < top_k; i += blockDim.x) {
        dst[i] = idxs[i];
    }
}

__global__ static void indexed_topk_sort_512_asc_kernel(
        int32_t *dst,
        const int32_t *src,
        uint32_t n_tokens) {
    const uint32_t t = blockIdx.x;
    const uint32_t tid = threadIdx.x;
    if (t >= n_tokens || tid >= 512u) return;
    __shared__ int32_t rows[512];

    const int32_t *src_row = src + (uint64_t)t * 512u;
    int32_t *dst_row = dst + (uint64_t)t * 512u;
    rows[tid] = src_row[tid];
    __syncthreads();

    for (uint32_t k = 2u; k <= 512u; k <<= 1u) {
        for (uint32_t j = k >> 1u; j > 0u; j >>= 1u) {
            const uint32_t other = tid ^ j;
            if (other > tid && other < 512u) {
                const int32_t a = rows[tid];
                const int32_t b = rows[other];
                const bool up = (tid & k) == 0u;
                if ((up && a > b) || (!up && a < b)) {
                    rows[tid] = b;
                    rows[other] = a;
                }
            }
            __syncthreads();
        }
    }

    dst_row[tid] = rows[tid];
}

__global__ static void topk_mask_kernel(float *mask, const uint32_t *topk, uint32_t n_comp, uint32_t n_tokens, uint32_t top_k) {
    uint64_t gid = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    uint64_t n = (uint64_t)n_tokens * n_comp;
    if (gid >= n) return;
    uint32_t t = gid / n_comp;
    uint32_t c = gid - (uint64_t)t * n_comp;
    float v = -INFINITY;
    for (uint32_t k = 0; k < top_k; k++) {
        if (topk[(uint64_t)t * top_k + k] == c) {
            v = 0.0f;
            break;
        }
    }
    mask[gid] = v;
}

extern "C" int ds4_gpu_embed_token_hc_tensor(ds4_gpu_tensor *out_hc, const void *model_map, uint64_t model_size, uint64_t weight_offset, uint32_t n_vocab, uint32_t token, uint32_t n_embd, uint32_t n_hc) {
    (void)n_vocab;
    if (!out_hc || !model_map || weight_offset >= model_size) return 0;
    uint64_t weight_bytes = (uint64_t)n_vocab * n_embd * sizeof(uint16_t);
    if (weight_offset > model_size || weight_bytes > model_size - weight_offset) return 0;
    const char *wptr = cuda_model_range_ptr(model_map, weight_offset, weight_bytes, "token_embd");
    if (!wptr) return 0;
    uint32_t n = n_embd * n_hc;
    embed_token_hc_kernel<<<(n + 255) / 256, 256>>>((float *)out_hc->ptr, (const unsigned short *)wptr, token, n_embd, n_hc);
    return cuda_ok(cudaGetLastError(), "embed token launch");
}

extern "C" int ds4_gpu_embed_tokens_hc_tensor(
        ds4_gpu_tensor       *out_hc,
        const ds4_gpu_tensor *tokens_t,
        const void             *model_map,
        uint64_t                model_size,
        uint64_t                weight_offset,
        uint32_t                n_vocab,
        uint32_t                n_tokens,
        uint32_t                n_embd,
        uint32_t                n_hc) {
    if (!out_hc || !tokens_t || !model_map ||
        weight_offset > model_size ||
        (uint64_t)n_vocab * n_embd * sizeof(uint16_t) > model_size - weight_offset ||
        tokens_t->bytes < (uint64_t)n_tokens * sizeof(int32_t) ||
        out_hc->bytes < (uint64_t)n_tokens * n_hc * n_embd * sizeof(float)) {
        return 0;
    }
    const char *wptr = cuda_model_range_ptr(model_map, weight_offset,
                                            (uint64_t)n_vocab * n_embd * sizeof(uint16_t),
                                            "token_embd");
    if (!wptr) return 0;
    uint64_t n = (uint64_t)n_tokens * n_hc * n_embd;
    embed_tokens_hc_kernel<<<(n + 255) / 256, 256>>>(
        (float *)out_hc->ptr,
        (const int32_t *)tokens_t->ptr,
        (const unsigned short *)wptr,
        n_vocab, n_tokens, n_embd, n_hc);
    return cuda_ok(cudaGetLastError(), "embed tokens launch");
}

static int indexer_scores_launch(
        ds4_gpu_tensor       *scores,
        const ds4_gpu_tensor *q,
        const ds4_gpu_tensor *weights,
        const ds4_gpu_tensor *index_comp,
        uint32_t                n_comp,
        uint32_t                n_tokens,
        uint32_t                pos0,
        uint32_t                n_head,
        uint32_t                head_dim,
        uint32_t                ratio,
        float                   scale,
        uint32_t                causal) {
    if (!scores || !q || !weights || !index_comp ||
        n_comp == 0 || n_tokens == 0 || n_head == 0 || head_dim == 0 ||
        q->bytes < (uint64_t)n_tokens * n_head * head_dim * sizeof(float) ||
        weights->bytes < (uint64_t)n_tokens * n_head * sizeof(float) ||
        index_comp->bytes < (uint64_t)n_comp * head_dim * sizeof(float) ||
        scores->bytes < (uint64_t)n_tokens * n_comp * sizeof(float)) {
        return 0;
    }
    if (causal && ratio == 0) return 0;
    if (n_tokens == 1u && head_dim == 128u && n_head == 64u &&
        getenv("DS4_CUDA_NO_INDEXER_DIRECT_ONE") == NULL) {
        indexer_score_one_direct_kernel<<<n_comp, 128>>>((float *)scores->ptr,
                                                         (const float *)q->ptr,
                                                         (const float *)weights->ptr,
                                                         (const float *)index_comp->ptr,
                                                         n_comp, pos0, ratio,
                                                         scale, causal ? 1 : 0);
        return cuda_ok(cudaGetLastError(), "indexer score one direct launch");
    }
    if (!g_quality_mode && head_dim == 128u && n_head == 64u &&
        getenv("DS4_CUDA_NO_INDEXER_WMMA") == NULL) {
        if (getenv("DS4_CUDA_NO_INDEXER_WMMA128") == NULL) {
            dim3 grid((n_comp + 127u) / 128u, (n_tokens + 15u) / 16u, 1);
            indexer_scores_wmma128_kernel<<<grid, 256>>>((float *)scores->ptr,
                                                         (const float *)q->ptr,
                                                         (const float *)weights->ptr,
                                                         (const float *)index_comp->ptr,
                                                         n_comp, n_tokens, pos0, n_head,
                                                         head_dim, ratio, scale, causal ? 1 : 0);
            return cuda_ok(cudaGetLastError(), "indexer scores wmma128 launch");
        } else if (getenv("DS4_CUDA_NO_INDEXER_WMMA64") == NULL) {
            dim3 grid((n_comp + 63u) / 64u, (n_tokens + 15u) / 16u, 1);
            indexer_scores_wmma64_kernel<<<grid, 128>>>((float *)scores->ptr,
                                                        (const float *)q->ptr,
                                                        (const float *)weights->ptr,
                                                        (const float *)index_comp->ptr,
                                                        n_comp, n_tokens, pos0, n_head,
                                                        head_dim, ratio, scale, causal ? 1 : 0);
            return cuda_ok(cudaGetLastError(), "indexer scores wmma64 launch");
        } else if (getenv("DS4_CUDA_NO_INDEXER_WMMA32") == NULL) {
            dim3 grid((n_comp + 31u) / 32u, (n_tokens + 15u) / 16u, 1);
            indexer_scores_wmma32_kernel<<<grid, 64>>>((float *)scores->ptr,
                                                       (const float *)q->ptr,
                                                       (const float *)weights->ptr,
                                                       (const float *)index_comp->ptr,
                                                       n_comp, n_tokens, pos0, n_head,
                                                       head_dim, ratio, scale, causal ? 1 : 0);
            return cuda_ok(cudaGetLastError(), "indexer scores wmma32 launch");
        } else {
            dim3 grid((n_comp + 15u) / 16u, (n_tokens + 15u) / 16u, 1);
            indexer_scores_wmma_kernel<<<grid, 32>>>((float *)scores->ptr,
                                                     (const float *)q->ptr,
                                                     (const float *)weights->ptr,
                                                     (const float *)index_comp->ptr,
                                                     n_comp, n_tokens, pos0, n_head,
                                                     head_dim, ratio, scale, causal ? 1 : 0);
            return cuda_ok(cudaGetLastError(), "indexer scores wmma launch");
        }
    }
    dim3 grid(n_comp, n_tokens, 1);
    indexer_scores_kernel<<<grid, 256>>>((float *)scores->ptr,
                                         (const float *)q->ptr,
                                         (const float *)weights->ptr,
                                         (const float *)index_comp->ptr,
                                         n_comp, n_tokens, pos0, n_head,
                                         head_dim, ratio, scale, causal ? 1 : 0);
    return cuda_ok(cudaGetLastError(), "indexer scores launch");
}

extern "C" int ds4_gpu_indexer_score_one_tensor(
        ds4_gpu_tensor       *scores,
        const ds4_gpu_tensor *q,
        const ds4_gpu_tensor *weights,
        const ds4_gpu_tensor *index_comp,
        uint32_t                n_comp,
        uint32_t                n_head,
        uint32_t                head_dim,
        float                   scale) {
    return indexer_scores_launch(scores, q, weights, index_comp, n_comp, 1, 0,
                                 n_head, head_dim, 1, scale, 0);
}

extern "C" int ds4_gpu_indexer_scores_prefill_tensor(
        ds4_gpu_tensor       *scores,
        const ds4_gpu_tensor *q,
        const ds4_gpu_tensor *weights,
        const ds4_gpu_tensor *index_comp,
        uint32_t                n_comp,
        uint32_t                n_tokens,
        uint32_t                n_head,
        uint32_t                head_dim,
        uint32_t                ratio,
        float                   scale) {
    return indexer_scores_launch(scores, q, weights, index_comp, n_comp, n_tokens, 0,
                                 n_head, head_dim, ratio, scale, 1);
}

extern "C" int ds4_gpu_indexer_scores_decode_batch_tensor(
        ds4_gpu_tensor       *scores,
        const ds4_gpu_tensor *q,
        const ds4_gpu_tensor *weights,
        const ds4_gpu_tensor *index_comp,
        uint32_t                n_comp,
        uint32_t                n_tokens,
        uint32_t                pos0,
        uint32_t                n_head,
        uint32_t                head_dim,
        uint32_t                ratio,
        float                   scale) {
    return indexer_scores_launch(scores, q, weights, index_comp, n_comp, n_tokens, pos0,
                                 n_head, head_dim, ratio, scale, 1);
}

extern "C" int ds4_gpu_indexer_topk_tensor(
        ds4_gpu_tensor       *selected,
        const ds4_gpu_tensor *scores,
        uint32_t                n_comp,
        uint32_t                n_tokens,
        uint32_t                top_k) {
    if (!selected || !scores || n_comp == 0 || n_tokens == 0 || top_k == 0 ||
        top_k > n_comp ||
        scores->bytes < (uint64_t)n_tokens * n_comp * sizeof(float) ||
        selected->bytes < (uint64_t)n_tokens * top_k * sizeof(uint32_t)) {
        return 0;
    }
    if (top_k == 512u && n_comp <= 1024u &&
        getenv("DS4_CUDA_NO_TOPK1024") == NULL) {
        indexer_topk_1024_kernel<<<n_tokens, 1024>>>((uint32_t *)selected->ptr,
                                                     (const float *)scores->ptr,
                                                     n_comp, n_tokens, top_k);
        return cuda_ok(cudaGetLastError(), "indexer topk 1024 launch");
    }
    if (top_k == 512u && n_comp <= 2048u &&
        getenv("DS4_CUDA_NO_TOPK2048") == NULL) {
        indexer_topk_pow2_kernel<2048><<<n_tokens, 1024>>>((uint32_t *)selected->ptr,
                                                           (const float *)scores->ptr,
                                                           n_comp, n_tokens, top_k);
        return cuda_ok(cudaGetLastError(), "indexer topk 2048 launch");
    }
    if (top_k == 512u && n_comp <= 4096u &&
        getenv("DS4_CUDA_NO_TOPK2048") == NULL) {
        if (n_comp == 4096u) {
            using TopkCubSort = cub::BlockRadixSort<uint64_t, 512, 16>;
            const int smem = (int)sizeof(typename TopkCubSort::TempStorage);
            int dev = 0;
            int max_optin_smem = 0;
            cudaError_t attr_err = cudaGetDevice(&dev);
            if (attr_err == cudaSuccess) {
                attr_err = cudaDeviceGetAttribute(&max_optin_smem,
                                                  cudaDevAttrMaxSharedMemoryPerBlockOptin,
                                                  dev);
            }
            if (attr_err == cudaSuccess && max_optin_smem >= smem) {
                attr_err = cudaFuncSetAttribute(indexer_topk_8192_cub_kernel,
                                                cudaFuncAttributeMaxDynamicSharedMemorySize,
                                                smem);
                if (attr_err == cudaSuccess) {
                    indexer_topk_8192_cub_kernel<<<n_tokens, 512, (size_t)smem>>>((uint32_t *)selected->ptr,
                                                                                 (const float *)scores->ptr,
                                                                                 n_comp, n_tokens, top_k);
                    return cuda_ok(cudaGetLastError(), "indexer topk 4096 cub launch");
                }
            }
        }
        indexer_topk_pow2_kernel<4096><<<n_tokens, 1024>>>((uint32_t *)selected->ptr,
                                                           (const float *)scores->ptr,
                                                           n_comp, n_tokens, top_k);
        return cuda_ok(cudaGetLastError(), "indexer topk 4096 launch");
    }
    if (top_k == 512u && n_comp <= 8192u &&
        getenv("DS4_CUDA_NO_TOPK2048") == NULL &&
        getenv("DS4_CUDA_NO_TOPK8192") == NULL) {
        if (n_comp > 4096u) {
            using TopkCubSort = cub::BlockRadixSort<uint64_t, 512, 16>;
            const int smem = (int)sizeof(typename TopkCubSort::TempStorage);
            int dev = 0;
            int max_optin_smem = 0;
            cudaError_t attr_err = cudaGetDevice(&dev);
            if (attr_err == cudaSuccess) {
                attr_err = cudaDeviceGetAttribute(&max_optin_smem,
                                                  cudaDevAttrMaxSharedMemoryPerBlockOptin,
                                                  dev);
            }
            if (attr_err == cudaSuccess && max_optin_smem >= smem) {
                attr_err = cudaFuncSetAttribute(indexer_topk_8192_cub_kernel,
                                                cudaFuncAttributeMaxDynamicSharedMemorySize,
                                                smem);
                if (attr_err == cudaSuccess) {
                    indexer_topk_8192_cub_kernel<<<n_tokens, 512, (size_t)smem>>>((uint32_t *)selected->ptr,
                                                                                 (const float *)scores->ptr,
                                                                                 n_comp, n_tokens, top_k);
                    return cuda_ok(cudaGetLastError(), "indexer topk 8192 cub launch");
                }
            }
        }
        indexer_topk_pow2_u16_kernel<8192><<<n_tokens, 1024>>>((uint32_t *)selected->ptr,
                                                               (const float *)scores->ptr,
                                                               n_comp, n_tokens, top_k);
        return cuda_ok(cudaGetLastError(), "indexer topk 8192 launch");
    }
    if (top_k == 512u && getenv("DS4_CUDA_NO_TOPK2048") == NULL &&
        getenv("DS4_CUDA_NO_TOPK_CHUNKED") == NULL) {
        const uint32_t chunk_n = 4096u;
        const uint32_t n_chunks = (n_comp + chunk_n - 1u) / chunk_n;
        const uint32_t candidate_stride = n_chunks * top_k;
        uint32_t n_sets = n_chunks;
        uint64_t scratch_u32_per_token = candidate_stride;
        while (n_sets > DS4_CUDA_TOPK_MERGE_GROUP) {
            n_sets = (n_sets + DS4_CUDA_TOPK_MERGE_GROUP - 1u) / DS4_CUDA_TOPK_MERGE_GROUP;
            scratch_u32_per_token += (uint64_t)n_sets * top_k;
        }
        if (scratch_u32_per_token > UINT64_MAX / n_tokens / sizeof(uint32_t)) return 0;
        const uint64_t tmp_bytes = (uint64_t)n_tokens * scratch_u32_per_token * sizeof(uint32_t);
        uint32_t *scratch = (uint32_t *)cuda_tmp_alloc(tmp_bytes, "indexer topk tree");
        if (!scratch) return 0;

        uint32_t *cur = scratch;
        n_sets = n_chunks;
        uint32_t cur_stride = candidate_stride;
        dim3 grid_chunks(n_tokens, n_chunks, 1);
        indexer_topk_chunk_pow2_kernel<4096><<<grid_chunks, 1024>>>(cur,
                                                                    (const float *)scores->ptr,
                                                                    n_comp,
                                                                    n_tokens,
                                                                    top_k,
                                                                    candidate_stride);
        if (!cuda_ok(cudaGetLastError(), "indexer topk chunk launch")) return 0;

        while (n_sets > DS4_CUDA_TOPK_MERGE_GROUP) {
            const uint32_t next_sets = (n_sets + DS4_CUDA_TOPK_MERGE_GROUP - 1u) / DS4_CUDA_TOPK_MERGE_GROUP;
            const uint32_t next_stride = next_sets * top_k;
            uint32_t *next = cur + (uint64_t)n_tokens * cur_stride;
            dim3 grid_merge(n_tokens, next_sets, 1);
            indexer_topk_tree_merge_pow2_kernel<4096><<<grid_merge, 1024>>>(
                    next,
                    cur,
                    (const float *)scores->ptr,
                    n_comp,
                    n_tokens,
                    top_k,
                    n_sets,
                    DS4_CUDA_TOPK_MERGE_GROUP,
                    cur_stride,
                    next_stride);
            if (!cuda_ok(cudaGetLastError(), "indexer topk tree merge launch")) return 0;
            cur = next;
            n_sets = next_sets;
            cur_stride = next_stride;
        }

        indexer_topk_merge_pow2_kernel<4096><<<n_tokens, 1024>>>((uint32_t *)selected->ptr,
                                                                 cur,
                                                                 (const float *)scores->ptr,
                                                                 n_comp,
                                                                 n_tokens,
                                                                 top_k,
                                                                 n_sets * top_k,
                                                                 cur_stride);
        return cuda_ok(cudaGetLastError(), "indexer topk tree final launch");
    }
    indexer_topk_kernel<<<n_tokens, 1>>>((uint32_t *)selected->ptr,
                                         (const float *)scores->ptr,
                                         n_comp, n_tokens, top_k);
    return cuda_ok(cudaGetLastError(), "indexer topk launch");
}

extern "C" int ds4_gpu_dsv4_topk_mask_tensor(
        ds4_gpu_tensor       *mask,
        const ds4_gpu_tensor *topk,
        uint32_t                n_comp,
        uint32_t                n_tokens,
        uint32_t                top_k) {
    if (!mask || !topk || n_comp == 0 || n_tokens == 0 || top_k == 0 ||
        mask->bytes < (uint64_t)n_tokens * n_comp * sizeof(float) ||
        topk->bytes < (uint64_t)n_tokens * top_k * sizeof(uint32_t)) {
        return 0;
    }
    uint64_t n = (uint64_t)n_tokens * n_comp;
    uint64_t nk = (uint64_t)n_tokens * top_k;
    uint64_t blocks = ((n > nk ? n : nk) + 255) / 256;
    topk_mask_kernel<<<blocks, 256>>>((float *)mask->ptr,
                                      (const uint32_t *)topk->ptr,
                                      n_comp, n_tokens, top_k);
    return cuda_ok(cudaGetLastError(), "topk mask launch");
}
static int cuda_matmul_q8_0_tensor_labeled(ds4_gpu_tensor *out, const void *model_map, uint64_t model_size, uint64_t weight_offset, uint64_t in_dim, uint64_t out_dim, const ds4_gpu_tensor *x, uint64_t n_tok, const char *label) {
    if (!out || !x || !model_map) return 0;
    uint64_t blocks = (in_dim + 31) / 32;
    if (weight_offset > model_size || out_dim > UINT64_MAX / (blocks * 34)) return 0;
    uint64_t weight_bytes = out_dim * blocks * 34;
    if (weight_bytes > model_size - weight_offset) return 0;
    if (x->bytes < n_tok * in_dim * sizeof(float) ||
        out->bytes < n_tok * out_dim * sizeof(float)) return 0;
    const char *wptr = cuda_model_range_ptr(model_map, weight_offset, weight_bytes, "q8_0");
    if (!wptr) return 0;
    if (g_cublas_ready && n_tok > 1) {
        const float *w_f32 = cuda_q8_f32_ptr(model_map, weight_offset, weight_bytes, in_dim, out_dim, label);
        if (w_f32) {
            const float alpha = 1.0f;
            const float beta = 0.0f;
            cublasStatus_t st = cublasSgemm(g_cublas,
                                            CUBLAS_OP_T,
                                            CUBLAS_OP_N,
                                            (int)out_dim,
                                            (int)n_tok,
                                            (int)in_dim,
                                            &alpha,
                                            w_f32,
                                            (int)in_dim,
                                            (const float *)x->ptr,
                                            (int)in_dim,
                                            &beta,
                                            (float *)out->ptr,
                                            (int)out_dim);
            return cublas_ok(st, "q8 fp32 matmul");
        }
        const __half *w_f16 = cuda_q8_f16_ptr(model_map, weight_offset, weight_bytes, in_dim, out_dim, label);
        if (w_f16) {
            const uint64_t xh_count = n_tok * in_dim;
            __half *xh = (__half *)cuda_tmp_alloc(xh_count * sizeof(__half), "q8 f16 gemm activations");
            if (!xh) return 0;
            f32_to_f16_kernel<<<(xh_count + 255) / 256, 256>>>(xh, (const float *)x->ptr, xh_count);
            if (!cuda_ok(cudaGetLastError(), "q8 f16 activation convert launch")) return 0;
            const float alpha = 1.0f;
            const float beta = 0.0f;
            cublasStatus_t st = cublasGemmEx(g_cublas,
                                             CUBLAS_OP_T,
                                             CUBLAS_OP_N,
                                             (int)out_dim,
                                             (int)n_tok,
                                             (int)in_dim,
                                             &alpha,
                                             w_f16,
                                             CUDA_R_16F,
                                             (int)in_dim,
                                             xh,
                                             CUDA_R_16F,
                                             (int)in_dim,
                                             &beta,
                                             out->ptr,
                                             CUDA_R_32F,
                                             (int)out_dim,
                                             CUDA_R_32F,
                                             CUBLAS_GEMM_DEFAULT);
            if (st == CUBLAS_STATUS_SUCCESS) return 1;
            fprintf(stderr, "ds4: cuBLAS q8 f16 matmul failed: status %d\n", (int)st);
            cuda_q8_f16_cache_disable_after_failure("cuBLAS f16 matmul failure",
                                                    in_dim * out_dim * sizeof(__half));
            /* The F16 expansion cache is only an optimization.  If cuBLAS
             * rejects the cached path under memory pressure, retry the same
             * operation through the native Q8 kernels below. */
        }
    }
    const uint64_t xq_bytes = n_tok * blocks * 32u;
    const uint64_t scale_offset = (xq_bytes + 15u) & ~15ull;
    const uint64_t tmp_bytes = scale_offset + n_tok * blocks * sizeof(float);
    void *tmp = cuda_tmp_alloc(tmp_bytes, "q8_0 prequant");
    if (!tmp) return 0;
    int8_t *xq = (int8_t *)tmp;
    float *xscale = (float *)((char *)tmp + scale_offset);
    const int use_dp4a = cuda_q8_use_dp4a();
    dim3 qgrid((unsigned)blocks, (unsigned)n_tok, 1);
    quantize_q8_0_f32_kernel<<<qgrid, 32>>>(xq, xscale, (const float *)x->ptr, in_dim, blocks);
    if (!cuda_ok(cudaGetLastError(), "matmul_q8_0 quantize launch")) return 0;
    if (n_tok == 1) {
        matmul_q8_0_preq_warp8_kernel<<<((unsigned)out_dim + 7u) / 8u, 256>>>(
                (float *)out->ptr,
                reinterpret_cast<const unsigned char *>(wptr),
                xq,
                xscale,
                in_dim,
                out_dim,
                blocks,
                use_dp4a);
        return cuda_ok(cudaGetLastError(), "matmul_q8_0 warp launch");
    }
    if (getenv("DS4_CUDA_NO_Q8_BATCH_WARP") == NULL && blocks <= 32u) {
        dim3 bgrid(((unsigned)out_dim + 7u) / 8u, (unsigned)n_tok, 1);
        matmul_q8_0_preq_batch_warp8_kernel<<<bgrid, 256>>>(
                (float *)out->ptr,
                reinterpret_cast<const unsigned char *>(wptr),
                xq,
                xscale,
                in_dim,
                out_dim,
                n_tok,
                blocks,
                use_dp4a);
        return cuda_ok(cudaGetLastError(), "matmul_q8_0 batch warp launch");
    }
    dim3 grid((unsigned)out_dim, (unsigned)n_tok, 1);
    matmul_q8_0_preq_kernel<<<grid, 256>>>((float *)out->ptr,
                                           reinterpret_cast<const unsigned char *>(wptr),
                                           xq,
                                           xscale,
                                           in_dim, out_dim, n_tok, blocks,
                                           use_dp4a);
    return cuda_ok(cudaGetLastError(), "matmul_q8_0 launch");
}

extern "C" int ds4_gpu_matmul_q8_0_tensor(ds4_gpu_tensor *out, const void *model_map, uint64_t model_size, uint64_t weight_offset, uint64_t in_dim, uint64_t out_dim, const ds4_gpu_tensor *x, uint64_t n_tok) {
    return cuda_matmul_q8_0_tensor_labeled(out, model_map, model_size, weight_offset,
                                           in_dim, out_dim, x, n_tok, "q8_0");
}

static int cuda_q8_0_arena_matmul_view_ok(const ds4_gpu_arena *arena,
                                          const ds4_gpu_source_row_view *view,
                                          const ds4_gpu_tensor *x_f32,
                                          const ds4_gpu_tensor *out_f32,
                                          uint64_t n_tok,
                                          uint64_t *blocks_out) {
    if (!arena || !view || !x_f32 || !out_f32 || !arena->valid ||
        !arena->ptr || !x_f32->ptr || !out_f32->ptr || n_tok == 0) {
        return 0;
    }
    if (x_f32->device != arena->gpu || out_f32->device != arena->gpu) return 0;
    if (view->rows == 0 || view->cols == 0 || view->row_stride_bytes == 0) return 0;
    if (!cuda_arena_range_ok(arena, view->arena_offset, view->byte_length)) return 0;

    const uint64_t blocks = ((uint64_t)view->cols + 31ull) / 32ull;
    if (blocks == 0 || blocks > UINT64_MAX / 34ull) return 0;
    const uint64_t row_bytes = blocks * 34ull;
    if ((uint64_t)view->row_stride_bytes < row_bytes) return 0;

    uint64_t last_start = 0;
    if (checked_mul_u64((uint64_t)view->rows - 1u,
                        (uint64_t)view->row_stride_bytes,
                        &last_start)) {
        return 0;
    }
    if (last_start > view->byte_length || row_bytes > view->byte_length - last_start) {
        return 0;
    }

    uint64_t x_values = 0;
    uint64_t out_values = 0;
    uint64_t x_bytes = 0;
    uint64_t out_bytes = 0;
    if (checked_mul_u64(n_tok, (uint64_t)view->cols, &x_values) ||
        checked_mul_u64(n_tok, (uint64_t)view->rows, &out_values) ||
        checked_mul_u64(x_values, sizeof(float), &x_bytes) ||
        checked_mul_u64(out_values, sizeof(float), &out_bytes)) {
        return 0;
    }
    if (x_f32->bytes < x_bytes || out_f32->bytes < out_bytes) return 0;
    if (n_tok > UINT32_MAX || view->rows > UINT32_MAX || blocks > UINT32_MAX) return 0;
    if (blocks_out) *blocks_out = blocks;
    return 1;
}

extern "C" int ds4_gpu_arena_q8_0_matmul_f32(
        const ds4_gpu_arena           *arena,
        const ds4_gpu_source_row_view *view,
        const ds4_gpu_tensor          *x_f32,
        ds4_gpu_tensor                *out_f32,
        uint64_t                       n_tok) {
    uint64_t blocks = 0;
    if (!cuda_q8_0_arena_matmul_view_ok(arena, view, x_f32, out_f32, n_tok, &blocks)) {
        return 1;
    }
    if (!cuda_ok(cudaSetDevice(arena->gpu), "q8 arena matmul set device")) return 1;

    const uint64_t xq_bytes = n_tok * blocks * 32u;
    const uint64_t scale_offset = (xq_bytes + 15u) & ~15ull;
    const uint64_t tmp_bytes = scale_offset + n_tok * blocks * sizeof(float);
    void *tmp = cuda_tmp_alloc(tmp_bytes, "q8_0 arena prequant");
    if (!tmp) return 1;
    int8_t *xq = (int8_t *)tmp;
    float *xscale = (float *)((char *)tmp + scale_offset);

    dim3 qgrid((unsigned)blocks, (unsigned)n_tok, 1);
    quantize_q8_0_f32_kernel<<<qgrid, 32>>>(
            xq,
            xscale,
            (const float *)x_f32->ptr,
            view->cols,
            blocks);
    if (!cuda_ok(cudaGetLastError(), "q8 arena quantize launch")) return 1;

    const unsigned char *wptr =
        (const unsigned char *)((const char *)arena->ptr + view->arena_offset);
    const int use_dp4a = cuda_q8_use_dp4a();
    if (n_tok == 1) {
        matmul_q8_0_preq_warp8_kernel<<<((unsigned)view->rows + 7u) / 8u, 256>>>(
                (float *)out_f32->ptr,
                wptr,
                xq,
                xscale,
                view->cols,
                view->rows,
                blocks,
                use_dp4a);
        return cuda_ok(cudaGetLastError(), "q8 arena warp launch") ? 0 : 1;
    }
    if (getenv("DS4_CUDA_NO_Q8_BATCH_WARP") == NULL && blocks <= 32u) {
        dim3 bgrid(((unsigned)view->rows + 7u) / 8u, (unsigned)n_tok, 1);
        matmul_q8_0_preq_batch_warp8_kernel<<<bgrid, 256>>>(
                (float *)out_f32->ptr,
                wptr,
                xq,
                xscale,
                view->cols,
                view->rows,
                n_tok,
                blocks,
                use_dp4a);
        return cuda_ok(cudaGetLastError(), "q8 arena batch warp launch") ? 0 : 1;
    }
    dim3 grid((unsigned)view->rows, (unsigned)n_tok, 1);
    matmul_q8_0_preq_kernel<<<grid, 256>>>(
            (float *)out_f32->ptr,
            wptr,
            xq,
            xscale,
            view->cols,
            view->rows,
            n_tok,
            blocks,
            use_dp4a);
    return cuda_ok(cudaGetLastError(), "q8 arena launch") ? 0 : 1;
}

static int cuda_f32_arena_norm_view_ok(const ds4_gpu_arena *arena,
                                       const ds4_gpu_source_row_view *weight,
                                       const ds4_gpu_tensor *x_f32,
                                       const ds4_gpu_tensor *out_f32,
                                       uint32_t n,
                                       uint32_t rows) {
    if (!arena || !weight || !x_f32 || !out_f32 || !arena->valid ||
        !arena->ptr || !x_f32->ptr || !out_f32->ptr || n == 0 || rows == 0) {
        return 0;
    }
    if (x_f32->device != arena->gpu || out_f32->device != arena->gpu) return 0;
    if (weight->rows != 1 || weight->cols != n) return 0;
    if ((weight->arena_offset % sizeof(float)) != 0 ||
        (weight->row_stride_bytes % sizeof(float)) != 0) {
        return 0;
    }
    if (!cuda_arena_range_ok(arena, weight->arena_offset, weight->byte_length)) return 0;

    uint64_t row_bytes = 0;
    uint64_t values = 0;
    uint64_t tensor_bytes = 0;
    if (checked_mul_u64((uint64_t)n, sizeof(float), &row_bytes) ||
        weight->row_stride_bytes < row_bytes ||
        weight->byte_length < row_bytes ||
        checked_mul_u64((uint64_t)n, (uint64_t)rows, &values) ||
        checked_mul_u64(values, sizeof(float), &tensor_bytes)) {
        return 0;
    }
    if (x_f32->bytes < tensor_bytes || out_f32->bytes < tensor_bytes) return 0;
    return 1;
}

extern "C" int ds4_gpu_arena_f32_rms_norm_f32(
        const ds4_gpu_arena           *arena,
        const ds4_gpu_source_row_view *weight,
        const ds4_gpu_tensor          *x_f32,
        ds4_gpu_tensor                *out_f32,
        uint32_t                       n,
        uint32_t                       rows,
        float                          eps) {
    if (!cuda_f32_arena_norm_view_ok(arena, weight, x_f32, out_f32, n, rows)) {
        return 1;
    }
    if (!cuda_ok(cudaSetDevice(arena->gpu), "f32 arena rms norm set device")) return 1;

    const float *w =
        (const float *)((const char *)arena->ptr + weight->arena_offset);
    rms_norm_weight_kernel<<<rows, 256>>>(
            (float *)out_f32->ptr,
            (const float *)x_f32->ptr,
            w,
            n,
            rows,
            eps);
    return cuda_ok(cudaGetLastError(), "f32 arena rms norm launch") ? 0 : 1;
}

extern "C" int ds4_gpu_matmul_q8_0_pair_tensor(
        ds4_gpu_tensor *out0,
        ds4_gpu_tensor *out1,
        const void *model_map,
        uint64_t model_size,
        uint64_t weight0_offset,
        uint64_t weight1_offset,
        uint64_t in_dim,
        uint64_t out0_dim,
        uint64_t out1_dim,
        const ds4_gpu_tensor *x,
        uint64_t n_tok) {
    if (!out0 || !out1 || !x || !model_map || in_dim == 0 || out0_dim == 0 || out1_dim == 0 || n_tok == 0) {
        return 0;
    }
    if (n_tok != 1) {
        return cuda_matmul_q8_0_tensor_labeled(out0, model_map, model_size, weight0_offset,
                                               in_dim, out0_dim, x, n_tok, "q8_0_pair0") &&
               cuda_matmul_q8_0_tensor_labeled(out1, model_map, model_size, weight1_offset,
                                               in_dim, out1_dim, x, n_tok, "q8_0_pair1");
    }
    const uint64_t blocks = (in_dim + 31) / 32;
    if (weight0_offset > model_size || weight1_offset > model_size ||
        out0_dim > UINT64_MAX / (blocks * 34) ||
        out1_dim > UINT64_MAX / (blocks * 34)) {
        return 0;
    }
    const uint64_t weight0_bytes = out0_dim * blocks * 34;
    const uint64_t weight1_bytes = out1_dim * blocks * 34;
    if (weight0_bytes > model_size - weight0_offset ||
        weight1_bytes > model_size - weight1_offset ||
        x->bytes < in_dim * sizeof(float) ||
        out0->bytes < out0_dim * sizeof(float) ||
        out1->bytes < out1_dim * sizeof(float)) {
        return 0;
    }
    const char *w0 = cuda_model_range_ptr(model_map, weight0_offset, weight0_bytes, "q8_0_pair0");
    const char *w1 = cuda_model_range_ptr(model_map, weight1_offset, weight1_bytes, "q8_0_pair1");
    if (!w0 || !w1) return 0;

    const uint64_t xq_bytes = blocks * 32u;
    const uint64_t scale_offset = (xq_bytes + 15u) & ~15ull;
    const uint64_t tmp_bytes = scale_offset + blocks * sizeof(float);
    void *tmp = cuda_tmp_alloc(tmp_bytes, "q8_0 pair prequant");
    if (!tmp) return 0;
    int8_t *xq = (int8_t *)tmp;
    float *xscale = (float *)((char *)tmp + scale_offset);
    const int use_dp4a = cuda_q8_use_dp4a();
    dim3 qgrid((unsigned)blocks, 1, 1);
    quantize_q8_0_f32_kernel<<<qgrid, 32>>>(xq, xscale, (const float *)x->ptr, in_dim, blocks);
    if (!cuda_ok(cudaGetLastError(), "matmul_q8_0 pair quantize launch")) return 0;
    const uint64_t max_out = out0_dim > out1_dim ? out0_dim : out1_dim;
    matmul_q8_0_pair_preq_warp8_kernel<<<((unsigned)max_out + 7u) / 8u, 256>>>(
            (float *)out0->ptr,
            (float *)out1->ptr,
            reinterpret_cast<const unsigned char *>(w0),
            reinterpret_cast<const unsigned char *>(w1),
            xq,
            xscale,
            in_dim,
            out0_dim,
            out1_dim,
            blocks,
            use_dp4a);
    return cuda_ok(cudaGetLastError(), "matmul_q8_0 pair warp launch");
}

static int cuda_matmul_q8_0_hc_expand_tensor_labeled(
        ds4_gpu_tensor       *out_hc,
        ds4_gpu_tensor       *block_out,
        const void             *model_map,
        uint64_t                model_size,
        uint64_t                weight_offset,
        uint64_t                in_dim,
        uint64_t                out_dim,
        const ds4_gpu_tensor *x,
        const ds4_gpu_tensor *block_add,
        const ds4_gpu_tensor *residual_hc,
        const ds4_gpu_tensor *split,
        uint32_t                n_embd,
        uint32_t                n_hc,
        const char             *label) {
    if (!out_hc || !block_out || !x || !residual_hc || !split || !model_map ||
        in_dim == 0 || out_dim == 0 || n_embd == 0 || n_hc == 0 ||
        out_dim != (uint64_t)n_embd) {
        return 0;
    }
    const uint64_t blocks = (in_dim + 31) / 32;
    if (weight_offset > model_size || out_dim > UINT64_MAX / (blocks * 34)) return 0;
    const uint64_t weight_bytes = out_dim * blocks * 34;
    const uint64_t hc_bytes = (uint64_t)n_hc * n_embd * sizeof(float);
    const uint64_t split_bytes = (uint64_t)(2u * n_hc + n_hc * n_hc) * sizeof(float);
    if (weight_bytes > model_size - weight_offset ||
        x->bytes < in_dim * sizeof(float) ||
        block_out->bytes < out_dim * sizeof(float) ||
        residual_hc->bytes < hc_bytes ||
        split->bytes < split_bytes ||
        out_hc->bytes < hc_bytes ||
        (block_add && block_add->bytes < out_dim * sizeof(float))) {
        return 0;
    }
    const char *wptr = cuda_model_range_ptr(model_map, weight_offset, weight_bytes, label ? label : "q8_0_hc_expand");
    if (!wptr) return 0;

    const uint64_t xq_bytes = blocks * 32u;
    const uint64_t scale_offset = (xq_bytes + 15u) & ~15ull;
    const uint64_t tmp_bytes = scale_offset + blocks * sizeof(float);
    void *tmp = cuda_tmp_alloc(tmp_bytes, "q8_0 hc expand prequant");
    if (!tmp) return 0;
    int8_t *xq = (int8_t *)tmp;
    float *xscale = (float *)((char *)tmp + scale_offset);
    const int use_dp4a = cuda_q8_use_dp4a();
    quantize_q8_0_f32_kernel<<<(unsigned)blocks, 32>>>(xq, xscale, (const float *)x->ptr, in_dim, blocks);
    if (!cuda_ok(cudaGetLastError(), "matmul_q8_0_hc_expand quantize launch")) return 0;
    matmul_q8_0_hc_expand_preq_warp8_kernel<<<((unsigned)out_dim + 7u) / 8u, 256>>>(
            (float *)out_hc->ptr,
            (float *)block_out->ptr,
            block_add ? (const float *)block_add->ptr : (const float *)block_out->ptr,
            (const float *)residual_hc->ptr,
            (const float *)split->ptr,
            reinterpret_cast<const unsigned char *>(wptr),
            xq,
            xscale,
            in_dim,
            out_dim,
            n_embd,
            n_hc,
            blocks,
            block_add ? 1 : 0,
            use_dp4a);
    return cuda_ok(cudaGetLastError(), "matmul_q8_0_hc_expand launch");
}

extern "C" int ds4_gpu_matmul_f16_tensor(ds4_gpu_tensor *out, const void *model_map, uint64_t model_size, uint64_t weight_offset, uint64_t in_dim, uint64_t out_dim, const ds4_gpu_tensor *x, uint64_t n_tok) {
    if (!out || !x || !model_map) return 0;
    if (weight_offset > model_size || out_dim > UINT64_MAX / in_dim) return 0;
    uint64_t weight_bytes = out_dim * in_dim * sizeof(uint16_t);
    if (weight_bytes > model_size - weight_offset) return 0;
    if (x->bytes < n_tok * in_dim * sizeof(float) ||
        out->bytes < n_tok * out_dim * sizeof(float)) return 0;
    const char *wptr = cuda_model_range_ptr(model_map, weight_offset, weight_bytes, "f16");
    if (!wptr) return 0;
    const __half *w = (const __half *)wptr;
    const int serial_f16 = getenv("DS4_CUDA_SERIAL_F16_MATMUL") != NULL;
    const int router_shape = in_dim == 4096u && out_dim == 256u && n_tok == 1u;
    const int serial_router =
        !serial_f16 &&
        router_shape &&
        getenv("DS4_CUDA_SERIAL_ROUTER") != NULL;
    const int ordered_router =
        !serial_f16 &&
        !serial_router &&
        n_tok == 1u &&
        getenv("DS4_CUDA_NO_ORDERED_F16_MATMUL") == NULL;
    if (!serial_f16 && g_cublas_ready && n_tok > 1) {
        const uint64_t xh_count = n_tok * in_dim;
        __half *xh = (__half *)cuda_tmp_alloc(xh_count * sizeof(__half), "f16 gemm activations");
        if (!xh) return 0;
        f32_to_f16_kernel<<<(xh_count + 255) / 256, 256>>>(xh, (const float *)x->ptr, xh_count);
        if (!cuda_ok(cudaGetLastError(), "f16 activation convert launch")) return 0;
        const float alpha = 1.0f;
        const float beta = 0.0f;
        cublasStatus_t st = cublasGemmEx(g_cublas,
                                         CUBLAS_OP_T,
                                         CUBLAS_OP_N,
                                         (int)out_dim,
                                         (int)n_tok,
                                         (int)in_dim,
                                         &alpha,
                                         w,
                                         CUDA_R_16F,
                                         (int)in_dim,
                                         xh,
                                         CUDA_R_16F,
                                         (int)in_dim,
                                         &beta,
                                         out->ptr,
                                         CUDA_R_32F,
                                         (int)out_dim,
                                         CUDA_R_32F,
                                         CUBLAS_GEMM_DEFAULT);
        return cublas_ok(st, "f16 matmul");
    }
    dim3 grid((unsigned)out_dim, (unsigned)n_tok, 1);
    if (serial_f16 || serial_router) {
        matmul_f16_serial_kernel<<<grid, 1>>>((float *)out->ptr, w, (const float *)x->ptr, in_dim, out_dim, n_tok);
        return cuda_ok(cudaGetLastError(), serial_router ? "matmul_f16_router_serial launch" : "matmul_f16_serial launch");
    }
    if (ordered_router) {
        matmul_f16_ordered_chunks_kernel<<<grid, 32>>>((float *)out->ptr, w, (const float *)x->ptr, in_dim, out_dim, n_tok);
        return cuda_ok(cudaGetLastError(), "matmul_f16_ordered_chunks launch");
    }
    matmul_f16_kernel<<<grid, 256>>>((float *)out->ptr, w, (const float *)x->ptr, in_dim, out_dim, n_tok);
    return cuda_ok(cudaGetLastError(), "matmul_f16 launch");
}

extern "C" int ds4_gpu_matmul_f16_pair_tensor(
        ds4_gpu_tensor *out0,
        ds4_gpu_tensor *out1,
        const void *model_map,
        uint64_t model_size,
        uint64_t weight0_offset,
        uint64_t weight1_offset,
        uint64_t in_dim,
        uint64_t out_dim,
        const ds4_gpu_tensor *x,
        uint64_t n_tok) {
    if (!out0 || !out1 || !x || !model_map || in_dim == 0 || out_dim == 0 || n_tok == 0) {
        return 0;
    }
    if (n_tok != 1 ||
        getenv("DS4_CUDA_NO_F16_PAIR_MATMUL") != NULL ||
        getenv("DS4_CUDA_SERIAL_F16_MATMUL") != NULL ||
        getenv("DS4_CUDA_SERIAL_ROUTER") != NULL ||
        getenv("DS4_CUDA_NO_ORDERED_F16_MATMUL") != NULL) {
        return ds4_gpu_matmul_f16_tensor(out0, model_map, model_size, weight0_offset,
                                           in_dim, out_dim, x, n_tok) &&
               ds4_gpu_matmul_f16_tensor(out1, model_map, model_size, weight1_offset,
                                           in_dim, out_dim, x, n_tok);
    }
    if (weight0_offset > model_size || weight1_offset > model_size ||
        out_dim > UINT64_MAX / in_dim) {
        return 0;
    }
    const uint64_t weight_bytes = out_dim * in_dim * sizeof(uint16_t);
    if (weight_bytes > model_size - weight0_offset ||
        weight_bytes > model_size - weight1_offset ||
        x->bytes < in_dim * sizeof(float) ||
        out0->bytes < out_dim * sizeof(float) ||
        out1->bytes < out_dim * sizeof(float)) {
        return 0;
    }
    const __half *w0 = (const __half *)cuda_model_range_ptr(model_map, weight0_offset, weight_bytes, "f16_pair0");
    const __half *w1 = (const __half *)cuda_model_range_ptr(model_map, weight1_offset, weight_bytes, "f16_pair1");
    if (!w0 || !w1) return 0;
    matmul_f16_pair_ordered_chunks_kernel<<<(unsigned)out_dim, 32>>>(
        (float *)out0->ptr,
        (float *)out1->ptr,
        w0,
        w1,
        (const float *)x->ptr,
        in_dim,
        out_dim,
        out_dim);
    return cuda_ok(cudaGetLastError(), "matmul_f16_pair_ordered_chunks launch");
}

extern "C" int ds4_gpu_matmul_f32_tensor(ds4_gpu_tensor *out, const void *model_map, uint64_t model_size, uint64_t weight_offset, uint64_t in_dim, uint64_t out_dim, const ds4_gpu_tensor *x, uint64_t n_tok) {
    if (!out || !x || !model_map || in_dim == 0 || out_dim == 0 || n_tok == 0) return 0;
    if (weight_offset > model_size || out_dim > UINT64_MAX / in_dim) return 0;
    uint64_t weight_elems = out_dim * in_dim;
    if (weight_elems > UINT64_MAX / sizeof(float)) return 0;
    uint64_t weight_bytes = weight_elems * sizeof(float);
    if (weight_bytes > model_size - weight_offset) return 0;
    if (x->bytes < n_tok * in_dim * sizeof(float) ||
        out->bytes < n_tok * out_dim * sizeof(float)) return 0;
    const char *wptr = cuda_model_range_ptr(model_map, weight_offset, weight_bytes, "f32");
    if (!wptr) return 0;
    const float *w = (const float *)wptr;
    if (g_cublas_ready && n_tok > 1) {
        const float alpha = 1.0f;
        const float beta = 0.0f;
        cublasStatus_t st = cublasSgemm(g_cublas,
                                        CUBLAS_OP_T,
                                        CUBLAS_OP_N,
                                        (int)out_dim,
                                        (int)n_tok,
                                        (int)in_dim,
                                        &alpha,
                                        w,
                                        (int)in_dim,
                                        (const float *)x->ptr,
                                        (int)in_dim,
                                        &beta,
                                        (float *)out->ptr,
                                        (int)out_dim);
        return cublas_ok(st, "f32 matmul");
    }
    dim3 grid((unsigned)out_dim, (unsigned)n_tok, 1);
    matmul_f32_kernel<<<grid, 256>>>((float *)out->ptr, w, (const float *)x->ptr, in_dim, out_dim, n_tok);
    return cuda_ok(cudaGetLastError(), "matmul_f32 launch");
}

extern "C" int ds4_gpu_repeat_hc_tensor(ds4_gpu_tensor *out, const ds4_gpu_tensor *row, uint32_t n_embd, uint32_t n_hc) {
    if (!out || !row || n_embd == 0 || n_hc == 0 ||
        row->bytes < (uint64_t)n_embd * sizeof(float) ||
        out->bytes < (uint64_t)n_embd * n_hc * sizeof(float)) {
        return 0;
    }
    uint64_t n = (uint64_t)n_embd * n_hc;
    repeat_hc_kernel<<<(n + 255) / 256, 256>>>((float *)out->ptr, (const float *)row->ptr, n_embd, n_hc);
    return cuda_ok(cudaGetLastError(), "repeat_hc launch");
}

extern "C" int ds4_gpu_rms_norm_plain_tensor(ds4_gpu_tensor *out, const ds4_gpu_tensor *x, uint32_t n, float eps) {
    if (!out || !x || out->bytes < (uint64_t)n * sizeof(float) ||
        x->bytes < (uint64_t)n * sizeof(float)) return 0;
    rms_norm_plain_kernel<<<1, 256>>>((float *)out->ptr, (const float *)x->ptr, n, 1, eps);
    return cuda_ok(cudaGetLastError(), "rms_norm_plain launch");
}
extern "C" int ds4_gpu_rms_norm_plain_rows_tensor(ds4_gpu_tensor *out, const ds4_gpu_tensor *x, uint32_t n, uint32_t rows, float eps) {
    if (!out || !x || out->bytes < (uint64_t)n * rows * sizeof(float) ||
        x->bytes < (uint64_t)n * rows * sizeof(float)) return 0;
    rms_norm_plain_kernel<<<rows, 256>>>((float *)out->ptr, (const float *)x->ptr, n, rows, eps);
    return cuda_ok(cudaGetLastError(), "rms_norm_plain launch");
}
extern "C" int ds4_gpu_rms_norm_weight_tensor(ds4_gpu_tensor *out, const ds4_gpu_tensor *x, const void *model_map, uint64_t model_size, uint64_t weight_offset, uint32_t n, float eps) {
    if (!out || !x || !model_map || weight_offset > model_size ||
        model_size - weight_offset < (uint64_t)n * sizeof(float) ||
        out->bytes < (uint64_t)n * sizeof(float) ||
        x->bytes < (uint64_t)n * sizeof(float)) return 0;
    const char *wptr = cuda_model_range_ptr(model_map, weight_offset, (uint64_t)n * sizeof(float), "rms_weight");
    if (!wptr) return 0;
    const float *w = (const float *)wptr;
    rms_norm_weight_kernel<<<1, 256>>>((float *)out->ptr, (const float *)x->ptr, w, n, 1, eps);
    return cuda_ok(cudaGetLastError(), "rms_norm_weight launch");
}
extern "C" int ds4_gpu_rms_norm_weight_rows_tensor(ds4_gpu_tensor *out, const ds4_gpu_tensor *x, const void *model_map, uint64_t model_size, uint64_t weight_offset, uint32_t n, uint32_t rows, float eps) {
    if (!out || !x || !model_map || weight_offset > model_size ||
        model_size - weight_offset < (uint64_t)n * sizeof(float) ||
        out->bytes < (uint64_t)n * rows * sizeof(float) ||
        x->bytes < (uint64_t)n * rows * sizeof(float)) return 0;
    const char *wptr = cuda_model_range_ptr(model_map, weight_offset, (uint64_t)n * sizeof(float), "rms_weight");
    if (!wptr) return 0;
    const float *w = (const float *)wptr;
    rms_norm_weight_kernel<<<rows, 256>>>((float *)out->ptr, (const float *)x->ptr, w, n, rows, eps);
    return cuda_ok(cudaGetLastError(), "rms_norm_weight launch");
}
extern "C" int ds4_gpu_dsv4_qkv_rms_norm_rows_tensor(
        ds4_gpu_tensor       *q_out,
        const ds4_gpu_tensor *q,
        const void             *model_map,
        uint64_t                model_size,
        uint64_t                q_weight_offset,
        uint32_t                q_n,
        ds4_gpu_tensor       *kv_out,
        const ds4_gpu_tensor *kv,
        uint64_t                kv_weight_offset,
        uint32_t                kv_n,
        uint32_t                rows,
        float                   eps) {
    if (getenv("DS4_CUDA_DISABLE_QKV_RMS_FUSED") == NULL) {
        if (!q_out || !q || !kv_out || !kv || !model_map ||
            q_weight_offset > model_size ||
            kv_weight_offset > model_size ||
            model_size - q_weight_offset < (uint64_t)q_n * sizeof(float) ||
            model_size - kv_weight_offset < (uint64_t)kv_n * sizeof(float) ||
            q_out->bytes < (uint64_t)q_n * rows * sizeof(float) ||
            q->bytes < (uint64_t)q_n * rows * sizeof(float) ||
            kv_out->bytes < (uint64_t)kv_n * rows * sizeof(float) ||
            kv->bytes < (uint64_t)kv_n * rows * sizeof(float)) {
            return 0;
        }
        const float *q_w = (const float *)cuda_model_range_ptr(model_map,
                q_weight_offset, (uint64_t)q_n * sizeof(float), "q_rms_weight");
        const float *kv_w = (const float *)cuda_model_range_ptr(model_map,
                kv_weight_offset, (uint64_t)kv_n * sizeof(float), "kv_rms_weight");
        if (!q_w || !kv_w) return 0;
        dim3 grid(rows, 2u, 1u);
        dsv4_qkv_rms_norm_rows_kernel<<<grid, 256>>>(
                (float *)q_out->ptr,
                (const float *)q->ptr,
                q_w,
                q_n,
                (float *)kv_out->ptr,
                (const float *)kv->ptr,
                kv_w,
                kv_n,
                rows,
                eps);
        return cuda_ok(cudaGetLastError(), "dsv4 qkv rms norm rows launch");
    }
    return ds4_gpu_rms_norm_weight_rows_tensor(q_out, q, model_map, model_size,
                                                 q_weight_offset, q_n, rows, eps) &&
           ds4_gpu_rms_norm_weight_rows_tensor(kv_out, kv, model_map, model_size,
                                                 kv_weight_offset, kv_n, rows, eps);
}
extern "C" int ds4_gpu_head_rms_norm_tensor(ds4_gpu_tensor *x, uint32_t n_tok, uint32_t n_head, uint32_t head_dim, float eps) {
    if (!x || x->bytes < (uint64_t)n_tok * n_head * head_dim * sizeof(float)) return 0;
    head_rms_norm_kernel<<<n_tok * n_head, 256>>>((float *)x->ptr, n_tok, n_head, head_dim, eps);
    return cuda_ok(cudaGetLastError(), "head_rms_norm launch");
}
extern "C" int ds4_gpu_head_rms_norm_rope_tail_tensor(ds4_gpu_tensor *x, uint32_t n_tok, uint32_t n_head, uint32_t head_dim, uint32_t n_rot, uint32_t pos0, uint32_t n_ctx_orig, bool inverse, float freq_base, float freq_scale, float ext_factor, float attn_factor, float beta_fast, float beta_slow, float eps) {
    if (!x || n_rot > head_dim || (n_rot & 1u) ||
        x->bytes < (uint64_t)n_tok * n_head * head_dim * sizeof(float)) return 0;
    head_rms_norm_rope_tail_kernel<<<n_tok * n_head, 256>>>((float *)x->ptr, n_tok, n_head, head_dim, n_rot, pos0, n_ctx_orig, inverse ? 1 : 0, freq_base, freq_scale, ext_factor, attn_factor, beta_fast, beta_slow, eps);
    return cuda_ok(cudaGetLastError(), "head_rms_norm_rope_tail launch");
}
extern "C" int ds4_gpu_dsv4_fp8_kv_quantize_tensor(ds4_gpu_tensor *x, uint32_t n_tok, uint32_t head_dim, uint32_t n_rot) {
    if (!x || n_rot > head_dim || x->bytes < (uint64_t)n_tok * head_dim * sizeof(float)) return 0;
    fp8_kv_quantize_kernel<<<n_tok, 64>>>((float *)x->ptr, n_tok, head_dim, n_rot);
    return cuda_ok(cudaGetLastError(), "fp8_kv_quantize launch");
}
extern "C" int ds4_gpu_rope_tail_tensor(ds4_gpu_tensor *x, uint32_t n_tok, uint32_t n_head, uint32_t head_dim, uint32_t n_rot, uint32_t pos0, uint32_t n_ctx_orig, bool inverse, float freq_base, float freq_scale, float ext_factor, float attn_factor, float beta_fast, float beta_slow) {
    if (!x || n_rot > head_dim || (n_rot & 1) || x->bytes < (uint64_t)n_tok * n_head * head_dim * sizeof(float)) return 0;
    uint32_t pairs = n_tok * n_head * (n_rot / 2);
    rope_tail_kernel<<<(pairs + 255) / 256, 256>>>((float *)x->ptr, n_tok, n_head, head_dim, n_rot, pos0, n_ctx_orig, inverse ? 1 : 0, freq_base, freq_scale, ext_factor, attn_factor, beta_fast, beta_slow);
    return cuda_ok(cudaGetLastError(), "rope_tail launch");
}
extern "C" int ds4_gpu_store_raw_kv_tensor(ds4_gpu_tensor *raw_cache, const ds4_gpu_tensor *kv, uint32_t raw_cap, uint32_t row, uint32_t head_dim);
extern "C" int ds4_gpu_kv_fp8_store_raw_tensor(
        ds4_gpu_tensor *kv,
        ds4_gpu_tensor *raw_cache,
        uint32_t          raw_cap,
        uint32_t          raw_row,
        uint32_t          head_dim,
        uint32_t          n_rot) {
    return ds4_gpu_dsv4_fp8_kv_quantize_tensor(kv, 1, head_dim, n_rot) &&
           ds4_gpu_store_raw_kv_tensor(raw_cache, kv, raw_cap, raw_row, head_dim);
}
extern "C" int ds4_gpu_store_raw_kv_tensor(ds4_gpu_tensor *raw_cache, const ds4_gpu_tensor *kv, uint32_t raw_cap, uint32_t row, uint32_t head_dim) {
    if (!raw_cache || !kv || raw_cap == 0 ||
        raw_cache->bytes < (uint64_t)raw_cap * head_dim * sizeof(float) ||
        kv->bytes < (uint64_t)head_dim * sizeof(float)) return 0;
    store_raw_kv_batch_kernel<<<(head_dim + 255) / 256, 256>>>((float *)raw_cache->ptr, (const float *)kv->ptr, raw_cap, row, 1, head_dim);
    return cuda_ok(cudaGetLastError(), "store_raw_kv launch");
}
extern "C" int ds4_gpu_store_raw_kv_batch_tensor(ds4_gpu_tensor *raw_cache, const ds4_gpu_tensor *kv, uint32_t raw_cap, uint32_t pos0, uint32_t n_tokens, uint32_t head_dim) {
    if (!raw_cache || !kv || raw_cap == 0 ||
        raw_cache->bytes < (uint64_t)raw_cap * head_dim * sizeof(float) ||
        kv->bytes < (uint64_t)n_tokens * head_dim * sizeof(float)) return 0;
    uint64_t n = (uint64_t)n_tokens * head_dim;
    store_raw_kv_batch_kernel<<<(n + 255) / 256, 256>>>((float *)raw_cache->ptr, (const float *)kv->ptr, raw_cap, pos0, n_tokens, head_dim);
    return cuda_ok(cudaGetLastError(), "store_raw_kv_batch launch");
}

__global__ static void v100_prefill_kv_f16_row_kernel(
        __half *raw_swa,
        __half *compressed_attn,
        __half *indexer_kv,
        float *attn_state,
        float *indexer_state,
        const float *attn_row,
        const float *indexer_row,
        ds4_gpu_v100_prefill_kv_update update) {
    const uint64_t tid = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    const uint32_t head_dim = update.head_dim;
    const uint32_t indexer_dim = update.indexer_head_dim;

    if (tid < head_dim) {
        const __half v = __float2half_rn(attn_row[tid]);
        const uint64_t raw_base =
            ((uint64_t)update.slot * update.raw_rows + update.raw_row) * head_dim;
        const uint64_t comp_base =
            ((uint64_t)update.slot * update.comp_rows + update.comp_row) * head_dim;
        raw_swa[raw_base + tid] = v;
        compressed_attn[comp_base + tid] = v;
    }

    if (tid < update.attn_state_values) {
        const uint32_t lane = (uint32_t)(tid % head_dim);
        const uint32_t row = (uint32_t)(tid / head_dim);
        const uint64_t base = (uint64_t)update.slot * update.attn_state_values;
        attn_state[base + tid] = attn_row[lane] + (float)row * 0.125f +
                                 (float)update.ratio * 0.001f;
    }

    if (indexer_kv && indexer_row && tid < indexer_dim) {
        const __half v = __float2half_rn(indexer_row[tid]);
        const uint64_t base =
            ((uint64_t)update.slot * update.comp_rows + update.comp_row) * indexer_dim;
        indexer_kv[base + tid] = v;
    }

    if (indexer_state && indexer_row && tid < update.indexer_state_values) {
        const uint32_t lane = (uint32_t)(tid % indexer_dim);
        const uint32_t row = (uint32_t)(tid / indexer_dim);
        const uint64_t base = (uint64_t)update.slot * update.indexer_state_values;
        indexer_state[base + tid] = indexer_row[lane] - (float)row * 0.0625f -
                                    (float)update.ratio * 0.001f;
    }
}

static int v100_prefill_kv_update_validate(
        const ds4_gpu_tensor                   *raw_swa_f16,
        const ds4_gpu_tensor                   *compressed_attn_f16,
        const ds4_gpu_tensor                   *indexer_kv_f16,
        const ds4_gpu_tensor                   *attn_state_f32,
        const ds4_gpu_tensor                   *indexer_state_f32,
        const float                            *attn_row_f32,
        const float                            *indexer_row_f32,
        const ds4_gpu_v100_prefill_kv_update   *update) {
    if (!raw_swa_f16 || !compressed_attn_f16 || !attn_state_f32 ||
        !attn_row_f32 || !update || !raw_swa_f16->ptr ||
        !compressed_attn_f16->ptr || !attn_state_f32->ptr) {
        return 0;
    }
    if (update->ratio != 4u && update->ratio != 128u) return 0;
    if (update->slots == 0 || update->slot >= update->slots) return 0;
    if (update->raw_rows == 0 || update->raw_row >= update->raw_rows ||
        update->comp_rows == 0 || update->comp_row >= update->comp_rows ||
        update->head_dim == 0 || update->attn_state_values == 0) {
        return 0;
    }

    uint64_t raw_values = 0;
    uint64_t comp_values = 0;
    uint64_t attn_state_values = 0;
    if (checked_mul_u64((uint64_t)update->slots, (uint64_t)update->raw_rows, &raw_values) ||
        checked_mul_u64(raw_values, (uint64_t)update->head_dim, &raw_values) ||
        checked_mul_u64((uint64_t)update->slots, (uint64_t)update->comp_rows, &comp_values) ||
        checked_mul_u64(comp_values, (uint64_t)update->head_dim, &comp_values) ||
        checked_mul_u64((uint64_t)update->slots, (uint64_t)update->attn_state_values,
                        &attn_state_values)) {
        return 0;
    }
    if (raw_values > UINT64_MAX / sizeof(__half) ||
        comp_values > UINT64_MAX / sizeof(__half) ||
        attn_state_values > UINT64_MAX / sizeof(float) ||
        raw_swa_f16->bytes < raw_values * sizeof(__half) ||
        compressed_attn_f16->bytes < comp_values * sizeof(__half) ||
        attn_state_f32->bytes < attn_state_values * sizeof(float)) {
        return 0;
    }

    if (update->ratio == 4u) {
        if (!indexer_kv_f16 || !indexer_state_f32 || !indexer_row_f32 ||
            !indexer_kv_f16->ptr || !indexer_state_f32->ptr ||
            update->indexer_head_dim == 0 || update->indexer_state_values == 0) {
            return 0;
        }
        uint64_t index_values = 0;
        uint64_t index_state_values = 0;
        if (checked_mul_u64((uint64_t)update->slots, (uint64_t)update->comp_rows, &index_values) ||
            checked_mul_u64(index_values, (uint64_t)update->indexer_head_dim, &index_values) ||
            checked_mul_u64((uint64_t)update->slots, (uint64_t)update->indexer_state_values,
                            &index_state_values)) {
            return 0;
        }
        if (index_values > UINT64_MAX / sizeof(__half) ||
            index_state_values > UINT64_MAX / sizeof(float) ||
            indexer_kv_f16->bytes < index_values * sizeof(__half) ||
            indexer_state_f32->bytes < index_state_values * sizeof(float)) {
            return 0;
        }
    }

    return 1;
}

extern "C" int ds4_gpu_v100_prefill_kv_update_f16_tensor(
        ds4_gpu_tensor                         *raw_swa_f16,
        ds4_gpu_tensor                         *compressed_attn_f16,
        ds4_gpu_tensor                         *indexer_kv_f16,
        ds4_gpu_tensor                         *attn_state_f32,
        ds4_gpu_tensor                         *indexer_state_f32,
        const float                            *attn_row_f32,
        const float                            *indexer_row_f32,
        const ds4_gpu_v100_prefill_kv_update   *update) {
    if (!v100_prefill_kv_update_validate(raw_swa_f16, compressed_attn_f16,
                                         indexer_kv_f16, attn_state_f32,
                                         indexer_state_f32, attn_row_f32,
                                         indexer_row_f32, update)) {
        return 0;
    }

    float *dev_attn = NULL;
    float *dev_indexer = NULL;
    const uint64_t attn_bytes = (uint64_t)update->head_dim * sizeof(float);
    if (!cuda_ok(cudaMalloc(&dev_attn, (size_t)attn_bytes),
                 "v100 prefill kv attn row alloc")) {
        return 0;
    }
    int ok = cuda_ok(cudaMemcpy(dev_attn, attn_row_f32, (size_t)attn_bytes,
                                cudaMemcpyHostToDevice),
                     "v100 prefill kv attn row upload");

    if (ok && update->ratio == 4u) {
        const uint64_t indexer_bytes = (uint64_t)update->indexer_head_dim * sizeof(float);
        ok = cuda_ok(cudaMalloc(&dev_indexer, (size_t)indexer_bytes),
                     "v100 prefill kv indexer row alloc");
        if (ok) {
            ok = cuda_ok(cudaMemcpy(dev_indexer, indexer_row_f32, (size_t)indexer_bytes,
                                    cudaMemcpyHostToDevice),
                         "v100 prefill kv indexer row upload");
        }
    }

    if (ok) {
        uint64_t n = update->head_dim;
        if (update->attn_state_values > n) n = update->attn_state_values;
        if (update->ratio == 4u) {
            if (update->indexer_head_dim > n) n = update->indexer_head_dim;
            if (update->indexer_state_values > n) n = update->indexer_state_values;
        }
        if (n > (uint64_t)UINT32_MAX * 256ull) {
            ok = 0;
        } else {
            v100_prefill_kv_f16_row_kernel<<<(unsigned int)((n + 255u) / 256u), 256>>>(
                (__half *)raw_swa_f16->ptr,
                (__half *)compressed_attn_f16->ptr,
                update->ratio == 4u ? (__half *)indexer_kv_f16->ptr : NULL,
                (float *)attn_state_f32->ptr,
                update->ratio == 4u ? (float *)indexer_state_f32->ptr : NULL,
                dev_attn,
                dev_indexer,
                *update);
            ok = cuda_ok(cudaGetLastError(), "v100 prefill kv f16 update launch") &&
                 cuda_ok(cudaDeviceSynchronize(), "v100 prefill kv f16 update sync");
        }
    }

    if (dev_indexer) (void)cudaFree(dev_indexer);
    if (dev_attn) (void)cudaFree(dev_attn);
    return ok ? 1 : 0;
}

extern "C" int ds4_gpu_compressor_store_batch_tensor(
        const ds4_gpu_tensor *kv,
        const ds4_gpu_tensor *sc,
        ds4_gpu_tensor       *state_kv,
        ds4_gpu_tensor       *state_score,
        const void             *model_map,
        uint64_t                model_size,
        uint64_t                ape_offset,
        uint32_t                ape_type,
        uint32_t                head_dim,
        uint32_t                ratio,
        uint32_t                pos0,
        uint32_t                n_tokens) {
    if (!kv || !sc || !state_kv || !state_score || !model_map ||
        head_dim == 0 || ratio == 0 || n_tokens == 0 ||
        (ape_type != 0u && ape_type != 1u)) {
        return 0;
    }
    const uint32_t coff = ratio == 4u ? 2u : 1u;
    const uint32_t width = coff * head_dim;
    const uint32_t state_rows = coff * ratio;
    const uint64_t elem_ape = ape_type == 1u ? 2u : 4u;
    const uint64_t kv_bytes = (uint64_t)n_tokens * width * sizeof(float);
    const uint64_t state_bytes = (uint64_t)state_rows * width * sizeof(float);
    const uint64_t ape_bytes = (uint64_t)width * ratio * elem_ape;
    if (ape_offset > model_size || ape_bytes > model_size - ape_offset ||
        kv->bytes < kv_bytes || sc->bytes < kv_bytes ||
        state_kv->bytes < state_bytes || state_score->bytes < state_bytes) {
        return 0;
    }
    const char *ape = cuda_model_range_ptr(model_map, ape_offset, ape_bytes, "compressor_ape");
    if (!ape) return 0;
    uint64_t n = (uint64_t)n_tokens * width;
    compressor_store_kernel<<<(n + 255) / 256, 256>>>(
            (const float *)kv->ptr,
            (const float *)sc->ptr,
            (float *)state_kv->ptr,
            (float *)state_score->ptr,
            ape,
            0,
            ape_type,
            head_dim,
            ratio,
            pos0,
            n_tokens);
    return cuda_ok(cudaGetLastError(), "compressor store launch");
}

extern "C" int ds4_gpu_compressor_update_tensor(
        const ds4_gpu_tensor *kv_cur,
        const ds4_gpu_tensor *sc_cur,
        ds4_gpu_tensor       *state_kv,
        ds4_gpu_tensor       *state_score,
        ds4_gpu_tensor       *comp_cache,
        const void             *model_map,
        uint64_t                model_size,
        uint64_t                ape_offset,
        uint32_t                ape_type,
        uint64_t                norm_offset,
        uint32_t                norm_type,
        uint32_t                head_dim,
        uint32_t                ratio,
        uint32_t                pos,
        uint32_t                comp_row,
        uint32_t                n_rot,
        uint32_t                n_ctx_orig,
        float                   freq_base,
        float                   freq_scale,
        float                   ext_factor,
        float                   attn_factor,
        float                   beta_fast,
        float                   beta_slow,
        float                   rms_eps) {
    if (!kv_cur || !sc_cur || !state_kv || !state_score || !comp_cache ||
        !model_map || head_dim == 0 || ratio == 0 ||
        n_rot > head_dim || (n_rot & 1u) != 0 ||
        (ape_type != 0u && ape_type != 1u) || norm_type != 0u) {
        return 0;
    }
    const uint32_t coff = ratio == 4u ? 2u : 1u;
    const uint32_t width = coff * head_dim;
    const uint32_t state_rows = coff * ratio;
    const uint32_t emit = ((pos + 1u) % ratio) == 0u ? 1u : 0u;
    const uint64_t elem_ape = ape_type == 1u ? 2u : 4u;
    const uint64_t kv_bytes = (uint64_t)width * sizeof(float);
    const uint64_t state_bytes = (uint64_t)state_rows * width * sizeof(float);
    const uint64_t comp_bytes = (uint64_t)(comp_row + (emit ? 1u : 0u)) * head_dim * sizeof(float);
    const uint64_t ape_bytes = (uint64_t)width * ratio * elem_ape;
    const uint64_t norm_bytes = (uint64_t)head_dim * sizeof(float);
    if (ape_offset > model_size || ape_bytes > model_size - ape_offset ||
        norm_offset > model_size || norm_bytes > model_size - norm_offset ||
        kv_cur->bytes < kv_bytes || sc_cur->bytes < kv_bytes ||
        state_kv->bytes < state_bytes || state_score->bytes < state_bytes ||
        (emit && comp_cache->bytes < comp_bytes)) {
        return 0;
    }
    if (!ds4_gpu_compressor_store_batch_tensor(kv_cur, sc_cur, state_kv, state_score,
                                                 model_map, model_size, ape_offset, ape_type,
                                                 head_dim, ratio, pos, 1)) {
        return 0;
    }
    if (!emit) return 1;
    ds4_gpu_tensor *comp_row_view = ds4_gpu_tensor_view(
            comp_cache,
            (uint64_t)comp_row * head_dim * sizeof(float),
            (uint64_t)head_dim * sizeof(float));
    if (!comp_row_view) return 0;
    compressor_update_pool_kernel<<<(head_dim + 255) / 256, 256>>>(
            (float *)comp_row_view->ptr,
            (const float *)state_kv->ptr,
            (const float *)state_score->ptr,
            head_dim,
            ratio);
    int ok = cuda_ok(cudaGetLastError(), "compressor update pool launch");
    if (ok) ok = ds4_gpu_rms_norm_weight_rows_tensor(comp_row_view, comp_row_view,
                                                       model_map, model_size, norm_offset,
                                                       head_dim, 1, rms_eps);
    if (ok) ok = ds4_gpu_rope_tail_tensor(comp_row_view, 1, 1, head_dim, n_rot,
                                            pos + 1u - ratio, n_ctx_orig, false,
                                            freq_base, freq_scale, ext_factor, attn_factor,
                                            beta_fast, beta_slow);
    ds4_gpu_tensor_free(comp_row_view);
    if (ok && ratio == 4u) {
        uint64_t half = 4ull * width;
        compressor_shift_ratio4_kernel<<<(half + 255) / 256, 256>>>(
                (float *)state_kv->ptr, (float *)state_score->ptr, width);
        ok = cuda_ok(cudaGetLastError(), "compressor ratio4 shift launch");
    }
    return ok;
}
extern "C" int ds4_gpu_compressor_prefill_tensor(
        ds4_gpu_tensor       *comp_cache,
        ds4_gpu_tensor       *state_kv,
        ds4_gpu_tensor       *state_score,
        const ds4_gpu_tensor *kv,
        const ds4_gpu_tensor *sc,
        const void             *model_map,
        uint64_t                model_size,
        uint64_t                ape_offset,
        uint32_t                ape_type,
        uint64_t                norm_offset,
        uint32_t                norm_type,
        uint32_t                head_dim,
        uint32_t                ratio,
        uint32_t                pos0,
        uint32_t                n_tokens,
        uint32_t                n_rot,
        uint32_t                n_ctx_orig,
        bool                    quantize_fp8,
        float                   freq_base,
        float                   freq_scale,
        float                   ext_factor,
        float                   attn_factor,
        float                   beta_fast,
        float                   beta_slow,
        float                   rms_eps) {
    if (!comp_cache || !state_kv || !state_score || !kv || !sc || !model_map ||
        head_dim == 0 || ratio == 0 || n_tokens == 0 ||
        n_rot > head_dim || (n_rot & 1u) != 0 ||
        (ape_type != 0u && ape_type != 1u) || norm_type != 0u) {
        return 0;
    }

    const uint32_t coff = ratio == 4u ? 2u : 1u;
    const uint32_t width = coff * head_dim;
    const uint32_t state_rows = coff * ratio;
    const uint32_t n_comp = n_tokens / ratio;
    const uint32_t cutoff = n_comp * ratio;
    const uint32_t rem = n_tokens - cutoff;
    const uint64_t elem_ape = ape_type == 1u ? 2u : 4u;
    const uint64_t kv_bytes = (uint64_t)n_tokens * width * sizeof(float);
    const uint64_t state_bytes = (uint64_t)state_rows * width * sizeof(float);
    const uint64_t comp_bytes = (uint64_t)n_comp * head_dim * sizeof(float);
    const uint64_t ape_bytes = (uint64_t)width * ratio * elem_ape;
    const uint64_t norm_bytes = (uint64_t)head_dim * sizeof(float);

    if (ape_offset > model_size || ape_bytes > model_size - ape_offset ||
        norm_offset > model_size || norm_bytes > model_size - norm_offset ||
        kv->bytes < kv_bytes || sc->bytes < kv_bytes ||
        state_kv->bytes < state_bytes || state_score->bytes < state_bytes ||
        (n_comp && comp_cache->bytes < comp_bytes)) {
        return 0;
    }
    const char *ape = cuda_model_range_ptr(model_map, ape_offset, ape_bytes, "compressor_ape");
    if (!ape) return 0;

    uint64_t state_n = (uint64_t)state_rows * width;
    if (!cuda_ok(cudaMemsetAsync(state_kv->ptr, 0, (size_t)(state_n * sizeof(float))),
                 "compressor state kv zero")) return 0;
    fill_f32_kernel<<<(state_n + 255) / 256, 256>>>((float *)state_score->ptr, state_n, -INFINITY);
    if (!cuda_ok(cudaGetLastError(), "compressor state score fill launch")) return 0;

    if (ratio == 4u) {
        if (cutoff >= ratio) {
            uint32_t prev_start = cutoff - ratio;
            uint64_t n = (uint64_t)ratio * width;
            compressor_set_rows_kernel<<<(n + 255) / 256, 256>>>(
                    (float *)state_kv->ptr, (float *)state_score->ptr,
                    (const float *)kv->ptr, (const float *)sc->ptr,
                    ape, 0, ape_type, width, ratio, pos0,
                    prev_start, 0, ratio);
            if (!cuda_ok(cudaGetLastError(), "compressor prefill prev state launch")) return 0;
        }
        if (rem != 0) {
            uint64_t n = (uint64_t)rem * width;
            compressor_set_rows_kernel<<<(n + 255) / 256, 256>>>(
                    (float *)state_kv->ptr, (float *)state_score->ptr,
                    (const float *)kv->ptr, (const float *)sc->ptr,
                    ape, 0, ape_type, width, ratio, pos0,
                    cutoff, ratio, rem);
            if (!cuda_ok(cudaGetLastError(), "compressor prefill rem state launch")) return 0;
        }
    } else if (rem != 0) {
        uint64_t n = (uint64_t)rem * width;
        compressor_set_rows_kernel<<<(n + 255) / 256, 256>>>(
                (float *)state_kv->ptr, (float *)state_score->ptr,
                (const float *)kv->ptr, (const float *)sc->ptr,
                ape, 0, ape_type, width, ratio, pos0,
                cutoff, 0, rem);
        if (!cuda_ok(cudaGetLastError(), "compressor prefill rem state launch")) return 0;
    }
    if (n_comp != 0) {
        dim3 grid((head_dim + 255) / 256, n_comp, 1);
        compressor_prefill_pool_kernel<<<grid, 256>>>(
                (float *)comp_cache->ptr,
                (const float *)kv->ptr,
                (const float *)sc->ptr,
                (const float *)state_kv->ptr,
                (const float *)state_score->ptr,
                ape, 0, ape_type, head_dim, ratio, pos0, n_comp, 0);
        if (!cuda_ok(cudaGetLastError(), "compressor prefill pool launch")) return 0;
        if (!ds4_gpu_rms_norm_weight_rows_tensor(comp_cache, comp_cache,
                                                   model_map, model_size, norm_offset,
                                                   head_dim, n_comp, rms_eps)) return 0;
        if (n_rot != 0 && !ds4_gpu_rope_tail_tensor(comp_cache, n_comp, 1, head_dim,
                                                      n_rot, pos0, n_ctx_orig, false,
                                                      freq_base, freq_scale, ext_factor,
                                                      attn_factor, beta_fast, beta_slow)) return 0;
        if (quantize_fp8 && !ds4_gpu_dsv4_fp8_kv_quantize_tensor(comp_cache, n_comp, head_dim, n_rot)) return 0;
    }
    return 1;
}
extern "C" int ds4_gpu_compressor_prefill_ratio4_replay_tensor(
        ds4_gpu_tensor       *comp_cache,
        ds4_gpu_tensor       *state_kv,
        ds4_gpu_tensor       *state_score,
        const ds4_gpu_tensor *kv,
        const ds4_gpu_tensor *sc,
        const void             *model_map,
        uint64_t                model_size,
        uint64_t                ape_offset,
        uint32_t                ape_type,
        uint64_t                norm_offset,
        uint32_t                norm_type,
        uint32_t                head_dim,
        uint32_t                pos0,
        uint32_t                n_tokens,
        uint32_t                n_rot,
        uint32_t                n_ctx_orig,
        bool                    quantize_fp8,
        float                   freq_base,
        float                   freq_scale,
        float                   ext_factor,
        float                   attn_factor,
        float                   beta_fast,
        float                   beta_slow,
        float                   rms_eps) {
    if (!comp_cache || !state_kv || !state_score || !kv || !sc || !model_map ||
        head_dim == 0 || n_tokens == 0 || (n_tokens & 3u) != 0 || (pos0 & 3u) != 0 ||
        n_rot > head_dim || (n_rot & 1u) != 0 ||
        (ape_type != 0u && ape_type != 1u) || norm_type != 0u) {
        return 0;
    }

    const uint32_t ratio = 4u;
    const uint32_t width = 2u * head_dim;
    const uint32_t state_rows = 8u;
    const uint32_t n_comp = n_tokens / ratio;
    const uint64_t elem_ape = ape_type == 1u ? 2u : 4u;
    const uint64_t kv_bytes = (uint64_t)n_tokens * width * sizeof(float);
    const uint64_t state_bytes = (uint64_t)state_rows * width * sizeof(float);
    const uint64_t comp_bytes = (uint64_t)n_comp * head_dim * sizeof(float);
    const uint64_t ape_bytes = (uint64_t)width * ratio * elem_ape;
    const uint64_t norm_bytes = (uint64_t)head_dim * sizeof(float);
    if (ape_offset > model_size || ape_bytes > model_size - ape_offset ||
        norm_offset > model_size || norm_bytes > model_size - norm_offset ||
        kv->bytes < kv_bytes || sc->bytes < kv_bytes ||
        state_kv->bytes < state_bytes || state_score->bytes < state_bytes ||
        comp_cache->bytes < comp_bytes) {
        return 0;
    }
    const char *ape = cuda_model_range_ptr(model_map, ape_offset, ape_bytes, "compressor_ape");
    if (!ape) return 0;
    dim3 grid((head_dim + 255) / 256, n_comp, 1);
    compressor_prefill_pool_kernel<<<grid, 256>>>(
            (float *)comp_cache->ptr,
            (const float *)kv->ptr,
            (const float *)sc->ptr,
            (const float *)state_kv->ptr,
            (const float *)state_score->ptr,
            ape, 0, ape_type, head_dim, ratio, pos0, n_comp, 1);
    if (!cuda_ok(cudaGetLastError(), "compressor replay pool launch")) return 0;
    if (!ds4_gpu_rms_norm_weight_rows_tensor(comp_cache, comp_cache,
                                               model_map, model_size, norm_offset,
                                               head_dim, n_comp, rms_eps)) return 0;
    if (n_rot != 0 && !ds4_gpu_rope_tail_tensor(comp_cache, n_comp, 1, head_dim,
                                                  n_rot, pos0, n_ctx_orig, false,
                                                  freq_base, freq_scale, ext_factor,
                                                  attn_factor, beta_fast, beta_slow)) return 0;
    if (quantize_fp8 && !ds4_gpu_dsv4_fp8_kv_quantize_tensor(comp_cache, n_comp, head_dim, n_rot)) return 0;

    uint64_t state_n = (uint64_t)state_rows * width;
    if (!cuda_ok(cudaMemsetAsync(state_kv->ptr, 0, (size_t)(state_n * sizeof(float))),
                 "compressor replay state kv zero")) return 0;
    fill_f32_kernel<<<(state_n + 255) / 256, 256>>>((float *)state_score->ptr, state_n, -INFINITY);
    if (!cuda_ok(cudaGetLastError(), "compressor replay state score fill launch")) return 0;
    uint32_t prev_start = n_tokens - ratio;
    uint64_t n = (uint64_t)ratio * width;
    compressor_set_rows_kernel<<<(n + 255) / 256, 256>>>(
            (float *)state_kv->ptr, (float *)state_score->ptr,
            (const float *)kv->ptr, (const float *)sc->ptr,
            ape, 0, ape_type, width, ratio, pos0,
            prev_start, 0, ratio);
    return cuda_ok(cudaGetLastError(), "compressor replay state launch");
}
extern "C" int ds4_gpu_compressor_prefill_state_ratio4_tensor(
        ds4_gpu_tensor       *state_kv,
        ds4_gpu_tensor       *state_score,
        const ds4_gpu_tensor *kv_tail,
        const ds4_gpu_tensor *sc_tail,
        const void             *model_map,
        uint64_t                model_size,
        uint64_t                ape_offset,
        uint32_t                ape_type,
        uint32_t                head_dim,
        uint32_t                pos0) {
    if (!state_kv || !state_score || !kv_tail || !sc_tail || !model_map ||
        head_dim == 0 || (ape_type != 0u && ape_type != 1u)) {
        return 0;
    }
    const uint32_t ratio = 4u;
    const uint32_t width = 2u * head_dim;
    const uint32_t state_rows = 8u;
    const uint64_t elem_ape = ape_type == 1u ? 2u : 4u;
    const uint64_t tail_bytes = (uint64_t)ratio * width * sizeof(float);
    const uint64_t state_bytes = (uint64_t)state_rows * width * sizeof(float);
    const uint64_t ape_bytes = (uint64_t)ratio * width * elem_ape;
    if (ape_offset > model_size || ape_bytes > model_size - ape_offset ||
        kv_tail->bytes < tail_bytes || sc_tail->bytes < tail_bytes ||
        state_kv->bytes < state_bytes || state_score->bytes < state_bytes) {
        return 0;
    }
    const char *ape = cuda_model_range_ptr(model_map, ape_offset, ape_bytes, "compressor_ape");
    if (!ape) return 0;
    uint64_t state_n = (uint64_t)state_rows * width;
    if (!cuda_ok(cudaMemsetAsync(state_kv->ptr, 0, (size_t)(state_n * sizeof(float))),
                 "compressor state kv zero")) return 0;
    fill_f32_kernel<<<(state_n + 255) / 256, 256>>>((float *)state_score->ptr, state_n, -INFINITY);
    if (!cuda_ok(cudaGetLastError(), "compressor state score fill launch")) return 0;
    uint64_t n = (uint64_t)ratio * width;
    compressor_set_rows_kernel<<<(n + 255) / 256, 256>>>(
            (float *)state_kv->ptr, (float *)state_score->ptr,
            (const float *)kv_tail->ptr, (const float *)sc_tail->ptr,
            ape, 0, ape_type, width, ratio, pos0,
            0, 0, ratio);
    return cuda_ok(cudaGetLastError(), "compressor state set launch");
}
extern "C" int ds4_gpu_attention_decode_heads_tensor(
        ds4_gpu_tensor       *heads,
        const void             *model_map,
        uint64_t                model_size,
        uint64_t                sinks_offset,
        const ds4_gpu_tensor *q,
        const ds4_gpu_tensor *raw_kv,
        uint32_t                n_raw,
        uint32_t                raw_cap,
        uint32_t                raw_start,
        const ds4_gpu_tensor *comp_kv,
        uint32_t                n_comp,
        const ds4_gpu_tensor *comp_mask,
        uint32_t                use_mask,
        uint32_t                n_head,
        uint32_t                head_dim) {
    if (!heads || !q || !raw_kv || !model_map || n_raw == 0 || raw_cap < n_raw ||
        raw_start >= raw_cap || (n_comp != 0 && !comp_kv) || (use_mask && !comp_mask) ||
        sinks_offset > model_size ||
        (uint64_t)n_head * sizeof(float) > model_size - sinks_offset ||
        heads->bytes < (uint64_t)n_head * head_dim * sizeof(float) ||
        q->bytes < (uint64_t)n_head * head_dim * sizeof(float) ||
        raw_kv->bytes < (uint64_t)raw_cap * head_dim * sizeof(float) ||
        (n_comp && comp_kv->bytes < (uint64_t)n_comp * head_dim * sizeof(float)) ||
        (use_mask && comp_mask->bytes < (uint64_t)n_comp * sizeof(float))) {
        return 0;
    }
    const float *sinks = (const float *)cuda_model_range_ptr(
            model_map, sinks_offset, (uint64_t)n_head * sizeof(float), "attn_sinks");
    if (!sinks) return 0;
    if (!cuda_attention_score_buffer_fits(n_comp)) {
        if (!use_mask && head_dim == 512u &&
            getenv("DS4_CUDA_NO_WINDOW_ATTENTION") == NULL) {
            dim3 online_grid(1, (n_head + 7u) / 8u, 1);
            attention_decode_mixed_heads8_online_kernel<<<online_grid, 256>>>((float *)heads->ptr,
                                                                              sinks,
                                                                              (const float *)q->ptr,
                                                                              (const float *)raw_kv->ptr,
                                                                              n_comp ? (const float *)comp_kv->ptr : (const float *)raw_kv->ptr,
                                                                              1,
                                                                              0,
                                                                              n_raw,
                                                                              raw_cap,
                                                                              raw_start,
                                                                              n_comp,
                                                                              0,
                                                                              0,
                                                                              n_head,
                                                                              head_dim);
            return cuda_ok(cudaGetLastError(), "attention decode online launch");
        }
        fprintf(stderr, "ds4: CUDA attention score buffer too small for %u compressed rows\n", n_comp);
        return 0;
    }
    dim3 grid(1, n_head, 1);
    attention_decode_mixed_kernel<<<grid, 256>>>((float *)heads->ptr,
                                                 sinks,
                                                 (const float *)q->ptr,
                                                 (const float *)raw_kv->ptr,
                                                 n_comp ? (const float *)comp_kv->ptr : (const float *)raw_kv->ptr,
                                                 use_mask ? (const float *)comp_mask->ptr : NULL,
                                                 use_mask,
                                                 1, 0, n_raw, raw_cap, raw_start, n_comp,
                                                 0, 0, n_head, head_dim);
    return cuda_ok(cudaGetLastError(), "attention decode launch");
}

extern "C" int ds4_gpu_arena_attention_decode_heads_tensor(
        const ds4_gpu_arena           *arena,
        const ds4_gpu_source_row_view *sinks,
        ds4_gpu_tensor                *heads,
        const ds4_gpu_tensor          *q,
        const ds4_gpu_tensor          *raw_kv,
        uint32_t                       n_raw,
        uint32_t                       raw_cap,
        uint32_t                       raw_start,
        const ds4_gpu_tensor          *comp_kv,
        uint32_t                       n_comp,
        const ds4_gpu_tensor          *comp_mask,
        uint32_t                       use_mask,
        uint32_t                       n_head,
        uint32_t                       head_dim) {
    if (!arena || !sinks || !heads || !q || !raw_kv ||
        !arena->valid || !arena->ptr || !heads->ptr || !q->ptr || !raw_kv->ptr ||
        n_raw == 0 || raw_cap < n_raw || raw_start >= raw_cap ||
        (n_comp != 0 && (!comp_kv || !comp_kv->ptr)) ||
        (use_mask && (!comp_mask || !comp_mask->ptr)) ||
        sinks->rows != 1u || sinks->cols != n_head ||
        (sinks->arena_offset & 3ull) != 0 ||
        (sinks->row_stride_bytes & 3u) != 0 ||
        sinks->row_stride_bytes < n_head * sizeof(float) ||
        sinks->byte_length < (uint64_t)n_head * sizeof(float) ||
        !cuda_arena_range_ok(arena, sinks->arena_offset, sinks->byte_length) ||
        heads->device != arena->gpu ||
        q->device != arena->gpu ||
        raw_kv->device != arena->gpu ||
        (n_comp && comp_kv->device != arena->gpu) ||
        (use_mask && comp_mask->device != arena->gpu) ||
        heads->bytes < (uint64_t)n_head * head_dim * sizeof(float) ||
        q->bytes < (uint64_t)n_head * head_dim * sizeof(float) ||
        raw_kv->bytes < (uint64_t)raw_cap * head_dim * sizeof(float) ||
        (n_comp && comp_kv->bytes < (uint64_t)n_comp * head_dim * sizeof(float)) ||
        (use_mask && comp_mask->bytes < (uint64_t)n_comp * sizeof(float))) {
        return 1;
    }
    if (!cuda_ok(cudaSetDevice(arena->gpu), "arena attention decode set device")) return 1;

    const float *sinks_ptr =
        (const float *)((const char *)arena->ptr + sinks->arena_offset);
    if (!cuda_attention_score_buffer_fits(n_comp)) {
        if (!use_mask && head_dim == 512u &&
            getenv("DS4_CUDA_NO_WINDOW_ATTENTION") == NULL) {
            dim3 online_grid(1, (n_head + 7u) / 8u, 1);
            attention_decode_mixed_heads8_online_kernel<<<online_grid, 256>>>(
                    (float *)heads->ptr,
                    sinks_ptr,
                    (const float *)q->ptr,
                    (const float *)raw_kv->ptr,
                    n_comp ? (const float *)comp_kv->ptr : (const float *)raw_kv->ptr,
                    1,
                    0,
                    n_raw,
                    raw_cap,
                    raw_start,
                    n_comp,
                    0,
                    0,
                    n_head,
                    head_dim);
            return cuda_ok(cudaGetLastError(), "arena attention decode online launch") ? 0 : 1;
        }
        fprintf(stderr, "ds4: CUDA arena attention score buffer too small for %u compressed rows\n", n_comp);
        return 1;
    }
    dim3 grid(1, n_head, 1);
    attention_decode_mixed_kernel<<<grid, 256>>>(
            (float *)heads->ptr,
            sinks_ptr,
            (const float *)q->ptr,
            (const float *)raw_kv->ptr,
            n_comp ? (const float *)comp_kv->ptr : (const float *)raw_kv->ptr,
            use_mask ? (const float *)comp_mask->ptr : NULL,
            use_mask,
            1, 0, n_raw, raw_cap, raw_start, n_comp,
            0, 0, n_head, head_dim);
    return cuda_ok(cudaGetLastError(), "arena attention decode launch") ? 0 : 1;
}

extern "C" int ds4_gpu_attention_prefill_raw_heads_tensor(ds4_gpu_tensor *heads, const void *model_map, uint64_t model_size, uint64_t sinks_offset, const ds4_gpu_tensor *q, const ds4_gpu_tensor *raw_kv, uint32_t n_tokens, uint32_t window, uint32_t n_head, uint32_t head_dim) {
    if (!heads || !q || !raw_kv || !model_map || sinks_offset > model_size ||
        model_size - sinks_offset < (uint64_t)n_head * sizeof(float) ||
        heads->bytes < (uint64_t)n_tokens * n_head * head_dim * sizeof(float) ||
        q->bytes < (uint64_t)n_tokens * n_head * head_dim * sizeof(float) ||
        raw_kv->bytes < (uint64_t)n_tokens * head_dim * sizeof(float) ||
        window > 256) return 0;
    const float *sinks = (const float *)cuda_model_range_ptr(
            model_map, sinks_offset, (uint64_t)n_head * sizeof(float), "attn_sinks");
    if (!sinks) return 0;
    if (n_tokens > 1 && head_dim == 512 &&
        getenv("DS4_CUDA_NO_WINDOW_ATTENTION") == NULL &&
        (getenv("DS4_CUDA_WINDOW_ATTENTION") != NULL || (!g_quality_mode && n_tokens >= 128u))) {
        dim3 grid(n_tokens, (n_head + 7u) / 8u, 1);
        attention_static_mixed_heads8_online_kernel<<<grid, 256>>>((float *)heads->ptr,
                                                                   sinks,
                                                                   (const float *)q->ptr,
                                                                   (const float *)raw_kv->ptr,
                                                                   (const float *)raw_kv->ptr,
                                                                   n_tokens,
                                                                   0,
                                                                   window,
                                                                   1,
                                                                   n_head,
                                                                   head_dim);
        return cuda_ok(cudaGetLastError(), "attention raw window launch");
    }
    if (g_cublas_ready && n_tokens > 1 && head_dim == 512 &&
        getenv("DS4_CUDA_NO_CUBLAS_ATTENTION") == NULL) {
        const uint32_t n_keys = n_tokens;
        const uint64_t score_count = (uint64_t)n_head * n_tokens * n_keys;
        const uint64_t out_count = (uint64_t)n_head * n_tokens * head_dim;
        const uint64_t score_bytes = score_count * sizeof(float);
        const uint64_t out_offset = (score_bytes + 255u) & ~255ull;
        const uint64_t tmp_bytes = out_offset + out_count * sizeof(float);
        float *tmp = (float *)cuda_tmp_alloc(tmp_bytes, "attention raw cublas");
        if (!tmp) return 0;
        float *scores = tmp;
        float *out_tmp = (float *)((char *)tmp + out_offset);
        const float alpha = rsqrtf((float)head_dim);
        const float beta = 0.0f;
        cublasStatus_t st = cublasSgemmStridedBatched(g_cublas,
                                                      CUBLAS_OP_T,
                                                      CUBLAS_OP_N,
                                                      (int)n_keys,
                                                      (int)n_tokens,
                                                      (int)head_dim,
                                                      &alpha,
                                                      (const float *)raw_kv->ptr,
                                                      (int)head_dim,
                                                      0,
                                                      (const float *)q->ptr,
                                                      (int)(n_head * head_dim),
                                                      (long long)head_dim,
                                                      &beta,
                                                      scores,
                                                      (int)n_keys,
                                                      (long long)n_keys * n_tokens,
                                                      (int)n_head);
        if (!cublas_ok(st, "attention raw score gemm")) return 0;
        dim3 sgrid(n_tokens, n_head, 1);
        attention_prefill_raw_softmax_kernel<<<sgrid, 256>>>(scores, sinks, n_tokens, window, n_keys);
        if (!cuda_ok(cudaGetLastError(), "attention raw softmax launch")) return 0;
        const float one = 1.0f;
        st = cublasSgemmStridedBatched(g_cublas,
                                       CUBLAS_OP_N,
                                       CUBLAS_OP_N,
                                       (int)head_dim,
                                       (int)n_tokens,
                                       (int)n_keys,
                                       &one,
                                       (const float *)raw_kv->ptr,
                                       (int)head_dim,
                                       0,
                                       scores,
                                       (int)n_keys,
                                       (long long)n_keys * n_tokens,
                                       &beta,
                                       out_tmp,
                                       (int)head_dim,
                                       (long long)head_dim * n_tokens,
                                       (int)n_head);
        if (!cublas_ok(st, "attention raw value gemm")) return 0;
        uint64_t n = (uint64_t)n_tokens * n_head * head_dim;
        attention_prefill_unpack_heads_kernel<<<(n + 255) / 256, 256>>>((float *)heads->ptr,
                                                                        out_tmp,
                                                                        n_tokens,
                                                                        n_head,
                                                                        head_dim);
        return cuda_ok(cudaGetLastError(), "attention raw unpack launch");
    }
    dim3 grid(n_tokens, n_head, 1);
    attention_prefill_raw_kernel<<<grid, 128>>>((float *)heads->ptr,
                                                sinks,
                                                (const float *)q->ptr,
                                                (const float *)raw_kv->ptr,
                                                n_tokens, window, n_head, head_dim);
    return cuda_ok(cudaGetLastError(), "attention_prefill_raw launch");
}
static int attention_decode_batch_launch(
        ds4_gpu_tensor       *heads,
        const void             *model_map,
        uint64_t                model_size,
        uint64_t                sinks_offset,
        const ds4_gpu_tensor *q,
        const ds4_gpu_tensor *raw_kv,
        const ds4_gpu_tensor *comp_kv,
        const ds4_gpu_tensor *comp_mask,
        uint32_t                use_comp_mask,
        uint32_t                n_tokens,
        uint32_t                pos0,
        uint32_t                n_raw,
        uint32_t                raw_cap,
        uint32_t                raw_start,
        uint32_t                n_comp,
        uint32_t                window,
        uint32_t                ratio,
        uint32_t                n_head,
        uint32_t                head_dim) {
    if (!heads || !q || !raw_kv || !model_map || n_tokens == 0 ||
        n_raw == 0 || raw_cap < n_raw || raw_start >= raw_cap ||
        (n_comp != 0 && !comp_kv) || (use_comp_mask && !comp_mask) ||
        sinks_offset > model_size ||
        (uint64_t)n_head * sizeof(float) > model_size - sinks_offset ||
        heads->bytes < (uint64_t)n_tokens * n_head * head_dim * sizeof(float) ||
        q->bytes < (uint64_t)n_tokens * n_head * head_dim * sizeof(float) ||
        raw_kv->bytes < (uint64_t)raw_cap * head_dim * sizeof(float) ||
        (n_comp && comp_kv->bytes < (uint64_t)n_comp * head_dim * sizeof(float)) ||
        (use_comp_mask && comp_mask->bytes < (uint64_t)n_tokens * n_comp * sizeof(float))) {
        return 0;
    }
    if (n_comp != 0 && ratio == 0) return 0;
    const float *sinks = (const float *)cuda_model_range_ptr(
            model_map, sinks_offset, (uint64_t)n_head * sizeof(float), "attn_sinks");
    if (!sinks) return 0;
    if (!cuda_attention_score_buffer_fits(n_comp)) {
        if (!use_comp_mask && head_dim == 512u &&
            getenv("DS4_CUDA_NO_WINDOW_ATTENTION") == NULL) {
            dim3 online_grid(n_tokens, (n_head + 7u) / 8u, 1);
            attention_decode_mixed_heads8_online_kernel<<<online_grid, 256>>>((float *)heads->ptr,
                                                                              sinks,
                                                                              (const float *)q->ptr,
                                                                              (const float *)raw_kv->ptr,
                                                                              n_comp ? (const float *)comp_kv->ptr : (const float *)raw_kv->ptr,
                                                                              n_tokens,
                                                                              pos0,
                                                                              n_raw,
                                                                              raw_cap,
                                                                              raw_start,
                                                                              n_comp,
                                                                              window,
                                                                              ratio,
                                                                              n_head,
                                                                              head_dim);
            return cuda_ok(cudaGetLastError(), "attention decode online launch");
        }
        fprintf(stderr, "ds4: CUDA attention score buffer too small for %u compressed rows\n", n_comp);
        return 0;
    }
    if (!use_comp_mask && n_tokens > 1 && head_dim == 512 &&
        getenv("DS4_CUDA_NO_WINDOW_ATTENTION") == NULL &&
        (getenv("DS4_CUDA_WINDOW_ATTENTION") != NULL || (!g_quality_mode && n_tokens >= 128u))) {
        dim3 grid(n_tokens, (n_head + 7u) / 8u, 1);
        attention_decode_mixed_heads8_online_kernel<<<grid, 256>>>((float *)heads->ptr,
                                                                   sinks,
                                                                   (const float *)q->ptr,
                                                                   (const float *)raw_kv->ptr,
                                                                   n_comp ? (const float *)comp_kv->ptr : (const float *)raw_kv->ptr,
                                                                   n_tokens,
                                                                   pos0,
                                                                   n_raw,
                                                                   raw_cap,
                                                                   raw_start,
                                                                   n_comp,
                                                                   window,
                                                                   ratio,
                                                                   n_head,
                                                                   head_dim);
        return cuda_ok(cudaGetLastError(), "attention decode window launch");
    }
    dim3 grid(n_tokens, n_head, 1);
    attention_decode_mixed_kernel<<<grid, 256>>>((float *)heads->ptr,
                                                 sinks,
                                                 (const float *)q->ptr,
                                                 (const float *)raw_kv->ptr,
                                                 n_comp ? (const float *)comp_kv->ptr : (const float *)raw_kv->ptr,
                                                 use_comp_mask ? (const float *)comp_mask->ptr : NULL,
                                                 use_comp_mask, n_tokens, pos0, n_raw, raw_cap,
                                                 raw_start, n_comp, window, ratio, n_head, head_dim);
    return cuda_ok(cudaGetLastError(), "attention decode batch launch");
}

extern "C" int ds4_gpu_attention_decode_raw_batch_heads_tensor(
        ds4_gpu_tensor       *heads,
        const void             *model_map,
        uint64_t                model_size,
        uint64_t                sinks_offset,
        const ds4_gpu_tensor *q,
        const ds4_gpu_tensor *raw_kv,
        uint32_t                n_tokens,
        uint32_t                pos0,
        uint32_t                n_raw,
        uint32_t                raw_cap,
        uint32_t                raw_start,
        uint32_t                window,
        uint32_t                n_head,
        uint32_t                head_dim) {
    return attention_decode_batch_launch(heads, model_map, model_size, sinks_offset,
                                      q, raw_kv, NULL, NULL, 0, n_tokens, pos0,
                                      n_raw, raw_cap, raw_start, 0, window, 1,
                                      n_head, head_dim);
}

extern "C" int ds4_gpu_attention_decode_mixed_batch_heads_tensor(
        ds4_gpu_tensor       *heads,
        const void             *model_map,
        uint64_t                model_size,
        uint64_t                sinks_offset,
        const ds4_gpu_tensor *q,
        const ds4_gpu_tensor *raw_kv,
        const ds4_gpu_tensor *comp_kv,
        const ds4_gpu_tensor *comp_mask,
        uint32_t                use_comp_mask,
        uint32_t                n_tokens,
        uint32_t                pos0,
        uint32_t                n_raw,
        uint32_t                raw_cap,
        uint32_t                raw_start,
        uint32_t                n_comp,
        uint32_t                window,
        uint32_t                ratio,
        uint32_t                n_head,
        uint32_t                head_dim) {
    return attention_decode_batch_launch(heads, model_map, model_size, sinks_offset,
                                      q, raw_kv, comp_kv, comp_mask, use_comp_mask,
                                      n_tokens, pos0, n_raw, raw_cap, raw_start,
                                      n_comp, window, ratio, n_head, head_dim);
}

extern "C" int ds4_gpu_attention_indexed_mixed_batch_heads_tensor(
        ds4_gpu_tensor       *heads,
        const void             *model_map,
        uint64_t                model_size,
        uint64_t                sinks_offset,
        const ds4_gpu_tensor *q,
        const ds4_gpu_tensor *raw_kv,
        const ds4_gpu_tensor *comp_kv,
        const ds4_gpu_tensor *topk,
        uint32_t                n_tokens,
        uint32_t                pos0,
        uint32_t                n_raw,
        uint32_t                raw_cap,
        uint32_t                raw_start,
        uint32_t                n_comp,
        uint32_t                top_k,
        uint32_t                window,
        uint32_t                ratio,
        uint32_t                n_head,
        uint32_t                head_dim) {
    if (!heads || !q || !raw_kv || !comp_kv || !topk || !model_map ||
        n_tokens == 0 || n_raw == 0 || raw_cap < n_raw || raw_start >= raw_cap ||
        n_comp == 0 || top_k == 0 ||
        sinks_offset > model_size ||
        (uint64_t)n_head * sizeof(float) > model_size - sinks_offset ||
        heads->bytes < (uint64_t)n_tokens * n_head * head_dim * sizeof(float) ||
        q->bytes < (uint64_t)n_tokens * n_head * head_dim * sizeof(float) ||
        raw_kv->bytes < (uint64_t)raw_cap * head_dim * sizeof(float) ||
        comp_kv->bytes < (uint64_t)n_comp * head_dim * sizeof(float) ||
        topk->bytes < (uint64_t)n_tokens * top_k * sizeof(int32_t)) {
        return 0;
    }
    if (top_k > 512u) return 0;
    const float *sinks = (const float *)cuda_model_range_ptr(
            model_map, sinks_offset, (uint64_t)n_head * sizeof(float), "attn_sinks");
    if (!sinks) return 0;
    const int32_t *topk_ptr = (const int32_t *)topk->ptr;
    if (n_tokens > 1u && top_k == 512u &&
        getenv("DS4_CUDA_NO_INDEXED_TOPK_SORT") == NULL) {
        const uint64_t sort_bytes = (uint64_t)n_tokens * top_k * sizeof(int32_t);
        int32_t *sorted = (int32_t *)cuda_tmp_alloc(sort_bytes, "indexed attention topk sort");
        if (!sorted) return 0;
        indexed_topk_sort_512_asc_kernel<<<n_tokens, 512>>>(sorted, topk_ptr, n_tokens);
        if (!cuda_ok(cudaGetLastError(), "indexed attention topk sort launch")) return 0;
        topk_ptr = sorted;
    }
    if (n_tokens > 1 && head_dim == 512 && top_k <= 512u &&
        getenv("DS4_CUDA_NO_INDEXED_HEADS8") == NULL) {
        if (getenv("DS4_CUDA_INDEXED_TWOPASS") == NULL) {
            dim3 grid(n_tokens, (n_head + 15u) / 16u, 1);
            attention_indexed_mixed_heads8_online_kernel<8, 16><<<grid, 512>>>((float *)heads->ptr,
                                                                               sinks,
                                                                               (const float *)q->ptr,
                                                                               (const float *)raw_kv->ptr,
                                                                               (const float *)comp_kv->ptr,
                                                                               topk_ptr,
                                                                               n_tokens,
                                                                               pos0,
                                                                               n_raw,
                                                                               raw_cap,
                                                                               raw_start,
                                                                               n_comp,
                                                                               top_k,
                                                                               window,
                                                                               ratio,
                                                                               n_head,
                                                                               head_dim);
            return cuda_ok(cudaGetLastError(), "attention indexed online launch");
        }
        dim3 grid(n_tokens, (n_head + 7u) / 8u, 1);
        attention_indexed_mixed_heads8_rb4_kernel<<<grid, 256>>>((float *)heads->ptr,
                                                                 sinks,
                                                                 (const float *)q->ptr,
                                                                 (const float *)raw_kv->ptr,
                                                                 (const float *)comp_kv->ptr,
                                                                 topk_ptr,
                                                                 n_tokens,
                                                                 pos0,
                                                                 n_raw,
                                                                 raw_cap,
                                                                 raw_start,
                                                                 n_comp,
                                                                 top_k,
                                                                 window,
                                                                 ratio,
                                                                 n_head,
                                                                 head_dim);
        return cuda_ok(cudaGetLastError(), "attention indexed heads8 launch");
    }
    dim3 grid(n_tokens, n_head, 1);
    attention_indexed_mixed_kernel<<<grid, 256>>>((float *)heads->ptr,
                                                  sinks,
                                                  (const float *)q->ptr,
                                                  (const float *)raw_kv->ptr,
                                                  (const float *)comp_kv->ptr,
                                                  topk_ptr,
                                                  n_tokens,
                                                  pos0,
                                                  n_raw,
                                                  raw_cap,
                                                  raw_start,
                                                  n_comp,
                                                  top_k,
                                                  window,
                                                  ratio,
                                                  n_head,
                                                  head_dim);
    return cuda_ok(cudaGetLastError(), "attention indexed mixed launch");
}

static int attention_prefill_mixed_launch(
        ds4_gpu_tensor       *heads,
        const void             *model_map,
        uint64_t                model_size,
        uint64_t                sinks_offset,
        const ds4_gpu_tensor *q,
        const ds4_gpu_tensor *raw_kv,
        const ds4_gpu_tensor *comp_kv,
        const ds4_gpu_tensor *comp_mask,
        uint32_t                use_comp_mask,
        uint32_t                n_tokens,
        uint32_t                n_comp,
        uint32_t                window,
        uint32_t                ratio,
        uint32_t                n_head,
        uint32_t                head_dim) {
    if (!heads || !q || !raw_kv || !model_map || n_tokens == 0 || ratio == 0 ||
        (n_comp != 0 && !comp_kv) || (use_comp_mask && !comp_mask) ||
        sinks_offset > model_size ||
        (uint64_t)n_head * sizeof(float) > model_size - sinks_offset ||
        heads->bytes < (uint64_t)n_tokens * n_head * head_dim * sizeof(float) ||
        q->bytes < (uint64_t)n_tokens * n_head * head_dim * sizeof(float) ||
        raw_kv->bytes < (uint64_t)n_tokens * head_dim * sizeof(float) ||
        (n_comp && comp_kv->bytes < (uint64_t)n_comp * head_dim * sizeof(float)) ||
        (use_comp_mask && comp_mask->bytes < (uint64_t)n_tokens * n_comp * sizeof(float))) {
        return 0;
    }
    const float *sinks = (const float *)cuda_model_range_ptr(
            model_map, sinks_offset, (uint64_t)n_head * sizeof(float), "attn_sinks");
    if (!sinks) return 0;
    if (!use_comp_mask && n_tokens > 1 && head_dim == 512 &&
        getenv("DS4_CUDA_NO_WINDOW_ATTENTION") == NULL &&
        (getenv("DS4_CUDA_WINDOW_ATTENTION") != NULL || (!g_quality_mode && n_tokens >= 128u))) {
        dim3 grid(n_tokens, (n_head + 7u) / 8u, 1);
        attention_static_mixed_heads8_online_kernel<<<grid, 256>>>((float *)heads->ptr,
                                                                   sinks,
                                                                   (const float *)q->ptr,
                                                                   (const float *)raw_kv->ptr,
                                                                   n_comp ? (const float *)comp_kv->ptr : (const float *)raw_kv->ptr,
                                                                   n_tokens,
                                                                   n_comp,
                                                                   window,
                                                                   ratio,
                                                                   n_head,
                                                                   head_dim);
        return cuda_ok(cudaGetLastError(), "attention mixed window launch");
    }
    if (g_cublas_ready && n_tokens > 1 && head_dim == 512 &&
        getenv("DS4_CUDA_NO_CUBLAS_ATTENTION") == NULL) {
        const uint32_t n_keys = n_tokens + n_comp;
        const uint64_t kv_count = (uint64_t)n_keys * head_dim;
        const uint64_t score_count = (uint64_t)n_head * n_tokens * n_keys;
        const uint64_t out_count = (uint64_t)n_head * n_tokens * head_dim;
        const uint64_t kv_bytes = kv_count * sizeof(float);
        const uint64_t score_offset = (kv_bytes + 255u) & ~255ull;
        const uint64_t score_bytes = score_count * sizeof(float);
        const uint64_t out_offset = score_offset + ((score_bytes + 255u) & ~255ull);
        const uint64_t tmp_bytes = out_offset + out_count * sizeof(float);
        float *tmp = (float *)cuda_tmp_alloc(tmp_bytes, "attention mixed cublas");
        if (!tmp) return 0;
        float *kv = tmp;
        float *scores = (float *)((char *)tmp + score_offset);
        float *out_tmp = (float *)((char *)tmp + out_offset);
        attention_prefill_pack_mixed_kv_kernel<<<(kv_count + 255) / 256, 256>>>(
                kv,
                (const float *)raw_kv->ptr,
                n_comp ? (const float *)comp_kv->ptr : (const float *)raw_kv->ptr,
                n_tokens,
                n_comp,
                head_dim);
        if (!cuda_ok(cudaGetLastError(), "attention mixed kv pack launch")) return 0;
        const float alpha = rsqrtf((float)head_dim);
        const float beta = 0.0f;
        cublasStatus_t st = cublasSgemmStridedBatched(g_cublas,
                                                      CUBLAS_OP_T,
                                                      CUBLAS_OP_N,
                                                      (int)n_keys,
                                                      (int)n_tokens,
                                                      (int)head_dim,
                                                      &alpha,
                                                      kv,
                                                      (int)head_dim,
                                                      0,
                                                      (const float *)q->ptr,
                                                      (int)(n_head * head_dim),
                                                      (long long)head_dim,
                                                      &beta,
                                                      scores,
                                                      (int)n_keys,
                                                      (long long)n_keys * n_tokens,
                                                      (int)n_head);
        if (!cublas_ok(st, "attention mixed score gemm")) return 0;
        dim3 sgrid(n_tokens, n_head, 1);
        attention_prefill_mixed_softmax_kernel<<<sgrid, 256>>>(
                scores,
                sinks,
                use_comp_mask ? (const float *)comp_mask->ptr : NULL,
                use_comp_mask,
                n_tokens,
                n_comp,
                window,
                ratio,
                n_keys);
        if (!cuda_ok(cudaGetLastError(), "attention mixed softmax launch")) return 0;
        const float one = 1.0f;
        st = cublasSgemmStridedBatched(g_cublas,
                                       CUBLAS_OP_N,
                                       CUBLAS_OP_N,
                                       (int)head_dim,
                                       (int)n_tokens,
                                       (int)n_keys,
                                       &one,
                                       kv,
                                       (int)head_dim,
                                       0,
                                       scores,
                                       (int)n_keys,
                                       (long long)n_keys * n_tokens,
                                       &beta,
                                       out_tmp,
                                       (int)head_dim,
                                       (long long)head_dim * n_tokens,
                                       (int)n_head);
        if (!cublas_ok(st, "attention mixed value gemm")) return 0;
        uint64_t n = (uint64_t)n_tokens * n_head * head_dim;
        attention_prefill_unpack_heads_kernel<<<(n + 255) / 256, 256>>>((float *)heads->ptr,
                                                                        out_tmp,
                                                                        n_tokens,
                                                                        n_head,
                                                                        head_dim);
        return cuda_ok(cudaGetLastError(), "attention mixed unpack launch");
    }
    dim3 grid(n_tokens, n_head, 1);
    attention_prefill_mixed_kernel<<<grid, 256>>>((float *)heads->ptr,
                                                  sinks,
                                                  (const float *)q->ptr,
                                                  (const float *)raw_kv->ptr,
                                                  n_comp ? (const float *)comp_kv->ptr : (const float *)raw_kv->ptr,
                                                  use_comp_mask ? (const float *)comp_mask->ptr : NULL,
                                                  use_comp_mask, n_tokens, n_comp, window, ratio,
                                                  n_head, head_dim);
    return cuda_ok(cudaGetLastError(), "attention prefill mixed launch");
}

extern "C" int ds4_gpu_attention_prefill_static_mixed_heads_tensor(
        ds4_gpu_tensor       *heads,
        const void             *model_map,
        uint64_t                model_size,
        uint64_t                sinks_offset,
        const ds4_gpu_tensor *q,
        const ds4_gpu_tensor *raw_kv,
        const ds4_gpu_tensor *comp_kv,
        uint32_t                n_tokens,
        uint32_t                n_comp,
        uint32_t                window,
        uint32_t                ratio,
        uint32_t                n_head,
        uint32_t                head_dim) {
    return attention_prefill_mixed_launch(heads, model_map, model_size, sinks_offset,
                                       q, raw_kv, comp_kv, NULL, 0, n_tokens,
                                       n_comp, window, ratio, n_head, head_dim);
}

extern "C" int ds4_gpu_attention_prefill_masked_mixed_heads_tensor(
        ds4_gpu_tensor       *heads,
        const void             *model_map,
        uint64_t                model_size,
        uint64_t                sinks_offset,
        const ds4_gpu_tensor *q,
        const ds4_gpu_tensor *raw_kv,
        const ds4_gpu_tensor *comp_kv,
        const ds4_gpu_tensor *comp_mask,
        uint32_t                n_tokens,
        uint32_t                n_comp,
        uint32_t                window,
        uint32_t                ratio,
        uint32_t                n_head,
        uint32_t                head_dim) {
    return attention_prefill_mixed_launch(heads, model_map, model_size, sinks_offset,
                                       q, raw_kv, comp_kv, comp_mask, 1, n_tokens,
                                       n_comp, window, ratio, n_head, head_dim);
}
extern "C" int ds4_gpu_attention_output_q8_batch_tensor(
        ds4_gpu_tensor       *out,
        ds4_gpu_tensor       *low,
        ds4_gpu_tensor       *group_tmp,
        ds4_gpu_tensor       *low_tmp,
        const void             *model_map,
        uint64_t                model_size,
        uint64_t                out_a_offset,
        uint64_t                out_b_offset,
        uint64_t                group_dim,
        uint64_t                rank,
        uint32_t                n_groups,
        uint64_t                out_dim,
        const ds4_gpu_tensor *heads,
        uint32_t                n_tokens) {
    (void)group_tmp;
    (void)low_tmp;
    if (!out || !low || !heads || !model_map ||
        group_dim == 0 || rank == 0 || n_groups == 0 || out_dim == 0 || n_tokens == 0) {
        return 0;
    }
    const uint64_t low_dim = (uint64_t)n_groups * rank;
    const uint64_t blocks_a = (group_dim + 31) / 32;
    const uint64_t blocks_b = (low_dim + 31) / 32;
    const uint64_t out_a_bytes = (uint64_t)n_groups * rank * blocks_a * 34;
    const uint64_t out_b_bytes = out_dim * blocks_b * 34;
    if (out_a_offset > model_size || out_b_offset > model_size ||
        out_a_bytes > model_size - out_a_offset ||
        out_b_bytes > model_size - out_b_offset ||
        heads->bytes < (uint64_t)n_tokens * n_groups * group_dim * sizeof(float) ||
        low->bytes < (uint64_t)n_tokens * low_dim * sizeof(float) ||
        out->bytes < (uint64_t)n_tokens * out_dim * sizeof(float)) {
        return 0;
    }
    const unsigned char *out_a = reinterpret_cast<const unsigned char *>(
            cuda_model_range_ptr(model_map, out_a_offset, out_a_bytes, "attn_out_a"));
    const unsigned char *out_b = reinterpret_cast<const unsigned char *>(
            cuda_model_range_ptr(model_map, out_b_offset, out_b_bytes, "attn_out_b"));
    if (!out_a || !out_b) return 0;

    const __half *out_a_f16 = NULL;
    uint32_t out_a_cublas_min_tokens = 2u;
    const char *out_a_min_env = getenv("DS4_CUDA_ATTENTION_OUTPUT_A_CUBLAS_MIN");
    if (out_a_min_env && out_a_min_env[0]) {
        char *endp = NULL;
        long v = strtol(out_a_min_env, &endp, 10);
        if (endp != out_a_min_env && v > 1 && v < 4096) out_a_cublas_min_tokens = (uint32_t)v;
    }
    if (!g_quality_mode &&
        g_cublas_ready &&
        n_tokens >= out_a_cublas_min_tokens &&
        getenv("DS4_CUDA_NO_CUBLAS_ATTENTION_OUTPUT_A") == NULL) {
        out_a_f16 = cuda_q8_f16_ptr(model_map, out_a_offset, out_a_bytes, group_dim, low_dim, "attn_output_a");
    }
    if (out_a_f16) {
        const uint64_t heads_h_count = (uint64_t)n_groups * n_tokens * group_dim;
        const uint64_t low_tmp_count = (uint64_t)n_groups * n_tokens * rank;
        const uint64_t heads_h_bytes = heads_h_count * sizeof(__half);
        const uint64_t low_tmp_offset = (heads_h_bytes + 255u) & ~255ull;
        const uint64_t tmp_bytes = low_tmp_offset + low_tmp_count * sizeof(float);
        void *tmp = cuda_tmp_alloc(tmp_bytes, "attention output a cublas");
        if (!tmp) return 0;
        __half *heads_h = (__half *)tmp;
        float *low_packed = (float *)((char *)tmp + low_tmp_offset);
        attention_pack_group_heads_f16_kernel<<<(heads_h_count + 255) / 256, 256>>>(
                heads_h,
                (const float *)heads->ptr,
                n_tokens,
                n_groups,
                group_dim);
        if (!cuda_ok(cudaGetLastError(), "attention_output_q8_a pack launch")) return 0;
        const float alpha = 1.0f;
        const float beta = 0.0f;
        cublasStatus_t st = cublasGemmStridedBatchedEx(g_cublas,
                                                       CUBLAS_OP_T,
                                                       CUBLAS_OP_N,
                                                       (int)rank,
                                                       (int)n_tokens,
                                                       (int)group_dim,
                                                       &alpha,
                                                       out_a_f16,
                                                       CUDA_R_16F,
                                                       (int)group_dim,
                                                       (long long)rank * group_dim,
                                                       heads_h,
                                                       CUDA_R_16F,
                                                       (int)group_dim,
                                                       (long long)n_tokens * group_dim,
                                                       &beta,
                                                       low_packed,
                                                       CUDA_R_32F,
                                                       (int)rank,
                                                       (long long)rank * n_tokens,
                                                       (int)n_groups,
                                                       CUDA_R_32F,
                                                       CUBLAS_GEMM_DEFAULT);
        if (!cublas_ok(st, "attention output a gemm")) return 0;
        attention_unpack_group_low_kernel<<<(low_tmp_count + 255) / 256, 256>>>(
                (float *)low->ptr,
                low_packed,
                n_tokens,
                n_groups,
                rank);
        if (!cuda_ok(cudaGetLastError(), "attention_output_q8_a unpack launch")) return 0;
    } else {
        const uint64_t x_rows = (uint64_t)n_tokens * n_groups;
        const uint64_t xq_bytes = x_rows * blocks_a * 32u;
        const uint64_t scale_offset = (xq_bytes + 15u) & ~15ull;
        const uint64_t tmp_bytes = scale_offset + x_rows * blocks_a * sizeof(float);
        void *tmp = cuda_tmp_alloc(tmp_bytes, "attention output a q8 prequant");
        if (!tmp) return 0;
        int8_t *xq = (int8_t *)tmp;
        float *xscale = (float *)((char *)tmp + scale_offset);
        const int use_dp4a = cuda_q8_use_dp4a();
        dim3 qgrid((unsigned)blocks_a, (unsigned)x_rows, 1);
        quantize_q8_0_f32_kernel<<<qgrid, 32>>>(xq,
                                                xscale,
                                                (const float *)heads->ptr,
                                                group_dim,
                                                blocks_a);
        if (!cuda_ok(cudaGetLastError(), "attention_output_q8_a prequant launch")) return 0;
        dim3 grid_a(((unsigned)low_dim + 7u) / 8u, (unsigned)n_tokens, 1);
        grouped_q8_0_a_preq_warp8_kernel<<<grid_a, 256>>>((float *)low->ptr,
                                                          out_a,
                                                          xq,
                                                          xscale,
                                                          group_dim,
                                                          rank,
                                                          n_groups,
                                                          n_tokens,
                                                          blocks_a,
                                                          use_dp4a);
        if (!cuda_ok(cudaGetLastError(), "attention_output_q8_a preq launch")) return 0;
    }

    (void)out_b;
    return cuda_matmul_q8_0_tensor_labeled(out,
                                           model_map,
                                           model_size,
                                           out_b_offset,
                                           low_dim,
                                           out_dim,
                                           low,
                                           n_tokens,
                                           "attn_output_b");
}
extern "C" int ds4_gpu_attention_output_low_q8_tensor(
        ds4_gpu_tensor       *low,
        const void             *model_map,
        uint64_t                model_size,
        uint64_t                out_a_offset,
        uint64_t                group_dim,
        uint64_t                rank,
        uint32_t                n_groups,
        const ds4_gpu_tensor *heads) {
    if (!low || !heads || !model_map || group_dim == 0 || rank == 0 || n_groups == 0) {
        return 0;
    }
    const uint64_t low_dim = (uint64_t)n_groups * rank;
    const uint64_t blocks_a = (group_dim + 31) / 32;
    const uint64_t out_a_bytes = (uint64_t)n_groups * rank * blocks_a * 34;
    if (out_a_offset > model_size ||
        out_a_bytes > model_size - out_a_offset ||
        heads->bytes < (uint64_t)n_groups * group_dim * sizeof(float) ||
        low->bytes < low_dim * sizeof(float)) {
        return 0;
    }
    const unsigned char *out_a = reinterpret_cast<const unsigned char *>(
            cuda_model_range_ptr(model_map, out_a_offset, out_a_bytes, "attn_out_a"));
    if (!out_a) return 0;

    const uint64_t x_rows = (uint64_t)n_groups;
    const uint64_t xq_bytes = x_rows * blocks_a * 32u;
    const uint64_t scale_offset = (xq_bytes + 15u) & ~15ull;
    const uint64_t tmp_bytes = scale_offset + x_rows * blocks_a * sizeof(float);
    void *tmp = cuda_tmp_alloc(tmp_bytes, "attention output low q8 prequant");
    if (!tmp) return 0;
    int8_t *xq = (int8_t *)tmp;
    float *xscale = (float *)((char *)tmp + scale_offset);
    const int use_dp4a = cuda_q8_use_dp4a();
    dim3 qgrid((unsigned)blocks_a, (unsigned)x_rows, 1);
    quantize_q8_0_f32_kernel<<<qgrid, 32>>>(xq,
                                            xscale,
                                            (const float *)heads->ptr,
                                            group_dim,
                                            blocks_a);
    if (!cuda_ok(cudaGetLastError(), "attention_output_low_q8 prequant launch")) return 0;
    dim3 grid_a(((unsigned)low_dim + 7u) / 8u, 1, 1);
    grouped_q8_0_a_preq_warp8_kernel<<<grid_a, 256>>>((float *)low->ptr,
                                                      out_a,
                                                      xq,
                                                      xscale,
                                                      group_dim,
                                                      rank,
                                                      n_groups,
                                                      1,
                                                      blocks_a,
                                                      use_dp4a);
    return cuda_ok(cudaGetLastError(), "attention_output_low_q8 launch");
}
extern "C" int ds4_gpu_swiglu_tensor(ds4_gpu_tensor *out, const ds4_gpu_tensor *gate, const ds4_gpu_tensor *up, uint32_t n, float clamp, float weight) {
    if (!out || !gate || !up ||
        out->bytes < (uint64_t)n * sizeof(float) ||
        gate->bytes < (uint64_t)n * sizeof(float) ||
        up->bytes < (uint64_t)n * sizeof(float)) return 0;
    swiglu_kernel<<<(n + 255) / 256, 256>>>((float *)out->ptr, (const float *)gate->ptr, (const float *)up->ptr, n, clamp, weight);
    return cuda_ok(cudaGetLastError(), "swiglu launch");
}
extern "C" int ds4_gpu_shared_gate_up_swiglu_q8_0_tensor(
        ds4_gpu_tensor       *gate,
        ds4_gpu_tensor       *up,
        ds4_gpu_tensor       *mid,
        const void             *model_map,
        uint64_t                model_size,
        uint64_t                gate_offset,
        uint64_t                up_offset,
        uint64_t                in_dim,
        uint64_t                out_dim,
        const ds4_gpu_tensor *x) {
    if (getenv("DS4_CUDA_DISABLE_SHARED_GATE_UP_PAIR") == NULL) {
        return ds4_gpu_matmul_q8_0_pair_tensor(gate, up,
                                                 model_map, model_size,
                                                 gate_offset, up_offset,
                                                 in_dim, out_dim, out_dim,
                                                 x, 1) &&
               ds4_gpu_swiglu_tensor(mid, gate, up, (uint32_t)out_dim, 10.0f, 1.0f);
    }
    return ds4_gpu_matmul_q8_0_tensor(gate, model_map, model_size,
                                        gate_offset, in_dim, out_dim, x, 1) &&
           ds4_gpu_matmul_q8_0_tensor(up, model_map, model_size,
                                        up_offset, in_dim, out_dim, x, 1) &&
           ds4_gpu_swiglu_tensor(mid, gate, up, (uint32_t)out_dim, 10.0f, 1.0f);
}
extern "C" int ds4_gpu_add_tensor(ds4_gpu_tensor *out, const ds4_gpu_tensor *a, const ds4_gpu_tensor *b, uint32_t n) {
    if (!out || !a || !b ||
        out->bytes < (uint64_t)n * sizeof(float) ||
        a->bytes < (uint64_t)n * sizeof(float) ||
        b->bytes < (uint64_t)n * sizeof(float)) return 0;
    add_kernel<<<(n + 255) / 256, 256>>>((float *)out->ptr, (const float *)a->ptr, (const float *)b->ptr, n);
    return cuda_ok(cudaGetLastError(), "add launch");
}
extern "C" int ds4_gpu_directional_steering_project_tensor(
        ds4_gpu_tensor       *x,
        const ds4_gpu_tensor *directions,
        uint32_t                layer,
        uint32_t                width,
        uint32_t                rows,
        float                   scale) {
    if (!x || !directions || width == 0 || rows == 0 || scale == 0.0f) return 0;
    const uint64_t x_bytes = (uint64_t)width * rows * sizeof(float);
    const uint64_t dir_bytes = (uint64_t)(layer + 1u) * width * sizeof(float);
    if (x->bytes < x_bytes || directions->bytes < dir_bytes) return 0;

    uint32_t nth = 256u;
    while (nth > width && nth > 1u) nth >>= 1;
    directional_steering_project_kernel<<<rows, nth>>>(
            (float *)x->ptr,
            (const float *)directions->ptr,
            layer,
            width,
            rows,
            scale);
    return cuda_ok(cudaGetLastError(), "directional steering launch");
}
extern "C" int ds4_gpu_router_select_tensor(ds4_gpu_tensor *selected, ds4_gpu_tensor *weights, ds4_gpu_tensor *probs, const void *model_map, uint64_t model_size, uint64_t bias_offset, uint64_t hash_offset, uint32_t hash_rows, uint32_t token, uint32_t n_expert_groups, uint32_t n_group_used, bool has_bias, bool hash_mode, const ds4_gpu_tensor *logits) {
    if (!selected || !weights || !probs || !logits || !model_map || n_expert_groups > 1u || n_group_used > 0u) return 0;
    int32_t tok = (int32_t)token;
    int ok = 1;
    const float *bias = NULL;
    const int32_t *hash = NULL;
    if (ok && has_bias && !hash_mode) {
        if (bias_offset > model_size || model_size - bias_offset < 256u * sizeof(float)) ok = 0;
        else bias = (const float *)cuda_model_range_ptr(model_map, bias_offset, 256u * sizeof(float), "router_bias");
        if (!bias) ok = 0;
    }
    if (ok && hash_mode) {
        const uint64_t hash_bytes = (uint64_t)hash_rows * 6u * sizeof(int32_t);
        if (hash_offset > model_size || hash_bytes > model_size - hash_offset) ok = 0;
        else hash = (const int32_t *)cuda_model_range_ptr(model_map, hash_offset, hash_bytes, "router_hash");
        if (!hash) ok = 0;
    }
    if (ok) {
        if (getenv("DS4_CUDA_NO_WARP_ROUTER_SELECT") == NULL &&
            getenv("DS4_CUDA_NO_PARALLEL_ROUTER_SELECT") == NULL) {
            dim3 block(32, 4, 1);
            router_select_warp_topk_kernel<<<1, block>>>((int32_t *)selected->ptr, (float *)weights->ptr, (float *)probs->ptr,
                                                         bias, hash, (const float *)logits->ptr, NULL, tok, hash_rows, 1,
                                                         has_bias && !hash_mode, hash_mode);
        } else if (getenv("DS4_CUDA_NO_PARALLEL_ROUTER_SELECT") == NULL) {
            router_select_parallel_kernel<<<1, 256>>>((int32_t *)selected->ptr, (float *)weights->ptr, (float *)probs->ptr,
                                                      bias, hash, (const float *)logits->ptr, NULL, tok, hash_rows, 1,
                                                      has_bias && !hash_mode, hash_mode);
        } else {
            router_select_kernel<<<1, 1>>>((int32_t *)selected->ptr, (float *)weights->ptr, (float *)probs->ptr,
                                          bias, hash, (const float *)logits->ptr, NULL, tok, hash_rows, 1,
                                          has_bias && !hash_mode, hash_mode);
        }
        ok = cuda_ok(cudaGetLastError(), "router_select launch");
    }
    return ok;
}
extern "C" int ds4_gpu_router_select_batch_tensor(ds4_gpu_tensor *selected, ds4_gpu_tensor *weights, ds4_gpu_tensor *probs, const void *model_map, uint64_t model_size, uint64_t bias_offset, uint64_t hash_offset, uint32_t hash_rows, uint32_t n_expert_groups, uint32_t n_group_used, bool has_bias, bool hash_mode, const ds4_gpu_tensor *logits, const ds4_gpu_tensor *tokens, uint32_t n_tokens) {
    if (!selected || !weights || !probs || !logits || !tokens || !model_map || n_tokens == 0 ||
        n_expert_groups > 1u || n_group_used > 0u ||
        logits->bytes < (uint64_t)n_tokens * 256u * sizeof(float) ||
        probs->bytes < (uint64_t)n_tokens * 256u * sizeof(float) ||
        selected->bytes < (uint64_t)n_tokens * 6u * sizeof(int32_t) ||
        weights->bytes < (uint64_t)n_tokens * 6u * sizeof(float)) {
        return 0;
    }
    const float *bias = NULL;
    const int32_t *hash = NULL;
    if (has_bias && !hash_mode) {
        if (bias_offset > model_size || model_size - bias_offset < 256u * sizeof(float)) return 0;
        bias = (const float *)cuda_model_range_ptr(model_map, bias_offset, 256u * sizeof(float), "router_bias");
        if (!bias) return 0;
    }
    if (hash_mode) {
        const uint64_t hash_bytes = (uint64_t)hash_rows * 6u * sizeof(int32_t);
        if (hash_offset > model_size || hash_bytes > model_size - hash_offset) return 0;
        hash = (const int32_t *)cuda_model_range_ptr(model_map, hash_offset, hash_bytes, "router_hash");
        if (!hash) return 0;
    }
    if (getenv("DS4_CUDA_NO_WARP_ROUTER_SELECT") == NULL &&
        getenv("DS4_CUDA_NO_PARALLEL_ROUTER_SELECT") == NULL) {
        dim3 block(32, 4, 1);
        router_select_warp_topk_kernel<<<(n_tokens + 3u) / 4u, block>>>((int32_t *)selected->ptr,
                                                                        (float *)weights->ptr,
                                                                        (float *)probs->ptr,
                                                                        bias,
                                                                        hash,
                                                                        (const float *)logits->ptr,
                                                                        (const int32_t *)tokens->ptr,
                                                                        0,
                                                                        hash_rows,
                                                                        n_tokens,
                                                                        has_bias && !hash_mode,
                                                                        hash_mode);
    } else if (getenv("DS4_CUDA_NO_PARALLEL_ROUTER_SELECT") == NULL) {
        router_select_parallel_kernel<<<n_tokens, 256>>>((int32_t *)selected->ptr,
                                                         (float *)weights->ptr,
                                                         (float *)probs->ptr,
                                                         bias,
                                                         hash,
                                                         (const float *)logits->ptr,
                                                         (const int32_t *)tokens->ptr,
                                                         0,
                                                         hash_rows,
                                                         n_tokens,
                                                         has_bias && !hash_mode,
                                                         hash_mode);
    } else {
        router_select_kernel<<<n_tokens, 1>>>((int32_t *)selected->ptr,
                                              (float *)weights->ptr,
                                              (float *)probs->ptr,
                                              bias,
                                              hash,
                                              (const float *)logits->ptr,
                                              (const int32_t *)tokens->ptr,
                                              0,
                                              hash_rows,
                                              n_tokens,
                                              has_bias && !hash_mode,
                                              hash_mode);
    }
    return cuda_ok(cudaGetLastError(), "router_select launch");
}

extern "C" int ds4_gpu_arena_router_select_bias_tensor(
        const ds4_gpu_arena           *arena,
        const ds4_gpu_source_row_view *bias,
        ds4_gpu_tensor                *selected_i32,
        ds4_gpu_tensor                *weights_f32,
        ds4_gpu_tensor                *probs_f32,
        const ds4_gpu_tensor          *logits_f32) {
    if (!arena || !bias || !selected_i32 || !weights_f32 || !probs_f32 ||
        !logits_f32 || !arena->valid || !arena->ptr ||
        !selected_i32->ptr || !weights_f32->ptr || !probs_f32->ptr ||
        !logits_f32->ptr) {
        return 1;
    }
    if (bias->rows != 1u || bias->cols != 256u ||
        (bias->arena_offset & 3ull) != 0 ||
        (bias->row_stride_bytes & 3u) != 0 ||
        !cuda_arena_range_ok(arena, bias->arena_offset, bias->byte_length) ||
        bias->byte_length < 256ull * sizeof(float) ||
        bias->row_stride_bytes < 256u * sizeof(float)) {
        return 1;
    }
    if (selected_i32->device != arena->gpu ||
        weights_f32->device != arena->gpu ||
        probs_f32->device != arena->gpu ||
        logits_f32->device != arena->gpu ||
        selected_i32->bytes < 6ull * sizeof(int32_t) ||
        weights_f32->bytes < 6ull * sizeof(float) ||
        probs_f32->bytes < 256ull * sizeof(float) ||
        logits_f32->bytes < 256ull * sizeof(float)) {
        return 1;
    }
    if (!cuda_ok(cudaSetDevice(arena->gpu), "arena router select set device")) return 1;

    const float *bias_ptr =
        (const float *)((const char *)arena->ptr + bias->arena_offset);
    if (getenv("DS4_CUDA_NO_WARP_ROUTER_SELECT") == NULL &&
        getenv("DS4_CUDA_NO_PARALLEL_ROUTER_SELECT") == NULL) {
        dim3 block(32, 4, 1);
        router_select_warp_topk_kernel<<<1, block>>>(
                (int32_t *)selected_i32->ptr,
                (float *)weights_f32->ptr,
                (float *)probs_f32->ptr,
                bias_ptr,
                NULL,
                (const float *)logits_f32->ptr,
                NULL,
                0,
                0,
                1,
                1,
                0);
    } else if (getenv("DS4_CUDA_NO_PARALLEL_ROUTER_SELECT") == NULL) {
        router_select_parallel_kernel<<<1, 256>>>(
                (int32_t *)selected_i32->ptr,
                (float *)weights_f32->ptr,
                (float *)probs_f32->ptr,
                bias_ptr,
                NULL,
                (const float *)logits_f32->ptr,
                NULL,
                0,
                0,
                1,
                1,
                0);
    } else {
        router_select_kernel<<<1, 1>>>(
                (int32_t *)selected_i32->ptr,
                (float *)weights_f32->ptr,
                (float *)probs_f32->ptr,
                bias_ptr,
                NULL,
                (const float *)logits_f32->ptr,
                NULL,
                0,
                0,
                1,
                1,
                0);
    }
    return cuda_ok(cudaGetLastError(), "arena router select launch") ? 0 : 1;
}

__device__ static float dev_f16_to_f32(uint16_t v) {
    return __half2float(*reinterpret_cast<const __half *>(&v));
}

__device__ __forceinline__ static uint32_t dev_unpack_iq2_signs(uint32_t v) {
    const uint32_t p = __popc(v) & 1u;
    const uint32_t s = v ^ (p << 7u);
    return s * 0x01010101u;
}

__device__ __forceinline__ static int32_t dev_iq2_dp4a_8(uint64_t grid, uint32_t sign, const int8_t *q8, int32_t acc) {
    const uint32_t signs = dev_unpack_iq2_signs(sign);
    const int32_t sm0 = __vcmpne4(signs & 0x08040201u, 0);
    const int32_t sm1 = __vcmpne4(signs & 0x80402010u, 0);
    const int32_t g0 = __vsub4((int32_t)(uint32_t)grid ^ sm0, sm0);
    const int32_t g1 = __vsub4((int32_t)(uint32_t)(grid >> 32) ^ sm1, sm1);
    acc = __dp4a(g0, *(const int32_t *)(q8 + 0), acc);
    acc = __dp4a(g1, *(const int32_t *)(q8 + 4), acc);
    return acc;
}

__device__ static int32_t dev_dot_q2_16(const uint8_t *q2, const int8_t *q8, int shift) {
    int32_t sum = 0;
    #pragma unroll
    for (uint32_t i = 0; i < 16; i += 4) {
        const int32_t v = (*(const int32_t *)(q2 + i) >> shift) & 0x03030303;
        sum = __dp4a(v, *(const int32_t *)(q8 + i), sum);
    }
    return sum;
}

__device__ static int32_t dev_dot_iq2_pair_16(uint8_t grid0, uint32_t sign0, uint8_t grid1, uint32_t sign1, const int8_t *q8) {
    int32_t sum = 0;
    sum = dev_iq2_dp4a_8(cuda_iq2xxs_grid[grid0], cuda_ksigns_iq2xs[sign0], q8, sum);
    sum = dev_iq2_dp4a_8(cuda_iq2xxs_grid[grid1], cuda_ksigns_iq2xs[sign1], q8 + 8, sum);
    return sum;
}

__device__ __forceinline__ static void dev_iq2_i8x8_lut(
        const uint64_t *grid,
        const uint8_t *signs,
        uint8_t grid_idx,
        uint32_t sign_idx,
        int32_t *w0,
        int32_t *w1) {
    const uint32_t s = dev_unpack_iq2_signs(signs[sign_idx]);
    const int32_t sm0 = __vcmpne4(s & 0x08040201u, 0);
    const int32_t sm1 = __vcmpne4(s & 0x80402010u, 0);
    const uint64_t g = grid[grid_idx];
    *w0 = __vsub4((int32_t)(uint32_t)g ^ sm0, sm0);
    *w1 = __vsub4((int32_t)(uint32_t)(g >> 32) ^ sm1, sm1);
}

__device__ static float dev_dot_iq2_xxs_q8_K_block_lut(
        const cuda_block_iq2_xxs *x,
        const cuda_block_q8_K *y,
        const uint64_t *grid,
        const uint8_t *signs) {
    const float xd = dev_f16_to_f32(x->d);
    const uint16_t *q2 = x->qs;
    const int8_t *q8 = y->qs;
    int32_t bsum = 0;
    for (int ib32 = 0; ib32 < CUDA_QK_K / 32; ib32++) {
        const uint32_t aux0 = (uint32_t)q2[0] | ((uint32_t)q2[1] << 16);
        const uint32_t aux1 = (uint32_t)q2[2] | ((uint32_t)q2[3] << 16);
        q2 += 4;
        const int32_t ls = (int32_t)(2u * (aux1 >> 28) + 1u);
        int32_t w[8];
        dev_iq2_i8x8_lut(grid, signs, (uint8_t)(aux0 & 0xffu),           (aux1 >> 0)  & 127u, &w[0], &w[1]);
        dev_iq2_i8x8_lut(grid, signs, (uint8_t)((aux0 >> 8)  & 0xffu),   (aux1 >> 7)  & 127u, &w[2], &w[3]);
        dev_iq2_i8x8_lut(grid, signs, (uint8_t)((aux0 >> 16) & 0xffu),   (aux1 >> 14) & 127u, &w[4], &w[5]);
        dev_iq2_i8x8_lut(grid, signs, (uint8_t)((aux0 >> 24) & 0xffu),   (aux1 >> 21) & 127u, &w[6], &w[7]);
        int32_t sumi = 0;
        sumi = __dp4a(w[0], *(const int32_t *)(q8 + ib32 * 32u + 0),  sumi);
        sumi = __dp4a(w[1], *(const int32_t *)(q8 + ib32 * 32u + 4),  sumi);
        sumi = __dp4a(w[2], *(const int32_t *)(q8 + ib32 * 32u + 8),  sumi);
        sumi = __dp4a(w[3], *(const int32_t *)(q8 + ib32 * 32u + 12), sumi);
        sumi = __dp4a(w[4], *(const int32_t *)(q8 + ib32 * 32u + 16), sumi);
        sumi = __dp4a(w[5], *(const int32_t *)(q8 + ib32 * 32u + 20), sumi);
        sumi = __dp4a(w[6], *(const int32_t *)(q8 + ib32 * 32u + 24), sumi);
        sumi = __dp4a(w[7], *(const int32_t *)(q8 + ib32 * 32u + 28), sumi);
        bsum += sumi * ls;
    }
    return 0.125f * xd * y->d * (float)bsum;
}

__device__ static float dev_dot_iq2_xxs_q8_K_block(const cuda_block_iq2_xxs *x, const cuda_block_q8_K *y) {
    const float d = dev_f16_to_f32(x->d) * y->d;
    const uint16_t *q2 = x->qs;
    const int8_t *q8 = y->qs;
    int32_t bsum = 0;
    for (int ib32 = 0; ib32 < CUDA_QK_K / 32; ib32++) {
        const uint32_t aux0 = (uint32_t)q2[0] | ((uint32_t)q2[1] << 16);
        const uint32_t aux1 = (uint32_t)q2[2] | ((uint32_t)q2[3] << 16);
        q2 += 4;
        const uint32_t ls = 2u * (aux1 >> 28) + 1u;
        const uint8_t a0 = (uint8_t)(aux0 & 0xffu);
        const uint8_t a1 = (uint8_t)((aux0 >> 8) & 0xffu);
        const uint8_t a2 = (uint8_t)((aux0 >> 16) & 0xffu);
        const uint8_t a3 = (uint8_t)((aux0 >> 24) & 0xffu);
        int32_t sumi = 0;
        sumi += dev_dot_iq2_pair_16(a0, (aux1 >> 0) & 127u, a1, (aux1 >> 7) & 127u, q8);
        q8 += 16;
        sumi += dev_dot_iq2_pair_16(a2, (aux1 >> 14) & 127u, a3, (aux1 >> 21) & 127u, q8);
        q8 += 16;
        bsum += sumi * (int32_t)ls;
    }
    return 0.125f * d * (float)bsum;
}

__device__ static void dev_dot_iq2_xxs_q8_K_block8_deq_lut(
        const cuda_block_iq2_xxs *x,
        const cuda_block_q8_K *y0,
        const cuda_block_q8_K *y1,
        const cuda_block_q8_K *y2,
        const cuda_block_q8_K *y3,
        const cuda_block_q8_K *y4,
        const cuda_block_q8_K *y5,
        const cuda_block_q8_K *y6,
        const cuda_block_q8_K *y7,
        uint32_t n,
        float acc[8],
        const uint64_t *grid,
        const uint8_t *signs) {
    const float xd = dev_f16_to_f32(x->d);
    const uint16_t *q2 = x->qs;
    int32_t bsum[8] = {0, 0, 0, 0, 0, 0, 0, 0};
    const int8_t *q8[8] = {
        y0 ? y0->qs : NULL, y1 ? y1->qs : NULL, y2 ? y2->qs : NULL, y3 ? y3->qs : NULL,
        y4 ? y4->qs : NULL, y5 ? y5->qs : NULL, y6 ? y6->qs : NULL, y7 ? y7->qs : NULL,
    };
    for (int ib32 = 0; ib32 < CUDA_QK_K / 32; ib32++) {
        const uint32_t aux0 = (uint32_t)q2[0] | ((uint32_t)q2[1] << 16);
        const uint32_t aux1 = (uint32_t)q2[2] | ((uint32_t)q2[3] << 16);
        q2 += 4;
        const int32_t ls = (int32_t)(2u * (aux1 >> 28) + 1u);
        int32_t w[8];
        dev_iq2_i8x8_lut(grid, signs, (uint8_t)(aux0 & 0xffu),           (aux1 >> 0)  & 127u, &w[0], &w[1]);
        dev_iq2_i8x8_lut(grid, signs, (uint8_t)((aux0 >> 8)  & 0xffu),   (aux1 >> 7)  & 127u, &w[2], &w[3]);
        dev_iq2_i8x8_lut(grid, signs, (uint8_t)((aux0 >> 16) & 0xffu),   (aux1 >> 14) & 127u, &w[4], &w[5]);
        dev_iq2_i8x8_lut(grid, signs, (uint8_t)((aux0 >> 24) & 0xffu),   (aux1 >> 21) & 127u, &w[6], &w[7]);
        for (uint32_t p = 0; p < n; p++) {
            const int8_t *q = q8[p] + ib32 * 32;
            int32_t sumi = 0;
            sumi = __dp4a(w[0], *(const int32_t *)(q + 0),  sumi);
            sumi = __dp4a(w[1], *(const int32_t *)(q + 4),  sumi);
            sumi = __dp4a(w[2], *(const int32_t *)(q + 8),  sumi);
            sumi = __dp4a(w[3], *(const int32_t *)(q + 12), sumi);
            sumi = __dp4a(w[4], *(const int32_t *)(q + 16), sumi);
            sumi = __dp4a(w[5], *(const int32_t *)(q + 20), sumi);
            sumi = __dp4a(w[6], *(const int32_t *)(q + 24), sumi);
            sumi = __dp4a(w[7], *(const int32_t *)(q + 28), sumi);
            bsum[p] += sumi * ls;
        }
    }
    const cuda_block_q8_K *ys[8] = { y0, y1, y2, y3, y4, y5, y6, y7 };
    for (uint32_t p = 0; p < n; p++) acc[p] += 0.125f * xd * ys[p]->d * (float)bsum[p];
}

__device__ static void dev_dot_iq2_xxs_q8_K_block4(
        const cuda_block_iq2_xxs *x,
        const cuda_block_q8_K *y0,
        const cuda_block_q8_K *y1,
        const cuda_block_q8_K *y2,
        const cuda_block_q8_K *y3,
        uint32_t n,
        float acc[4]) {
    const float xd = dev_f16_to_f32(x->d);
    const uint16_t *q2 = x->qs;
    int32_t bsum[4] = {0, 0, 0, 0};
    const int8_t *q8[4] = {
        y0 ? y0->qs : NULL,
        y1 ? y1->qs : NULL,
        y2 ? y2->qs : NULL,
        y3 ? y3->qs : NULL,
    };
    for (int ib32 = 0; ib32 < CUDA_QK_K / 32; ib32++) {
        const uint32_t aux0 = (uint32_t)q2[0] | ((uint32_t)q2[1] << 16);
        const uint32_t aux1 = (uint32_t)q2[2] | ((uint32_t)q2[3] << 16);
        q2 += 4;
        const uint32_t ls = 2u * (aux1 >> 28) + 1u;
        const uint8_t a0 = (uint8_t)(aux0 & 0xffu);
        const uint8_t a1 = (uint8_t)((aux0 >> 8) & 0xffu);
        const uint8_t a2 = (uint8_t)((aux0 >> 16) & 0xffu);
        const uint8_t a3 = (uint8_t)((aux0 >> 24) & 0xffu);
        for (uint32_t p = 0; p < n; p++) {
            int32_t sumi = 0;
            sumi += dev_dot_iq2_pair_16(a0, (aux1 >> 0) & 127u, a1, (aux1 >> 7) & 127u, q8[p] + ib32 * 32);
            sumi += dev_dot_iq2_pair_16(a2, (aux1 >> 14) & 127u, a3, (aux1 >> 21) & 127u, q8[p] + ib32 * 32 + 16);
            bsum[p] += sumi * (int32_t)ls;
        }
    }
    const cuda_block_q8_K *ys[4] = { y0, y1, y2, y3 };
    for (uint32_t p = 0; p < n; p++) acc[p] += 0.125f * xd * ys[p]->d * (float)bsum[p];
}

__device__ static DS4_CUDA_UNUSED void dev_dot_iq2_xxs_q8_K_block8(
        const cuda_block_iq2_xxs *x,
        const cuda_block_q8_K *y0,
        const cuda_block_q8_K *y1,
        const cuda_block_q8_K *y2,
        const cuda_block_q8_K *y3,
        const cuda_block_q8_K *y4,
        const cuda_block_q8_K *y5,
        const cuda_block_q8_K *y6,
        const cuda_block_q8_K *y7,
        uint32_t n,
        float acc[8]) {
    const float xd = dev_f16_to_f32(x->d);
    const uint16_t *q2 = x->qs;
    int32_t bsum[8] = {0, 0, 0, 0, 0, 0, 0, 0};
    const int8_t *q8[8] = {
        y0 ? y0->qs : NULL, y1 ? y1->qs : NULL, y2 ? y2->qs : NULL, y3 ? y3->qs : NULL,
        y4 ? y4->qs : NULL, y5 ? y5->qs : NULL, y6 ? y6->qs : NULL, y7 ? y7->qs : NULL,
    };
    for (int ib32 = 0; ib32 < CUDA_QK_K / 32; ib32++) {
        const uint32_t aux0 = (uint32_t)q2[0] | ((uint32_t)q2[1] << 16);
        const uint32_t aux1 = (uint32_t)q2[2] | ((uint32_t)q2[3] << 16);
        q2 += 4;
        const uint32_t ls = 2u * (aux1 >> 28) + 1u;
        const uint8_t a0 = (uint8_t)(aux0 & 0xffu);
        const uint8_t a1 = (uint8_t)((aux0 >> 8) & 0xffu);
        const uint8_t a2 = (uint8_t)((aux0 >> 16) & 0xffu);
        const uint8_t a3 = (uint8_t)((aux0 >> 24) & 0xffu);
        for (uint32_t p = 0; p < n; p++) {
            int32_t sumi = 0;
            sumi += dev_dot_iq2_pair_16(a0, (aux1 >> 0) & 127u, a1, (aux1 >> 7) & 127u, q8[p] + ib32 * 32);
            sumi += dev_dot_iq2_pair_16(a2, (aux1 >> 14) & 127u, a3, (aux1 >> 21) & 127u, q8[p] + ib32 * 32 + 16);
            bsum[p] += sumi * (int32_t)ls;
        }
    }
    const cuda_block_q8_K *ys[8] = { y0, y1, y2, y3, y4, y5, y6, y7 };
    for (uint32_t p = 0; p < n; p++) acc[p] += 0.125f * xd * ys[p]->d * (float)bsum[p];
}

__device__ static void dev_q4_K_get_scale_min(
        uint32_t j,
        const uint8_t *scales,
        uint8_t *d_out,
        uint8_t *m_out) {
    if (j < 4u) {
        *d_out = scales[j] & 63u;
        *m_out = scales[j + 4u] & 63u;
    } else {
        *d_out = (scales[j + 4u] & 0x0fu) | ((scales[j - 4u] >> 6u) << 4u);
        *m_out = (scales[j + 4u] >> 4u) | ((scales[j] >> 6u) << 4u);
    }
}

__device__ __forceinline__ static int32_t dev_dot_q4_32(const uint8_t *qs, const int8_t *q8, int shift) {
    int32_t sum = 0;
    #pragma unroll
    for (uint32_t i = 0; i < 32u; i += 4u) {
        const int32_t v = (*(const int32_t *)(qs + i) >> shift) & 0x0f0f0f0f;
        sum = __dp4a(v, *(const int32_t *)(q8 + i), sum);
    }
    return sum;
}

__device__ static float dev_dot_q4_K_q8_K_block(const cuda_block_q4_K *x, const cuda_block_q8_K *y) {
    const float xd = dev_f16_to_f32(x->d);
    const float xmin = dev_f16_to_f32(x->dmin);
    int isum = 0;
    int summs = 0;
    #pragma unroll
    for (uint32_t j = 0; j < 8u; j++) {
        uint8_t sc, m;
        dev_q4_K_get_scale_min(j, x->scales, &sc, &m);
        summs += (int)m * (int)(y->bsums[2u * j] + y->bsums[2u * j + 1u]);
        const uint32_t byte_off = (j >> 1u) * 32u;
        const int shift = (j & 1u) ? 4 : 0;
        isum += (int)sc * dev_dot_q4_32(x->qs + byte_off, y->qs + j * 32u, shift);
    }
    return y->d * xd * (float)isum - y->d * xmin * (float)summs;
}

__device__ static float dev_dot_q2_K_q8_K_block(const cuda_block_q2_K *x, const cuda_block_q8_K *y) {
    const uint8_t *q2 = x->qs;
    const int8_t *q8 = y->qs;
    const uint8_t *sc = x->scales;
    int summs = 0;
    for (int j = 0; j < 16; j++) summs += y->bsums[j] * (sc[j] >> 4);
    const float dall = y->d * dev_f16_to_f32(x->d);
    const float dmin = y->d * dev_f16_to_f32(x->dmin);
    int isum = 0;
    int is = 0;
    for (int k = 0; k < CUDA_QK_K / 128; k++) {
        int shift = 0;
        for (int j = 0; j < 4; j++) {
            int d = sc[is++] & 0x0f;
            isum += d * dev_dot_q2_16(q2, q8, shift);
            d = sc[is++] & 0x0f;
            isum += d * dev_dot_q2_16(q2 + 16, q8 + 16, shift);
            shift += 2;
            q8 += 32;
        }
        q2 += 32;
    }
    return dall * (float)isum - dmin * (float)summs;
}

__device__ static void dev_dot_q2_K_q8_K_block4(
        const cuda_block_q2_K *x,
        const cuda_block_q8_K *y0,
        const cuda_block_q8_K *y1,
        const cuda_block_q8_K *y2,
        const cuda_block_q8_K *y3,
        uint32_t n,
        float acc[4]) {
    const uint8_t *sc = x->scales;
    const float xd = dev_f16_to_f32(x->d);
    const float xmin = dev_f16_to_f32(x->dmin);
    const cuda_block_q8_K *ys[4] = { y0, y1, y2, y3 };
    int isum[4] = {0, 0, 0, 0};
    int summs[4] = {0, 0, 0, 0};
    for (uint32_t p = 0; p < n; p++) {
        for (int j = 0; j < 16; j++) summs[p] += ys[p]->bsums[j] * (sc[j] >> 4);
    }
    for (uint32_t p = 0; p < n; p++) {
        const uint8_t *q2 = x->qs;
        const int8_t *q8 = ys[p]->qs;
        int is = 0;
        for (int k = 0; k < CUDA_QK_K / 128; k++) {
            int shift = 0;
            for (int j = 0; j < 4; j++) {
                int d = sc[is++] & 0x0f;
                isum[p] += d * dev_dot_q2_16(q2, q8, shift);
                d = sc[is++] & 0x0f;
                isum[p] += d * dev_dot_q2_16(q2 + 16, q8 + 16, shift);
                shift += 2;
                q8 += 32;
            }
            q2 += 32;
        }
    }
    for (uint32_t p = 0; p < n; p++) {
        const float yd = ys[p]->d;
        acc[p] += yd * xd * (float)isum[p] - yd * xmin * (float)summs[p];
    }
}

__device__ static void dev_dot_q2_K_q8_K_block8(
        const cuda_block_q2_K *x,
        const cuda_block_q8_K *y0,
        const cuda_block_q8_K *y1,
        const cuda_block_q8_K *y2,
        const cuda_block_q8_K *y3,
        const cuda_block_q8_K *y4,
        const cuda_block_q8_K *y5,
        const cuda_block_q8_K *y6,
        const cuda_block_q8_K *y7,
        uint32_t n,
        float acc[8]) {
    const uint8_t *sc = x->scales;
    const float xd = dev_f16_to_f32(x->d);
    const float xmin = dev_f16_to_f32(x->dmin);
    const cuda_block_q8_K *ys[8] = { y0, y1, y2, y3, y4, y5, y6, y7 };
    int isum[8] = {0, 0, 0, 0, 0, 0, 0, 0};
    int summs[8] = {0, 0, 0, 0, 0, 0, 0, 0};
    for (uint32_t p = 0; p < n; p++) {
        for (int j = 0; j < 16; j++) summs[p] += ys[p]->bsums[j] * (sc[j] >> 4);
    }
    for (uint32_t p = 0; p < n; p++) {
        const uint8_t *q2 = x->qs;
        const int8_t *q8 = ys[p]->qs;
        int is = 0;
        for (int k = 0; k < CUDA_QK_K / 128; k++) {
            int shift = 0;
            for (int j = 0; j < 4; j++) {
                int d = sc[is++] & 0x0f;
                isum[p] += d * dev_dot_q2_16(q2, q8, shift);
                d = sc[is++] & 0x0f;
                isum[p] += d * dev_dot_q2_16(q2 + 16, q8 + 16, shift);
                shift += 2;
                q8 += 32;
            }
            q2 += 32;
        }
    }
    for (uint32_t p = 0; p < n; p++) {
        const float yd = ys[p]->d;
        acc[p] += yd * xd * (float)isum[p] - yd * xmin * (float)summs[p];
    }
}

__device__ static float half_warp_sum_f32(float v, uint32_t lane16) {
    uint32_t mask = 0xffffu << (threadIdx.x & 16u);
    for (int offset = 8; offset > 0; offset >>= 1) {
        v += __shfl_down_sync(mask, v, offset, 16);
    }
    (void)lane16;
    return v;
}

__device__ static float quarter_warp_sum_f32(float v, uint32_t lane8) {
    uint32_t mask = 0xffu << (threadIdx.x & 24u);
    for (int offset = 4; offset > 0; offset >>= 1) {
        v += __shfl_down_sync(mask, v, offset, 8);
    }
    (void)lane8;
    return v;
}

__global__ static void q8_K_quantize_kernel(cuda_block_q8_K *out, const float *x, uint32_t in_dim, uint32_t n_rows) {
    uint32_t b = blockIdx.x;
    uint32_t row = blockIdx.y;
    if (row >= n_rows || b >= in_dim / CUDA_QK_K) return;
    const float *xr = x + (uint64_t)row * in_dim + (uint64_t)b * CUDA_QK_K;
    cuda_block_q8_K *yb = out + (uint64_t)row * (in_dim / CUDA_QK_K) + b;
    __shared__ float abs_part[256];
    __shared__ float val_part[256];
    __shared__ float maxv_s;
    __shared__ float iscale_s;
    uint32_t tid = threadIdx.x;
    float v = tid < CUDA_QK_K ? xr[tid] : 0.0f;
    abs_part[tid] = tid < CUDA_QK_K ? fabsf(v) : 0.0f;
    val_part[tid] = v;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (tid < stride && abs_part[tid + stride] > abs_part[tid]) {
            abs_part[tid] = abs_part[tid + stride];
            val_part[tid] = val_part[tid + stride];
        }
        __syncthreads();
    }
    float amax = abs_part[0];
    if (amax == 0.0f) {
        if (tid == 0) yb->d = 0.0f;
        if (tid < CUDA_QK_K) yb->qs[tid] = 0;
        if (tid < CUDA_QK_K / 16) yb->bsums[tid] = 0;
        return;
    }
    if (tid == 0) {
        maxv_s = val_part[0];
        iscale_s = -127.0f / maxv_s;
    }
    __syncthreads();
    if (tid < CUDA_QK_K) {
        int qv = (int)lrintf(iscale_s * xr[tid]);
        if (qv > 127) qv = 127;
        if (qv < -128) qv = -128;
        yb->qs[tid] = (int8_t)qv;
    }
    __syncthreads();
    if (tid < CUDA_QK_K / 16) {
        int sum = 0;
        for (int i = 0; i < 16; i++) sum += yb->qs[tid * 16 + i];
        yb->bsums[tid] = (int16_t)sum;
    }
    if (tid == 0) yb->d = 1.0f / iscale_s;
}

__global__ static DS4_CUDA_UNUSED void moe_gate_up_mid_kernel(
        float *gate_out,
        float *up_out,
        float *mid_out,
        const char *gate_base,
        const char *up_base,
        const cuda_block_q8_K *xq,
        const int32_t *selected,
        const float *weights,
        uint64_t gate_expert_bytes,
        uint64_t gate_row_bytes,
        uint32_t xq_blocks,
        uint32_t expert_mid_dim,
        uint32_t n_expert,
        float clamp) {
    uint32_t row = blockIdx.x;
    uint32_t pair = blockIdx.y;
    if (row >= expert_mid_dim) return;
    uint32_t tok = pair / n_expert;
    uint32_t slot = pair - tok * n_expert;
    int32_t expert_i = selected[(uint64_t)tok * n_expert + slot];
    if (expert_i < 0) expert_i = 0;
    uint32_t expert = (uint32_t)expert_i;
    const cuda_block_iq2_xxs *gr = (const cuda_block_iq2_xxs *)(gate_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
    const cuda_block_iq2_xxs *ur = (const cuda_block_iq2_xxs *)(up_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
    const cuda_block_q8_K *xqb = xq + (uint64_t)tok * xq_blocks;
    float gate = 0.0f;
    float up = 0.0f;
    for (uint32_t b = threadIdx.x; b < xq_blocks; b += blockDim.x) {
        gate += dev_dot_iq2_xxs_q8_K_block(gr + b, xqb + b);
        up += dev_dot_iq2_xxs_q8_K_block(ur + b, xqb + b);
    }
    __shared__ float partial_gate[256];
    __shared__ float partial_up[256];
    partial_gate[threadIdx.x] = gate;
    partial_up[threadIdx.x] = up;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) {
            partial_gate[threadIdx.x] += partial_gate[threadIdx.x + stride];
            partial_up[threadIdx.x] += partial_up[threadIdx.x + stride];
        }
        __syncthreads();
    }
    if (threadIdx.x == 0) {
        gate = partial_gate[0];
        up = partial_up[0];
        if (clamp > 1.0e-6f) {
            if (gate > clamp) gate = clamp;
            if (up > clamp) up = clamp;
            if (up < -clamp) up = -clamp;
        }
        const uint64_t off = (uint64_t)pair * expert_mid_dim + row;
        gate_out[off] = gate;
        up_out[off] = up;
        mid_out[off] = (gate / (1.0f + expf(-gate))) * up * weights[(uint64_t)tok * n_expert + slot];
    }
}

__global__ static DS4_CUDA_UNUSED void moe_gate_up_mid_warp8_kernel(
        float *gate_out,
        float *up_out,
        float *mid_out,
        const char *gate_base,
        const char *up_base,
        const cuda_block_q8_K *xq,
        const int32_t *selected,
        const float *weights,
        uint64_t gate_expert_bytes,
        uint64_t gate_row_bytes,
        uint32_t xq_blocks,
        uint32_t expert_mid_dim,
        uint32_t n_expert,
        float clamp) {
    uint32_t lane = threadIdx.x & 31u;
    uint32_t warp = threadIdx.x >> 5u;
    uint32_t row = blockIdx.x * 8u + warp;
    uint32_t pair = blockIdx.y;
    if (row >= expert_mid_dim) return;
    uint32_t tok = pair / n_expert;
    uint32_t slot = pair - tok * n_expert;
    int32_t expert_i = selected[(uint64_t)tok * n_expert + slot];
    if (expert_i < 0) expert_i = 0;
    uint32_t expert = (uint32_t)expert_i;
    const cuda_block_iq2_xxs *gr = (const cuda_block_iq2_xxs *)(gate_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
    const cuda_block_iq2_xxs *ur = (const cuda_block_iq2_xxs *)(up_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
    const cuda_block_q8_K *xqb = xq + (uint64_t)tok * xq_blocks;
    float gate = 0.0f;
    float up = 0.0f;
    for (uint32_t b = lane; b < xq_blocks; b += 32u) {
        gate += dev_dot_iq2_xxs_q8_K_block(gr + b, xqb + b);
        up += dev_dot_iq2_xxs_q8_K_block(ur + b, xqb + b);
    }
    gate = warp_sum_f32(gate);
    up = warp_sum_f32(up);
    if (lane == 0) {
        if (clamp > 1.0e-6f) {
            if (gate > clamp) gate = clamp;
            if (up > clamp) up = clamp;
            if (up < -clamp) up = -clamp;
        }
        const uint64_t off = (uint64_t)pair * expert_mid_dim + row;
        gate_out[off] = gate;
        up_out[off] = up;
        mid_out[off] = (gate / (1.0f + expf(-gate))) * up * weights[(uint64_t)tok * n_expert + slot];
    }
}

__global__ static DS4_CUDA_UNUSED void moe_gate_up_mid_hwarp16_kernel(
        float *gate_out,
        float *up_out,
        float *mid_out,
        const char *gate_base,
        const char *up_base,
        const cuda_block_q8_K *xq,
        const int32_t *selected,
        const float *weights,
        uint64_t gate_expert_bytes,
        uint64_t gate_row_bytes,
        uint32_t xq_blocks,
        uint32_t expert_mid_dim,
        uint32_t n_expert,
        float clamp) {
    uint32_t lane = threadIdx.x & 15u;
    uint32_t row = blockIdx.x * 16u + (threadIdx.x >> 4u);
    uint32_t pair = blockIdx.y;
    if (row >= expert_mid_dim) return;
    uint32_t tok = pair / n_expert;
    uint32_t slot = pair - tok * n_expert;
    int32_t expert_i = selected[(uint64_t)tok * n_expert + slot];
    if (expert_i < 0) expert_i = 0;
    uint32_t expert = (uint32_t)expert_i;
    const cuda_block_iq2_xxs *gr = (const cuda_block_iq2_xxs *)(gate_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
    const cuda_block_iq2_xxs *ur = (const cuda_block_iq2_xxs *)(up_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
    const cuda_block_q8_K *xqb = xq + (uint64_t)tok * xq_blocks;
    float gate = 0.0f;
    float up = 0.0f;
    for (uint32_t b = lane; b < xq_blocks; b += 16u) {
        gate += dev_dot_iq2_xxs_q8_K_block(gr + b, xqb + b);
        up += dev_dot_iq2_xxs_q8_K_block(ur + b, xqb + b);
    }
    gate = half_warp_sum_f32(gate, lane);
    up = half_warp_sum_f32(up, lane);
    if (lane == 0) {
        if (clamp > 1.0e-6f) {
            if (gate > clamp) gate = clamp;
            if (up > clamp) up = clamp;
            if (up < -clamp) up = -clamp;
        }
        const uint64_t off = (uint64_t)pair * expert_mid_dim + row;
        gate_out[off] = gate;
        up_out[off] = up;
        mid_out[off] = (gate / (1.0f + expf(-gate))) * up * weights[(uint64_t)tok * n_expert + slot];
    }
}

__global__ static void moe_gate_up_mid_qwarp32_kernel(
        float *gate_out,
        float *up_out,
        float *mid_out,
        const char *gate_base,
        const char *up_base,
        const cuda_block_q8_K *xq,
        const int32_t *selected,
        const float *weights,
        uint64_t gate_expert_bytes,
        uint64_t gate_row_bytes,
        uint32_t xq_blocks,
        uint32_t expert_mid_dim,
        uint32_t n_expert,
        float clamp) {
    uint32_t lane = threadIdx.x & 7u;
    uint32_t row_lane = threadIdx.x >> 3u;
    uint32_t pair = blockIdx.y;
    uint32_t tok = pair / n_expert;
    uint32_t slot = pair - tok * n_expert;
    int32_t expert_i = selected[(uint64_t)tok * n_expert + slot];
    if (expert_i < 0) expert_i = 0;
    uint32_t expert = (uint32_t)expert_i;
    const cuda_block_q8_K *xqb = xq + (uint64_t)tok * xq_blocks;
    for (uint32_t rr = 0; rr < 4u; rr++) {
        uint32_t row = blockIdx.x * 128u + row_lane + rr * 32u;
        if (row >= expert_mid_dim) continue;
        const cuda_block_iq2_xxs *gr = (const cuda_block_iq2_xxs *)(gate_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
        const cuda_block_iq2_xxs *ur = (const cuda_block_iq2_xxs *)(up_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
        float gate = 0.0f;
        float up = 0.0f;
        for (uint32_t b = lane; b < xq_blocks; b += 8u) {
            gate += dev_dot_iq2_xxs_q8_K_block(gr + b, xqb + b);
            up += dev_dot_iq2_xxs_q8_K_block(ur + b, xqb + b);
        }
        gate = quarter_warp_sum_f32(gate, lane);
        up = quarter_warp_sum_f32(up, lane);
        if (lane == 0) {
            if (clamp > 1.0e-6f) {
                if (gate > clamp) gate = clamp;
                if (up > clamp) up = clamp;
                if (up < -clamp) up = -clamp;
            }
            const uint64_t off = (uint64_t)pair * expert_mid_dim + row;
            gate_out[off] = gate;
            up_out[off] = up;
            mid_out[off] = (gate / (1.0f + expf(-gate))) * up * weights[(uint64_t)tok * n_expert + slot];
        }
    }
}

__global__ static void moe_gate_up_mid_decode_lut_qwarp32_kernel(
        float *gate_out,
        float *up_out,
        float *mid_out,
        const char *gate_base,
        const char *up_base,
        const cuda_block_q8_K *xq,
        const int32_t *selected,
        const float *weights,
        uint64_t gate_expert_bytes,
        uint64_t gate_row_bytes,
        uint32_t xq_blocks,
        uint32_t expert_mid_dim,
        uint32_t n_expert,
        uint32_t write_aux,
        float clamp) {
    uint32_t lane = threadIdx.x & 7u;
    uint32_t row_lane = threadIdx.x >> 3u;
    uint32_t pair = blockIdx.y;
    uint32_t tok = pair / n_expert;
    uint32_t slot = pair - tok * n_expert;
    int32_t expert_i = selected[(uint64_t)tok * n_expert + slot];
    if (expert_i < 0) expert_i = 0;
    uint32_t expert = (uint32_t)expert_i;
    const cuda_block_q8_K *xqb = xq + (uint64_t)tok * xq_blocks;
    __shared__ cuda_block_q8_K sxq[16];
    __shared__ uint64_t s_iq2_grid[256];
    __shared__ uint8_t s_iq2_signs[128];
    if (xq_blocks <= 16u) {
        for (uint32_t i = threadIdx.x; i < xq_blocks; i += blockDim.x) sxq[i] = xqb[i];
        for (uint32_t i = threadIdx.x; i < 256u; i += blockDim.x) s_iq2_grid[i] = cuda_iq2xxs_grid[i];
        for (uint32_t i = threadIdx.x; i < 128u; i += blockDim.x) s_iq2_signs[i] = cuda_ksigns_iq2xs[i];
        __syncthreads();
        xqb = sxq;
    }
    for (uint32_t rr = 0; rr < 4u; rr++) {
        uint32_t row = blockIdx.x * 128u + row_lane + rr * 32u;
        if (row >= expert_mid_dim) continue;
        const cuda_block_iq2_xxs *gr = (const cuda_block_iq2_xxs *)(gate_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
        const cuda_block_iq2_xxs *ur = (const cuda_block_iq2_xxs *)(up_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
        float gate = 0.0f;
        float up = 0.0f;
        for (uint32_t b = lane; b < xq_blocks; b += 8u) {
            gate += dev_dot_iq2_xxs_q8_K_block_lut(gr + b, xqb + b, s_iq2_grid, s_iq2_signs);
            up += dev_dot_iq2_xxs_q8_K_block_lut(ur + b, xqb + b, s_iq2_grid, s_iq2_signs);
        }
        gate = quarter_warp_sum_f32(gate, lane);
        up = quarter_warp_sum_f32(up, lane);
        if (lane == 0) {
            if (clamp > 1.0e-6f) {
                if (gate > clamp) gate = clamp;
                if (up > clamp) up = clamp;
                if (up < -clamp) up = -clamp;
            }
            const uint64_t off = (uint64_t)pair * expert_mid_dim + row;
            if (write_aux) {
                gate_out[off] = gate;
                up_out[off] = up;
            }
            mid_out[off] = (gate / (1.0f + expf(-gate))) * up * weights[(uint64_t)tok * n_expert + slot];
        }
    }
}

__global__ static void moe_count_sorted_pairs_kernel(
        uint32_t *counts,
        const int32_t *selected,
        uint32_t pair_count) {
    uint32_t pair = (uint32_t)((uint64_t)blockIdx.x * blockDim.x + threadIdx.x);
    if (pair >= pair_count) return;
    int32_t expert_i = selected[pair];
    if (expert_i < 0) expert_i = 0;
    atomicAdd(counts + (uint32_t)expert_i, 1u);
}

__global__ static void moe_prefix_sorted_pairs_kernel(
        uint32_t *offsets,
        uint32_t *cursors,
        const uint32_t *counts) {
    if (threadIdx.x == 0) {
        uint32_t sum = 0;
        for (uint32_t e = 0; e < 256u; e++) {
            offsets[e] = sum;
            cursors[e] = sum;
            sum += counts[e];
        }
        offsets[256] = sum;
    }
}

__global__ static void moe_scatter_sorted_pairs_kernel(
        uint32_t *sorted_pairs,
        uint32_t *cursors,
        const int32_t *selected,
        uint32_t pair_count) {
    uint32_t pair = (uint32_t)((uint64_t)blockIdx.x * blockDim.x + threadIdx.x);
    if (pair >= pair_count) return;
    int32_t expert_i = selected[pair];
    if (expert_i < 0) expert_i = 0;
    uint32_t pos = atomicAdd(cursors + (uint32_t)expert_i, 1u);
    sorted_pairs[pos] = pair;
}

__global__ static void moe_build_expert_tile_offsets_kernel(
        uint32_t *tile_offsets,
        uint32_t *tile_total,
        const uint32_t *counts,
        uint32_t block_m) {
    if (threadIdx.x == 0) {
        uint32_t sum = 0;
        for (uint32_t e = 0; e < 256u; e++) {
            tile_offsets[e] = sum;
            sum += (counts[e] + block_m - 1u) / block_m;
        }
        tile_offsets[256] = sum;
        *tile_total = sum;
    }
}

__global__ static void moe_build_expert_tiles_kernel(
        uint32_t *tile_experts,
        uint32_t *tile_starts,
        const uint32_t *tile_offsets,
        const uint32_t *counts,
        uint32_t block_m) {
    uint32_t e = threadIdx.x;
    if (e >= 256u) return;
    uint32_t ntiles = (counts[e] + block_m - 1u) / block_m;
    uint32_t off = tile_offsets[e];
    for (uint32_t t = 0; t < ntiles; t++) {
        tile_experts[off + t] = e;
        tile_starts[off + t] = t * block_m;
    }
}

__global__ static void moe_gate_up_mid_sorted_qwarp32_kernel(
        float *gate_out,
        float *up_out,
        float *mid_out,
        const char *gate_base,
        const char *up_base,
        const cuda_block_q8_K *xq,
        const uint32_t *sorted_pairs,
        const int32_t *selected,
        const float *weights,
        uint64_t gate_expert_bytes,
        uint64_t gate_row_bytes,
        uint32_t xq_blocks,
        uint32_t expert_mid_dim,
        uint32_t n_expert,
        float clamp) {
    uint32_t lane = threadIdx.x & 7u;
    uint32_t row = blockIdx.x * 32u + (threadIdx.x >> 3u);
    uint32_t pair = sorted_pairs[blockIdx.y];
    if (row >= expert_mid_dim) return;
    uint32_t tok = pair / n_expert;
    uint32_t slot = pair - tok * n_expert;
    int32_t expert_i = selected[(uint64_t)tok * n_expert + slot];
    if (expert_i < 0) expert_i = 0;
    uint32_t expert = (uint32_t)expert_i;
    const cuda_block_iq2_xxs *gr = (const cuda_block_iq2_xxs *)(gate_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
    const cuda_block_iq2_xxs *ur = (const cuda_block_iq2_xxs *)(up_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
    const cuda_block_q8_K *xqb = xq + (uint64_t)tok * xq_blocks;
    float gate = 0.0f;
    float up = 0.0f;
    for (uint32_t b = lane; b < xq_blocks; b += 8u) {
        gate += dev_dot_iq2_xxs_q8_K_block(gr + b, xqb + b);
        up += dev_dot_iq2_xxs_q8_K_block(ur + b, xqb + b);
    }
    gate = quarter_warp_sum_f32(gate, lane);
    up = quarter_warp_sum_f32(up, lane);
    if (lane == 0) {
        if (clamp > 1.0e-6f) {
            if (gate > clamp) gate = clamp;
            if (up > clamp) up = clamp;
            if (up < -clamp) up = -clamp;
        }
        const uint64_t off = (uint64_t)pair * expert_mid_dim + row;
        gate_out[off] = gate;
        up_out[off] = up;
        mid_out[off] = (gate / (1.0f + expf(-gate))) * up * weights[(uint64_t)tok * n_expert + slot];
    }
}

__global__ static DS4_CUDA_UNUSED void moe_gate_up_mid_expert_tile8_kernel(
        float *gate_out,
        float *up_out,
        float *mid_out,
        const char *gate_base,
        const char *up_base,
        const cuda_block_q8_K *xq,
        const uint32_t *sorted_pairs,
        const uint32_t *offsets,
        const uint32_t *counts,
        const uint32_t *tile_total,
        const uint32_t *tile_experts,
        const uint32_t *tile_starts,
        const float *weights,
        uint64_t gate_expert_bytes,
        uint64_t gate_row_bytes,
        uint32_t xq_blocks,
        uint32_t expert_mid_dim,
        uint32_t n_expert,
        float clamp) {
    uint32_t tile = blockIdx.y;
    if (tile >= *tile_total) return;
    uint32_t group = threadIdx.x >> 3u;
    uint32_t lane = threadIdx.x & 7u;
    uint32_t pair_slot = group & 7u;
    uint32_t row_lane = group >> 3u;
    uint32_t expert = tile_experts[tile];
    uint32_t local_pair = tile_starts[tile] + pair_slot;
    if (local_pair >= counts[expert]) return;
    uint32_t sorted_idx = offsets[expert] + local_pair;
    uint32_t pair = sorted_pairs[sorted_idx];
    uint32_t tok = pair / n_expert;
    uint32_t slot = pair - tok * n_expert;
    const cuda_block_q8_K *xqb = xq + (uint64_t)tok * xq_blocks;

    for (uint32_t rr = 0; rr < 2u; rr++) {
        uint32_t row = blockIdx.x * 8u + row_lane + rr * 4u;
        if (row >= expert_mid_dim) continue;
        const cuda_block_iq2_xxs *gr = (const cuda_block_iq2_xxs *)(gate_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
        const cuda_block_iq2_xxs *ur = (const cuda_block_iq2_xxs *)(up_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
        float gate = 0.0f;
        float up = 0.0f;
        for (uint32_t b = lane; b < xq_blocks; b += 8u) {
            gate += dev_dot_iq2_xxs_q8_K_block(gr + b, xqb + b);
            up += dev_dot_iq2_xxs_q8_K_block(ur + b, xqb + b);
        }
        gate = quarter_warp_sum_f32(gate, lane);
        up = quarter_warp_sum_f32(up, lane);
        if (lane == 0) {
            if (clamp > 1.0e-6f) {
                if (gate > clamp) gate = clamp;
                if (up > clamp) up = clamp;
                if (up < -clamp) up = -clamp;
            }
            const uint64_t off = (uint64_t)pair * expert_mid_dim + row;
            gate_out[off] = gate;
            up_out[off] = up;
            mid_out[off] = (gate / (1.0f + expf(-gate))) * up * weights[(uint64_t)tok * n_expert + slot];
        }
    }
}

__global__ static void moe_gate_up_mid_expert_tile4_row32_kernel(
        float *gate_out,
        float *up_out,
        float *mid_out,
        const char *gate_base,
        const char *up_base,
        const cuda_block_q8_K *xq,
        const uint32_t *sorted_pairs,
        const uint32_t *offsets,
        const uint32_t *counts,
        const uint32_t *tile_total,
        const uint32_t *tile_experts,
        const uint32_t *tile_starts,
        const float *weights,
        uint64_t gate_expert_bytes,
        uint64_t gate_row_bytes,
        uint32_t xq_blocks,
        uint32_t expert_mid_dim,
        uint32_t n_expert,
        uint32_t write_aux,
        float clamp) {
    uint32_t tile = blockIdx.y;
    if (tile >= *tile_total) return;
    uint32_t lane = threadIdx.x & 7u;
    uint32_t row = blockIdx.x * 32u + (threadIdx.x >> 3u);
    uint32_t expert = tile_experts[tile];
    uint32_t local_start = tile_starts[tile];
    __shared__ cuda_block_q8_K sxq[4][16];
    uint32_t pair[4] = {0, 0, 0, 0};
    uint32_t tok[4] = {0, 0, 0, 0};
    uint32_t slot[4] = {0, 0, 0, 0};
    const cuda_block_q8_K *xqb[4] = {NULL, NULL, NULL, NULL};
    uint32_t np = 0;
    for (; np < 4u; np++) {
        uint32_t local_pair = local_start + np;
        if (local_pair >= counts[expert]) break;
        pair[np] = sorted_pairs[offsets[expert] + local_pair];
        tok[np] = pair[np] / n_expert;
        slot[np] = pair[np] - tok[np] * n_expert;
        xqb[np] = xq + (uint64_t)tok[np] * xq_blocks;
    }
    if (xq_blocks <= 16u) {
        for (uint32_t i = threadIdx.x; i < np * xq_blocks; i += blockDim.x) {
            uint32_t p = i / xq_blocks;
            uint32_t b = i - p * xq_blocks;
            sxq[p][b] = xqb[p][b];
        }
        __syncthreads();
        for (uint32_t p = 0; p < np; p++) xqb[p] = sxq[p];
    }
    if (row >= expert_mid_dim) return;
    const cuda_block_iq2_xxs *gr = (const cuda_block_iq2_xxs *)(gate_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
    const cuda_block_iq2_xxs *ur = (const cuda_block_iq2_xxs *)(up_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
    float gate[4] = {0.0f, 0.0f, 0.0f, 0.0f};
    float up[4] = {0.0f, 0.0f, 0.0f, 0.0f};
    for (uint32_t b = lane; b < xq_blocks; b += 8u) {
        dev_dot_iq2_xxs_q8_K_block4(gr + b, xqb[0] ? xqb[0] + b : NULL, xqb[1] ? xqb[1] + b : NULL,
                                    xqb[2] ? xqb[2] + b : NULL, xqb[3] ? xqb[3] + b : NULL, np, gate);
        dev_dot_iq2_xxs_q8_K_block4(ur + b, xqb[0] ? xqb[0] + b : NULL, xqb[1] ? xqb[1] + b : NULL,
                                    xqb[2] ? xqb[2] + b : NULL, xqb[3] ? xqb[3] + b : NULL, np, up);
    }
    for (uint32_t p = 0; p < np; p++) {
        gate[p] = quarter_warp_sum_f32(gate[p], lane);
        up[p] = quarter_warp_sum_f32(up[p], lane);
        if (lane == 0) {
            if (clamp > 1.0e-6f) {
                if (gate[p] > clamp) gate[p] = clamp;
                if (up[p] > clamp) up[p] = clamp;
                if (up[p] < -clamp) up[p] = -clamp;
            }
            const uint64_t off = (uint64_t)pair[p] * expert_mid_dim + row;
            if (write_aux) {
                gate_out[off] = gate[p];
                up_out[off] = up[p];
            }
            mid_out[off] = (gate[p] / (1.0f + expf(-gate[p]))) * up[p] * weights[(uint64_t)tok[p] * n_expert + slot[p]];
        }
    }
}

__global__ static void moe_gate_up_mid_expert_tile8_row32_kernel(
        float *gate_out,
        float *up_out,
        float *mid_out,
        const char *gate_base,
        const char *up_base,
        const cuda_block_q8_K *xq,
        const uint32_t *sorted_pairs,
        const uint32_t *offsets,
        const uint32_t *counts,
        const uint32_t *tile_total,
        const uint32_t *tile_experts,
        const uint32_t *tile_starts,
        const float *weights,
        uint64_t gate_expert_bytes,
        uint64_t gate_row_bytes,
        uint32_t xq_blocks,
        uint32_t expert_mid_dim,
        uint32_t n_expert,
        uint32_t write_aux,
        float clamp) {
    uint32_t tile = blockIdx.y;
    if (tile >= *tile_total) return;
    uint32_t lane = threadIdx.x & 7u;
    uint32_t row = blockIdx.x * 32u + (threadIdx.x >> 3u);
    uint32_t expert = tile_experts[tile];
    uint32_t local_start = tile_starts[tile];
    __shared__ cuda_block_q8_K sxq[8][16];
    __shared__ uint64_t s_iq2_grid[256];
    __shared__ uint8_t s_iq2_signs[128];
    uint32_t pair[8] = {0, 0, 0, 0, 0, 0, 0, 0};
    uint32_t tok[8] = {0, 0, 0, 0, 0, 0, 0, 0};
    uint32_t slot[8] = {0, 0, 0, 0, 0, 0, 0, 0};
    const cuda_block_q8_K *xqb[8] = {NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL};
    uint32_t np = 0;
    for (; np < 8u; np++) {
        uint32_t local_pair = local_start + np;
        if (local_pair >= counts[expert]) break;
        pair[np] = sorted_pairs[offsets[expert] + local_pair];
        tok[np] = pair[np] / n_expert;
        slot[np] = pair[np] - tok[np] * n_expert;
        xqb[np] = xq + (uint64_t)tok[np] * xq_blocks;
    }
    if (xq_blocks <= 16u) {
        for (uint32_t i = threadIdx.x; i < np * xq_blocks; i += blockDim.x) {
            uint32_t p = i / xq_blocks;
            uint32_t b = i - p * xq_blocks;
            sxq[p][b] = xqb[p][b];
        }
        for (uint32_t i = threadIdx.x; i < 256u; i += blockDim.x) s_iq2_grid[i] = cuda_iq2xxs_grid[i];
        for (uint32_t i = threadIdx.x; i < 128u; i += blockDim.x) s_iq2_signs[i] = cuda_ksigns_iq2xs[i];
        __syncthreads();
        for (uint32_t p = 0; p < np; p++) xqb[p] = sxq[p];
    }
    if (row >= expert_mid_dim) return;
    const cuda_block_iq2_xxs *gr = (const cuda_block_iq2_xxs *)(gate_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
    const cuda_block_iq2_xxs *ur = (const cuda_block_iq2_xxs *)(up_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
    float gate[8] = {0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f};
    float up[8] = {0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f};
    for (uint32_t b = lane; b < xq_blocks; b += 8u) {
        dev_dot_iq2_xxs_q8_K_block8_deq_lut(gr + b, xqb[0] ? xqb[0] + b : NULL, xqb[1] ? xqb[1] + b : NULL,
                                            xqb[2] ? xqb[2] + b : NULL, xqb[3] ? xqb[3] + b : NULL,
                                            xqb[4] ? xqb[4] + b : NULL, xqb[5] ? xqb[5] + b : NULL,
                                            xqb[6] ? xqb[6] + b : NULL, xqb[7] ? xqb[7] + b : NULL, np, gate,
                                            s_iq2_grid, s_iq2_signs);
        dev_dot_iq2_xxs_q8_K_block8_deq_lut(ur + b, xqb[0] ? xqb[0] + b : NULL, xqb[1] ? xqb[1] + b : NULL,
                                            xqb[2] ? xqb[2] + b : NULL, xqb[3] ? xqb[3] + b : NULL,
                                            xqb[4] ? xqb[4] + b : NULL, xqb[5] ? xqb[5] + b : NULL,
                                            xqb[6] ? xqb[6] + b : NULL, xqb[7] ? xqb[7] + b : NULL, np, up,
                                            s_iq2_grid, s_iq2_signs);
    }
    for (uint32_t p = 0; p < np; p++) {
        gate[p] = quarter_warp_sum_f32(gate[p], lane);
        up[p] = quarter_warp_sum_f32(up[p], lane);
        if (lane == 0) {
            if (clamp > 1.0e-6f) {
                if (gate[p] > clamp) gate[p] = clamp;
                if (up[p] > clamp) up[p] = clamp;
                if (up[p] < -clamp) up[p] = -clamp;
            }
            const uint64_t off = (uint64_t)pair[p] * expert_mid_dim + row;
            if (write_aux) {
                gate_out[off] = gate[p];
                up_out[off] = up[p];
            }
            mid_out[off] = (gate[p] / (1.0f + expf(-gate[p]))) * up[p] * weights[(uint64_t)tok[p] * n_expert + slot[p]];
        }
    }
}

__global__ static void moe_gate_up_mid_expert_tile8_row2048_kernel(
        float *gate_out,
        float *up_out,
        float *mid_out,
        const char *gate_base,
        const char *up_base,
        const cuda_block_q8_K *xq,
        const uint32_t *sorted_pairs,
        const uint32_t *offsets,
        const uint32_t *counts,
        const uint32_t *tile_total,
        const uint32_t *tile_experts,
        const uint32_t *tile_starts,
        const float *weights,
        uint64_t gate_expert_bytes,
        uint64_t gate_row_bytes,
        uint32_t xq_blocks,
        uint32_t expert_mid_dim,
        uint32_t n_expert,
        uint32_t write_aux,
        float clamp) {
    uint32_t tile = blockIdx.y;
    if (tile >= *tile_total) return;
    uint32_t lane = threadIdx.x & 7u;
    uint32_t row_lane = threadIdx.x >> 3u;
    uint32_t expert = tile_experts[tile];
    uint32_t local_start = tile_starts[tile];
    __shared__ cuda_block_q8_K sxq[8][16];
    __shared__ uint64_t s_iq2_grid[256];
    __shared__ uint8_t s_iq2_signs[128];
    uint32_t pair[8] = {0, 0, 0, 0, 0, 0, 0, 0};
    uint32_t tok[8] = {0, 0, 0, 0, 0, 0, 0, 0};
    uint32_t slot[8] = {0, 0, 0, 0, 0, 0, 0, 0};
    const cuda_block_q8_K *xqb[8] = {NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL};
    uint32_t np = 0;
    for (; np < 8u; np++) {
        uint32_t local_pair = local_start + np;
        if (local_pair >= counts[expert]) break;
        pair[np] = sorted_pairs[offsets[expert] + local_pair];
        tok[np] = pair[np] / n_expert;
        slot[np] = pair[np] - tok[np] * n_expert;
        xqb[np] = xq + (uint64_t)tok[np] * xq_blocks;
    }
    if (xq_blocks <= 16u) {
        for (uint32_t i = threadIdx.x; i < np * xq_blocks; i += blockDim.x) {
            uint32_t p = i / xq_blocks;
            uint32_t b = i - p * xq_blocks;
            sxq[p][b] = xqb[p][b];
        }
        for (uint32_t i = threadIdx.x; i < 256u; i += blockDim.x) s_iq2_grid[i] = cuda_iq2xxs_grid[i];
        for (uint32_t i = threadIdx.x; i < 128u; i += blockDim.x) s_iq2_signs[i] = cuda_ksigns_iq2xs[i];
        __syncthreads();
        for (uint32_t p = 0; p < np; p++) xqb[p] = sxq[p];
    }
    for (uint32_t rr = 0; rr < 64u; rr++) {
        uint32_t row = blockIdx.x * 2048u + row_lane + rr * 32u;
        if (row >= expert_mid_dim) continue;
        const cuda_block_iq2_xxs *gr = (const cuda_block_iq2_xxs *)(gate_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
        const cuda_block_iq2_xxs *ur = (const cuda_block_iq2_xxs *)(up_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
        float gate[8] = {0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f};
        float up[8] = {0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f};
        for (uint32_t b = lane; b < xq_blocks; b += 8u) {
            dev_dot_iq2_xxs_q8_K_block8_deq_lut(gr + b, xqb[0] ? xqb[0] + b : NULL, xqb[1] ? xqb[1] + b : NULL,
                                                xqb[2] ? xqb[2] + b : NULL, xqb[3] ? xqb[3] + b : NULL,
                                                xqb[4] ? xqb[4] + b : NULL, xqb[5] ? xqb[5] + b : NULL,
                                                xqb[6] ? xqb[6] + b : NULL, xqb[7] ? xqb[7] + b : NULL, np, gate,
                                                s_iq2_grid, s_iq2_signs);
            dev_dot_iq2_xxs_q8_K_block8_deq_lut(ur + b, xqb[0] ? xqb[0] + b : NULL, xqb[1] ? xqb[1] + b : NULL,
                                                xqb[2] ? xqb[2] + b : NULL, xqb[3] ? xqb[3] + b : NULL,
                                                xqb[4] ? xqb[4] + b : NULL, xqb[5] ? xqb[5] + b : NULL,
                                                xqb[6] ? xqb[6] + b : NULL, xqb[7] ? xqb[7] + b : NULL, np, up,
                                                s_iq2_grid, s_iq2_signs);
        }
        for (uint32_t p = 0; p < np; p++) {
            gate[p] = quarter_warp_sum_f32(gate[p], lane);
            up[p] = quarter_warp_sum_f32(up[p], lane);
            if (lane == 0) {
                if (clamp > 1.0e-6f) {
                    if (gate[p] > clamp) gate[p] = clamp;
                    if (up[p] > clamp) up[p] = clamp;
                    if (up[p] < -clamp) up[p] = -clamp;
                }
                const uint64_t off = (uint64_t)pair[p] * expert_mid_dim + row;
                if (write_aux) {
                    gate_out[off] = gate[p];
                    up_out[off] = up[p];
                }
                mid_out[off] = (gate[p] / (1.0f + expf(-gate[p]))) * up[p] * weights[(uint64_t)tok[p] * n_expert + slot[p]];
            }
        }
    }
}

template <uint32_t ROW_SPAN>
__global__ static void moe_gate_up_mid_expert_tile8_rowspan_kernel(
        float *gate_out,
        float *up_out,
        float *mid_out,
        const char *gate_base,
        const char *up_base,
        const cuda_block_q8_K *xq,
        const uint32_t *sorted_pairs,
        const uint32_t *offsets,
        const uint32_t *counts,
        const uint32_t *tile_total,
        const uint32_t *tile_experts,
        const uint32_t *tile_starts,
        const float *weights,
        uint64_t gate_expert_bytes,
        uint64_t gate_row_bytes,
        uint32_t xq_blocks,
        uint32_t expert_mid_dim,
        uint32_t n_expert,
        uint32_t write_aux,
        float clamp) {
    uint32_t tile = blockIdx.y;
    if (tile >= *tile_total) return;
    uint32_t lane = threadIdx.x & 7u;
    uint32_t row_lane = threadIdx.x >> 3u;
    uint32_t expert = tile_experts[tile];
    uint32_t local_start = tile_starts[tile];
    __shared__ cuda_block_q8_K sxq[8][16];
    __shared__ uint64_t s_iq2_grid[256];
    __shared__ uint8_t s_iq2_signs[128];
    uint32_t pair[8] = {0, 0, 0, 0, 0, 0, 0, 0};
    uint32_t tok[8] = {0, 0, 0, 0, 0, 0, 0, 0};
    uint32_t slot[8] = {0, 0, 0, 0, 0, 0, 0, 0};
    const cuda_block_q8_K *xqb[8] = {NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL};
    uint32_t np = 0;
    for (; np < 8u; np++) {
        uint32_t local_pair = local_start + np;
        if (local_pair >= counts[expert]) break;
        pair[np] = sorted_pairs[offsets[expert] + local_pair];
        tok[np] = pair[np] / n_expert;
        slot[np] = pair[np] - tok[np] * n_expert;
        xqb[np] = xq + (uint64_t)tok[np] * xq_blocks;
    }
    if (xq_blocks <= 16u) {
        for (uint32_t i = threadIdx.x; i < np * xq_blocks; i += blockDim.x) {
            uint32_t p = i / xq_blocks;
            uint32_t b = i - p * xq_blocks;
            sxq[p][b] = xqb[p][b];
        }
        for (uint32_t i = threadIdx.x; i < 256u; i += blockDim.x) s_iq2_grid[i] = cuda_iq2xxs_grid[i];
        for (uint32_t i = threadIdx.x; i < 128u; i += blockDim.x) s_iq2_signs[i] = cuda_ksigns_iq2xs[i];
        __syncthreads();
        for (uint32_t p = 0; p < np; p++) xqb[p] = sxq[p];
    }
    for (uint32_t rr = 0; rr < ROW_SPAN / 32u; rr++) {
        uint32_t row = blockIdx.x * ROW_SPAN + row_lane + rr * 32u;
        if (row >= expert_mid_dim) continue;
        const cuda_block_iq2_xxs *gr = (const cuda_block_iq2_xxs *)(gate_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
        const cuda_block_iq2_xxs *ur = (const cuda_block_iq2_xxs *)(up_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
        float gate[8] = {0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f};
        float up[8] = {0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f};
        for (uint32_t b = lane; b < xq_blocks; b += 8u) {
            dev_dot_iq2_xxs_q8_K_block8_deq_lut(gr + b, xqb[0] ? xqb[0] + b : NULL, xqb[1] ? xqb[1] + b : NULL,
                                                xqb[2] ? xqb[2] + b : NULL, xqb[3] ? xqb[3] + b : NULL,
                                                xqb[4] ? xqb[4] + b : NULL, xqb[5] ? xqb[5] + b : NULL,
                                                xqb[6] ? xqb[6] + b : NULL, xqb[7] ? xqb[7] + b : NULL, np, gate,
                                                s_iq2_grid, s_iq2_signs);
            dev_dot_iq2_xxs_q8_K_block8_deq_lut(ur + b, xqb[0] ? xqb[0] + b : NULL, xqb[1] ? xqb[1] + b : NULL,
                                                xqb[2] ? xqb[2] + b : NULL, xqb[3] ? xqb[3] + b : NULL,
                                                xqb[4] ? xqb[4] + b : NULL, xqb[5] ? xqb[5] + b : NULL,
                                                xqb[6] ? xqb[6] + b : NULL, xqb[7] ? xqb[7] + b : NULL, np, up,
                                                s_iq2_grid, s_iq2_signs);
        }
        for (uint32_t p = 0; p < np; p++) {
            gate[p] = quarter_warp_sum_f32(gate[p], lane);
            up[p] = quarter_warp_sum_f32(up[p], lane);
            if (lane == 0) {
                if (clamp > 1.0e-6f) {
                    if (gate[p] > clamp) gate[p] = clamp;
                    if (up[p] > clamp) up[p] = clamp;
                    if (up[p] < -clamp) up[p] = -clamp;
                }
                const uint64_t off = (uint64_t)pair[p] * expert_mid_dim + row;
                if (write_aux) {
                    gate_out[off] = gate[p];
                    up_out[off] = up[p];
                }
                mid_out[off] = (gate[p] / (1.0f + expf(-gate[p]))) * up[p] * weights[(uint64_t)tok[p] * n_expert + slot[p]];
            }
        }
    }
}

__global__ static void moe_gate_up_mid_sorted_p2_qwarp32_kernel(
        float *gate_out,
        float *up_out,
        float *mid_out,
        const char *gate_base,
        const char *up_base,
        const cuda_block_q8_K *xq,
        const uint32_t *sorted_pairs,
        const int32_t *selected,
        const float *weights,
        uint64_t gate_expert_bytes,
        uint64_t gate_row_bytes,
        uint32_t xq_blocks,
        uint32_t expert_mid_dim,
        uint32_t n_expert,
        uint32_t pair_count,
        float clamp) {
    uint32_t lane = threadIdx.x & 7u;
    uint32_t pair_lane = (threadIdx.x >> 3u) & 1u;
    uint32_t row = blockIdx.x * 16u + (threadIdx.x >> 4u);
    uint32_t sorted_idx = blockIdx.y * 2u + pair_lane;
    if (row >= expert_mid_dim || sorted_idx >= pair_count) return;
    uint32_t pair = sorted_pairs[sorted_idx];
    uint32_t tok = pair / n_expert;
    uint32_t slot = pair - tok * n_expert;
    int32_t expert_i = selected[(uint64_t)tok * n_expert + slot];
    if (expert_i < 0) expert_i = 0;
    uint32_t expert = (uint32_t)expert_i;
    const cuda_block_iq2_xxs *gr = (const cuda_block_iq2_xxs *)(gate_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
    const cuda_block_iq2_xxs *ur = (const cuda_block_iq2_xxs *)(up_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
    const cuda_block_q8_K *xqb = xq + (uint64_t)tok * xq_blocks;
    float gate = 0.0f;
    float up = 0.0f;
    for (uint32_t b = lane; b < xq_blocks; b += 8u) {
        gate += dev_dot_iq2_xxs_q8_K_block(gr + b, xqb + b);
        up += dev_dot_iq2_xxs_q8_K_block(ur + b, xqb + b);
    }
    gate = quarter_warp_sum_f32(gate, lane);
    up = quarter_warp_sum_f32(up, lane);
    if (lane == 0) {
        if (clamp > 1.0e-6f) {
            if (gate > clamp) gate = clamp;
            if (up > clamp) up = clamp;
            if (up < -clamp) up = -clamp;
        }
        const uint64_t off = (uint64_t)pair * expert_mid_dim + row;
        gate_out[off] = gate;
        up_out[off] = up;
        mid_out[off] = (gate / (1.0f + expf(-gate))) * up * weights[(uint64_t)tok * n_expert + slot];
    }
}

__global__ static DS4_CUDA_UNUSED void moe_down_kernel(
        float *down_out,
        const char *down_base,
        const cuda_block_q8_K *midq,
        const int32_t *selected,
        uint64_t down_expert_bytes,
        uint64_t down_row_bytes,
        uint32_t midq_blocks,
        uint32_t out_dim,
        uint32_t n_expert) {
    uint32_t row = blockIdx.x;
    uint32_t pair = blockIdx.y;
    if (row >= out_dim) return;
    uint32_t tok = pair / n_expert;
    uint32_t slot = pair - tok * n_expert;
    int32_t expert_i = selected[(uint64_t)tok * n_expert + slot];
    if (expert_i < 0) expert_i = 0;
    const cuda_block_q2_K *wr = (const cuda_block_q2_K *)(down_base + (uint64_t)(uint32_t)expert_i * down_expert_bytes + (uint64_t)row * down_row_bytes);
    const cuda_block_q8_K *xq = midq + (uint64_t)pair * midq_blocks;
    float acc = 0.0f;
    for (uint32_t b = threadIdx.x; b < midq_blocks; b += blockDim.x) acc += dev_dot_q2_K_q8_K_block(wr + b, xq + b);
    __shared__ float partial[256];
    partial[threadIdx.x] = acc;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] += partial[threadIdx.x + stride];
        __syncthreads();
    }
    if (threadIdx.x == 0) down_out[(uint64_t)pair * out_dim + row] = partial[0];
}

__global__ static DS4_CUDA_UNUSED void moe_down_warp8_kernel(
        float *down_out,
        const char *down_base,
        const cuda_block_q8_K *midq,
        const int32_t *selected,
        uint64_t down_expert_bytes,
        uint64_t down_row_bytes,
        uint32_t midq_blocks,
        uint32_t out_dim,
        uint32_t n_expert) {
    uint32_t lane = threadIdx.x & 31u;
    uint32_t warp = threadIdx.x >> 5u;
    uint32_t row = blockIdx.x * 8u + warp;
    uint32_t pair = blockIdx.y;
    if (row >= out_dim) return;
    uint32_t tok = pair / n_expert;
    uint32_t slot = pair - tok * n_expert;
    int32_t expert_i = selected[(uint64_t)tok * n_expert + slot];
    if (expert_i < 0) expert_i = 0;
    const cuda_block_q2_K *wr = (const cuda_block_q2_K *)(down_base + (uint64_t)(uint32_t)expert_i * down_expert_bytes + (uint64_t)row * down_row_bytes);
    const cuda_block_q8_K *xq = midq + (uint64_t)pair * midq_blocks;
    float acc = 0.0f;
    for (uint32_t b = lane; b < midq_blocks; b += 32u) acc += dev_dot_q2_K_q8_K_block(wr + b, xq + b);
    acc = warp_sum_f32(acc);
    if (lane == 0) down_out[(uint64_t)pair * out_dim + row] = acc;
}

__global__ static DS4_CUDA_UNUSED void moe_down_hwarp16_kernel(
        float *down_out,
        const char *down_base,
        const cuda_block_q8_K *midq,
        const int32_t *selected,
        uint64_t down_expert_bytes,
        uint64_t down_row_bytes,
        uint32_t midq_blocks,
        uint32_t out_dim,
        uint32_t n_expert) {
    uint32_t lane = threadIdx.x & 15u;
    uint32_t row = blockIdx.x * 16u + (threadIdx.x >> 4u);
    uint32_t pair = blockIdx.y;
    if (row >= out_dim) return;
    uint32_t tok = pair / n_expert;
    uint32_t slot = pair - tok * n_expert;
    int32_t expert_i = selected[(uint64_t)tok * n_expert + slot];
    if (expert_i < 0) expert_i = 0;
    const cuda_block_q2_K *wr = (const cuda_block_q2_K *)(down_base + (uint64_t)(uint32_t)expert_i * down_expert_bytes + (uint64_t)row * down_row_bytes);
    const cuda_block_q8_K *xq = midq + (uint64_t)pair * midq_blocks;
    float acc = 0.0f;
    for (uint32_t b = lane; b < midq_blocks; b += 16u) acc += dev_dot_q2_K_q8_K_block(wr + b, xq + b);
    acc = half_warp_sum_f32(acc, lane);
    if (lane == 0) down_out[(uint64_t)pair * out_dim + row] = acc;
}

__global__ static void moe_down_qwarp32_kernel(
        float *down_out,
        const char *down_base,
        const cuda_block_q8_K *midq,
        const int32_t *selected,
        uint64_t down_expert_bytes,
        uint64_t down_row_bytes,
        uint32_t midq_blocks,
        uint32_t out_dim,
        uint32_t n_expert) {
    uint32_t lane = threadIdx.x & 7u;
    uint32_t row = blockIdx.x * 32u + (threadIdx.x >> 3u);
    uint32_t pair = blockIdx.y;
    if (row >= out_dim) return;
    uint32_t tok = pair / n_expert;
    uint32_t slot = pair - tok * n_expert;
    int32_t expert_i = selected[(uint64_t)tok * n_expert + slot];
    if (expert_i < 0) expert_i = 0;
    const cuda_block_q2_K *wr = (const cuda_block_q2_K *)(down_base + (uint64_t)(uint32_t)expert_i * down_expert_bytes + (uint64_t)row * down_row_bytes);
    const cuda_block_q8_K *xq = midq + (uint64_t)pair * midq_blocks;
    float acc = 0.0f;
    for (uint32_t b = lane; b < midq_blocks; b += 8u) acc += dev_dot_q2_K_q8_K_block(wr + b, xq + b);
    acc = quarter_warp_sum_f32(acc, lane);
    if (lane == 0) down_out[(uint64_t)pair * out_dim + row] = acc;
}

__global__ static void moe_gate_up_mid_decode_q4K_qwarp32_kernel(
        float *gate_out,
        float *up_out,
        float *mid_out,
        const char *gate_base,
        const char *up_base,
        const cuda_block_q8_K *xq,
        const int32_t *selected,
        const float *weights,
        uint64_t gate_expert_bytes,
        uint64_t gate_row_bytes,
        uint32_t xq_blocks,
        uint32_t expert_mid_dim,
        uint32_t n_expert,
        uint32_t write_aux,
        float clamp) {
    uint32_t lane = threadIdx.x & 7u;
    uint32_t row_lane = threadIdx.x >> 3u;
    uint32_t pair = blockIdx.y;
    uint32_t tok = pair / n_expert;
    uint32_t slot = pair - tok * n_expert;
    int32_t expert_i = selected[(uint64_t)tok * n_expert + slot];
    if (expert_i < 0) expert_i = 0;
    uint32_t expert = (uint32_t)expert_i;
    const cuda_block_q8_K *xqb = xq + (uint64_t)tok * xq_blocks;
    for (uint32_t rr = 0; rr < 4u; rr++) {
        uint32_t row = blockIdx.x * 128u + row_lane + rr * 32u;
        if (row >= expert_mid_dim) continue;
        const cuda_block_q4_K *gr = (const cuda_block_q4_K *)(gate_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
        const cuda_block_q4_K *ur = (const cuda_block_q4_K *)(up_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
        float gate = 0.0f;
        float up = 0.0f;
        for (uint32_t b = lane; b < xq_blocks; b += 8u) {
            gate += dev_dot_q4_K_q8_K_block(gr + b, xqb + b);
            up += dev_dot_q4_K_q8_K_block(ur + b, xqb + b);
        }
        gate = quarter_warp_sum_f32(gate, lane);
        up = quarter_warp_sum_f32(up, lane);
        if (lane == 0) {
            if (clamp > 1.0e-6f) {
                if (gate > clamp) gate = clamp;
                if (up > clamp) up = clamp;
                if (up < -clamp) up = -clamp;
            }
            const uint64_t off = (uint64_t)pair * expert_mid_dim + row;
            if (write_aux) {
                gate_out[off] = gate;
                up_out[off] = up;
            }
            mid_out[off] = (gate / (1.0f + expf(-gate))) * up * weights[(uint64_t)tok * n_expert + slot];
        }
    }
}

__global__ static void moe_down_sum6_qwarp32_kernel(
        float *out,
        const char *down_base,
        const cuda_block_q8_K *midq,
        const int32_t *selected,
        uint64_t down_expert_bytes,
        uint64_t down_row_bytes,
        uint32_t midq_blocks,
        uint32_t out_dim) {
    uint32_t lane = threadIdx.x & 7u;
    uint32_t row = blockIdx.x * 32u + (threadIdx.x >> 3u);
    if (row >= out_dim) return;
    float total = 0.0f;
    #pragma unroll
    for (uint32_t slot = 0; slot < 6u; slot++) {
        int32_t expert_i = selected[slot];
        if (expert_i < 0) expert_i = 0;
        const cuda_block_q2_K *wr = (const cuda_block_q2_K *)(down_base + (uint64_t)(uint32_t)expert_i * down_expert_bytes + (uint64_t)row * down_row_bytes);
        const cuda_block_q8_K *xq = midq + (uint64_t)slot * midq_blocks;
        float acc = 0.0f;
        for (uint32_t b = lane; b < midq_blocks; b += 8u) acc += dev_dot_q2_K_q8_K_block(wr + b, xq + b);
        acc = quarter_warp_sum_f32(acc, lane);
        if (lane == 0) total += acc;
    }
    if (lane == 0) out[row] = total;
}

__global__ static void moe_down_q4K_sum6_qwarp32_kernel(
        float *out,
        const char *down_base,
        const cuda_block_q8_K *midq,
        const int32_t *selected,
        uint64_t down_expert_bytes,
        uint64_t down_row_bytes,
        uint32_t midq_blocks,
        uint32_t out_dim) {
    uint32_t lane = threadIdx.x & 7u;
    uint32_t row = blockIdx.x * 32u + (threadIdx.x >> 3u);
    if (row >= out_dim) return;
    float total = 0.0f;
    #pragma unroll
    for (uint32_t slot = 0; slot < 6u; slot++) {
        int32_t expert_i = selected[slot];
        if (expert_i < 0) expert_i = 0;
        const cuda_block_q4_K *wr = (const cuda_block_q4_K *)(down_base + (uint64_t)(uint32_t)expert_i * down_expert_bytes + (uint64_t)row * down_row_bytes);
        const cuda_block_q8_K *xq = midq + (uint64_t)slot * midq_blocks;
        float acc = 0.0f;
        for (uint32_t b = lane; b < midq_blocks; b += 8u) acc += dev_dot_q4_K_q8_K_block(wr + b, xq + b);
        acc = quarter_warp_sum_f32(acc, lane);
        if (lane == 0) total += acc;
    }
    if (lane == 0) out[row] = total;
}

__global__ static void moe_down_sorted_qwarp32_kernel(
        float *down_out,
        const char *down_base,
        const cuda_block_q8_K *midq,
        const uint32_t *sorted_pairs,
        const int32_t *selected,
        uint64_t down_expert_bytes,
        uint64_t down_row_bytes,
        uint32_t midq_blocks,
        uint32_t out_dim,
        uint32_t n_expert) {
    uint32_t lane = threadIdx.x & 7u;
    uint32_t row = blockIdx.x * 32u + (threadIdx.x >> 3u);
    uint32_t pair = sorted_pairs[blockIdx.y];
    if (row >= out_dim) return;
    uint32_t tok = pair / n_expert;
    uint32_t slot = pair - tok * n_expert;
    int32_t expert_i = selected[(uint64_t)tok * n_expert + slot];
    if (expert_i < 0) expert_i = 0;
    const cuda_block_q2_K *wr = (const cuda_block_q2_K *)(down_base + (uint64_t)(uint32_t)expert_i * down_expert_bytes + (uint64_t)row * down_row_bytes);
    const cuda_block_q8_K *xq = midq + (uint64_t)pair * midq_blocks;
    float acc = 0.0f;
    for (uint32_t b = lane; b < midq_blocks; b += 8u) acc += dev_dot_q2_K_q8_K_block(wr + b, xq + b);
    acc = quarter_warp_sum_f32(acc, lane);
    if (lane == 0) down_out[(uint64_t)pair * out_dim + row] = acc;
}

__global__ static DS4_CUDA_UNUSED void moe_down_expert_tile8_kernel(
        float *down_out,
        const char *down_base,
        const cuda_block_q8_K *midq,
        const uint32_t *sorted_pairs,
        const uint32_t *offsets,
        const uint32_t *counts,
        const uint32_t *tile_total,
        const uint32_t *tile_experts,
        const uint32_t *tile_starts,
        uint64_t down_expert_bytes,
        uint64_t down_row_bytes,
        uint32_t midq_blocks,
        uint32_t out_dim,
        uint32_t n_expert) {
    uint32_t tile = blockIdx.y;
    if (tile >= *tile_total) return;
    uint32_t group = threadIdx.x >> 3u;
    uint32_t lane = threadIdx.x & 7u;
    uint32_t pair_slot = group & 7u;
    uint32_t row_lane = group >> 3u;
    uint32_t expert = tile_experts[tile];
    uint32_t local_pair = tile_starts[tile] + pair_slot;
    if (local_pair >= counts[expert]) return;
    uint32_t sorted_idx = offsets[expert] + local_pair;
    uint32_t pair = sorted_pairs[sorted_idx];
    const cuda_block_q8_K *xq = midq + (uint64_t)pair * midq_blocks;

    for (uint32_t rr = 0; rr < 2u; rr++) {
        uint32_t row = blockIdx.x * 8u + row_lane + rr * 4u;
        if (row >= out_dim) continue;
        const cuda_block_q2_K *wr = (const cuda_block_q2_K *)(down_base + (uint64_t)expert * down_expert_bytes + (uint64_t)row * down_row_bytes);
        float acc = 0.0f;
        for (uint32_t b = lane; b < midq_blocks; b += 8u) acc += dev_dot_q2_K_q8_K_block(wr + b, xq + b);
        acc = quarter_warp_sum_f32(acc, lane);
        if (lane == 0) down_out[(uint64_t)pair * out_dim + row] = acc;
    }
}

__global__ static void moe_down_expert_tile4_row32_kernel(
        float *down_out,
        const char *down_base,
        const cuda_block_q8_K *midq,
        const uint32_t *sorted_pairs,
        const uint32_t *offsets,
        const uint32_t *counts,
        const uint32_t *tile_total,
        const uint32_t *tile_experts,
        const uint32_t *tile_starts,
        uint64_t down_expert_bytes,
        uint64_t down_row_bytes,
        uint32_t midq_blocks,
        uint32_t out_dim,
        uint32_t n_expert,
        uint32_t atomic_out) {
    uint32_t tile = blockIdx.y;
    if (tile >= *tile_total) return;
    uint32_t lane = threadIdx.x & 7u;
    uint32_t row = blockIdx.x * 32u + (threadIdx.x >> 3u);
    uint32_t expert = tile_experts[tile];
    uint32_t local_start = tile_starts[tile];
    __shared__ cuda_block_q8_K sxq[4][8];
    uint32_t pair[4] = {0, 0, 0, 0};
    const cuda_block_q8_K *xqb[4] = {NULL, NULL, NULL, NULL};
    uint32_t np = 0;
    for (; np < 4u; np++) {
        uint32_t local_pair = local_start + np;
        if (local_pair >= counts[expert]) break;
        pair[np] = sorted_pairs[offsets[expert] + local_pair];
        xqb[np] = midq + (uint64_t)pair[np] * midq_blocks;
    }
    if (midq_blocks <= 8u) {
        for (uint32_t i = threadIdx.x; i < np * midq_blocks; i += blockDim.x) {
            uint32_t p = i / midq_blocks;
            uint32_t b = i - p * midq_blocks;
            sxq[p][b] = xqb[p][b];
        }
        __syncthreads();
        for (uint32_t p = 0; p < np; p++) xqb[p] = sxq[p];
    }
    if (row >= out_dim) return;
    const cuda_block_q2_K *wr = (const cuda_block_q2_K *)(down_base + (uint64_t)expert * down_expert_bytes + (uint64_t)row * down_row_bytes);
    float acc[4] = {0.0f, 0.0f, 0.0f, 0.0f};
    for (uint32_t b = lane; b < midq_blocks; b += 8u) {
        dev_dot_q2_K_q8_K_block4(wr + b, xqb[0] ? xqb[0] + b : NULL, xqb[1] ? xqb[1] + b : NULL,
                                 xqb[2] ? xqb[2] + b : NULL, xqb[3] ? xqb[3] + b : NULL, np, acc);
    }
    for (uint32_t p = 0; p < np; p++) {
        acc[p] = quarter_warp_sum_f32(acc[p], lane);
        if (lane == 0) {
            if (atomic_out) {
                uint32_t tok = pair[p] / n_expert;
                atomicAdd(down_out + (uint64_t)tok * out_dim + row, acc[p]);
            } else {
                down_out[(uint64_t)pair[p] * out_dim + row] = acc[p];
            }
        }
    }
}

__global__ static void moe_down_expert_tile8_row32_kernel(
        float *down_out,
        const char *down_base,
        const cuda_block_q8_K *midq,
        const uint32_t *sorted_pairs,
        const uint32_t *offsets,
        const uint32_t *counts,
        const uint32_t *tile_total,
        const uint32_t *tile_experts,
        const uint32_t *tile_starts,
        uint64_t down_expert_bytes,
        uint64_t down_row_bytes,
        uint32_t midq_blocks,
        uint32_t out_dim,
        uint32_t n_expert,
        uint32_t atomic_out) {
    uint32_t tile = blockIdx.y;
    if (tile >= *tile_total) return;
    uint32_t lane = threadIdx.x & 7u;
    uint32_t row = blockIdx.x * 32u + (threadIdx.x >> 3u);
    uint32_t expert = tile_experts[tile];
    uint32_t local_start = tile_starts[tile];
    __shared__ cuda_block_q8_K sxq[8][8];
    uint32_t pair[8] = {0, 0, 0, 0, 0, 0, 0, 0};
    const cuda_block_q8_K *xqb[8] = {NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL};
    uint32_t np = 0;
    for (; np < 8u; np++) {
        uint32_t local_pair = local_start + np;
        if (local_pair >= counts[expert]) break;
        pair[np] = sorted_pairs[offsets[expert] + local_pair];
        xqb[np] = midq + (uint64_t)pair[np] * midq_blocks;
    }
    if (midq_blocks <= 8u) {
        for (uint32_t i = threadIdx.x; i < np * midq_blocks; i += blockDim.x) {
            uint32_t p = i / midq_blocks;
            uint32_t b = i - p * midq_blocks;
            sxq[p][b] = xqb[p][b];
        }
        __syncthreads();
        for (uint32_t p = 0; p < np; p++) xqb[p] = sxq[p];
    }
    if (row >= out_dim) return;
    const cuda_block_q2_K *wr = (const cuda_block_q2_K *)(down_base + (uint64_t)expert * down_expert_bytes + (uint64_t)row * down_row_bytes);
    float acc[8] = {0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f};
    for (uint32_t b = lane; b < midq_blocks; b += 8u) {
        dev_dot_q2_K_q8_K_block8(wr + b, xqb[0] ? xqb[0] + b : NULL, xqb[1] ? xqb[1] + b : NULL,
                                 xqb[2] ? xqb[2] + b : NULL, xqb[3] ? xqb[3] + b : NULL,
                                 xqb[4] ? xqb[4] + b : NULL, xqb[5] ? xqb[5] + b : NULL,
                                 xqb[6] ? xqb[6] + b : NULL, xqb[7] ? xqb[7] + b : NULL, np, acc);
    }
    for (uint32_t p = 0; p < np; p++) {
        acc[p] = quarter_warp_sum_f32(acc[p], lane);
        if (lane == 0) {
            if (atomic_out) {
                uint32_t tok = pair[p] / n_expert;
                atomicAdd(down_out + (uint64_t)tok * out_dim + row, acc[p]);
            } else {
                down_out[(uint64_t)pair[p] * out_dim + row] = acc[p];
            }
        }
    }
}

__global__ static void moe_down_expert_tile16_row32_kernel(
        float *down_out,
        const char *down_base,
        const cuda_block_q8_K *midq,
        const uint32_t *sorted_pairs,
        const uint32_t *offsets,
        const uint32_t *counts,
        const uint32_t *tile_total,
        const uint32_t *tile_experts,
        const uint32_t *tile_starts,
        uint64_t down_expert_bytes,
        uint64_t down_row_bytes,
        uint32_t midq_blocks,
        uint32_t out_dim,
        uint32_t n_expert,
        uint32_t atomic_out) {
    uint32_t tile = blockIdx.y;
    if (tile >= *tile_total) return;
    uint32_t local_start = tile_starts[tile];
    if (local_start & 8u) return;
    uint32_t lane = threadIdx.x & 7u;
    uint32_t row = blockIdx.x * 32u + (threadIdx.x >> 3u);
    uint32_t expert = tile_experts[tile];
    __shared__ cuda_block_q8_K sxq[16][8];
    uint32_t pair[16] = {0};
    const cuda_block_q8_K *xqb[16] = {NULL};
    uint32_t np = 0;
    for (; np < 16u; np++) {
        uint32_t local_pair = local_start + np;
        if (local_pair >= counts[expert]) break;
        pair[np] = sorted_pairs[offsets[expert] + local_pair];
        xqb[np] = midq + (uint64_t)pair[np] * midq_blocks;
    }
    if (midq_blocks <= 8u) {
        for (uint32_t i = threadIdx.x; i < np * midq_blocks; i += blockDim.x) {
            uint32_t p = i / midq_blocks;
            uint32_t b = i - p * midq_blocks;
            sxq[p][b] = xqb[p][b];
        }
        __syncthreads();
        for (uint32_t p = 0; p < np; p++) xqb[p] = sxq[p];
    }
    if (row >= out_dim) return;
    const cuda_block_q2_K *wr = (const cuda_block_q2_K *)(down_base + (uint64_t)expert * down_expert_bytes + (uint64_t)row * down_row_bytes);
    float acc[16] = {0.0f};
    for (uint32_t b = lane; b < midq_blocks; b += 8u) {
        dev_dot_q2_K_q8_K_block8(wr + b, xqb[0] ? xqb[0] + b : NULL, xqb[1] ? xqb[1] + b : NULL,
                                 xqb[2] ? xqb[2] + b : NULL, xqb[3] ? xqb[3] + b : NULL,
                                 xqb[4] ? xqb[4] + b : NULL, xqb[5] ? xqb[5] + b : NULL,
                                 xqb[6] ? xqb[6] + b : NULL, xqb[7] ? xqb[7] + b : NULL, np < 8u ? np : 8u, acc);
        if (np > 8u) {
            dev_dot_q2_K_q8_K_block8(wr + b, xqb[8] ? xqb[8] + b : NULL, xqb[9] ? xqb[9] + b : NULL,
                                     xqb[10] ? xqb[10] + b : NULL, xqb[11] ? xqb[11] + b : NULL,
                                     xqb[12] ? xqb[12] + b : NULL, xqb[13] ? xqb[13] + b : NULL,
                                     xqb[14] ? xqb[14] + b : NULL, xqb[15] ? xqb[15] + b : NULL, np - 8u, acc + 8);
        }
    }
    for (uint32_t p = 0; p < np; p++) {
        acc[p] = quarter_warp_sum_f32(acc[p], lane);
        if (lane == 0) {
            if (atomic_out) {
                uint32_t tok = pair[p] / n_expert;
                atomicAdd(down_out + (uint64_t)tok * out_dim + row, acc[p]);
            } else {
                down_out[(uint64_t)pair[p] * out_dim + row] = acc[p];
            }
        }
    }
}

__global__ static void moe_down_expert_tile16_row2048_kernel(
        float *down_out,
        const char *down_base,
        const cuda_block_q8_K *midq,
        const uint32_t *sorted_pairs,
        const uint32_t *offsets,
        const uint32_t *counts,
        const uint32_t *tile_total,
        const uint32_t *tile_experts,
        const uint32_t *tile_starts,
        uint64_t down_expert_bytes,
        uint64_t down_row_bytes,
        uint32_t midq_blocks,
        uint32_t out_dim,
        uint32_t n_expert,
        uint32_t atomic_out) {
    uint32_t tile = blockIdx.y;
    if (tile >= *tile_total) return;
    uint32_t local_start = tile_starts[tile];
    if (local_start & 8u) return;
    uint32_t lane = threadIdx.x & 7u;
    uint32_t row_lane = threadIdx.x >> 3u;
    uint32_t expert = tile_experts[tile];
    __shared__ cuda_block_q8_K sxq[16][8];
    uint32_t pair[16] = {0};
    const cuda_block_q8_K *xqb[16] = {NULL};
    uint32_t np = 0;
    for (; np < 16u; np++) {
        uint32_t local_pair = local_start + np;
        if (local_pair >= counts[expert]) break;
        pair[np] = sorted_pairs[offsets[expert] + local_pair];
        xqb[np] = midq + (uint64_t)pair[np] * midq_blocks;
    }
    if (midq_blocks <= 8u) {
        for (uint32_t i = threadIdx.x; i < np * midq_blocks; i += blockDim.x) {
            uint32_t p = i / midq_blocks;
            uint32_t b = i - p * midq_blocks;
            sxq[p][b] = xqb[p][b];
        }
        __syncthreads();
        for (uint32_t p = 0; p < np; p++) xqb[p] = sxq[p];
    }
    for (uint32_t rr = 0; rr < 64u; rr++) {
        uint32_t row = blockIdx.x * 2048u + row_lane + rr * 32u;
        if (row >= out_dim) continue;
        const cuda_block_q2_K *wr = (const cuda_block_q2_K *)(down_base + (uint64_t)expert * down_expert_bytes + (uint64_t)row * down_row_bytes);
        float acc[16] = {0.0f};
        for (uint32_t b = lane; b < midq_blocks; b += 8u) {
            dev_dot_q2_K_q8_K_block8(wr + b, xqb[0] ? xqb[0] + b : NULL, xqb[1] ? xqb[1] + b : NULL,
                                     xqb[2] ? xqb[2] + b : NULL, xqb[3] ? xqb[3] + b : NULL,
                                     xqb[4] ? xqb[4] + b : NULL, xqb[5] ? xqb[5] + b : NULL,
                                     xqb[6] ? xqb[6] + b : NULL, xqb[7] ? xqb[7] + b : NULL, np < 8u ? np : 8u, acc);
            if (np > 8u) {
                dev_dot_q2_K_q8_K_block8(wr + b, xqb[8] ? xqb[8] + b : NULL, xqb[9] ? xqb[9] + b : NULL,
                                         xqb[10] ? xqb[10] + b : NULL, xqb[11] ? xqb[11] + b : NULL,
                                         xqb[12] ? xqb[12] + b : NULL, xqb[13] ? xqb[13] + b : NULL,
                                         xqb[14] ? xqb[14] + b : NULL, xqb[15] ? xqb[15] + b : NULL, np - 8u, acc + 8);
            }
        }
        for (uint32_t p = 0; p < np; p++) {
            acc[p] = quarter_warp_sum_f32(acc[p], lane);
            if (lane == 0) {
                if (atomic_out) {
                    uint32_t tok = pair[p] / n_expert;
                    atomicAdd(down_out + (uint64_t)tok * out_dim + row, acc[p]);
                } else {
                    down_out[(uint64_t)pair[p] * out_dim + row] = acc[p];
                }
            }
        }
    }
}

template <uint32_t ROW_SPAN>
__global__ static void moe_down_expert_tile16_rowspan_kernel(
        float *down_out,
        const char *down_base,
        const cuda_block_q8_K *midq,
        const uint32_t *sorted_pairs,
        const uint32_t *offsets,
        const uint32_t *counts,
        const uint32_t *tile_total,
        const uint32_t *tile_experts,
        const uint32_t *tile_starts,
        uint64_t down_expert_bytes,
        uint64_t down_row_bytes,
        uint32_t midq_blocks,
        uint32_t out_dim,
        uint32_t n_expert,
        uint32_t atomic_out) {
    uint32_t tile = blockIdx.y;
    if (tile >= *tile_total) return;
    uint32_t local_start = tile_starts[tile];
    if (local_start & 8u) return;
    uint32_t lane = threadIdx.x & 7u;
    uint32_t row_lane = threadIdx.x >> 3u;
    uint32_t expert = tile_experts[tile];
    __shared__ cuda_block_q8_K sxq[16][8];
    uint32_t pair[16] = {0};
    const cuda_block_q8_K *xqb[16] = {NULL};
    uint32_t np = 0;
    for (; np < 16u; np++) {
        uint32_t local_pair = local_start + np;
        if (local_pair >= counts[expert]) break;
        pair[np] = sorted_pairs[offsets[expert] + local_pair];
        xqb[np] = midq + (uint64_t)pair[np] * midq_blocks;
    }
    if (midq_blocks <= 8u) {
        for (uint32_t i = threadIdx.x; i < np * midq_blocks; i += blockDim.x) {
            uint32_t p = i / midq_blocks;
            uint32_t b = i - p * midq_blocks;
            sxq[p][b] = xqb[p][b];
        }
        __syncthreads();
        for (uint32_t p = 0; p < np; p++) xqb[p] = sxq[p];
    }
    for (uint32_t rr = 0; rr < ROW_SPAN / 32u; rr++) {
        uint32_t row = blockIdx.x * ROW_SPAN + row_lane + rr * 32u;
        if (row >= out_dim) continue;
        const cuda_block_q2_K *wr = (const cuda_block_q2_K *)(down_base + (uint64_t)expert * down_expert_bytes + (uint64_t)row * down_row_bytes);
        float acc[16] = {0.0f};
        for (uint32_t b = lane; b < midq_blocks; b += 8u) {
            dev_dot_q2_K_q8_K_block8(wr + b, xqb[0] ? xqb[0] + b : NULL, xqb[1] ? xqb[1] + b : NULL,
                                     xqb[2] ? xqb[2] + b : NULL, xqb[3] ? xqb[3] + b : NULL,
                                     xqb[4] ? xqb[4] + b : NULL, xqb[5] ? xqb[5] + b : NULL,
                                     xqb[6] ? xqb[6] + b : NULL, xqb[7] ? xqb[7] + b : NULL, np < 8u ? np : 8u, acc);
            if (np > 8u) {
                dev_dot_q2_K_q8_K_block8(wr + b, xqb[8] ? xqb[8] + b : NULL, xqb[9] ? xqb[9] + b : NULL,
                                         xqb[10] ? xqb[10] + b : NULL, xqb[11] ? xqb[11] + b : NULL,
                                         xqb[12] ? xqb[12] + b : NULL, xqb[13] ? xqb[13] + b : NULL,
                                         xqb[14] ? xqb[14] + b : NULL, xqb[15] ? xqb[15] + b : NULL, np - 8u, acc + 8);
            }
        }
        for (uint32_t p = 0; p < np; p++) {
            acc[p] = quarter_warp_sum_f32(acc[p], lane);
            if (lane == 0) {
                if (atomic_out) {
                    uint32_t tok = pair[p] / n_expert;
                    atomicAdd(down_out + (uint64_t)tok * out_dim + row, acc[p]);
                } else {
                    down_out[(uint64_t)pair[p] * out_dim + row] = acc[p];
                }
            }
        }
    }
}

__global__ static void moe_down_sorted_p2_qwarp32_kernel(
        float *down_out,
        const char *down_base,
        const cuda_block_q8_K *midq,
        const uint32_t *sorted_pairs,
        const int32_t *selected,
        uint64_t down_expert_bytes,
        uint64_t down_row_bytes,
        uint32_t midq_blocks,
        uint32_t out_dim,
        uint32_t n_expert,
        uint32_t pair_count) {
    uint32_t lane = threadIdx.x & 7u;
    uint32_t pair_lane = (threadIdx.x >> 3u) & 1u;
    uint32_t row = blockIdx.x * 16u + (threadIdx.x >> 4u);
    uint32_t sorted_idx = blockIdx.y * 2u + pair_lane;
    if (row >= out_dim || sorted_idx >= pair_count) return;
    uint32_t pair = sorted_pairs[sorted_idx];
    uint32_t tok = pair / n_expert;
    uint32_t slot = pair - tok * n_expert;
    int32_t expert_i = selected[(uint64_t)tok * n_expert + slot];
    if (expert_i < 0) expert_i = 0;
    const cuda_block_q2_K *wr = (const cuda_block_q2_K *)(down_base + (uint64_t)(uint32_t)expert_i * down_expert_bytes + (uint64_t)row * down_row_bytes);
    const cuda_block_q8_K *xq = midq + (uint64_t)pair * midq_blocks;
    float acc = 0.0f;
    for (uint32_t b = lane; b < midq_blocks; b += 8u) acc += dev_dot_q2_K_q8_K_block(wr + b, xq + b);
    acc = quarter_warp_sum_f32(acc, lane);
    if (lane == 0) down_out[(uint64_t)pair * out_dim + row] = acc;
}

__global__ static void moe_sum_kernel(float *out, const float *down, uint32_t out_dim, uint32_t n_expert, uint32_t n_tokens) {
    uint64_t gid = (uint64_t)blockIdx.x * blockDim.x + threadIdx.x;
    uint64_t n = (uint64_t)n_tokens * out_dim;
    if (gid >= n) return;
    uint32_t tok = gid / out_dim;
    uint32_t row = gid - (uint64_t)tok * out_dim;
    float acc = 0.0f;
    for (uint32_t e = 0; e < n_expert; e++) acc += down[((uint64_t)tok * n_expert + e) * out_dim + row];
    out[gid] = acc;
}

__device__ static float dev_iq2_xxs_dot_f32(const cuda_block_iq2_xxs *row, const float *x, uint32_t nb) {
    float acc = 0.0f;
    for (uint32_t b = 0; b < nb; b++) {
        const cuda_block_iq2_xxs *xb = row + b;
        const float d = dev_f16_to_f32(xb->d);
        const uint16_t *q2 = xb->qs;
        const float *xf = x + (uint64_t)b * CUDA_QK_K;
        for (uint32_t ib32 = 0; ib32 < CUDA_QK_K / 32; ib32++) {
            const uint32_t aux_g = (uint32_t)q2[0] | ((uint32_t)q2[1] << 16);
            const uint32_t aux_s = (uint32_t)q2[2] | ((uint32_t)q2[3] << 16);
            q2 += 4;
            const float dl = d * (0.5f + (float)(aux_s >> 28)) * 0.25f;
            const uint8_t grids[4] = {
                (uint8_t)(aux_g & 0xffu),
                (uint8_t)((aux_g >> 8) & 0xffu),
                (uint8_t)((aux_g >> 16) & 0xffu),
                (uint8_t)((aux_g >> 24) & 0xffu),
            };
            for (uint32_t half = 0; half < 2; half++) {
                for (uint32_t g = 0; g < 2; g++) {
                    const uint32_t gi = half * 2 + g;
                    const uint64_t grid = cuda_iq2xxs_grid[grids[gi]];
                    const uint8_t signs = cuda_ksigns_iq2xs[(aux_s >> (14u * half + 7u * g)) & 127u];
                    for (uint32_t i = 0; i < 8; i++) {
                        float w = (float)((grid >> (8u * i)) & 0xffu);
                        if (signs & (1u << i)) w = -w;
                        acc += dl * w * xf[ib32 * 32u + half * 16u + g * 8u + i];
                    }
                }
            }
        }
    }
    return acc;
}

__device__ static float dev_q2_K_dot_f32(const cuda_block_q2_K *row, const float *x, uint32_t nb) {
    float acc = 0.0f;
    for (uint32_t b = 0; b < nb; b++) {
        const cuda_block_q2_K *xb = row + b;
        const float d = dev_f16_to_f32(xb->d);
        const float dmin = dev_f16_to_f32(xb->dmin);
        for (uint32_t il = 0; il < 16; il++) {
            const uint32_t chunk = il / 8u;
            const uint32_t pair = il & 1u;
            const uint32_t shift = ((il / 2u) & 3u) * 2u;
            const uint8_t sc = xb->scales[il];
            const float dl = d * (float)(sc & 0x0fu);
            const float ml = dmin * (float)(sc >> 4);
            const uint8_t *q = xb->qs + 32u * chunk + 16u * pair;
            const float *xf = x + (uint64_t)b * CUDA_QK_K + chunk * 128u + ((il % 8u) / 2u) * 32u + pair * 16u;
            for (uint32_t i = 0; i < 16; i++) {
                const float w = dl * (float)((q[i] >> shift) & 3u) - ml;
                acc += w * xf[i];
            }
        }
    }
    return acc;
}

__global__ static void moe_gate_up_mid_f32_kernel(
        float *gate_out,
        float *up_out,
        float *mid_out,
        const char *gate_base,
        const char *up_base,
        const float *x,
        const int32_t *selected,
        const float *weights,
        uint64_t gate_expert_bytes,
        uint64_t gate_row_bytes,
        uint32_t expert_in_dim,
        uint32_t expert_mid_dim,
        uint32_t n_expert,
        float clamp) {
    uint32_t row = blockIdx.x;
    uint32_t pair = blockIdx.y;
    if (row >= expert_mid_dim) return;
    uint32_t tok = pair / n_expert;
    uint32_t slot = pair - tok * n_expert;
    int32_t expert_i = selected[(uint64_t)tok * n_expert + slot];
    if (expert_i < 0) expert_i = 0;
    uint32_t expert = (uint32_t)expert_i;
    const uint32_t nb = expert_in_dim / CUDA_QK_K;
    const cuda_block_iq2_xxs *gr = (const cuda_block_iq2_xxs *)(gate_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
    const cuda_block_iq2_xxs *ur = (const cuda_block_iq2_xxs *)(up_base + (uint64_t)expert * gate_expert_bytes + (uint64_t)row * gate_row_bytes);
    const float *xr = x + (uint64_t)tok * expert_in_dim;
    float gate = 0.0f;
    float up = 0.0f;
    for (uint32_t b = threadIdx.x; b < nb; b += blockDim.x) {
        gate += dev_iq2_xxs_dot_f32(gr + b, xr + (uint64_t)b * CUDA_QK_K, 1);
        up += dev_iq2_xxs_dot_f32(ur + b, xr + (uint64_t)b * CUDA_QK_K, 1);
    }
    __shared__ float partial_gate[256];
    __shared__ float partial_up[256];
    partial_gate[threadIdx.x] = gate;
    partial_up[threadIdx.x] = up;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) {
            partial_gate[threadIdx.x] += partial_gate[threadIdx.x + stride];
            partial_up[threadIdx.x] += partial_up[threadIdx.x + stride];
        }
        __syncthreads();
    }
    if (threadIdx.x == 0) {
        gate = partial_gate[0];
        up = partial_up[0];
        if (clamp > 1.0e-6f) {
            if (gate > clamp) gate = clamp;
            if (up > clamp) up = clamp;
            if (up < -clamp) up = -clamp;
        }
        const uint64_t off = (uint64_t)pair * expert_mid_dim + row;
        gate_out[off] = gate;
        up_out[off] = up;
        mid_out[off] = (gate / (1.0f + expf(-gate))) * up * weights[(uint64_t)tok * n_expert + slot];
    }
}

__global__ static void moe_down_f32_kernel(
        float *down_out,
        const char *down_base,
        const float *mid,
        const int32_t *selected,
        uint64_t down_expert_bytes,
        uint64_t down_row_bytes,
        uint32_t expert_mid_dim,
        uint32_t out_dim,
        uint32_t n_expert) {
    uint32_t row = blockIdx.x;
    uint32_t pair = blockIdx.y;
    if (row >= out_dim) return;
    uint32_t tok = pair / n_expert;
    uint32_t slot = pair - tok * n_expert;
    int32_t expert_i = selected[(uint64_t)tok * n_expert + slot];
    if (expert_i < 0) expert_i = 0;
    const uint32_t nb = expert_mid_dim / CUDA_QK_K;
    const cuda_block_q2_K *wr = (const cuda_block_q2_K *)(down_base + (uint64_t)(uint32_t)expert_i * down_expert_bytes + (uint64_t)row * down_row_bytes);
    const float *xr = mid + (uint64_t)pair * expert_mid_dim;
    float acc = 0.0f;
    for (uint32_t b = threadIdx.x; b < nb; b += blockDim.x) acc += dev_q2_K_dot_f32(wr + b, xr + (uint64_t)b * CUDA_QK_K, 1);
    __shared__ float partial[256];
    partial[threadIdx.x] = acc;
    __syncthreads();
    for (uint32_t stride = blockDim.x >> 1; stride > 0; stride >>= 1) {
        if (threadIdx.x < stride) partial[threadIdx.x] += partial[threadIdx.x + stride];
        __syncthreads();
    }
    if (threadIdx.x == 0) down_out[(uint64_t)pair * out_dim + row] = partial[0];
}

static int routed_moe_launch(
        ds4_gpu_tensor *out,
        ds4_gpu_tensor *gate,
        ds4_gpu_tensor *up,
        ds4_gpu_tensor *mid,
        ds4_gpu_tensor *down,
        const void *model_map,
        uint64_t model_size,
        uint64_t gate_offset,
        uint64_t up_offset,
        uint64_t down_offset,
        uint32_t gate_type,
        uint32_t down_type,
        uint64_t gate_expert_bytes,
        uint64_t gate_row_bytes,
        uint64_t down_expert_bytes,
        uint64_t down_row_bytes,
        uint32_t expert_in_dim,
        uint32_t expert_mid_dim,
        uint32_t out_dim,
        const ds4_gpu_tensor *selected,
        const ds4_gpu_tensor *weights,
        uint32_t n_expert,
        float clamp,
        const ds4_gpu_tensor *x,
        uint32_t n_tokens) {
    if (!out || !gate || !up || !mid || !down || !model_map || !selected || !weights || !x ||
        n_tokens == 0 || n_expert == 0 ||
        expert_in_dim % CUDA_QK_K != 0 || expert_mid_dim % CUDA_QK_K != 0 ||
        gate_offset > model_size || up_offset > model_size || down_offset > model_size ||
        x->bytes < (uint64_t)n_tokens * expert_in_dim * sizeof(float) ||
        selected->bytes < (uint64_t)n_tokens * n_expert * sizeof(int32_t) ||
        weights->bytes < (uint64_t)n_tokens * n_expert * sizeof(float) ||
        gate->bytes < (uint64_t)n_tokens * n_expert * expert_mid_dim * sizeof(float) ||
        up->bytes < (uint64_t)n_tokens * n_expert * expert_mid_dim * sizeof(float) ||
        mid->bytes < (uint64_t)n_tokens * n_expert * expert_mid_dim * sizeof(float) ||
        down->bytes < (uint64_t)n_tokens * n_expert * out_dim * sizeof(float) ||
        out->bytes < (uint64_t)n_tokens * out_dim * sizeof(float)) {
        return 0;
    }
    const int q4k_path = (gate_type == 12u && down_type == 12u);
    if (!q4k_path && (gate_type != 16u || down_type != 10u)) return 0;
    if (q4k_path && (n_tokens != 1u || n_expert != 6u)) return 0;
    const uint64_t gate_bytes = 256ull * gate_expert_bytes;
    const uint64_t down_bytes = 256ull * down_expert_bytes;
    if (gate_bytes > model_size - gate_offset ||
        gate_bytes > model_size - up_offset ||
        down_bytes > model_size - down_offset) {
        return 0;
    }
    const char *gate_w = cuda_model_range_ptr(model_map, gate_offset, gate_bytes, "moe_gate");
    const char *up_w = cuda_model_range_ptr(model_map, up_offset, gate_bytes, "moe_up");
    const char *down_w = cuda_model_range_ptr(model_map, down_offset, down_bytes, "moe_down");
    if (!gate_w || !up_w || !down_w) return 0;

    int ok = 1;
    const uint32_t xq_blocks = expert_in_dim / CUDA_QK_K;
    const uint32_t midq_blocks = expert_mid_dim / CUDA_QK_K;
    const uint64_t xq_count = (uint64_t)n_tokens * xq_blocks;
    const uint64_t midq_count = (uint64_t)n_tokens * n_expert * midq_blocks;
    const uint64_t xq_bytes = xq_count * sizeof(cuda_block_q8_K);
    const uint64_t midq_bytes = midq_count * sizeof(cuda_block_q8_K);
    if (down->bytes >= xq_bytes && gate->bytes >= midq_bytes) {
        cuda_block_q8_K *xq = (cuda_block_q8_K *)down->ptr;
        cuda_block_q8_K *midq = (cuda_block_q8_K *)gate->ptr;
        const uint32_t profile_moe = getenv("DS4_CUDA_MOE_PROFILE") != NULL;
        cudaEvent_t prof_ev[7] = {NULL, NULL, NULL, NULL, NULL, NULL, NULL};
        if (profile_moe) {
            for (uint32_t i = 0; i < 7u; i++) {
                if (cudaEventCreate(&prof_ev[i]) != cudaSuccess) {
                    for (uint32_t j = 0; j < i; j++) (void)cudaEventDestroy(prof_ev[j]);
                    memset(prof_ev, 0, sizeof(prof_ev));
                    break;
                }
            }
            if (prof_ev[0]) (void)cudaEventRecord(prof_ev[0], 0);
        }
        const uint32_t pair_count = n_tokens * n_expert;
        const uint32_t use_sorted_pairs = n_tokens > 1u;
        const uint32_t use_expert_tiles = use_sorted_pairs && getenv("DS4_CUDA_MOE_NO_EXPERT_TILES") == NULL;
        const uint32_t expert_tile_m = getenv("DS4_CUDA_MOE_TILE4") ? 4u : 8u;
        const uint32_t write_gate_up = getenv("DS4_CUDA_MOE_WRITE_GATE_UP") != NULL;
        const uint32_t use_p2_sorted = use_sorted_pairs && getenv("DS4_CUDA_MOE_NO_P2") == NULL;
        const uint32_t use_atomic_down = use_expert_tiles &&
            (getenv("DS4_CUDA_MOE_ATOMIC_DOWN") != NULL ||
             (n_tokens >= 128u && getenv("DS4_CUDA_MOE_NO_ATOMIC_DOWN") == NULL));
        const uint32_t use_gate_row2048 = use_expert_tiles && expert_tile_m == 8u &&
            (getenv("DS4_CUDA_MOE_GATE_ROW2048") != NULL ||
             getenv("DS4_CUDA_MOE_GATE_ROW256") != NULL ||
             getenv("DS4_CUDA_MOE_GATE_ROW128") != NULL ||
             (n_tokens >= 128u &&
              getenv("DS4_CUDA_MOE_NO_GATE_ROW2048") == NULL &&
              getenv("DS4_CUDA_MOE_NO_GATE_ROW256") == NULL &&
              getenv("DS4_CUDA_MOE_NO_GATE_ROW128") == NULL));
        const uint32_t use_down_tile16 = use_atomic_down && expert_tile_m == 8u &&
            n_tokens >= 128u && getenv("DS4_CUDA_MOE_NO_DOWN_TILE16") == NULL;
        const uint32_t use_decode_lut_gate =
            n_tokens == 1u && xq_blocks <= 16u &&
            getenv("DS4_CUDA_MOE_NO_DECODE_LUT_GATE") == NULL;
        const uint32_t gate_row_span =
            getenv("DS4_CUDA_MOE_GATE_ROW512") != NULL ? 512u :
            getenv("DS4_CUDA_MOE_GATE_ROW2048") != NULL ? 2048u : 1024u;
        const uint32_t down_row_span =
            getenv("DS4_CUDA_MOE_DOWN_ROW512") != NULL ? 512u :
            getenv("DS4_CUDA_MOE_DOWN_ROW1024") != NULL ? 1024u : 2048u;
        const uint32_t use_down_row2048 = use_atomic_down && expert_tile_m == 8u &&
            (getenv("DS4_CUDA_MOE_DOWN_ROW2048") != NULL ||
             getenv("DS4_CUDA_MOE_DOWN_ROW256") != NULL ||
             getenv("DS4_CUDA_MOE_DOWN_ROW128") != NULL ||
             getenv("DS4_CUDA_MOE_DOWN_ROW64") != NULL ||
             (use_down_tile16 &&
              getenv("DS4_CUDA_MOE_NO_DOWN_ROW2048") == NULL &&
              getenv("DS4_CUDA_MOE_NO_DOWN_ROW256") == NULL &&
              getenv("DS4_CUDA_MOE_NO_DOWN_ROW128") == NULL &&
              getenv("DS4_CUDA_MOE_NO_DOWN_ROW64") == NULL));
        const uint32_t use_direct_down_sum6 =
            n_tokens == 1u && n_expert == 6u &&
            getenv("DS4_CUDA_MOE_NO_DIRECT_DOWN_SUM6") == NULL;
        uint32_t *sorted_pairs = NULL;
        uint32_t *sorted_offsets = NULL;
        uint32_t *sorted_counts = NULL;
        uint32_t *tile_total = NULL;
        uint32_t *tile_experts = NULL;
        uint32_t *tile_starts = NULL;
        uint32_t *tile16_total = NULL;
        uint32_t *tile16_experts = NULL;
        uint32_t *tile16_starts = NULL;
        uint32_t tile_capacity = 0;
        uint32_t tile16_capacity = 0;
        dim3 xq_grid(xq_blocks, n_tokens, 1);
        q8_K_quantize_kernel<<<xq_grid, 256>>>(xq, (const float *)x->ptr, expert_in_dim, n_tokens);
        ok = cuda_ok(cudaGetLastError(), "routed_moe x quantize launch");
        if (prof_ev[1]) (void)cudaEventRecord(prof_ev[1], 0);
        if (ok && use_sorted_pairs) {
            const uint64_t counts_bytes = 256ull * sizeof(uint32_t);
            const uint64_t offsets_bytes = 257ull * sizeof(uint32_t);
            const uint64_t cursors_bytes = 256ull * sizeof(uint32_t);
            const uint64_t sorted_bytes = (uint64_t)pair_count * sizeof(uint32_t);
            tile_capacity = (pair_count + expert_tile_m - 1u) / expert_tile_m + 256u;
            tile16_capacity = use_down_tile16 ? ((pair_count + 15u) / 16u + 256u) : 0u;
            const uint64_t tile_offsets_bytes = 257ull * sizeof(uint32_t);
            const uint64_t tile_total_bytes = sizeof(uint32_t);
            const uint64_t tile_experts_bytes = (uint64_t)tile_capacity * sizeof(uint32_t);
            const uint64_t tile_starts_bytes = (uint64_t)tile_capacity * sizeof(uint32_t);
            const uint64_t tile16_offsets_bytes = use_down_tile16 ? 257ull * sizeof(uint32_t) : 0u;
            const uint64_t tile16_total_bytes = use_down_tile16 ? sizeof(uint32_t) : 0u;
            const uint64_t tile16_experts_bytes = (uint64_t)tile16_capacity * sizeof(uint32_t);
            const uint64_t tile16_starts_bytes = (uint64_t)tile16_capacity * sizeof(uint32_t);
            const uint64_t tile_offsets_off = counts_bytes + offsets_bytes + cursors_bytes + sorted_bytes;
            const uint64_t tile_total_off = tile_offsets_off + tile_offsets_bytes;
            const uint64_t tile_experts_off = tile_total_off + tile_total_bytes;
            const uint64_t tile_starts_off = tile_experts_off + tile_experts_bytes;
            const uint64_t tile16_offsets_off = tile_starts_off + tile_starts_bytes;
            const uint64_t tile16_total_off = tile16_offsets_off + tile16_offsets_bytes;
            const uint64_t tile16_experts_off = tile16_total_off + tile16_total_bytes;
            const uint64_t tile16_starts_off = tile16_experts_off + tile16_experts_bytes;
            const uint64_t scratch_bytes = tile16_starts_off + tile16_starts_bytes;
            uint8_t *scratch = (uint8_t *)cuda_tmp_alloc(scratch_bytes,
                                                         "routed_moe sorted pairs");
            if (!scratch) {
                ok = 0;
            } else {
                uint32_t *counts = (uint32_t *)scratch;
                uint32_t *offsets = (uint32_t *)(scratch + counts_bytes);
                uint32_t *cursors = (uint32_t *)(scratch + counts_bytes + offsets_bytes);
                sorted_pairs = (uint32_t *)(scratch + counts_bytes + offsets_bytes + cursors_bytes);
                sorted_offsets = offsets;
                sorted_counts = counts;
                uint32_t *tile_offsets = (uint32_t *)(scratch + tile_offsets_off);
                tile_total = (uint32_t *)(scratch + tile_total_off);
                tile_experts = (uint32_t *)(scratch + tile_experts_off);
                tile_starts = (uint32_t *)(scratch + tile_starts_off);
                uint32_t *tile16_offsets = use_down_tile16 ? (uint32_t *)(scratch + tile16_offsets_off) : NULL;
                tile16_total = use_down_tile16 ? (uint32_t *)(scratch + tile16_total_off) : NULL;
                tile16_experts = use_down_tile16 ? (uint32_t *)(scratch + tile16_experts_off) : NULL;
                tile16_starts = use_down_tile16 ? (uint32_t *)(scratch + tile16_starts_off) : NULL;
                ok = cuda_ok(cudaMemset(counts, 0, counts_bytes), "routed_moe sorted counts clear");
                if (ok) {
                    moe_count_sorted_pairs_kernel<<<(pair_count + 255u) / 256u, 256>>>(
                        counts,
                        (const int32_t *)selected->ptr,
                        pair_count);
                    ok = cuda_ok(cudaGetLastError(), "routed_moe sorted count launch");
                }
                if (ok) {
                    moe_prefix_sorted_pairs_kernel<<<1, 1>>>(offsets, cursors, counts);
                    ok = cuda_ok(cudaGetLastError(), "routed_moe sorted prefix launch");
                }
                if (ok) {
                    moe_scatter_sorted_pairs_kernel<<<(pair_count + 255u) / 256u, 256>>>(
                        sorted_pairs,
                        cursors,
                        (const int32_t *)selected->ptr,
                        pair_count);
                    ok = cuda_ok(cudaGetLastError(), "routed_moe sorted scatter launch");
                }
                if (ok && use_expert_tiles) {
                    moe_build_expert_tile_offsets_kernel<<<1, 1>>>(tile_offsets, tile_total, counts, expert_tile_m);
                    ok = cuda_ok(cudaGetLastError(), "routed_moe expert tile offsets launch");
                }
                if (ok && use_expert_tiles) {
                    moe_build_expert_tiles_kernel<<<1, 256>>>(tile_experts, tile_starts, tile_offsets, counts, expert_tile_m);
                    ok = cuda_ok(cudaGetLastError(), "routed_moe expert tiles launch");
                }
                if (ok && use_expert_tiles && use_down_tile16) {
                    moe_build_expert_tile_offsets_kernel<<<1, 1>>>(tile16_offsets, tile16_total, counts, 16u);
                    ok = cuda_ok(cudaGetLastError(), "routed_moe expert tile16 offsets launch");
                }
                if (ok && use_expert_tiles && use_down_tile16) {
                    moe_build_expert_tiles_kernel<<<1, 256>>>(tile16_experts, tile16_starts, tile16_offsets, counts, 16u);
                    ok = cuda_ok(cudaGetLastError(), "routed_moe expert tile16 launch");
                }
            }
        }
        if (prof_ev[2]) (void)cudaEventRecord(prof_ev[2], 0);
        if (ok) {
            dim3 mgrid((expert_mid_dim + 31u) / 32u, n_tokens * n_expert, 1);
            if (ok && sorted_pairs && use_expert_tiles && sorted_offsets && sorted_counts && tile_total && tile_experts && tile_starts) {
                if (use_gate_row2048) {
                    if (gate_row_span == 512u) {
                        dim3 tgrid((expert_mid_dim + 511u) / 512u, tile_capacity, 1);
                        moe_gate_up_mid_expert_tile8_rowspan_kernel<512><<<tgrid, 256>>>(
                            (float *)gate->ptr, (float *)up->ptr, (float *)mid->ptr,
                            gate_w, up_w, xq, sorted_pairs, sorted_offsets, sorted_counts,
                            tile_total, tile_experts, tile_starts, (const float *)weights->ptr,
                            gate_expert_bytes, gate_row_bytes, xq_blocks, expert_mid_dim, n_expert,
                            write_gate_up, clamp);
                    } else if (gate_row_span == 1024u) {
                        dim3 tgrid((expert_mid_dim + 1023u) / 1024u, tile_capacity, 1);
                        moe_gate_up_mid_expert_tile8_rowspan_kernel<1024><<<tgrid, 256>>>(
                            (float *)gate->ptr, (float *)up->ptr, (float *)mid->ptr,
                            gate_w, up_w, xq, sorted_pairs, sorted_offsets, sorted_counts,
                            tile_total, tile_experts, tile_starts, (const float *)weights->ptr,
                            gate_expert_bytes, gate_row_bytes, xq_blocks, expert_mid_dim, n_expert,
                            write_gate_up, clamp);
                    } else {
                        dim3 tgrid((expert_mid_dim + 2047u) / 2048u, tile_capacity, 1);
                        moe_gate_up_mid_expert_tile8_row2048_kernel<<<tgrid, 256>>>(
                            (float *)gate->ptr, (float *)up->ptr, (float *)mid->ptr,
                            gate_w, up_w, xq, sorted_pairs, sorted_offsets, sorted_counts,
                            tile_total, tile_experts, tile_starts, (const float *)weights->ptr,
                            gate_expert_bytes, gate_row_bytes, xq_blocks, expert_mid_dim, n_expert,
                            write_gate_up, clamp);
                    }
                } else if (expert_tile_m == 8u) {
                    dim3 tgrid((expert_mid_dim + 31u) / 32u, tile_capacity, 1);
                    moe_gate_up_mid_expert_tile8_row32_kernel<<<tgrid, 256>>>(
                        (float *)gate->ptr, (float *)up->ptr, (float *)mid->ptr,
                        gate_w, up_w, xq, sorted_pairs, sorted_offsets, sorted_counts,
                        tile_total, tile_experts, tile_starts, (const float *)weights->ptr,
                        gate_expert_bytes, gate_row_bytes, xq_blocks, expert_mid_dim, n_expert,
                        write_gate_up, clamp);
                } else {
                    dim3 tgrid((expert_mid_dim + 31u) / 32u, tile_capacity, 1);
                    moe_gate_up_mid_expert_tile4_row32_kernel<<<tgrid, 256>>>(
                        (float *)gate->ptr, (float *)up->ptr, (float *)mid->ptr,
                        gate_w, up_w, xq, sorted_pairs, sorted_offsets, sorted_counts,
                        tile_total, tile_experts, tile_starts, (const float *)weights->ptr,
                        gate_expert_bytes, gate_row_bytes, xq_blocks, expert_mid_dim, n_expert,
                        write_gate_up, clamp);
                }
            } else if (ok && sorted_pairs && use_p2_sorted) {
                dim3 p2_mgrid((expert_mid_dim + 15u) / 16u, (pair_count + 1u) / 2u, 1);
                moe_gate_up_mid_sorted_p2_qwarp32_kernel<<<p2_mgrid, 256>>>(
                    (float *)gate->ptr,
                    (float *)up->ptr,
                    (float *)mid->ptr,
                    gate_w,
                    up_w,
                    xq,
                    sorted_pairs,
                    (const int32_t *)selected->ptr,
                    (const float *)weights->ptr,
                    gate_expert_bytes,
                    gate_row_bytes,
                    xq_blocks,
                    expert_mid_dim,
                    n_expert,
                    pair_count,
                    clamp);
            } else if (ok && sorted_pairs) {
                moe_gate_up_mid_sorted_qwarp32_kernel<<<mgrid, 256>>>(
                    (float *)gate->ptr,
                    (float *)up->ptr,
                    (float *)mid->ptr,
                    gate_w,
                    up_w,
                    xq,
                    sorted_pairs,
                    (const int32_t *)selected->ptr,
                    (const float *)weights->ptr,
                    gate_expert_bytes,
                    gate_row_bytes,
                    xq_blocks,
                    expert_mid_dim,
                    n_expert,
                    clamp);
            } else if (ok) {
                dim3 qgrid((expert_mid_dim + 127u) / 128u, n_tokens * n_expert, 1);
                if (use_decode_lut_gate && q4k_path) {
                    moe_gate_up_mid_decode_q4K_qwarp32_kernel<<<qgrid, 256>>>(
                        (float *)gate->ptr,
                        (float *)up->ptr,
                        (float *)mid->ptr,
                        gate_w,
                        up_w,
                        xq,
                        (const int32_t *)selected->ptr,
                        (const float *)weights->ptr,
                        gate_expert_bytes,
                        gate_row_bytes,
                        xq_blocks,
                        expert_mid_dim,
                        n_expert,
                        write_gate_up,
                        clamp);
                } else if (use_decode_lut_gate) {
                    moe_gate_up_mid_decode_lut_qwarp32_kernel<<<qgrid, 256>>>(
                        (float *)gate->ptr,
                        (float *)up->ptr,
                        (float *)mid->ptr,
                        gate_w,
                        up_w,
                        xq,
                        (const int32_t *)selected->ptr,
                        (const float *)weights->ptr,
                        gate_expert_bytes,
                        gate_row_bytes,
                        xq_blocks,
                        expert_mid_dim,
                        n_expert,
                        write_gate_up,
                        clamp);
                } else {
                    moe_gate_up_mid_qwarp32_kernel<<<qgrid, 256>>>(
                        (float *)gate->ptr,
                        (float *)up->ptr,
                        (float *)mid->ptr,
                        gate_w,
                        up_w,
                        xq,
                        (const int32_t *)selected->ptr,
                        (const float *)weights->ptr,
                        gate_expert_bytes,
                        gate_row_bytes,
                        xq_blocks,
                        expert_mid_dim,
                        n_expert,
                        clamp);
                }
            }
            ok = cuda_ok(cudaGetLastError(), "routed_moe gate/up launch");
        }
        if (prof_ev[3]) (void)cudaEventRecord(prof_ev[3], 0);
        if (ok) {
            dim3 midq_grid(midq_blocks, n_tokens * n_expert, 1);
            q8_K_quantize_kernel<<<midq_grid, 256>>>(midq, (const float *)mid->ptr, expert_mid_dim, n_tokens * n_expert);
            ok = cuda_ok(cudaGetLastError(), "routed_moe mid quantize launch");
        }
        if (prof_ev[4]) (void)cudaEventRecord(prof_ev[4], 0);
        if (ok) {
            dim3 dgrid((out_dim + 31u) / 32u, n_tokens * n_expert, 1);
            uint32_t *down_tile_total = tile_total;
            uint32_t *down_tile_experts = tile_experts;
            uint32_t *down_tile_starts = tile_starts;
            uint32_t down_tile_capacity = tile_capacity;
            if (use_down_tile16 && tile16_total && tile16_experts && tile16_starts) {
                down_tile_total = tile16_total;
                down_tile_experts = tile16_experts;
                down_tile_starts = tile16_starts;
                down_tile_capacity = tile16_capacity;
            }
            if (use_direct_down_sum6) {
                dim3 sgrid((out_dim + 31u) / 32u, 1, 1);
                if (q4k_path) {
                    moe_down_q4K_sum6_qwarp32_kernel<<<sgrid, 256>>>(
                        (float *)out->ptr,
                        down_w,
                        midq,
                        (const int32_t *)selected->ptr,
                        down_expert_bytes,
                        down_row_bytes,
                        midq_blocks,
                        out_dim);
                } else {
                    moe_down_sum6_qwarp32_kernel<<<sgrid, 256>>>(
                        (float *)out->ptr,
                        down_w,
                        midq,
                        (const int32_t *)selected->ptr,
                        down_expert_bytes,
                        down_row_bytes,
                        midq_blocks,
                        out_dim);
                }
            } else if (use_atomic_down) {
                uint64_t n = (uint64_t)n_tokens * out_dim;
                zero_kernel<<<(n + 255u) / 256u, 256>>>((float *)out->ptr, n);
                ok = cuda_ok(cudaGetLastError(), "routed_moe atomic zero launch");
            }
            if (use_direct_down_sum6) {
                /* The direct decode kernel writes the final token row. */
            } else if (sorted_pairs && use_expert_tiles && sorted_offsets && sorted_counts &&
                down_tile_total && down_tile_experts && down_tile_starts) {
                if (use_down_row2048) {
                    if (down_row_span == 512u) {
                        dim3 tgrid((out_dim + 511u) / 512u, down_tile_capacity, 1);
                        moe_down_expert_tile16_rowspan_kernel<512><<<tgrid, 256>>>(
                            use_atomic_down ? (float *)out->ptr : (float *)down->ptr,
                            down_w, midq, sorted_pairs, sorted_offsets, sorted_counts,
                            down_tile_total, down_tile_experts, down_tile_starts, down_expert_bytes, down_row_bytes,
                            midq_blocks, out_dim, n_expert, use_atomic_down);
                    } else if (down_row_span == 1024u) {
                        dim3 tgrid((out_dim + 1023u) / 1024u, down_tile_capacity, 1);
                        moe_down_expert_tile16_rowspan_kernel<1024><<<tgrid, 256>>>(
                            use_atomic_down ? (float *)out->ptr : (float *)down->ptr,
                            down_w, midq, sorted_pairs, sorted_offsets, sorted_counts,
                            down_tile_total, down_tile_experts, down_tile_starts, down_expert_bytes, down_row_bytes,
                            midq_blocks, out_dim, n_expert, use_atomic_down);
                    } else {
                        dim3 tgrid((out_dim + 2047u) / 2048u, down_tile_capacity, 1);
                        moe_down_expert_tile16_row2048_kernel<<<tgrid, 256>>>(
                            use_atomic_down ? (float *)out->ptr : (float *)down->ptr,
                            down_w, midq, sorted_pairs, sorted_offsets, sorted_counts,
                            down_tile_total, down_tile_experts, down_tile_starts, down_expert_bytes, down_row_bytes,
                            midq_blocks, out_dim, n_expert, use_atomic_down);
                    }
                } else if (use_down_tile16) {
                    dim3 tgrid((out_dim + 31u) / 32u, down_tile_capacity, 1);
                    moe_down_expert_tile16_row32_kernel<<<tgrid, 256>>>(
                        use_atomic_down ? (float *)out->ptr : (float *)down->ptr,
                        down_w, midq, sorted_pairs, sorted_offsets, sorted_counts,
                        down_tile_total, down_tile_experts, down_tile_starts, down_expert_bytes, down_row_bytes,
                        midq_blocks, out_dim, n_expert, use_atomic_down);
                } else if (expert_tile_m == 8u) {
                    dim3 tgrid((out_dim + 31u) / 32u, down_tile_capacity, 1);
                    moe_down_expert_tile8_row32_kernel<<<tgrid, 256>>>(
                        use_atomic_down ? (float *)out->ptr : (float *)down->ptr,
                        down_w, midq, sorted_pairs, sorted_offsets, sorted_counts,
                        down_tile_total, down_tile_experts, down_tile_starts, down_expert_bytes, down_row_bytes,
                        midq_blocks, out_dim, n_expert, use_atomic_down);
                } else {
                    dim3 tgrid((out_dim + 31u) / 32u, down_tile_capacity, 1);
                    moe_down_expert_tile4_row32_kernel<<<tgrid, 256>>>(
                        use_atomic_down ? (float *)out->ptr : (float *)down->ptr,
                        down_w, midq, sorted_pairs, sorted_offsets, sorted_counts,
                        down_tile_total, down_tile_experts, down_tile_starts, down_expert_bytes, down_row_bytes,
                        midq_blocks, out_dim, n_expert, use_atomic_down);
                }
            } else if (sorted_pairs && use_p2_sorted) {
                dim3 p2_dgrid((out_dim + 15u) / 16u, (pair_count + 1u) / 2u, 1);
                moe_down_sorted_p2_qwarp32_kernel<<<p2_dgrid, 256>>>(
                    (float *)down->ptr,
                    down_w,
                    midq,
                    sorted_pairs,
                    (const int32_t *)selected->ptr,
                    down_expert_bytes,
                    down_row_bytes,
                    midq_blocks,
                    out_dim,
                    n_expert,
                    pair_count);
            } else if (sorted_pairs) {
                moe_down_sorted_qwarp32_kernel<<<dgrid, 256>>>(
                    (float *)down->ptr,
                    down_w,
                    midq,
                    sorted_pairs,
                    (const int32_t *)selected->ptr,
                    down_expert_bytes,
                    down_row_bytes,
                    midq_blocks,
                    out_dim,
                    n_expert);
            } else {
                moe_down_qwarp32_kernel<<<dgrid, 256>>>(
                    (float *)down->ptr,
                    down_w,
                    midq,
                    (const int32_t *)selected->ptr,
                    down_expert_bytes,
                    down_row_bytes,
                    midq_blocks,
                    out_dim,
                    n_expert);
            }
            ok = cuda_ok(cudaGetLastError(), "routed_moe down launch");
        }
        if (prof_ev[5]) (void)cudaEventRecord(prof_ev[5], 0);
        if (ok && !use_atomic_down && !use_direct_down_sum6) {
            uint64_t n = (uint64_t)n_tokens * out_dim;
            moe_sum_kernel<<<(n + 255) / 256, 256>>>((float *)out->ptr, (const float *)down->ptr, out_dim, n_expert, n_tokens);
            ok = cuda_ok(cudaGetLastError(), "routed_moe sum launch");
        }
        if (prof_ev[6]) {
            (void)cudaEventRecord(prof_ev[6], 0);
            if (cudaEventSynchronize(prof_ev[6]) == cudaSuccess) {
                float ms_xq = 0.0f, ms_sort = 0.0f, ms_gate = 0.0f, ms_midq = 0.0f, ms_down = 0.0f, ms_sum = 0.0f, ms_total = 0.0f;
                (void)cudaEventElapsedTime(&ms_xq, prof_ev[0], prof_ev[1]);
                (void)cudaEventElapsedTime(&ms_sort, prof_ev[1], prof_ev[2]);
                (void)cudaEventElapsedTime(&ms_gate, prof_ev[2], prof_ev[3]);
                (void)cudaEventElapsedTime(&ms_midq, prof_ev[3], prof_ev[4]);
                (void)cudaEventElapsedTime(&ms_down, prof_ev[4], prof_ev[5]);
                (void)cudaEventElapsedTime(&ms_sum, prof_ev[5], prof_ev[6]);
                (void)cudaEventElapsedTime(&ms_total, prof_ev[0], prof_ev[6]);
                fprintf(stderr,
                        "ds4: CUDA MoE profile tokens=%u pairs=%u xq=%.3f sort=%.3f gateup=%.3f midq=%.3f down=%.3f sum=%.3f total=%.3f ms\n",
                        n_tokens, pair_count, ms_xq, ms_sort, ms_gate, ms_midq, ms_down, ms_sum, ms_total);
            }
            for (uint32_t i = 0; i < 7u; i++) (void)cudaEventDestroy(prof_ev[i]);
        }
        return ok;
    }

    if (ok) {
        dim3 mgrid(expert_mid_dim, n_tokens * n_expert, 1);
        moe_gate_up_mid_f32_kernel<<<mgrid, 256>>>(
            (float *)gate->ptr,
            (float *)up->ptr,
            (float *)mid->ptr,
            gate_w,
            up_w,
            (const float *)x->ptr,
            (const int32_t *)selected->ptr,
            (const float *)weights->ptr,
            gate_expert_bytes,
            gate_row_bytes,
            expert_in_dim,
            expert_mid_dim,
            n_expert,
            clamp);
        ok = cuda_ok(cudaGetLastError(), "routed_moe gate/up launch");
    }
    if (ok) {
        dim3 dgrid(out_dim, n_tokens * n_expert, 1);
        moe_down_f32_kernel<<<dgrid, 256>>>(
            (float *)down->ptr,
            down_w,
            (const float *)mid->ptr,
            (const int32_t *)selected->ptr,
            down_expert_bytes,
            down_row_bytes,
            expert_mid_dim,
            out_dim,
            n_expert);
        ok = cuda_ok(cudaGetLastError(), "routed_moe down launch");
    }
    if (ok) {
        uint64_t n = (uint64_t)n_tokens * out_dim;
        moe_sum_kernel<<<(n + 255) / 256, 256>>>((float *)out->ptr, (const float *)down->ptr, out_dim, n_expert, n_tokens);
        ok = cuda_ok(cudaGetLastError(), "routed_moe sum launch");
    }
    return ok;
}

extern "C" int ds4_gpu_routed_moe_one_tensor(ds4_gpu_tensor *out, ds4_gpu_tensor *gate, ds4_gpu_tensor *up, ds4_gpu_tensor *mid, ds4_gpu_tensor *down, const void *model_map, uint64_t model_size, uint64_t gate_offset, uint64_t up_offset, uint64_t down_offset, uint32_t gate_type, uint32_t down_type, uint64_t gate_expert_bytes, uint64_t gate_row_bytes, uint64_t down_expert_bytes, uint64_t down_row_bytes, uint32_t expert_in_dim, uint32_t expert_mid_dim, uint32_t out_dim, const ds4_gpu_tensor *selected, const ds4_gpu_tensor *weights, uint32_t n_expert, float clamp, const ds4_gpu_tensor *x) {
    return routed_moe_launch(out, gate, up, mid, down, model_map, model_size,
                             gate_offset, up_offset, down_offset,
                             gate_type, down_type,
                             gate_expert_bytes, gate_row_bytes,
                             down_expert_bytes, down_row_bytes,
                             expert_in_dim, expert_mid_dim, out_dim,
                             selected, weights, n_expert, clamp, x, 1);
}
extern "C" int ds4_gpu_routed_moe_batch_tensor(ds4_gpu_tensor *out, ds4_gpu_tensor *gate, ds4_gpu_tensor *up, ds4_gpu_tensor *mid, ds4_gpu_tensor *down, const void *model_map, uint64_t model_size, uint64_t gate_offset, uint64_t up_offset, uint64_t down_offset, uint32_t gate_type, uint32_t down_type, uint64_t gate_expert_bytes, uint64_t gate_row_bytes, uint64_t down_expert_bytes, uint64_t down_row_bytes, uint32_t expert_in_dim, uint32_t expert_mid_dim, uint32_t out_dim, const ds4_gpu_tensor *selected, const ds4_gpu_tensor *weights, uint32_t n_expert, float clamp, const ds4_gpu_tensor *x, uint32_t n_tokens) {
    return routed_moe_launch(out, gate, up, mid, down, model_map, model_size,
                             gate_offset, up_offset, down_offset,
                             gate_type, down_type,
                             gate_expert_bytes, gate_row_bytes,
                             down_expert_bytes, down_row_bytes,
                             expert_in_dim, expert_mid_dim, out_dim,
                             selected, weights, n_expert, clamp, x, n_tokens);
}

static int q4_k_expert_row_bytes(uint32_t cols, uint64_t *out) {
    if (cols == 0 || cols % CUDA_QK_K) return 1;
    const uint64_t blocks = (uint64_t)cols / CUDA_QK_K;
    if (blocks > UINT64_MAX / sizeof(cuda_block_q4_K)) return 1;
    *out = blocks * sizeof(cuda_block_q4_K);
    return 0;
}

static int cuda_q4_k_expert_view_ok(
        const ds4_gpu_arena *arena,
        const ds4_gpu_q4_k_expert_view *view,
        uint32_t cols,
        uint32_t rows,
        uint64_t *row_bytes_out,
        uint64_t *expert_bytes_out) {
    if (!arena || !view || !arena->valid || !arena->ptr ||
        view->experts == 0 || view->rows != rows || view->cols != cols ||
        view->row_stride_bytes == 0 || view->expert_stride_bytes == 0) {
        return 0;
    }
    if (!cuda_arena_range_ok(arena, view->arena_offset, view->byte_length)) return 0;

    uint64_t row_bytes = 0;
    if (q4_k_expert_row_bytes(cols, &row_bytes)) return 0;
    if ((uint64_t)view->row_stride_bytes < row_bytes) return 0;

    uint64_t last_row_start = 0;
    if (checked_mul_u64((uint64_t)rows - 1u,
                        (uint64_t)view->row_stride_bytes,
                        &last_row_start)) {
        return 0;
    }
    if (last_row_start > view->expert_stride_bytes ||
        row_bytes > view->expert_stride_bytes - last_row_start) {
        return 0;
    }

    uint64_t min_expert_bytes = 0;
    if (checked_mul_u64((uint64_t)rows,
                        (uint64_t)view->row_stride_bytes,
                        &min_expert_bytes)) {
        return 0;
    }
    if (view->expert_stride_bytes < min_expert_bytes) return 0;

    uint64_t required_bytes = 0;
    if (checked_mul_u64((uint64_t)view->experts,
                        view->expert_stride_bytes,
                        &required_bytes)) {
        return 0;
    }
    if (required_bytes > view->byte_length) return 0;

    if (row_bytes_out) *row_bytes_out = row_bytes;
    if (expert_bytes_out) *expert_bytes_out = view->expert_stride_bytes;
    return 1;
}

extern "C" int ds4_gpu_arena_q4_k_routed_moe_one_f32(
        const ds4_gpu_arena             *arena,
        const ds4_gpu_q4_k_expert_view  *gate,
        const ds4_gpu_q4_k_expert_view  *up,
        const ds4_gpu_q4_k_expert_view  *down_w,
        ds4_gpu_tensor                  *out_f32,
        ds4_gpu_tensor                  *gate_tmp_f32,
        ds4_gpu_tensor                  *up_tmp_f32,
        ds4_gpu_tensor                  *mid_tmp_f32,
        ds4_gpu_tensor                  *down_tmp_f32,
        const ds4_gpu_tensor            *selected_i32,
        const ds4_gpu_tensor            *weights_f32,
        const ds4_gpu_tensor            *x_f32,
        uint32_t                         n_expert,
        float                            clamp) {
    uint64_t gate_row_bytes = 0;
    uint64_t gate_expert_bytes = 0;
    uint64_t up_row_bytes = 0;
    uint64_t up_expert_bytes = 0;
    uint64_t down_row_bytes = 0;
    uint64_t down_expert_bytes = 0;
    if (!arena || !gate || !up || !down_w || !out_f32 || !gate_tmp_f32 ||
        !up_tmp_f32 || !mid_tmp_f32 || !down_tmp_f32 || !selected_i32 ||
        !weights_f32 || !x_f32 || n_expert != 6u || gate->experts != 256u ||
        up->experts != 256u || down_w->experts != 256u) {
        return 1;
    }
    if (!cuda_q4_k_expert_view_ok(arena,
                                  gate,
                                  gate->cols,
                                  gate->rows,
                                  &gate_row_bytes,
                                  &gate_expert_bytes) ||
        !cuda_q4_k_expert_view_ok(arena,
                                  up,
                                  gate->cols,
                                  gate->rows,
                                  &up_row_bytes,
                                  &up_expert_bytes) ||
        !cuda_q4_k_expert_view_ok(arena,
                                  down_w,
                                  gate->rows,
                                  down_w->rows,
                                  &down_row_bytes,
                                  &down_expert_bytes)) {
        return 1;
    }
    if (gate->cols == 0 || gate->rows == 0 || down_w->rows == 0 ||
        gate->cols % CUDA_QK_K != 0 || gate->rows % CUDA_QK_K != 0 ||
        up_row_bytes != gate_row_bytes || up_expert_bytes != gate_expert_bytes ||
        down_w->cols != gate->rows) {
        return 1;
    }

    ds4_gpu_tensor *tensors[] = {
        out_f32, gate_tmp_f32, up_tmp_f32, mid_tmp_f32, down_tmp_f32,
        (ds4_gpu_tensor *)selected_i32, (ds4_gpu_tensor *)weights_f32,
        (ds4_gpu_tensor *)x_f32,
    };
    for (uint32_t i = 0; i < sizeof(tensors) / sizeof(tensors[0]); i++) {
        if (!tensors[i] || !tensors[i]->ptr || tensors[i]->device != arena->gpu) return 1;
    }

    const uint32_t expert_in_dim = gate->cols;
    const uint32_t expert_mid_dim = gate->rows;
    const uint32_t out_dim = down_w->rows;
    const uint32_t xq_blocks = expert_in_dim / CUDA_QK_K;
    const uint32_t midq_blocks = expert_mid_dim / CUDA_QK_K;
    const uint64_t gate_values = (uint64_t)n_expert * expert_mid_dim;
    const uint64_t down_values = (uint64_t)n_expert * out_dim;
    const uint64_t xq_bytes = (uint64_t)xq_blocks * sizeof(cuda_block_q8_K);
    const uint64_t midq_bytes = (uint64_t)n_expert * midq_blocks * sizeof(cuda_block_q8_K);
    if (x_f32->bytes < (uint64_t)expert_in_dim * sizeof(float) ||
        selected_i32->bytes < (uint64_t)n_expert * sizeof(int32_t) ||
        weights_f32->bytes < (uint64_t)n_expert * sizeof(float) ||
        out_f32->bytes < (uint64_t)out_dim * sizeof(float) ||
        gate_tmp_f32->bytes < gate_values * sizeof(float) ||
        up_tmp_f32->bytes < gate_values * sizeof(float) ||
        mid_tmp_f32->bytes < gate_values * sizeof(float) ||
        down_tmp_f32->bytes < down_values * sizeof(float) ||
        down_tmp_f32->bytes < xq_bytes ||
        gate_tmp_f32->bytes < midq_bytes) {
        return 1;
    }
    if (!cuda_ok(cudaSetDevice(arena->gpu), "q4_k arena routed moe set device")) return 1;

    const char *gate_w = (const char *)arena->ptr + gate->arena_offset;
    const char *up_w = (const char *)arena->ptr + up->arena_offset;
    const char *down_ptr = (const char *)arena->ptr + down_w->arena_offset;
    cuda_block_q8_K *xq = (cuda_block_q8_K *)down_tmp_f32->ptr;
    cuda_block_q8_K *midq = (cuda_block_q8_K *)gate_tmp_f32->ptr;

    dim3 xq_grid(xq_blocks, 1, 1);
    q8_K_quantize_kernel<<<xq_grid, 256>>>(
            xq,
            (const float *)x_f32->ptr,
            expert_in_dim,
            1);
    if (!cuda_ok(cudaGetLastError(), "q4_k arena x quantize launch")) return 1;

    dim3 qgrid((expert_mid_dim + 127u) / 128u, n_expert, 1);
    moe_gate_up_mid_decode_q4K_qwarp32_kernel<<<qgrid, 256>>>(
            (float *)gate_tmp_f32->ptr,
            (float *)up_tmp_f32->ptr,
            (float *)mid_tmp_f32->ptr,
            gate_w,
            up_w,
            xq,
            (const int32_t *)selected_i32->ptr,
            (const float *)weights_f32->ptr,
            gate_expert_bytes,
            gate_row_bytes,
            xq_blocks,
            expert_mid_dim,
            n_expert,
            0,
            clamp);
    if (!cuda_ok(cudaGetLastError(), "q4_k arena gate/up launch")) return 1;

    dim3 midq_grid(midq_blocks, n_expert, 1);
    q8_K_quantize_kernel<<<midq_grid, 256>>>(
            midq,
            (const float *)mid_tmp_f32->ptr,
            expert_mid_dim,
            n_expert);
    if (!cuda_ok(cudaGetLastError(), "q4_k arena mid quantize launch")) return 1;

    dim3 sgrid((out_dim + 31u) / 32u, 1, 1);
    moe_down_q4K_sum6_qwarp32_kernel<<<sgrid, 256>>>(
            (float *)out_f32->ptr,
            down_ptr,
            midq,
            (const int32_t *)selected_i32->ptr,
            down_expert_bytes,
            down_row_bytes,
            midq_blocks,
            out_dim);
    return cuda_ok(cudaGetLastError(), "q4_k arena down launch") ? 0 : 1;
}
extern "C" int ds4_gpu_hc_split_sinkhorn_tensor(ds4_gpu_tensor *out, const ds4_gpu_tensor *mix, const void *model_map, uint64_t model_size, uint64_t scale_offset, uint64_t base_offset, uint32_t n_hc, uint32_t sinkhorn_iters, float eps) {
    if (!out || !mix || !model_map || n_hc != 4) return 0;
    const uint64_t mix_bytes = 24ull * sizeof(float);
    if (scale_offset > model_size || model_size - scale_offset < 3ull * sizeof(float) ||
        base_offset > model_size || model_size - base_offset < mix_bytes ||
        mix->bytes < mix_bytes || out->bytes < mix_bytes) return 0;
    const float *scale = (const float *)cuda_model_range_ptr(model_map, scale_offset, 3ull * sizeof(float), "hc_scale");
    const float *base = (const float *)cuda_model_range_ptr(model_map, base_offset, mix_bytes, "hc_base");
    if (!scale || !base) return 0;
    uint32_t n_rows = (uint32_t)(mix->bytes / mix_bytes);
    if (out->bytes / mix_bytes < n_rows) n_rows = (uint32_t)(out->bytes / mix_bytes);
    hc_split_sinkhorn_kernel<<<(n_rows + 255) / 256, 256>>>(
        (float *)out->ptr, (const float *)mix->ptr,
        scale,
        base,
        n_rows, sinkhorn_iters, eps);
    return cuda_ok(cudaGetLastError(), "hc_split_sinkhorn launch");
}
extern "C" int ds4_gpu_hc_weighted_sum_tensor(ds4_gpu_tensor *out, const ds4_gpu_tensor *residual_hc, const ds4_gpu_tensor *weights, uint32_t n_embd, uint32_t n_hc) {
    if (!out || !residual_hc || !weights || n_embd == 0 || n_hc == 0) return 0;
    uint32_t n_tokens = (uint32_t)(out->bytes / ((uint64_t)n_embd * sizeof(float)));
    hc_weighted_sum_kernel<<<((uint64_t)n_embd * n_tokens + 255) / 256, 256>>>(
        (float *)out->ptr, (const float *)residual_hc->ptr, (const float *)weights->ptr,
        n_embd, n_hc, n_tokens, n_hc);
    return cuda_ok(cudaGetLastError(), "hc_weighted_sum launch");
}
extern "C" int ds4_gpu_hc_weighted_sum_split_tensor(ds4_gpu_tensor *out, const ds4_gpu_tensor *residual_hc, const ds4_gpu_tensor *split, uint32_t n_embd, uint32_t n_hc) {
    if (!out || !residual_hc || !split || n_embd == 0 || n_hc == 0) return 0;
    uint32_t n_tokens = (uint32_t)(out->bytes / ((uint64_t)n_embd * sizeof(float)));
    uint32_t stride = (uint32_t)(2u * n_hc + n_hc * n_hc);
    hc_weighted_sum_kernel<<<((uint64_t)n_embd * n_tokens + 255) / 256, 256>>>(
        (float *)out->ptr, (const float *)residual_hc->ptr, (const float *)split->ptr,
        n_embd, n_hc, n_tokens, stride);
    return cuda_ok(cudaGetLastError(), "hc_weighted_sum_split launch");
}
extern "C" int ds4_gpu_hc_split_weighted_sum_tensor(
        ds4_gpu_tensor       *out,
        ds4_gpu_tensor       *split,
        const ds4_gpu_tensor *mix,
        const ds4_gpu_tensor *residual_hc,
        const void             *model_map,
        uint64_t                model_size,
        uint64_t                scale_offset,
        uint64_t                base_offset,
        uint32_t                n_embd,
        uint32_t                n_hc,
        uint32_t                sinkhorn_iters,
        float                   eps) {
    if (!out || !split || !mix || !residual_hc || !model_map ||
        n_embd == 0 || n_hc != 4) {
        return 0;
    }
    const uint64_t mix_hc = 2ull * n_hc + (uint64_t)n_hc * n_hc;
    const uint64_t mix_bytes = mix_hc * sizeof(float);
    const uint64_t out_row_bytes = (uint64_t)n_embd * sizeof(float);
    const uint64_t residual_row_bytes = (uint64_t)n_hc * n_embd * sizeof(float);
    if (out->bytes < out_row_bytes || out->bytes % out_row_bytes != 0 ||
        scale_offset > model_size || 3ull * sizeof(float) > model_size - scale_offset ||
        base_offset > model_size || mix_bytes > model_size - base_offset) {
        return 0;
    }
    uint64_t n_rows = out->bytes / out_row_bytes;
    if (mix->bytes < n_rows * mix_bytes ||
        split->bytes < n_rows * mix_bytes ||
        residual_hc->bytes < n_rows * residual_row_bytes) {
        return 0;
    }
    const float *scale = (const float *)cuda_model_range_ptr(model_map, scale_offset, 3ull * sizeof(float), "hc_scale");
    const float *base = (const float *)cuda_model_range_ptr(model_map, base_offset, mix_bytes, "hc_base");
    if (!scale || !base) return 0;
    hc_split_weighted_sum_fused_kernel<<<(uint32_t)n_rows, 256>>>(
            (float *)out->ptr,
            (float *)split->ptr,
            (const float *)mix->ptr,
            (const float *)residual_hc->ptr,
            scale,
            base,
            n_embd, n_hc, (uint32_t)n_rows, sinkhorn_iters, eps);
    return cuda_ok(cudaGetLastError(), "hc split weighted sum launch");
}

extern "C" int ds4_gpu_arena_hc_split_weighted_sum_tensor(
        const ds4_gpu_arena           *arena,
        const ds4_gpu_source_row_view *scale,
        const ds4_gpu_source_row_view *base,
        ds4_gpu_tensor                *out,
        ds4_gpu_tensor                *split,
        const ds4_gpu_tensor          *mix,
        const ds4_gpu_tensor          *residual_hc,
        uint32_t                       n_embd,
        uint32_t                       n_hc,
        uint32_t                       sinkhorn_iters,
        float                          eps) {
    if (!arena || !scale || !base || !out || !split || !mix || !residual_hc ||
        !arena->valid || !arena->ptr || !out->ptr || !split->ptr ||
        !mix->ptr || !residual_hc->ptr || n_embd == 0 || n_hc != 4) {
        return 1;
    }
    const uint64_t mix_hc = 2ull * n_hc + (uint64_t)n_hc * n_hc;
    const uint64_t mix_bytes = mix_hc * sizeof(float);
    const uint64_t out_row_bytes = (uint64_t)n_embd * sizeof(float);
    const uint64_t residual_row_bytes = (uint64_t)n_hc * n_embd * sizeof(float);
    if (scale->rows != 1u || scale->cols != 3u ||
        base->rows != 1u || base->cols != mix_hc ||
        (scale->arena_offset & 3ull) != 0 ||
        (base->arena_offset & 3ull) != 0 ||
        scale->byte_length < 3ull * sizeof(float) ||
        base->byte_length < mix_bytes ||
        !cuda_arena_range_ok(arena, scale->arena_offset, scale->byte_length) ||
        !cuda_arena_range_ok(arena, base->arena_offset, base->byte_length) ||
        out->bytes < out_row_bytes ||
        out->bytes % out_row_bytes != 0) {
        return 1;
    }
    const uint64_t n_rows = out->bytes / out_row_bytes;
    if (mix->bytes < n_rows * mix_bytes ||
        split->bytes < n_rows * mix_bytes ||
        residual_hc->bytes < n_rows * residual_row_bytes ||
        out->device != arena->gpu ||
        split->device != arena->gpu ||
        mix->device != arena->gpu ||
        residual_hc->device != arena->gpu ||
        n_rows > UINT32_MAX) {
        return 1;
    }
    if (!cuda_ok(cudaSetDevice(arena->gpu), "arena hc split weighted sum set device")) return 1;

    const float *scale_ptr =
        (const float *)((const char *)arena->ptr + scale->arena_offset);
    const float *base_ptr =
        (const float *)((const char *)arena->ptr + base->arena_offset);
    hc_split_weighted_sum_fused_kernel<<<(uint32_t)n_rows, 256>>>(
            (float *)out->ptr,
            (float *)split->ptr,
            (const float *)mix->ptr,
            (const float *)residual_hc->ptr,
            scale_ptr,
            base_ptr,
            n_embd, n_hc, (uint32_t)n_rows, sinkhorn_iters, eps);
    return cuda_ok(cudaGetLastError(), "arena hc split weighted sum launch") ? 0 : 1;
}

extern "C" int ds4_gpu_hc_split_weighted_sum_norm_tensor(
        ds4_gpu_tensor       *out,
        ds4_gpu_tensor       *norm_out,
        ds4_gpu_tensor       *split,
        const ds4_gpu_tensor *mix,
        const ds4_gpu_tensor *residual_hc,
        const void             *model_map,
        uint64_t                model_size,
        uint64_t                scale_offset,
        uint64_t                base_offset,
        uint64_t                norm_weight_offset,
        uint32_t                n_embd,
        uint32_t                n_hc,
        uint32_t                sinkhorn_iters,
        float                   eps,
        float                   norm_eps) {
    if (getenv("DS4_CUDA_DISABLE_HC_SPLIT_NORM_FUSED") == NULL) {
        if (!out || !norm_out || !split || !mix || !residual_hc || !model_map ||
            n_embd == 0 || n_hc != 4) {
            return 0;
        }
        const uint64_t mix_hc = 2ull * n_hc + (uint64_t)n_hc * n_hc;
        const uint64_t mix_bytes = mix_hc * sizeof(float);
        const uint64_t out_row_bytes = (uint64_t)n_embd * sizeof(float);
        const uint64_t residual_row_bytes = (uint64_t)n_hc * n_embd * sizeof(float);
        if (out->bytes < out_row_bytes || out->bytes % out_row_bytes != 0 ||
            norm_out->bytes < out->bytes ||
            scale_offset > model_size || 3ull * sizeof(float) > model_size - scale_offset ||
            base_offset > model_size || mix_bytes > model_size - base_offset ||
            norm_weight_offset > model_size ||
            (uint64_t)n_embd * sizeof(float) > model_size - norm_weight_offset) {
            return 0;
        }
        uint64_t n_rows = out->bytes / out_row_bytes;
        if (n_rows == 1) {
            if (mix->bytes < n_rows * mix_bytes ||
                split->bytes < n_rows * mix_bytes ||
                residual_hc->bytes < n_rows * residual_row_bytes) {
                return 0;
            }
            const float *scale = (const float *)cuda_model_range_ptr(model_map, scale_offset,
                    3ull * sizeof(float), "hc_scale");
            const float *base = (const float *)cuda_model_range_ptr(model_map, base_offset,
                    mix_bytes, "hc_base");
            const float *norm_w = (const float *)cuda_model_range_ptr(model_map, norm_weight_offset,
                    (uint64_t)n_embd * sizeof(float), "hc_norm_weight");
            if (!scale || !base || !norm_w) return 0;
            hc_split_weighted_sum_norm_fused_kernel<<<(uint32_t)n_rows, 256>>>(
                    (float *)out->ptr,
                    (float *)norm_out->ptr,
                    (float *)split->ptr,
                    (const float *)mix->ptr,
                    (const float *)residual_hc->ptr,
                    scale,
                    base,
                    norm_w,
                    n_embd, n_hc, (uint32_t)n_rows, sinkhorn_iters, eps, norm_eps);
            return cuda_ok(cudaGetLastError(), "hc split weighted sum norm launch");
        }
    }
    return ds4_gpu_hc_split_weighted_sum_tensor(out, split, mix, residual_hc,
                                                  model_map, model_size,
                                                  scale_offset, base_offset,
                                                  n_embd, n_hc,
                                                  sinkhorn_iters, eps) &&
           ds4_gpu_rms_norm_weight_tensor(norm_out, out, model_map, model_size,
                                            norm_weight_offset, n_embd, norm_eps);
}
extern "C" int ds4_gpu_output_hc_weights_tensor(
        ds4_gpu_tensor       *out,
        const ds4_gpu_tensor *pre,
        const void             *model_map,
        uint64_t                model_size,
        uint64_t                scale_offset,
        uint64_t                base_offset,
        uint32_t                n_hc,
        float                   eps) {
    if (!out || !pre || !model_map || n_hc == 0) return 0;
    const uint64_t row_bytes = (uint64_t)n_hc * sizeof(float);
    if (row_bytes == 0 || out->bytes < row_bytes || out->bytes % row_bytes != 0 ||
        pre->bytes < out->bytes ||
        scale_offset > model_size || sizeof(float) > model_size - scale_offset ||
        base_offset > model_size || row_bytes > model_size - base_offset) {
        return 0;
    }
    const uint64_t n_tokens = out->bytes / row_bytes;
    const float *scale = (const float *)cuda_model_range_ptr(model_map, scale_offset, sizeof(float), "output_hc_scale");
    const float *base = (const float *)cuda_model_range_ptr(model_map, base_offset, row_bytes, "output_hc_base");
    if (!scale || !base) return 0;
    uint64_t n = n_tokens * n_hc;
    output_hc_weights_kernel<<<(n + 255) / 256, 256>>>(
            (float *)out->ptr,
            (const float *)pre->ptr,
            scale,
            base,
            n_hc,
            (uint32_t)n_tokens,
            eps);
    return cuda_ok(cudaGetLastError(), "output hc weights launch");
}

extern "C" int ds4_gpu_arena_output_hc_weights_tensor(
        const ds4_gpu_arena           *arena,
        const ds4_gpu_source_row_view *scale,
        const ds4_gpu_source_row_view *base,
        ds4_gpu_tensor                *out,
        const ds4_gpu_tensor          *pre,
        uint32_t                       n_hc,
        float                          eps) {
    if (!arena || !scale || !base || !out || !pre || !arena->valid ||
        !arena->ptr || !out->ptr || !pre->ptr || n_hc == 0) {
        return 1;
    }
    const uint64_t row_bytes = (uint64_t)n_hc * sizeof(float);
    if (row_bytes == 0 ||
        out->bytes < row_bytes ||
        out->bytes % row_bytes != 0 ||
        pre->bytes < out->bytes ||
        out->device != arena->gpu ||
        pre->device != arena->gpu ||
        scale->rows != 1 ||
        scale->cols != 1 ||
        base->rows != 1 ||
        base->cols != n_hc ||
        scale->row_stride_bytes < sizeof(float) ||
        base->row_stride_bytes < row_bytes ||
        (scale->arena_offset % sizeof(float)) != 0 ||
        (base->arena_offset % sizeof(float)) != 0 ||
        !cuda_arena_range_ok(arena, scale->arena_offset, sizeof(float)) ||
        !cuda_arena_range_ok(arena, base->arena_offset, row_bytes)) {
        return 1;
    }
    const uint64_t n_tokens = out->bytes / row_bytes;
    if (n_tokens > UINT32_MAX) return 1;
    if (!cuda_ok(cudaSetDevice(arena->gpu), "arena output hc weights set device")) {
        return 1;
    }
    const float *scale_ptr =
        (const float *)((const char *)arena->ptr + scale->arena_offset);
    const float *base_ptr =
        (const float *)((const char *)arena->ptr + base->arena_offset);
    const uint64_t n = n_tokens * n_hc;
    output_hc_weights_kernel<<<(n + 255) / 256, 256>>>(
            (float *)out->ptr,
            (const float *)pre->ptr,
            scale_ptr,
            base_ptr,
            n_hc,
            (uint32_t)n_tokens,
            eps);
    return cuda_ok(cudaGetLastError(), "arena output hc weights launch") ? 0 : 1;
}
extern "C" int ds4_gpu_hc_expand_tensor(ds4_gpu_tensor *out_hc, const ds4_gpu_tensor *block_out, const ds4_gpu_tensor *residual_hc, const ds4_gpu_tensor *post, const ds4_gpu_tensor *comb, uint32_t n_embd, uint32_t n_hc) {
    if (!out_hc || !block_out || !residual_hc || !post || !comb || n_embd == 0 || n_hc == 0) return 0;
    uint32_t n_tokens = (uint32_t)(out_hc->bytes / ((uint64_t)n_hc * n_embd * sizeof(float)));
    uint64_t n_elem = (uint64_t)n_tokens * n_hc * n_embd;
    hc_expand_kernel<<<(n_elem + 255) / 256, 256>>>((float *)out_hc->ptr,
                                                    (const float *)block_out->ptr,
                                                    (const float *)block_out->ptr,
                                                    (const float *)residual_hc->ptr,
                                                    (const float *)post->ptr,
                                                    (const float *)comb->ptr,
                                                    n_embd, n_hc, n_tokens,
                                                    n_hc, n_hc * n_hc, 0);
    return cuda_ok(cudaGetLastError(), "hc_expand launch");
}
extern "C" int ds4_gpu_hc_expand_split_tensor(ds4_gpu_tensor *out_hc, const ds4_gpu_tensor *block_out, const ds4_gpu_tensor *residual_hc, const ds4_gpu_tensor *split, uint32_t n_embd, uint32_t n_hc) {
    if (!out_hc || !block_out || !residual_hc || !split || n_embd == 0 || n_hc == 0) return 0;
    uint32_t n_tokens = (uint32_t)(out_hc->bytes / ((uint64_t)n_hc * n_embd * sizeof(float)));
    uint32_t mix_hc = 2u * n_hc + n_hc * n_hc;
    uint64_t n_elem = (uint64_t)n_tokens * n_hc * n_embd;
    const float *base = (const float *)split->ptr;
    hc_expand_kernel<<<(n_elem + 255) / 256, 256>>>((float *)out_hc->ptr,
                                                    (const float *)block_out->ptr,
                                                    (const float *)block_out->ptr,
                                                    (const float *)residual_hc->ptr,
                                                    base + n_hc,
                                                    base + 2u * n_hc,
                                                    n_embd, n_hc, n_tokens,
                                                    mix_hc, mix_hc, 0);
    return cuda_ok(cudaGetLastError(), "hc_expand_split launch");
}
extern "C" int ds4_gpu_hc_expand_add_split_tensor(ds4_gpu_tensor *out_hc, const ds4_gpu_tensor *block_out, const ds4_gpu_tensor *block_add, const ds4_gpu_tensor *residual_hc, const ds4_gpu_tensor *split, uint32_t n_embd, uint32_t n_hc) {
    if (!out_hc || !block_out || !block_add || !residual_hc || !split || n_embd == 0 || n_hc == 0) return 0;
    uint32_t n_tokens = (uint32_t)(out_hc->bytes / ((uint64_t)n_hc * n_embd * sizeof(float)));
    uint32_t mix_hc = 2u * n_hc + n_hc * n_hc;
    uint64_t n_elem = (uint64_t)n_tokens * n_hc * n_embd;
    const float *base = (const float *)split->ptr;
    hc_expand_kernel<<<(n_elem + 255) / 256, 256>>>((float *)out_hc->ptr,
                                                    (const float *)block_out->ptr,
                                                    (const float *)block_add->ptr,
                                                    (const float *)residual_hc->ptr,
                                                    base + n_hc,
                                                    base + 2u * n_hc,
                                                    n_embd, n_hc, n_tokens,
                                                    mix_hc, mix_hc, 1);
    return cuda_ok(cudaGetLastError(), "hc_expand_add_split launch");
}
extern "C" int ds4_gpu_shared_down_hc_expand_q8_0_tensor(
        ds4_gpu_tensor       *out_hc,
        ds4_gpu_tensor       *shared_out,
        const void             *model_map,
        uint64_t                model_size,
        uint64_t                weight_offset,
        uint64_t                in_dim,
        uint64_t                out_dim,
        const ds4_gpu_tensor *shared_mid,
        const ds4_gpu_tensor *routed_out,
        const ds4_gpu_tensor *residual_hc,
        const ds4_gpu_tensor *split,
        uint32_t                n_embd,
        uint32_t                n_hc) {
    if (getenv("DS4_CUDA_DISABLE_Q8_HC_EXPAND_FUSED") == NULL) {
        return cuda_matmul_q8_0_hc_expand_tensor_labeled(out_hc, shared_out,
                                                        model_map, model_size,
                                                        weight_offset,
                                                        in_dim, out_dim,
                                                        shared_mid,
                                                        routed_out,
                                                        residual_hc,
                                                        split,
                                                        n_embd, n_hc,
                                                        "shared_down_hc_expand");
    }
    return ds4_gpu_matmul_q8_0_tensor(shared_out, model_map, model_size,
                                        weight_offset, in_dim, out_dim,
                                        shared_mid, 1) &&
           ds4_gpu_hc_expand_add_split_tensor(out_hc, shared_out, routed_out,
                                                residual_hc, split, n_embd, n_hc);
}

extern "C" int ds4_gpu_matmul_q8_0_hc_expand_tensor(
        ds4_gpu_tensor       *out_hc,
        ds4_gpu_tensor       *block_out,
        const void             *model_map,
        uint64_t                model_size,
        uint64_t                weight_offset,
        uint64_t                in_dim,
        uint64_t                out_dim,
        const ds4_gpu_tensor *x,
        const ds4_gpu_tensor *residual_hc,
        const ds4_gpu_tensor *split,
        uint32_t                n_embd,
        uint32_t                n_hc) {
    if (getenv("DS4_CUDA_DISABLE_Q8_HC_EXPAND_FUSED") == NULL) {
        return cuda_matmul_q8_0_hc_expand_tensor_labeled(out_hc, block_out,
                                                        model_map, model_size,
                                                        weight_offset,
                                                        in_dim, out_dim,
                                                        x,
                                                        NULL,
                                                        residual_hc,
                                                        split,
                                                        n_embd, n_hc,
                                                        "q8_hc_expand");
    }
    return ds4_gpu_matmul_q8_0_tensor(block_out, model_map, model_size,
                                        weight_offset, in_dim, out_dim, x, 1) &&
           ds4_gpu_hc_expand_split_tensor(out_hc, block_out, residual_hc,
                                            split, n_embd, n_hc);
}
